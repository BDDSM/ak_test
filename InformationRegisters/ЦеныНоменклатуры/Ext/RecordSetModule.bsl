
//+++АК KIRN 2018.03.02 ИП-00017976.01    
Процедура ПриЗаписи(Отказ, Замещение)
	//ЭтотОбъект.ДополнительныеСвойства.Вставить("РезультатПроверки","Ок");
	Если ЭтотОбъект.Количество()=0 Тогда
		Возврат;
	Конецесли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(т.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	т.НомерСтроки,
	               |	ВЫРАЗИТЬ(т.ТипЦен КАК Справочник.ТипыЦен) КАК ТипЦен,
	               |	ВЫРАЗИТЬ(т.ТорговаяТочка КАК Справочник.СтруктурныеЕдиницы) КАК ТорговаяТочка
	               |ПОМЕСТИТЬ вт_Набор0
	               |ИЗ
	               |	&Набор КАК т
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	т.Номенклатура КАК Номенклатура,
	               |	т.НомерСтроки,
	               |	т.ТипЦен КАК ТипЦены,
	               |	т.ТорговаяТочка КАК ТорговаяТочка,
	               |	т.Номенклатура.ТорговаяМарка КАК ТорговаяМарка,
	               |	т.ТипЦен.ТорговаяМарка КАК ТорговаяМаркаЦены,
	               |	т.ТорговаяТочка.ТипРозничнойТочки КАК ТипТТ
	               |ПОМЕСТИТЬ вт_Набор
	               |ИЗ
	               |	вт_Набор0 КАК т
				   |ГДЕ 
				   |	т.ТорговаяТочка <> &ПустаяТТ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	вт.НомерСтроки,
	               |	вт.Номенклатура,
	               |	вт.ТорговаяМарка,
	               |	вт.ТорговаяМаркаЦены,
	               |	вт.ТипЦены,
	               |	вт.ТорговаяТочка,
	               |	вт.ТипТТ,
	               |	рс.ТорговаяМарка КАК ТорговаяМаркаС
	               |ИЗ
	               |	вт_Набор КАК вт
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеТиповРозничныхТочекИТорговыхМарок КАК рс
	               |		ПО вт.ТорговаяМарка = рс.ТорговаяМарка
	               |			И вт.ТипТТ = рс.ТипРозничнойТочки
	               |ГДЕ
	               |	(вт.ТорговаяМарка <> вт.ТорговаяМаркаЦены
	               |			ИЛИ рс.ТорговаяМарка ЕСТЬ NULL)";
	тз_Набор = ЭтотОбъект.Выгрузить();
	Запрос.УстановитьПараметр("Набор",тз_Набор);
	Запрос.УстановитьПараметр("ПустаяТТ",Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕСли;
	Выборка = РезультатЗапроса.Выбрать();
	ТекстОшибки = "";
	Пока Выборка.Следующий() Цикл
		Отказ = Истина;
		Если Выборка.ТорговаяМарка <> Выборка.ТорговаяМаркаЦены Тогда
			ТекстОшибки = ТекстОшибки+"
			|№"+Выборка.НомерСтроки+" для номенклатуры <"+Выборка.Номенклатура+"> торговая марка <"+Выборка.ТорговаяМарка+"> не соответствует торговой марке цены <"+Выборка.ТорговаяМаркаЦены+">";
		ИначеЕсли Не ЗначениеЗаполнено(Выборка.ТорговаяМаркаС) Тогда
			ТекстОшибки = ТекстОшибки+"
			|№"+Выборка.НомерСтроки+" для номенклатуры <"+Выборка.Номенклатура+"> торговая марка <"+Выборка.ТорговаяМарка+"> не соответствует типу розничной точки <"+Выборка.ТипТТ+">";
		КонецЕСли;
	КонецЦикла;
	
	Если Отказ Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ТекстОшибки;
		Сообщение.УстановитьДанные(тз_Набор[0].Регистратор);
		Сообщение.Сообщить();
	КонецЕСли;
	
КонецПроцедуры
//---АК KIRN 
