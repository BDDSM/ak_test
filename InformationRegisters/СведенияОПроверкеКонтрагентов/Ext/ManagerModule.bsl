Функция ПолучитьРекомендацииИПояснения(Контрагент,Дата) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("РекомендацияУФК","");
	Результат.Вставить("ПоясненияУФК","");
	Результат.Вставить("Рекомендация1cont","");
	Результат.Вставить("Пояснения1cont","");
	Результат.Вставить("Период1cont","");
	Результат.Вставить("ПериодУФК","");
	Результат.Вставить("ОтветПолучен","");	
	
	Если не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Результат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОПроверкеКонтрагентовСрезПоследних.Рекомендация КАК Рекомендация1cont,
		|	СведенияОПроверкеКонтрагентовСрезПоследних.Пояснения КАК Пояснения1cont,
		|	СведенияОПроверкеКонтрагентовСрезПоследних.Период КАК Период1cont
		|ИЗ
		|	РегистрСведений.СведенияОПроверкеКонтрагентов.СрезПоследних(
		|			&Дата,
		|			ТипСведений В (ЗНАЧЕНИЕ(Перечисление.СведенияОПроверке.Сведения1Cont), ЗНАЧЕНИЕ(Перечисление.СведенияОПроверке.СведенияОбщие))
		//+++АК SHEP 2018.01.22 ИП-00017714
		|			И НЕ Рекомендация = ЗНАЧЕНИЕ(Справочник.Рекомендации.ПустаяСсылка)
		//---АК SHEP 2018.01.22
		|				И Контрагент = &Контрагент) КАК СведенияОПроверкеКонтрагентовСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период1cont";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Результат,ВыборкаДетальныеЗаписи);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОПроверкеКонтрагентовСрезПоследних.РекомендацияВВ КАК РекомендацияУФК,
		|	СведенияОПроверкеКонтрагентовСрезПоследних.ПоясненияВВ КАК ПоясненияУФК,
		|	СведенияОПроверкеКонтрагентовСрезПоследних.Период КАК ПериодУФК,
		|	СведенияОПроверкеКонтрагентовСрезПоследних.ОтветПолучен КАК ОтветПолучен
		|ИЗ
		|	РегистрСведений.СведенияОПроверкеКонтрагентов.СрезПоследних(
		|			&Дата,
		|			ТипСведений В (ЗНАЧЕНИЕ(Перечисление.СведенияОПроверке.СведенияУФК), ЗНАЧЕНИЕ(Перечисление.СведенияОПроверке.СведенияОбщие))
		//+++АК SHEP 2018.01.22 ИП-00017714: очищалась информация по УФК
		|			И НЕ РекомендацияВВ = ЗНАЧЕНИЕ(Справочник.Рекомендации.ПустаяСсылка)
		//---АК SHEP 2018.01.22
		|				И Контрагент = &Контрагент) КАК СведенияОПроверкеКонтрагентовСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодУФК";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Результат,ВыборкаДетальныеЗаписи);
		
	КонецЦикла;
		
	Возврат Результат;
	
КонецФункции	

Процедура ЗаполнитьСправочникРекомендации() Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(СведенияОПроверкеКонтрагентов.РекомендацияСтрока КАК СТРОКА(150)) КАК РекомендацияВВСтрока
		|ПОМЕСТИТЬ втРекомендации
		|ИЗ
		|	РегистрСведений.СведенияОПроверкеКонтрагентов КАК СведенияОПроверкеКонтрагентов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(СведенияОПроверкеКонтрагентов.РекомендацияВВСтрока КАК СТРОКА(150))
		|ИЗ
		|	РегистрСведений.СведенияОПроверкеКонтрагентов КАК СведенияОПроверкеКонтрагентов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втРекомендации.РекомендацияВВСтрока КАК Рекомендация
		|ИЗ
		|	втРекомендации КАК втРекомендации";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Рекомендация = СокрЛП(ВыборкаДетальныеЗаписи.Рекомендация);
		Если Рекомендация <> "" Тогда
			
			НовыйЭлемент = Справочники.Рекомендации.СоздатьЭлемент();
			НовыйЭлемент.Наименование = ВыборкаДетальныеЗаписи.Рекомендация;
			НовыйЭлемент.Записать();
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

Процедура ЗаполнитьРекомендацииВРегистре() Экспорт
	
	НаборЗаписей = РегистрыСведений.СведенияОПроверкеКонтрагентов.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	Для каждого запись из НаборЗаписей Цикл
		
		Рекомендация = СокрЛП(запись.РекомендацияСтрока);
		Если  Рекомендация = "" Тогда 
			Продолжить;
		КонецЕсли;	
		запись.Рекомендация = Справочники.Рекомендации.НайтиПоНаименованию(Рекомендация);		
		Рекомендация = СокрЛП(запись.РекомендацияВВСтрока);
		Если  Рекомендация = "" Тогда 
			Продолжить;
		КонецЕсли;	
		запись.РекомендацияВВ = Справочники.Рекомендации.НайтиПоНаименованию(Рекомендация);		
		
	КонецЦикла;	
	
	НаборЗаписей.Записать();
	
	
КонецПроцедуры	

Процедура ЗаполнитьСведения() Экспорт
	
	НаборЗаписей = РегистрыСведений.СведенияОПроверкеКонтрагентов.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	Для каждого запись из НаборЗаписей Цикл
		
		Если Не ЗначениеЗаполнено(запись.ТипСведений) Тогда
			запись.ТипСведений = Перечисления.СведенияОПроверке.СведенияОбщие;
		КонецЕсли;	
			
	КонецЦикла;	
	
	НаборЗаписей.Записать();
	
	
КонецПроцедуры