////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает сгенерированный номер протокола (генерация номеров в разрезе организации, вида аттестации и года)
//
// Параметры:
//  СтруктураПараметров  - <Тип.Структура> - Структура параметров (Организация, ВидАттестации, ВремяНачалаТестирования)
//
// Возвращаемое значение:
//   <Тип.Число>   - номер протокола
//
Функция СгенерироватьНомерПротокола(СтруктураПараметров) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИтогиПрохожденияАттестацииСотрудников.Организация,
	|	ИтогиПрохожденияАттестацииСотрудников.ВидАттестации,
	|	МАКСИМУМ(ИтогиПрохожденияАттестацииСотрудников.НомерПротокола) КАК НомерПротокола
	|ИЗ
	|	РегистрСведений.ИтогиПрохожденияАттестацииСотрудников КАК ИтогиПрохожденияАттестацииСотрудников
	|ГДЕ
	|	ИтогиПрохожденияАттестацииСотрудников.Период >= &Период
	|	И ИтогиПрохожденияАттестацииСотрудников.Организация = &Организация
	|	И ИтогиПрохожденияАттестацииСотрудников.ВидАттестации = &ВидАттестации
	|
	|СГРУППИРОВАТЬ ПО
	|	ИтогиПрохожденияАттестацииСотрудников.Организация,
	|	ИтогиПрохожденияАттестацииСотрудников.ВидАттестации";
	
	Запрос.УстановитьПараметр("ВидАттестации", СтруктураПараметров.ВидАттестации);
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("Период", НачалоГода(СтруктураПараметров.ВремяНачалаТестирования));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 1;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
        Возврат Выборка.НомерПротокола + 1; 
	КонецЕсли;
	
КонецФункции

// Процедура формирует табличный документ протокола по охране труда
//
// Параметры:
//  Структура параметров  - <Тип.Структура> - Структура для отборов (Организация, ВидАттестации, ФизЛицо, Период)
//                 <продолжение описания параметра>
//  ТабДок  - <Тип.ТабличныйДокумент> - Табличный Документ
//
Процедура НапечататьПротоколАттестации(СтруктураПараметров, ТабДок) Экспорт
	
	ТабДок.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.Период,
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.ВидАттестации,
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.Организация,
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.ФизЛицо КАК Сотрудник,
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.ФизЛицо.Должность КАК Должность,
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.Пройдена,
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.ПротоколПринял,
	|	ИтогиПрохожденияАттестацииСотрудниковСрезПоследних.НомерПротокола
	|ИЗ
	|	РегистрСведений.ИтогиПрохожденияАттестацииСотрудников.СрезПоследних(
	|			ДОБАВИТЬКДАТЕ(&Период, СЕКУНДА, 1),
	|			Организация = &Организация
	|				И ВидАттестации = &ВидАттестации
	|				И ФизЛицо = &ФизЛицо) КАК ИтогиПрохожденияАттестацииСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("ВидАттестации", СтруктураПараметров.ВидАттестации);
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("Период", НачалоДня(СтруктураПараметров.Период));
	Запрос.УстановитьПараметр("ФизЛицо", СтруктураПараметров.ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Макет = РегистрыСведений.ИтогиПрохожденияАттестацииСотрудников.ПолучитьМакет("Протокол");
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьСостав   = Макет.ПолучитьОбласть("Состав");
		ОбластьПроверка = Макет.ПолучитьОбласть("Проверка");
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
				
		Выборка = РезультатЗапроса.Выбрать();
		
		Выборка.Следующий(); 
		
		ЗаполнитьЗначенияСвойств(ОбластьЗаголовок.Параметры, Выборка);
		ОбластьЗаголовок.Параметры.Дата  = Формат(НачалоДня(Выборка.Период),"ДЛФ=DD");
		
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьСостав);
		ТабДок.Вывести(ОбластьПроверка);
		ТабДок.Вывести(ОбластьШапкаТаблицы);
		
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, Выборка);
		ТабДок.Вывести(ОбластьСтрока);
		
		ТабДок.Вывести(ОбластьПодписи);
		
	КонецЕсли;
	
КонецПроцедуры
