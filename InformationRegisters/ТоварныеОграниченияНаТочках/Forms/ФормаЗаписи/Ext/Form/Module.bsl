
&НаСервереБезКонтекста
Функция РольДоступнаНаСервере(Роль)
	Возврат РольДоступна(Роль)
КонецФункции

Процедура ЗаполнитьРасчетныеКоличества()
	
	Если Запись.ТорговаяТочка.Пустая()
			ИЛИ Запись.ГруппаНоменклатуры.Пустая() Тогда // рассчитывать нечего
		ЭтаФорма.КвантУпаковки 					= 0;
		ЭтаФорма.МинимальноВозможныйМаксОстаток = 0;
		ЭтаФорма.ВтораяМаксПродажа 				= 0;
		ЭтаФорма.РекомендуемыйМаксОстаток		= 0;
		Возврат;
	КонецЕсли;
	
	
	// квант упаковки
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТорговаяТочка"	, Запись.ТорговаяТочка);
	Запрос.УстановитьПараметр("Номенклатура"	, Запись.ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("ГруппаУРЗ"		, Запись.ГруппаНоменклатуры.ГруппаНоменклатурыУРЗ);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КоличествоВКоробкеСрезПоследних.Характеристика КАК Характеристика,
	|	КоличествоВКоробкеСрезПоследних.Количество КАК Количество
	|ПОМЕСТИТЬ ВТКоличествоВКоробке
	|ИЗ
	|	РегистрСведений.КоличествоВКоробке.СрезПоследних(, Номенклатура = &Номенклатура) КАК КоличествоВКоробкеСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОбеспеченияТорговыхТочек.СрезПоследних(
	|				,
	|				ГруппаУРЗ = &ГруппаУРЗ
	|					И ТорговаяТочка = &ТорговаяТочка) КАК ПорядокОбеспеченияТорговыхТочек
	|		ПО КоличествоВКоробкеСрезПоследних.СтруктурнаяЕдиница = ПорядокОбеспеченияТорговыхТочек.Расчетчик
	|ГДЕ
	|	КоличествоВКоробкеСрезПоследних.Характеристика.БратьКоличествоВКоробкеПоСкладуДляРаспределения
	|	И НЕ КоличествоВКоробкеСрезПоследних.Количество = 0
	|	И НЕ ПорядокОбеспеченияТорговыхТочек.Расчетчик ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТКоличествоВКоробке.Количество, 0) = 0
	|				ТОГДА ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, 0)
	|			ИНАЧЕ ВЫБОР
	|					КОГДА СпрХарактеристикиНоменклатуры.Владелец.Весовой
	|						ТОГДА ВТКоличествоВКоробке.Количество
	|					ИНАЧЕ ВЫРАЗИТЬ(ВТКоличествоВКоробке.Количество КАК ЧИСЛО(15, 0))
	|				КОНЕЦ
	|		КОНЕЦ) КАК Количество
	|ИЗ
	|	РегистрСведений.ТоварныйАссортиментТочек.СрезПоследних(
	|			,
	|			ТорговаяТочка = &ТорговаяТочка
	|				И Номенклатура = &Номенклатура) КАК ТоварныйАссортиментТочек
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристикиНоменклатуры
	|		ПО (СпрХарактеристикиНоменклатуры.Ссылка = ТоварныйАссортиментТочек.Характеристика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.КоличествоВУпаковке))
	|			И (ЗначенияСвойствОбъектов.Объект = ТоварныйАссортиментТочек.Характеристика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоВКоробке КАК ВТКоличествоВКоробке
	|		ПО (ВТКоличествоВКоробке.Характеристика = ТоварныйАссортиментТочек.Характеристика)
	|ГДЕ
	|	СпрХарактеристикиНоменклатуры.Владелец = &Номенклатура";
	                                                            
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЭтаФорма.КвантУпаковки = Выборка.Количество;
	Иначе
		ЭтаФорма.КвантУпаковки = 0;
	КонецЕсли;
	ЭтаФорма.МинимальноВозможныйМаксОстаток = ЭтаФорма.КвантУпаковки * 1.15;
	
	
	// Вторая макс. продажа
	ADOСоединение = Новый COMОбъект("ADODB.Connection");
	ADOСоединение.ConnectionTimeOut = 0;
	ADOСоединение.CommandTimeOut    = 0;
	ADOСоединение.ConnectionString  = ОбменСAccess.ПолучитьСтрокуСоединения("SMS_Izbenka");
	ADOСоединение.Open();
	
	ТекстЗапроса = "
	|IF OBJECT_ID ('tempdb..#ls') IS NOT NULL Drop Table #ls
	|
	|create table #ls (date_tt date, quantity numeric(15, 3)) 
    |
	|Insert into #ls
	|Exec (
	|'Select TOP 2
	|	dtt.date_tt,
	|	cast(SUM(dtt.quantity - dtt.discount50_qty) as numeric(10, 1)) as quantity
	|	
	|From [vv03].[dbo].[DTT] as DTT (nolock) 
    |
	|Where
	|	dtt.date_tt >= DATEADD(WEEK, -3, Convert(DATE, GETDATE()))
	|	And id_tov = " + Формат(Запись.ГруппаНоменклатуры.id_tov, "ЧГ=;ЧН=") + "
	|	And id_tt = " + Формат(Запись.ТорговаяТочка.id_TT, "ЧГ=;ЧН=") + "
	|Group by
	|	dtt.date_tt
	|Order by
	|	quantity Desc
	|') at [SRV-SQL03]
	|
	|Select top 2
	|	dtt.date_tt,
	|	cast(SUM(dtt.quantity) as numeric(10, 1)) as quantity
	|From #ls as dtt
	|Group by
	|	dtt.date_tt
	|Order by
	|	quantity Desc";
	
	Выборка = ADOСоединение.Execute(ТекстЗапроса);
	
	мКоличество = 0;
	Пока НЕ Выборка = Неопределено Цикл
		Если Выборка.Fields.Count > 0 Тогда
			Пока НЕ Выборка.EOF Цикл
				Если НЕ Выборка.EOF Тогда
					мКоличество = Выборка.Fields("quantity").Value;
					Выборка.MoveNext();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Выборка = Выборка.NextRecordSet();
	КонецЦикла;
	
	ADOСоединение.Close();		
	
	//
	ЭтаФорма.ВтораяМаксПродажа 			= мКоличество;
	ЭтаФорма.РекомендуемыйМаксОстаток 	= мКоличество + ЭтаФорма.МинимальноВозможныйМаксОстаток;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьРасчетныеКоличества();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	
	//Если Запись.МинимальныйОстаток > 10 * ЭтаФорма.КвантУпаковки Тогда
	//	ОбщегоНазначения.СообщитьОбОшибке("Мин. остаток превышает квант упаковки более чем в 10 раз! Запись невозможна.", Отказ);
	//КонецЕсли;
		
	Если Запись.МаксимальныйОстаток > 0
			И Запись.МинимальныйОстаток + ЭтаФорма.КвантУпаковки > Запись.МаксимальныйОстаток Тогда
	  Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ТорговаяТочка, "ТипРозничнойТочки") <> ПредопределенноеЗначение("Перечисление.ТипыРозничныхТочек.Вкусомат") Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Макс. остаток превышает минимальный более чем на квант! Запись невозможна.", Отказ);
	  КонецЕсли; //+++АК SHEP 2018.10.14 ИП-00020147: добавил условие
	КонецЕсли;
	
	// tuta +++
	МаксМинимальныйОстаток = ЭтаФорма.ВтораяМаксПродажа + ЭтаФорма.КвантУпаковки;
	Если ЭтаФорма.КвантУпаковки > 0
			И Запись.МинимальныйОстаток > МаксМинимальныйОстаток Тогда
		//Если РольДоступнаНаСервере("Помощник")
		//		ИЛИ РольДоступнаНаСервере("ПолныеПрава") Тогда
		МожетПревышать = УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(
												ПредопределенноеЗначение("ПланВидовХарактеристик.ПраваПользователей.МожетПревышатьМинОстатокВТТНадКвантомБолееЧемВ10Раз"), Ложь);
		Если МожетПревышать = Истина Тогда
			Отказ = Отказ
						ИЛИ Вопрос("Мин. остаток превышает квант более чем на вторую макс. продажу! Уверены, что хотите записать данные?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет;
		Иначе
			ОбщегоНазначения.СообщитьОбОшибке("Мин. остаток превышает максимально допустимое значение (" + Формат(МаксМинимальныйОстаток, "ЧГ=") +
													")! Недостаточно прав для записи.", Отказ);
		КонецЕсли;
	КонецЕсли;
	// tuta ---
	
КонецПроцедуры


&НаКлиенте
Процедура ГруппаНоменклатурыПриИзменении(Элемент)
	
	ЗаполнитьРасчетныеКоличества();
	
КонецПроцедуры

&НаКлиенте
Процедура ТорговаяТочкаПриИзменении(Элемент)
	
	ЗаполнитьРасчетныеКоличества();
	
КонецПроцедуры

&НаКлиенте
Процедура МинимальныйОстатокПриИзменении(Элемент)
	
	Если Запись.МинимальныйОстаток > (ЭтаФорма.КвантУпаковки * 10)
			И Вопрос("Мин. остаток превышает квант упаковки более чем в 10 раз! Вы уверены?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		Запись.МинимальныйОстаток = 0;
	КонецЕсли;
	
КонецПроцедуры
