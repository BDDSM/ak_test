
Процедура ПередЗаписью(Отказ, Замещение)
	//+++АК mika 2018.10.19 ИП-00019964 
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;	
	//---АК mika
	
	//mind 2017-09-12 запишем изменения в лог
	СтруктураОтбор = Новый Структура;
	Для Каждого ЭлементОтбор Из ЭтотОбъект.Отбор Цикл
		Если ЭлементОтбор.Использование Тогда
			СтруктураОтбор.Вставить(ЭлементОтбор.Имя, ЭлементОтбор.Значение);
		КонецЕсли;	
	КонецЦикла;
	
	Если СтруктураОтбор.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		ТекстЗапроса =
		"ВЫБРАТЬ * ИЗ РегистрСведений." + ЭтотОбъект.Метаданные().Имя + " КАК Таб
		|ГДЕ
		|	ИСТИНА";
		
		Для Каждого ЭлементОтбор Из СтруктураОтбор Цикл
			ТекстЗапроса = ТекстЗапроса + Символы.ПС + "И " + ЭлементОтбор.Ключ + " = &Параметр" + ЭлементОтбор.Ключ;
			Запрос.УстановитьПараметр("Параметр" + ЭлементОтбор.Ключ, ЭлементОтбор.Значение);
		КонецЦикла;	
		Запрос.Текст = ТекстЗапроса;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Запись = РегистрыСведений.ЖурналИзмененияТабелей.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.Период 			= ТекущаяДата();
			Запись.ДатаТабеля 		= Выборка.Период;
			Запись.АвторИзменений 	= ПараметрыСеанса.ТекущийПользователь;
			Запись.ТипДействия 		= "Удаление";
			Запись.ТипТабеля 		= "Промоутеры";
			Попытка
				Если ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПродавец) Тогда
					Запись.ИзмененияСделаныСМагазина = ПараметрыСеанса.ТорговаяТочкаПоАйпи;
				КонецЕсли;	
			Исключение
			КонецПопытки;
			Запись.Записать();
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	//+++АК mika 2018.10.19 ИП-00019964 
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;	
	//---АК mika

	//mind 2017-09-12 запишем изменения в лог
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		Запись = РегистрыСведений.ЖурналИзмененияТабелей.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, ЗаписьНабора);
		Запись.Период 			= ТекущаяДата() + 1;
		Запись.ДатаТабеля 		= ЗаписьНабора.Период;
		Запись.АвторИзменений 	= ПараметрыСеанса.ТекущийПользователь;
		Запись.ТипДействия 		= "Добавление";
		Запись.ТипТабеля 		= "Промоутеры";
		Попытка
			Если ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПродавец) Тогда
				Запись.ИзмененияСделаныСМагазина = ПараметрыСеанса.ТорговаяТочкаПоАйпи;
			КонецЕсли;	
		Исключение
		КонецПопытки;
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры
