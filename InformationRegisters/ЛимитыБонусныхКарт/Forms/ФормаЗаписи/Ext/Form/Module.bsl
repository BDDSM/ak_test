//+++АК CISA 2018.10.02 ИП-00019752
&НаСервереБезКонтекста
Функция База_Подключение(СтрокаПодключения)	
	
	Попытка
		CurrentConnection = Новый COMОбъект("ADODB.Connection");
		Catalog = Новый COMОбъект("ADOX.Catalog");			
		
		Catalog.ActiveConnection = СтрокаПодключения;
		CurrentConnection.Open(СтрокаПодключения);
		Возврат CurrentConnection;	
		
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();		
		#Если НаКлиенте тогда
		Сообщить(ОписаниеОшибки);			
		#КонецЕсли		
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

//+++АК CISA 2018.10.02 ИП-00019752
&НаСервереБезКонтекста
Функция База_ВыполнитьЗапрос(ТекстЗапроса, допПараметры = Неопределено, СтрокаПодключения = "")  
	Попытка
		Command = Новый COMОбъект("ADODB.Command");
			
		Если ТипЗнч(допПараметры) = Тип("Структура") тогда
			ЗаполнитьЗначенияСвойств(Command, допПараметры);
		КонецЕсли;			
		CurrentConnection = База_Подключение(СтрокаПодключения);
		Command.ActiveConnection = CurrentConnection;
		Command.CommandTimeout = 0;
		Command.CommandText = ТекстЗапроса;
		RecordSet = Новый COMОбъект("ADODB.RecordSet");
		RecordSet = Command.Execute(); //Выполнение и получение набора данных
				
		Возврат RecordSet;
		
	Исключение
		Сообщить(ТекстЗапроса);
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;	
КонецФункции

//+++АК CISA 2018.10.02 ИП-00019752
&НаСервереБезКонтекста
Функция СформироватьИВыполнитьЗапросSQL(НомерБонуснойКарты)
	
	ТекстЗапроса = "EXEC Loyalty.dbo.FOR1C_Ins_Limit_BonusCard @BONUSCARD = "+НомерБонуснойКарты;
	СтрокаПодключения = ВнешниеДанные.ПолучитьСтрокуПодключенияMSSQL("srv-sql01", "Loyalty");
	RecordSet = База_ВыполнитьЗапрос(ТекстЗапроса, , СтрокаПодключения);
	
	Попытка
		Пока RecordSet <> Неопределено
    	 И RecordSet.Fields.Count <= 0 Цикл
    		RecordSet=RecordSet.NextRecordSet();
		КонецЦикла;		
		КвоКолонок = RecordSet.Fields.Count;		
		Если КвоКолонок > 0 Тогда
			Ответ = RecordSet.Fields(КвоКолонок-1).Value;
			RecordSet.Close();
		Иначе
			Ответ = -5;
		КонецЕсли;
	Исключение
		Ответ = -2;
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

//+++АК CISA 2018.10.02 ИП-00019752
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ЗаписьСуществует = ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи);
	Если НЕ ЗаписьСуществует Тогда
		Если ЗначениеЗаполнено(Запись.НомерБонуснойКарты) Тогда
			Если СтрДлина(Запись.НомерБонуснойКарты) <> 7 Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Номер карты должен состоять из 7 цифр!";
				Сообщение.Поле = "Запись.НомерБонуснойКарты";
				Сообщение.УстановитьДанные(Запись);
				Сообщение.Сообщить();
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
			Колво = СформироватьИВыполнитьЗапросSQL(ВнешниеДанные.ФорматПоля(Запись.НомерБонуснойКарты));
			Если Колво = 1 Тогда
				//only_plastic уставновлен в 1 в SQL базе;
			ИначеЕсли Колво = 0 ИЛИ Колво = -1 Тогда				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Карта с таким номером не найдена!";
				Сообщение.Поле = "Запись.НомерБонуснойКарты";
				Сообщение.УстановитьДанные(Запись);
				Сообщение.Сообщить();
				Отказ = Истина;
			ИначеЕсли Колво = -2 Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Возникла ошибка SQL. Пожалуйста, повторите попытку.";
				Сообщение.Сообщить();
				Отказ = Истина;	
			ИначеЕсли Колво = -5 Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Нештатная ситуация. SQL вернул RecordSet.Fields.Count=0.";
				Сообщение.Сообщить();
				Отказ = Истина;	
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


