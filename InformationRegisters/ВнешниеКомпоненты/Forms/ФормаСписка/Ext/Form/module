////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура - обработчик действия "Сохранить файл" командной панели
//
Процедура ДействияФормыСохранитьФайл(Кнопка)
	
	ВыделенныеСтроки = ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок                   = "Выберите каталог, в который следует сохранить файлы";
	ДиалогВыбораФайла.Каталог                     = КаталогПрограммы();
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	Если Не ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ВнешниеКомпоненты.СоздатьНаборЗаписей();
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		НаборЗаписей.Отбор.ИмяФайла.Установить(ВыделеннаяСтрока.ИмяФайла);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Попытка 
				НаборЗаписей[0].ХранилищеФайла.Получить().Записать(ДиалогВыбораФайла.Каталог + "\" + ВыделеннаяСтрока.ИмяФайла);
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ДействияФормыСохранитьФайл()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ РегистрСведенийСписок

// Процедура - обработчик события "ПередНачаломДобавления"
//
Процедура РегистрСведенийСписокПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок                   = "Выбор добавляемых файлов";
	ДиалогВыбораФайла.МножественныйВыбор          = Истина;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.Фильтр                      = "dll файлы (*.dll)|*.dll|Все файлы (*.*)|*.*|";
		
	Если НЕ ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
		
	ИсходныеФайлы = ДиалогВыбораФайла.ВыбранныеФайлы;
		
	Для Каждого ИсходныйФайл Из ИсходныеФайлы Цикл
		УниверсальныеМеханизмы.ДобавлениеВнешнейКомпоненты(ИсходныйФайл);
	КонецЦикла;
	
КонецПроцедуры // РегистрСведенийСписокПередНачаломДобавления()

// Процедура - обработчик события "ПередНачаломИзменения"
//
Процедура РегистрСведенийСписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ИмяФайла = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные.ИмяФайла;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок                   = "Выбор изменяемого файла";
	ДиалогВыбораФайла.МножественныйВыбор          = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.Фильтр                      = ИмяФайла + "|" + ИмяФайла;
	
	Если НЕ ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныеФайлы = ДиалогВыбораФайла.ВыбранныеФайлы;
	
	Для каждого ИсходныйФайл Из ИсходныеФайлы Цикл
		УниверсальныеМеханизмы.ДобавлениеВнешнейКомпоненты(ИсходныйФайл);
	КонецЦикла;
	
КонецПроцедуры // РегистрСведенийСписокПередНачаломИзменения()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ





