
//LATV: Используется только в тонком клиенте. Если захотят обычное приложение - надо пересмотреть отбор по складу для магазинов

#Область ОбработчикиСобытийФормы

//+++АК LATV 2018.10.12 ИП-00020001
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДатаСледующегоРаспределения = КонецДня(ТекущаяДата()) + 1;
	Пока Обработки.ЗагрузитьРаспределение1.МагазиныКПересчетуНазначены(ДатаСледующегоРаспределения) Цикл
		ДатаСледующегоРаспределения = ДатаСледующегоРаспределения + 24*60*60;
	КонецЦикла;
	
	Дата = ДатаСледующегоРаспределения;

КонецПроцедуры

//+++АК LATV 2018.10.15 ИП-00020001
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	УстановитьОтборСклад();

КонецПроцедуры

//+++АК LATV 2018.10.12 ИП-00020001
&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если Записать Тогда
		Записать = Ложь;
		
		Если Не ПроверитьЗаполнение() Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ЗаписатьНаСервере(Отказ);
		Возврат;
		
	ИначеЕсли Модифицированность Тогда
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемПодтверждение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, 30);
		Возврат;
		
	КонецЕсли;

КонецПроцедуры

//+++АК LATV 2018.10.12 ИП-00020001
&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Для Каждого ТекСтрокаМагазины Из Магазины Цикл
		Если ТекСтрокаМагазины.Магазин.Пустая() Тогда
			ИндекСтроки = Магазины.Индекс(ТекСтрокаМагазины);
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не заполнено поле ""Магазин"" в строке %1'"), ИндекСтроки);
			ПутьКДанным = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Магазины[%1].Магазин", ИндекСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

//+++АК LATV 2018.10.15 ИП-00020001
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	Если Дата < ДатаСледующегоРаспределения Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Магазины к пересчету на %1 уже назначены. Слудеющее назначение возможно начиная с %2'")
			, Формат(Дата, "ДФ=dd.MM.yyyy"), Формат(ДатаСледующегоРаспределения, "ДФ=dd.MM.yyyy"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "Дата");
		
		Дата = ДатаСледующегоРаспределения;
	КонецЕсли;

КонецПроцедуры

//+++АК LATV 2018.10.12 ИП-00020001
&НаКлиенте
Процедура СкладПриИзменении(Элемент)

	УстановитьОтборСклад();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Магазины

//+++АК LATV 2018.10.12 ИП-00020001
&НаКлиенте
Процедура МагазиныОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Для Каждого Ссылка Из ВыбранноеЗначение Цикл
		НоваяСтрока = Магазины.Добавить();
		НоваяСтрока.Магазин = Ссылка;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

//+++АК LATV 2018.10.12 ИП-00020001
&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	Записать = Истина;
	Закрыть();

КонецПроцедуры

//+++АК LATV 2018.10.12 ИП-00020001
&НаКлиенте
Процедура Записать(Команда)

	ЗаписатьНаСервере();

КонецПроцедуры

//+++АК LATV 2018.10.12 ИП-00020001
&НаКлиенте
Процедура Подбор(Команда)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",			Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",	Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",	Ложь);
	
	Отбор = Новый Структура;
	
	Если Не Склад.Пустая() Тогда
		Отбор.Вставить("ОсновнойСклад", Склад);
	КонецЕсли;
	
	Отбор.Вставить("ПриемкаТовараБезПересчета", Истина);
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Справочник.СтруктурныеЕдиницы.ФормаВыбора", ПараметрыФормы, Элементы.Магазины, УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.10.12 ИП-00020001
&НаКлиенте
Процедура ПередЗакрытиемПодтверждение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать = Истина;
		Закрыть();
		
	КонецЕсли;

КонецПроцедуры

//+++АК LATV 2018.10.12 ИП-00020001
&НаСервере
Процедура ЗаписатьНаСервере(Отказ = Ложь)

	Если Обработки.ЗагрузитьРаспределение1.МагазиныКПересчетуНазначены(Дата) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Магазины к пересчету на %1 уже назначены. Добавление магазинов к пересчету невозможно'"), Формат(Дата, "ДФ=dd.MM.yyyy"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "Дата", Отказ);
		
		ДатаСледующегоРаспределения = Дата + 24*60*60;
		Возврат;
	КонецЕсли;
	
	ГруппировкаМагазины = Магазины.Выгрузить(, "Магазин");
	ГруппировкаМагазины.Свернуть("Магазин");
	
	НаборЗаписей = РегистрыСведений.МагазиныКПересчетуНаДату.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Дата.Установить(Дата);
	
	Для Каждого ТекСтрокаМагазин Из ГруппировкаМагазины Цикл
		НаборЗаписей.Очистить();
		
		НаборЗаписей.Отбор.Магазин.Установить(ТекСтрокаМагазин.Магазин);
		
		НоваяСтрока = НаборЗаписей.Добавить();
		НоваяСтрока.Дата	= Дата;
		НоваяСтрока.Магазин	= ТекСтрокаМагазин.Магазин;
		
		НоваяСтрока.ДобавленСкладом	= Истина;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось добавить магазин ""%1"" на пересчет по причине: %2'")
				, ТекСтрокаМагазин.Магазин, ОписаниеОшибки());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			Возврат;
		КонецПопытки;
		
	КонецЦикла;
	
	Модифицированность	= Ложь;
	Записать			= Ложь;

КонецПроцедуры

//+++АК LATV 2018.10.15 ИП-00020001
&НаСервере
Процедура УстановитьОтборСклад()

	СвязиПараметровВыбора = Новый Массив();
	
	Если Не Склад.Пустая() Тогда
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.ОсновнойСклад", "Склад"
			, РежимИзмененияСвязанногоЗначения.НеИзменять);
		СвязиПараметровВыбора.Добавить(НоваяСвязь);
	КонецЕсли;
	
	Элементы.МагазиныМагазин.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);

КонецПроцедуры

#КонецОбласти