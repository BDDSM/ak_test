
Функция ПолучитьЦенуПоставщика(Номенклатура, Характеристика, Период, Поставщик = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЦеныПоставщиковСрезПоследних.Поставщик,
		|	ЦеныПоставщиковСрезПоследних.Цена
		|ИЗ
		|	РегистрСведений.ЦеныПоставщиков.СрезПоследних(
		|			&Период,
		|			Номенклатура = &Номенклатура
		|				И ВЫБОР
		|					КОГДА &Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					КОГДА &Поставщик = НЕОПРЕДЕЛЕНО
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Поставщик = &Поставщик
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА ВЫРАЗИТЬ(&Номенклатура КАК Справочник.Номенклатура).НеВедетсяУчетПоХарактеристикам
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ Характеристика = &Характеристика
		|				КОНЕЦ) КАК ЦеныПоставщиковСрезПоследних");
	Запрос.УстановитьПараметр("Период"		, Период);
	Запрос.УстановитьПараметр("Поставщик"	, Поставщик);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда Возврат 0; КонецЕсли;
	
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаЗапроса.Следующий();
	
	Если НЕ ЗначениеЗаполнено(Поставщик) Тогда Поставщик = ВыборкаЗапроса.Поставщик; КонецЕсли;
	
	Возврат ВыборкаЗапроса.Цена;
	
КонецФункции

// Процедура анализирует повышение закупочной цены в разрезе Товара Характеристики и Поставщика
// (Актуально только при ручном редактировании записей регистра сведений)
// Параметры:
//  ТаблицаЗаписей  - <Тип.ТаблицаЗначений> - Таблица введенных/измененных пользователем записей
//
Процедура ПроверкаНеобходимостиОтправикиПисьмаОПовышенииЦены(ТаблицаЗаписей) Экспорт//+++АК mika 2018.02.08 ИП-00017505
	
	Если ТаблицаЗаписей.Количество() > 0 Тогда
		
		Запрос = Новый Запрос("Выбрать * ПОМЕСТИТЬ ВТ Из &ВременнаяТаблица Как ВременнаяТаблица");
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ВременнаяТаблица", ТаблицаЗаписей);
		Запрос.Выполнить();
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ.Период,
		|	ВТ.Номенклатура,
		|	ВТ.Характеристика,
		|	ВТ.Цена,
		|	ВТ.Поставщик,
		|	ВТ.Документ
		|ИЗ
		|	ВТ КАК ВТ
		|ГДЕ
		|ВТ.Документ = ЗНАЧЕНИЕ(Документ.ИзменениеЗакупочныхЦен.ПустаяСсылка)";
		
		Запрос.Выполнить();
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВТ.Период,
		|	ВТ.Номенклатура,
		|	ВТ.Характеристика,
		|	ЗначенияСвойствОбъектов.Значение КАК Производитель,
		|	ВТ.Поставщик,
		|	ВТ.Цена
		|ПОМЕСТИТЬ ВТ_ТаблицаТекущихЦен
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|		ПО ВТ.Характеристика = ЗначенияСвойствОбъектов.Объект
		|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))
		|ГДЕ
		|	ВТ.Документ = ЗНАЧЕНИЕ(Документ.ИзменениеЗакупочныхЦен.ПустаяСсылка)
		|	И НЕ ВТ.Номенклатура В
		|				(ВЫБРАТЬ
		|					Номенклатура.Ссылка
		|				ИЗ
		|					Справочник.Номенклатура КАК Номенклатура
		|				ГДЕ
		|					НЕ Номенклатура.ЭтоГруппа
		|					И (Номенклатура.Родитель.Наименование ПОДОБНО ""Овощи. Грибы""
		|						ИЛИ Номенклатура.Родитель.Наименование ПОДОБНО ""Фрукты. Ягоды""))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таб_ПериодыХарактеристик.Период,
		|	Таб_ПериодыХарактеристик.Номенклатура,
		|	Таб_ПериодыХарактеристик.Характеристика,
		|	ЗначенияСвойствОбъектов.Значение КАК Производитель,
		|	ЦеныПоставщиков.Поставщик,
		|	ЦеныПоставщиков.Цена
		|ПОМЕСТИТЬ ВТ_ПоследняяЦенаХарактеристика
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ЦеныПоставщиков.Период) КАК Период,
		|		ЦеныПоставщиков.Номенклатура КАК Номенклатура,
		|		ЦеныПоставщиков.Характеристика КАК Характеристика
		|	ИЗ
		|		ВТ_ТаблицаТекущихЦен КАК ВТ_ТаблицаТекущихЦен
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныПоставщиков КАК ЦеныПоставщиков
		|			ПО ВТ_ТаблицаТекущихЦен.Номенклатура = ЦеныПоставщиков.Номенклатура
		|				И ВТ_ТаблицаТекущихЦен.Характеристика = ЦеныПоставщиков.Характеристика
		|				И ВТ_ТаблицаТекущихЦен.Период > ЦеныПоставщиков.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЦеныПоставщиков.Номенклатура,
		|		ЦеныПоставщиков.Характеристика) КАК Таб_ПериодыХарактеристик
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныПоставщиков КАК ЦеныПоставщиков
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ЦеныПоставщиков.Характеристика = ЗначенияСвойствОбъектов.Объект
		|				И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))
		|		ПО Таб_ПериодыХарактеристик.Период = ЦеныПоставщиков.Период
		|			И Таб_ПериодыХарактеристик.Номенклатура = ЦеныПоставщиков.Номенклатура
		|			И Таб_ПериодыХарактеристик.Характеристика = ЦеныПоставщиков.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таб_ПериодыХарактеристик.Период,
		|	Таб_ПериодыХарактеристик.Номенклатура,
		|	Таб_ПериодыХарактеристик.Характеристика,
		|	ЗначенияСвойствОбъектов.Значение КАК Производитель,
		|	ЦеныПоставщиков.Поставщик,
		|	ЦеныПоставщиков.Цена
		|ПОМЕСТИТЬ ВТ_ПоследняяЦенаМинимальнаяЦены
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ЦеныПоставщиков.Период) КАК Период,
		|		ЦеныПоставщиков.Номенклатура КАК Номенклатура,
		|		ЦеныПоставщиков.Характеристика КАК Характеристика
		|	ИЗ
		|		ВТ_ТаблицаТекущихЦен КАК ВТ_ТаблицаТекущихЦен
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныПоставщиков КАК ЦеныПоставщиков
		|			ПО ВТ_ТаблицаТекущихЦен.Номенклатура = ЦеныПоставщиков.Номенклатура
		|				И ВТ_ТаблицаТекущихЦен.Период > ЦеныПоставщиков.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЦеныПоставщиков.Номенклатура,
		|		ЦеныПоставщиков.Характеристика) КАК Таб_ПериодыХарактеристик
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныПоставщиков КАК ЦеныПоставщиков
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ЦеныПоставщиков.Характеристика = ЗначенияСвойствОбъектов.Объект
		|				И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))
		|		ПО Таб_ПериодыХарактеристик.Период = ЦеныПоставщиков.Период
		|			И Таб_ПериодыХарактеристик.Номенклатура = ЦеныПоставщиков.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таб_ПериодыХарактеристик.Период,
		|	Таб_ПериодыХарактеристик.Номенклатура,
		|	Таб_ПериодыХарактеристик.Характеристика,
		|	ЗначенияСвойствОбъектов.Значение КАК Производитель,
		|	ЦеныПоставщиков.Поставщик,
		|	ЦеныПоставщиков.Цена
		|ПОМЕСТИТЬ ВТ_ПоследняяЦенаМинимальная
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ЦеныПоставщиков.Период) КАК Период,
		|		ЦеныПоставщиков.Номенклатура КАК Номенклатура,
		|		ЦеныПоставщиков.Характеристика КАК Характеристика
		|	ИЗ
		|		ВТ_ТаблицаТекущихЦен КАК ВТ_ТаблицаТекущихЦен
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныПоставщиков КАК ЦеныПоставщиков
		|			ПО ВТ_ТаблицаТекущихЦен.Номенклатура = ЦеныПоставщиков.Номенклатура
		|				И ВТ_ТаблицаТекущихЦен.Период > ЦеныПоставщиков.Период
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ЦеныПоставщиков.Номенклатура,
		|		ЦеныПоставщиков.Характеристика) КАК Таб_ПериодыХарактеристик
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныПоставщиков КАК ЦеныПоставщиков
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ЦеныПоставщиков.Характеристика = ЗначенияСвойствОбъектов.Объект
		|				И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))
		|		ПО Таб_ПериодыХарактеристик.Период = ЦеныПоставщиков.Период
		|			И Таб_ПериодыХарактеристик.Номенклатура = ЦеныПоставщиков.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаТекущихЦен.Номенклатура КАК Номенклатура,
		|	ВТ_ТаблицаТекущихЦен.Характеристика КАК Характеристика,
		|	ВТ_ТаблицаТекущихЦен.Производитель,
		|	ВТ_ПоследняяЦенаХарактеристика.Поставщик КАК ПредыдущийПоставщик,
		|	ВТ_ПоследняяЦенаХарактеристика.Период КАК Дата,
		|	ВТ_ПоследняяЦенаХарактеристика.Цена КАК Цена,
		|	ВТ_ТаблицаТекущихЦен.Поставщик КАК ТекущийПоставщик,
		|	ВТ_ТаблицаТекущихЦен.Период КАК НачалаДата,
		|	ВТ_ТаблицаТекущихЦен.Цена КАК НоваяЦена,
		|	1 КАК Порядок,
		|	ВТ_ПоследняяЦенаХарактеристика.Поставщик <> ВТ_ТаблицаТекущихЦен.Поставщик КАК ИзмененПоставщик
		|ИЗ
		|	ВТ_ТаблицаТекущихЦен КАК ВТ_ТаблицаТекущихЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоследняяЦенаХарактеристика КАК ВТ_ПоследняяЦенаХарактеристика
		|		ПО ВТ_ТаблицаТекущихЦен.Номенклатура = ВТ_ПоследняяЦенаХарактеристика.Номенклатура
		|			И ВТ_ТаблицаТекущихЦен.Характеристика = ВТ_ПоследняяЦенаХарактеристика.Характеристика
		|			И ВТ_ТаблицаТекущихЦен.Цена > ВТ_ПоследняяЦенаХарактеристика.Цена
		|ГДЕ
		|	НЕ ВТ_ПоследняяЦенаХарактеристика.Период ЕСТЬ NULL
		|	И ВТ_ПоследняяЦенаХарактеристика.Поставщик <> ВТ_ТаблицаТекущихЦен.Поставщик
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таб_МинимальнаяЦена.Номенклатура,
		|	Таб_МинимальнаяЦена.Характеристика,
		|	Таб_МинимальнаяЦена.Производитель,
		|	Таб_МинимальнаяЦена.Поставщик,
		|	Таб_МинимальнаяЦена.Период,
		|	Таб_МинимальнаяЦена.Цена,
		|	Таб_МинимальнаяЦена.ПоставщикМин,
		|	Таб_МинимальнаяЦена.ПериодМин,
		|	Таб_МинимальнаяЦена.ЦенаМин,
		|	Таб_МинимальнаяЦена.Порядок,
		|	Таб_МинимальнаяЦена.Поставщик <> Таб_МинимальнаяЦена.ПоставщикМин
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ТаблицаТекущихЦен.Номенклатура КАК Номенклатура,
		|		ВТ_ТаблицаТекущихЦен.Характеристика КАК Характеристика,
		|		ВТ_ТаблицаТекущихЦен.Производитель КАК Производитель,
		|		ВТ_ПоследняяЦенаМинимальная.Поставщик КАК Поставщик,
		|		ВТ_ПоследняяЦенаМинимальная.Период КАК Период,
		|		ВТ_ПоследняяЦенаМинимальная.Цена КАК Цена,
		|		ВТ_ТаблицаТекущихЦен.Поставщик КАК ПоставщикМин,
		|		ВТ_ТаблицаТекущихЦен.Период КАК ПериодМин,
		|		ВТ_ТаблицаТекущихЦен.Цена КАК ЦенаМин,
		|		2 КАК Порядок
		|	ИЗ
		|		ВТ_ТаблицаТекущихЦен КАК ВТ_ТаблицаТекущихЦен
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоследняяЦенаМинимальная КАК ВТ_ПоследняяЦенаМинимальная
		|			ПО ВТ_ТаблицаТекущихЦен.Номенклатура = ВТ_ПоследняяЦенаМинимальная.Номенклатура
		|	ГДЕ
		|		НЕ ВТ_ПоследняяЦенаМинимальная.Период ЕСТЬ NULL) КАК Таб_МинимальнаяЦена
		|ГДЕ
		|	Таб_МинимальнаяЦена.Цена < Таб_МинимальнаяЦена.ЦенаМин
		|	И Таб_МинимальнаяЦена.Поставщик <> Таб_МинимальнаяЦена.ПоставщикМин
		|
		|УПОРЯДОЧИТЬ ПО
		|	НачалаДата,
		|	Номенклатура,
		|	Характеристика,
		|	Порядок,
		|	Дата
		|ИТОГИ ПО
		|	Номенклатура,
		|	Характеристика
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Ответственный = ТаблицаЗаписей[0].Автор;
			
			ТаблицаНовыхЦен = Новый ТаблицаЗначений;
			
			ТаблицаНовыхЦен.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
			ТаблицаНовыхЦен.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
			ТаблицаНовыхЦен.Колонки.Добавить("ТекущийПоставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
			ТаблицаНовыхЦен.Колонки.Добавить("ПредыдущийПоставщик", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
			ТаблицаНовыхЦен.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
			ТаблицаНовыхЦен.Колонки.Добавить("НачалаДата", Новый ОписаниеТипов("Дата"));
			ТаблицаНовыхЦен.Колонки.Добавить("Цена"		, Новый ОписаниеТипов("Число"	, Новый КвалификаторыЧисла(10, 2)));
			ТаблицаНовыхЦен.Колонки.Добавить("НоваяЦена"	, Новый ОписаниеТипов("Число"	, Новый КвалификаторыЧисла(10, 2)));
			ТаблицаНовыхЦен.Колонки.Добавить("Состояние", Новый ОписаниеТипов("Строка"));
			ТаблицаНовыхЦен.Колонки.Добавить("Ответственный", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));

			ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ВыборкаХарактеристика = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаХарактеристика.Следующий() Цикл
					
					ВыборкаДетальныеЗаписи = ВыборкаХарактеристика.Выбрать();
					
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						Если ВыборкаДетальныеЗаписи.Порядок = 1 Тогда
							НоваяСтрока = ТаблицаНовыхЦен.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
							НоваяСтрока.Состояние = "Изменен поставщик характеристики";
							НоваяСтрока.Ответственный = Ответственный;
							Прервать;
						ИначеЕсли ВыборкаДетальныеЗаписи.Порядок = 2 Тогда
							НоваяСтрока = ТаблицаНовыхЦен.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
							НоваяСтрока.Состояние = "Новая характеристика, где цена ""выше минимальной"" по всем поставщикам";
							НоваяСтрока.Ответственный = Ответственный;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
				
			КонецЦикла;
			
			Если ТаблицаНовыхЦен.Количество() > 0 Тогда
				
				Документы.ИзменениеЗакупочныхЦен.ОтправитьУведомленияОРучномПовышенииЦен(ТаблицаНовыхЦен); 

				Для каждого строка Из ТаблицаНовыхЦен Цикл
					
					ТекстСообщения = "Ручной ввод данных. Увеличение закупочной цены (&Состояние):
					|Номенклатура: &Номенклатура, Характеристика: &Характеристика, 
					|Предыщущий поставщик: &ПредыдущийПоставщик, Дата: &Дата, Цена: &Цена
					|Текущий поставщик: &ТекущийПоставщик, Дата начала: &НачалаДата, Цена новая: &НоваяЦена
					|Ответственный: &Ответственный
					|";
					
					Для каждого Колонка Из ТаблицаНовыхЦен.Колонки Цикл
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "&" + Колонка.Имя, строка [Колонка.Имя]);
					КонецЦикла;
					
					Сообщить(ТекстСообщения);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПроверкаНеобходимостьОтправикиПисьмаОПовышенииЦены()
