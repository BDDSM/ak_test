
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоРедактировать = УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РедактированиеРегистраЦеныПоставщиков, Ложь)
								ИЛИ УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.ПолныеПраваПоРекламнымМатериалам, Ложь);
	ЭтаФорма.ТолькоПросмотр = НЕ ЕстьПравоРедактировать;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	РегистрСведенийСписок.Порядок.Установить("Номенклатура");
	
КонецПроцедуры


Процедура ДействияФормыОбновитьЦены(Кнопка)
	
	Предупреждение("Синхронизация цен с акцесом отключена!", 60);
	Возврат;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Обработки.ЗагрузкаДанныхИзAccess.ЗагрузитьДанныеОНачальныхЦенахЗакупки(НачалоМесяца(ДобавитьМесяц(ТекущаяДата(), -1)));
	ДатаОбработки = НачалоМесяца(ДобавитьМесяц(ТекущаяДата(), -1)) + 86400;
	Пока ДатаОбработки <= ТекущаяДата() Цикл
		Обработки.ЗагрузкаДанныхИзAccess.ЗагрузитьДанныеОЦенахЗакупки(ДатаОбработки);
		ДатаОбработки = ДатаОбработки + 86400;
	КонецЦикла;	
	
КонецПроцедуры


Процедура РегистрСведенийСписокПередУдалением(Элемент, Отказ)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.ПолныеПраваПоРекламнымМатериалам, Ложь)
			И НЕ ТекДанные.Номенклатура.РекламныйМатериал Тогда
		Сообщить("Нет прав на удаление цен поставщиков не рекламных материалов!");
		Отказ = Истина;
	КонецЕсли;
	
	//+++АК mika 2018.02.21 ИП-00017505
	Если ЗначениеЗаполнено(ТекДанные.Документ) Тогда
		Сообщить("Изменить текущую запись можно только через документ: " + ТекДанные.Документ);
		Отказ = Истина;
	КонецЕсли;
	//---АК mika

КонецПроцедуры

Процедура РегистрСведенийСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	МассивХарактеристик = Новый Массив;
	МассивТовары 		= Новый Массив;
	МассивПоставщики 	= Новый Массив;
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ТекДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		МассивХарактеристик.Добавить(ТекДанныеСтроки.Характеристика);
		МассивТовары.Добавить(ТекДанныеСтроки.Номенклатура);
		МассивПоставщики.Добавить(ТекДанныеСтроки.Поставщик);
	КонецЦикла;
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивХарактеристик"	, МассивХарактеристик);
	Запрос.УстановитьПараметр("Товары"				, МассивТовары);
	Запрос.УстановитьПараметр("Поставщики"			, МассивПоставщики);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПроизводителиТоваров.Объект КАК Характеристика,
	|	ВЫРАЗИТЬ(ПроизводителиТоваров.Значение КАК Справочник.Контрагенты).ГоловнойКонтрагент КАК Холдинг
	|ИЗ
	|	РегистрСведений.ЗначенияСвойствОбъектов КАК ПроизводителиТоваров
	|ГДЕ
	|	ПроизводителиТоваров.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель)
	|	И ПроизводителиТоваров.Объект В(&МассивХарактеристик)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	Номенклатура.НеВедетсяУчетПоХарактеристикам,
	|	Номенклатура.СтавкаНДС
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&Товары)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	Контрагенты.СтавкаНДС
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка В(&Поставщики)
	|;
	|
	//+++АК SHEP 2018.05.29 ИП-00018047
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПереходНаСвоюУпаковку.Номенклатура,
	|	ПереходНаСвоюУпаковку.Характеристика
	|ИЗ
	|	Документ.ПереходНаСвоюУпаковку КАК ПереходНаСвоюУпаковку
	|ГДЕ
	|	ПереходНаСвоюУпаковку.Проведен
	|	И НЕ ПереходНаСвоюУпаковку.Отработан
	|	И ПереходНаСвоюУпаковку.Номенклатура В(&Товары)
	|	И ПереходНаСвоюУпаковку.Характеристика В(&МассивХарактеристик)";
	//---АК SHEP 2018.05.30
	Результаты = Запрос.ВыполнитьПакет();
	ТабКешХарактеристика 	= Результаты[0].Выгрузить();
	ТабКешТовары 			= Результаты[1].Выгрузить();
	ТабКешПоставщики 		= Результаты[2].Выгрузить();
	ТабКешПереходНаСвоюУпаковку = Результаты[3].Выгрузить(); //+++АК SHEP 2018.05.29 ИП-00018047
	
	//
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ТекДанныеСтроки = ОформлениеСтроки.ДанныеСтроки;
		
		ТекХарактеристика = ТекДанныеСтроки.Характеристика;
		СтрокаХолдинга = ТабКешХарактеристика.Найти(ТекХарактеристика, "Характеристика");
		Если НЕ СтрокаХолдинга = Неопределено Тогда
			ТекстХолдинга = СокрЛП(СтрокаХолдинга.Холдинг.Наименование);
		Иначе
			ТекстХолдинга = "";
		КонецЕсли;
		ОформлениеСтроки.Ячейки.Холдинг.УстановитьТекст(ТекстХолдинга);
		
		Если ТекХарактеристика.НеАктивная Тогда
			ОформлениеСтроки.ЦветТекста = ЦветаСтиля.СтатусОтложен;
		КонецЕсли;
		
		СтрокаКешТовар = ТабКешТовары.Найти(ТекДанныеСтроки.Номенклатура, "Ссылка");
		Если СтрокаКешТовар <> Неопределено
				И СтрокаКешТовар.НеВедетсяУчетПоХарактеристикам Тогда
			ОформлениеСтроки.Ячейки.Характеристика.УстановитьТекст("<не ведется учет по характеристикам>");
			ОформлениеСтроки.Ячейки.Холдинг.УстановитьТекст("");
		КонецЕсли;
		
		СтрокаКешПоставщик = ТабКешПоставщики.Найти(ТекДанныеСтроки.Поставщик, "Ссылка");
		Если СтрокаКешПоставщик <> Неопределено Тогда
			Если СтрокаКешПоставщик.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС
					ИЛИ СтрокаКешПоставщик.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
				Если СтрокаКешТовар <> Неопределено Тогда
					ОформлениеСтроки.Ячейки.ЦенаСНДС.УстановитьТекст(Формат(ТекДанныеСтроки.Цена + ТекДанныеСтроки.Цена * УчетНДС.ПолучитьСтавкуНДС(СтрокаКешТовар.СтавкаНДС) / 100, "ЧДЦ=2"));
				КонецЕсли;	
			Иначе
				ОформлениеСтроки.Ячейки.ЦенаСНДС.УстановитьТекст(Формат(ТекДанныеСтроки.Цена, "ЧДЦ=2"));
			КонецЕсли;	
		Иначе
			ОформлениеСтроки.Ячейки.ЦенаСНДС.УстановитьТекст(ТекДанныеСтроки.Цена);
		КонецЕсли;
		
		//+++АК mika 2018.02.21 ИП-00017932
		Если ЗначениеЗаполнено(ОформлениеСтроки.ДанныеСтроки.Документ) Тогда
			ОформлениеСтроки.ЦветТекста = WebЦвета.СинийСоСтальнымОттенком; 
		КонецЕсли;
		//---АК mika
		
		//+++АК SHEP 2018.05.29 ИП-00018047
		Если ТабКешПереходНаСвоюУпаковку.НайтиСтроки(Новый Структура("Номенклатура,Характеристика", ТекДанныеСтроки.Номенклатура, ТекДанныеСтроки.Характеристика)).Количество() > 0 Тогда
			ОформлениеСтроки.ЦветФона = WebЦвета.Красный;
		КонецЕсли;
		//---АК SHEP 2018.05.29

	КонецЦикла;
	
КонецПроцедуры

Процедура РегистрСведенийСписокНоменклатураПриИзменении(Элемент)
	
	ТекДанные = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные;
	Если ТекДанные.Номенклатура.НеВедетсяУчетПоХарактеристикам
			ИЛИ НЕ ТекДанные.Характеристика.Владелец = ТекДанные.Номенклатура Тогда
		ТекДанные.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрСведенийСписокХарактеристикаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекДанные = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные;
	Если ТекДанные.Номенклатура.Пустая() Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	Если ТекДанные.Номенклатура.НеВедетсяУчетПоХарактеристикам Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрСведенийСписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка) //+++АК mika 2018.02.21 ИП-00017932																							   
	
	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Документ) Тогда                         
		СтандартнаяОбработка = Ложь;
		ОткрытьЗначение(Элемент.ТекущиеДанные.Документ);
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрСведенийСписокПередНачаломИзменения(Элемент, Отказ) //+++АК mika 2018.02.21 ИП-00017505  

	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Документ) Тогда
		Сообщить("Изменить текущую запись можно только через документ: " + Элемент.ТекущиеДанные.Документ);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрСведенийСписокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование) //+++АК mika 2017.11.21 ИП-00016603.01
	
	Если Копирование Тогда
		Элемент.ТекущиеДанные.Документ = Документы.ИзменениеЗакупочныхЦен.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры


Процедура РегистрСведенийСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	//+++АК BARA ИП-00018047  2018.03.18    
	ЗаписиЗапрет = РегистрыСведений.ЗапретИзмененияЦены.СрезПоследних(ДанныеСтроки.Период, Новый Структура("Номенклатура, Характеристика", ДанныеСтроки.Номенклатура, ДанныеСтроки.Характеристика));
	Если НЕ ЗаписиЗапрет.Количество() = 0 Тогда
		ОформлениеСтроки.ЦветФона = Новый Цвет(200,0,0);	
	КонецЕсли;
	//---АК BARA ИП-00018047  2018.03.18   
КонецПроцедуры

