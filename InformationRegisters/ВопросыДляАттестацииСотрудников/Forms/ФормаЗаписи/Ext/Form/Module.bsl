


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьЭлементовСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПроверитьКорректностьВводаПравильногоОтвета(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПравильныйОтветПриИзменении(Элемент)
	
	ПроверитьКорректностьВводаПравильногоОтвета();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидАттестацииПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементовСервер();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДобавитьКартинку(Команда)
	
	Если НЕ ЗначениеЗаполнено(Запись.Организация) ИЛИ НЕ ЗначениеЗаполнено(Запись.ВидАттестации) ИЛИ НЕ ЗначениеЗаполнено(Запись.НомерВопроса) Тогда
		ПоказатьПредупреждение(,"Перед добавлением картинки заполните обязательные поля!");
		Возврат;
	КонецЕсли;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Изображение'") + "(*.jpg, *.jpeg, *.png, *.gif)|*.jpg;*.jpeg;*.png;*.gif";
	
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	
	ДиалогОткрытияФайла.Заголовок = "Выберите файл";
	
	НачатьПомещениеФайлов(Новый ОписаниеОповещения("ОбработатьВыборФайлаЗакрытие", ЭтаФорма), , ДиалогОткрытияФайла, Истина, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайлаЗакрытие(Результат, ДополнительныеПараметры)Экспорт
	
	Если Результат <> Неопределено Тогда
		Для Каждого ИмяФайла Из Результат Цикл
			ОбработатьВыбранныйФайлКлиент(ИмяФайла);
		КонецЦикла;
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Файл не выбран!'; en = 'File not selected!'"))
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьКартинку(Команда)
	
	Если ЗначениеЗаполнено(Запись.ВопросКартинка) Тогда
		Оповещение = Новый ОписаниеОповещения("УдалитьКартинкуВопросЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Текущая картинка будет удалена! Продолжить?'", "ru"), РежимДиалогаВопрос.ДаНет, 15 );
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УдалитьКартинкуВопросЗавершение(Результат, ДополнительныеПараметры)Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьКартинкуСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьКорректностьВводаПравильногоОтвета(Отказ = Ложь)
	
	Если ЗначениеЗаполнено(Запись.ПравильныйОтвет) И НЕ ЗначениеЗаполнено(Запись["Ответ"+Запись.ПравильныйОтвет]) Тогда
		ПоказатьПредупреждение(, "Заполните ""текст ответа"" перед указанием ""правильного ответа""!");
		Запись.ПравильныйОтвет = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовСервер()
	
	//Если ЗначениеЗаполнено(Запись.ВидАттестации) Тогда
	ИспользоватьКартинкиВопросов = Запись.ВидАттестации.ИспользоватьКартинкиВопросов;
	
	Элементы.КомандаДобавитьКартинку.Видимость = ИспользоватьКартинкиВопросов;
	Элементы.КартинкаВопроса.Видимость = ИспользоватьКартинкиВопросов;
	Элементы.ДекорацияКартинкаРамка.Видимость = НЕ ИспользоватьКартинкиВопросов;
	//КонецЕсли;
	
	Если ИспользоватьКартинкиВопросов Тогда
		КартинкаВопроса = ПоместитьВоВременноеХранилище(Запись.ВопросКартинка.Файл.Получить());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбранныйФайлКлиент(Адрес)
	
	//Определение имени файла
	ИмяФайла = Адрес.Имя;
	
	ЭтоКаталог = Найти(ИмяФайла,"\");
	Пока ЭтоКаталог > 0 Цикл
		ИмяФайла = Сред(ИмяФайла, ЭтоКаталог+1, СтрДлина(ИмяФайла));
		ЭтоКаталог = Найти(ИмяФайла,"\");
	КонецЦикла;
	
	//Заполнение реквизитов объекта на сервере
	ОбработатьВыбранныйФайлСервер(ИмяФайла, Адрес.Хранение);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыбранныйФайлСервер(ИмяФайла, Хранение)
	
	НаименованиеФайла = Строка(Запись.Организация) + "_" + Запись.ВидАттестации + "_" + Запись.НомерВопроса;
	
	НовыйФайлКартинки = Справочники.ФайлыАттестация.НайтиПоНаименованию(НаименованиеФайла);
	Если ЗначениеЗаполнено(НовыйФайлКартинки) Тогда
		НовыйФайлКартинки = НовыйФайлКартинки.ПолучитьОбъект();
	Иначе
		НовыйФайлКартинки = Справочники.ФайлыАттестация.СоздатьЭлемент();
	КонецЕсли;
	
	НовыйФайлКартинки.Файл = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Хранение));
	
	НовыйФайлКартинки.Наименование = НаименованиеФайла; 
	
	КартинкаВопроса = ПоместитьВоВременноеХранилище(НовыйФайлКартинки.Файл.Получить());
	НовыйФайлКартинки.Записать();
	
	Запись.ВопросКартинка = НовыйФайлКартинки.Ссылка;
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьКартинкуСервер()
	
	НачатьТранзакцию();
	
	СправочникОбъект = Запись.ВопросКартинка.ПолучитьОбъект();
	СправочникОбъект.ОбменДанными.Загрузка = Истина;
	
	СправочникОбъект.Удалить();
	
	Запись.ВопросКартинка = Справочники.ФайлыАттестация.ПустаяСсылка();
	Записать();
	
	КартинкаВопроса = "";
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти




