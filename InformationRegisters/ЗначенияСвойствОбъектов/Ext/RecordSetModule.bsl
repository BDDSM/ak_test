
Процедура ПередЗаписью(Отказ, Замещение)
	
	Производители = Новый Массив;
	
	Для Каждого Запись ИЗ ЭтотОбъект Цикл
		Если Запись.Свойство = ПланыВидовХарактеристик.СвойстваОбъектов.Производитель И ТипЗнч(Запись.Объект) = Тип("СправочникСсылка.ХарактеристикиНоменклатуры") И НЕ Запись.Объект.Неактивная Тогда
			Производители.Добавить(Запись.Значение);
		КонецЕсли; 
	КонецЦикла;  
	
	НовыеПроизводители = Новый Массив;	

	Если Производители.Количество()>0 Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
		                      |	ЗначенияСвойствОбъектов.Значение
		                      |ИЗ
		                      |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		                      |ГДЕ
		                      |	ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель)
		                      |	И ЗначенияСвойствОбъектов.Значение В(&Производители)
		                      |	И ЗначенияСвойствОбъектов.Объект.Неактивная = ЛОЖЬ");
		
		Запрос.УстановитьПараметр("Производители", Производители);
		
		СуществующиеПроизводители = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Значение");
		
		Для Каждого Производитель ИЗ Производители Цикл
			Если СуществующиеПроизводители.Найти(Производитель) = Неопределено Тогда
				НовыеПроизводители.Добавить(Производитель);
			КонецЕсли; 
		КонецЦикла;  
	КонецЕсли; 
	
	ДополнительныеСвойства.Вставить("НовыеПроизводители", НовыеПроизводители);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ДополнительныеСвойства.Свойство("НовыеПроизводители") И ДополнительныеСвойства.НовыеПроизводители.Количество()>0 Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      	|	МобильноеПриложение.Ссылка
		                      	|ИЗ
		                      	|	ПланОбмена.МобильноеПриложение КАК МобильноеПриложение
		                      	|ГДЕ
		                      	|	МобильноеПриложение.Магазин.Ссылка ЕСТЬ NULL
								|	И МобильноеПриложение.Профиль <> ЗНАЧЕНИЕ(Справочник.МП_ПрофилиИспользования.Строитель)");
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.Ссылка <> ПланыОбмена.МобильноеПриложение.ЭтотУзел() Тогда
				Для Каждого Производитель ИЗ ДополнительныеСвойства.НовыеПроизводители Цикл
					ПланыОбмена.ЗарегистрироватьИзменения(Выборка.Ссылка, Производитель);
				КонецЦикла;  
			КонецЕсли;
		КонецЦикла;  
	КонецЕсли;
	
	//+++АК MIND 2018.02.13 если поменяют привязку не через форму, то надо отработать, обращение к реквизитам оставляю в цикле так как здесь всегда будет по идее один проход
	Для Каждого Запись ИЗ ЭтотОбъект Цикл
		Если Запись.Свойство = ПланыВидовХарактеристик.СвойстваОбъектов.Производитель И ТипЗнч(Запись.Объект) = Тип("СправочникСсылка.ХарактеристикиНоменклатуры")
			И ТипЗнч(Запись.Значение) = Тип("СправочникСсылка.Контрагенты")
			И Запись.Объект.id_kontr <> Запись.Значение.ИД Тогда
			СпрОбъект = Запись.Объект.ПолучитьОбъект();
			СпрОбъект.id_kontr = Запись.Значение.ИД;
			СпрОбъект.ОбменДанными.Загрузка = Истина;
			СпрОбъект.Записать();
		КонецЕсли; 
	КонецЦикла;
	//---АК MIND 
	
КонецПроцедуры

