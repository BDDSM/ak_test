
// Обработчик события "перед открытием" формы
// Заполняет дерево формы наименованиями объектов метаданных
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТипыОбъектовВДеревеЗначений();
	
КонецПроцедуры

// Заполняет дерево значений наименованиями объектов метаданных:
// справочники и документы.
//
Процедура ЗаполнитьТипыОбъектовВДеревеЗначений()
	
	ДеревоОбъектовМетаданных.Строки.Очистить();
	
	Если Метаданные.Справочники.Количество() >0 Тогда
		НовыйЭлементСпр = ДеревоОбъектовМетаданных.Строки.Добавить();
		НовыйЭлементСпр.СинонимНаименованияОбъекта = "Справочники";
		НовыйЭлементСпр.КлассОбъекта = "СправочникиКорень";
	КонецЕсли;
	
	Если Метаданные.Документы.Количество() >0 Тогда
		НовыйЭлементДок = ДеревоОбъектовМетаданных.Строки.Добавить();
		НовыйЭлементДок.СинонимНаименованияОбъекта = "Документы";
		НовыйЭлементДок.КлассОбъекта = "ДокументыКорень";
	КонецЕсли;
	
	Для Каждого Справочник Из Метаданные.Справочники Цикл
		НоваяСтрокаТаблицы = НовыйЭлементСпр.Строки.Добавить();
		НоваяСтрокаТаблицы.ТипОбъекта = Справочник.Имя;
		НоваяСтрокаТаблицы.СинонимНаименованияОбъекта = Справочник.Синоним;
		НоваяСтрокаТаблицы.КлассОбъекта = "Справочники";
		НоваяСтрокаТаблицы.Проводится = Ложь;
		
		Настройка = ВерсионированиеОбъектов.ЗагрузитьНастройкуВерсионированияПоОбъекту(
		                        НоваяСтрокаТаблицы.ТипОбъекта);
		
		НоваяСтрокаТаблицы.ВариантВерсионирования = Настройка;
	КонецЦикла;
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		НоваяСтрокаТаблицы = НовыйЭлементДок.Строки.Добавить();
		НоваяСтрокаТаблицы.ТипОбъекта = Документ.Имя;
		НоваяСтрокаТаблицы.СинонимНаименованияОбъекта = Документ.Синоним;
		НоваяСтрокаТаблицы.КлассОбъекта = "Документы";
		НоваяСтрокаТаблицы.Проводится = ВерсионированиеОбъектов.ДокументПроводится(Документ.Имя);
		
		Настройка = ВерсионированиеОбъектов.ЗагрузитьНастройкуВерсионированияПоОбъекту(
		                        НоваяСтрокаТаблицы.ТипОбъекта);
		
		НоваяСтрокаТаблицы.ВариантВерсионирования = Настройка;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДействияФормыУстановитьРежимНеВерсионировать(Кнопка)
	
	УстановитьРежимВерсионированияНеВерсионировать();
	
КонецПроцедуры

Процедура ДействияФормыУстановитьРежимВерсионироватьВсегда(Кнопка)
	
	УстановитьРежимВерсионированияВсегда();
	
КонецПроцедуры

Процедура ДействияФормыУстановитьРежимВерсионироватьПриПроведении(Кнопка)
	
	ТекстПредупреждения = "";
	
	Если СредиВыделенныхЭлементовЕстьСправочник() Тогда
		ТекстПредупреждения = "Справочникам не может быть установлен режим ""Версионировать при проведении"".";
	КонецЕсли;
	
	Если СредиВыделенныхДокументовЕстьНепроводящиеся() Тогда
		ТекстПредупреждения = ?(ПустаяСтрока(ТекстПредупреждения),
		                        ТекстПредупреждения,
		                        ТекстПредупреждения + Символы.ПС);
		
		ТекстПредупреждения = ТекстПредупреждения 
		              + "Документам, которые не могут быть проведены, будет установлен режим версонирования ""Версионировать всегда"".";
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстПредупреждения) Тогда
		Предупреждение(ТекстПредупреждения, , "Установка режима версионирования");
	КонецЕсли;
	
	УстановитьРежимВерсионированияПриПроведении();
	
КонецПроцедуры

Процедура ДействияФормыУстановитьРежимВерсионироватьПоУмолчанию(Кнопка)
	
	УстановитьНастройкиПоУмолчанию();
	
КонецПроцедуры

Процедура РегистрСведенийСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.СинонимНаименованияОбъекта.ОтображатьКартинку = Истина;
	
	Если      ДанныеСтроки.КлассОбъекта = "СправочникиКорень" Тогда
		ОформлениеСтроки.Ячейки.СинонимНаименованияОбъекта.ИндексКартинки = 0;
	ИначеЕсли ДанныеСтроки.КлассОбъекта = "Справочники" Тогда
		ОформлениеСтроки.Ячейки.СинонимНаименованияОбъекта.ИндексКартинки = 1;
	ИначеЕсли ДанныеСтроки.КлассОбъекта = "ДокументыКорень" Тогда
		ОформлениеСтроки.Ячейки.СинонимНаименованияОбъекта.ИндексКартинки = 2;
	ИначеЕсли ДанныеСтроки.КлассОбъекта = "Документы" Тогда
		ОформлениеСтроки.Ячейки.СинонимНаименованияОбъекта.ИндексКартинки = 3;
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрСведенийСписокВариантВерсионированияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтрокаДерева = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные;
	
	Если СтрокаДерева.КлассОбъекта = "СправочникиКорень"
	 ИЛИ СтрокаДерева.КлассОбъекта = "ДокументыКорень" Тогда
		
		СтандартнаяОбработка = Ложь;
		Возврат;
		
	КонецЕсли;
	
	СписокВыбора = СформироватьСписокВыбора(СтрокаДерева.КлассОбъекта, СтрокаДерева.Проводится);
	
	Элемент.СписокВыбора.Очистить();
	
	Для Каждого ЭлементСписка Из СписокВыбора Цикл
		Элемент.СписокВыбора.Добавить(ЭлементСписка.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Формирует список выбора по классу объекта. Для справочников "Версионировать"
// "Не версионировать"; для документов добавляется "Версионировать при проведении"
//
Функция СформироватьСписокВыбора(КлассОбъекта, знач Проводится = Истина)
	
	СписокВыбора = Новый СписокЗначений;
	
	СписокВыбора.Добавить(Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда);
	
	Если КлассОбъекта = "Документы" И Проводится Тогда
		СписокВыбора.Добавить(Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении);
	КонецЕсли;
	
	СписокВыбора.Добавить(Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать);
	
	Возврат СписокВыбора;
	
КонецФункции

// Устанавливает все настройки версионирования по умолчанию
// и переформирует дерево
//
Процедура УстановитьНастройкиПоУмолчанию()
	
	Для Каждого ИдентификаторСтроки Из ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки Цикл
		Если ИдентификаторСтроки.КлассОбъекта = "Справочники" Тогда
			ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать;
			ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
			                                 ИдентификаторСтроки.ТипОбъекта,
			                                 Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать);
		КонецЕсли;
		Если ИдентификаторСтроки.КлассОбъекта = "Документы" Тогда
			Если ИдентификаторСтроки.Проводится Тогда
				ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении;
				ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
				                                ИдентификаторСтроки.ТипОбъекта,
				                                Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении);
			Иначе
				ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда;
				ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
				                                ИдентификаторСтроки.ТипОбъекта,
				                                Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает все настройки версионирования "не версионировать"
// и переформирует дерево
//
Процедура УстановитьРежимВерсионированияНеВерсионировать()
	
	Для Каждого ИдентификаторСтроки Из ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки Цикл
		Если ИдентификаторСтроки.КлассОбъекта = "Справочники" Тогда
			ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать;
			ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
			                                   ИдентификаторСтроки.ТипОбъекта,
			                                   Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать);
		КонецЕсли;
		Если ИдентификаторСтроки.КлассОбъекта = "Документы" Тогда
			ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать;
			ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
			                                   ИдентификаторСтроки.ТипОбъекта,
			                                   Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает все настройки версионирования "всегда"
// и переформирует дерево
//
Процедура УстановитьРежимВерсионированияВсегда()
	
	Для Каждого ИдентификаторСтроки Из ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки Цикл
		Если ИдентификаторСтроки.КлассОбъекта = "Справочники" Тогда
			ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда;
			ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
			                               ИдентификаторСтроки.ТипОбъекта,
			                               Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда);
		КонецЕсли;
		Если ИдентификаторСтроки.КлассОбъекта = "Документы" Тогда
			ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда;
			ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
			                               ИдентификаторСтроки.ТипОбъекта,
			                               Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает все настройки версионирования "при проведении"
// и переформирует дерево
//
Процедура УстановитьРежимВерсионированияПриПроведении()
	
	Для Каждого ИдентификаторСтроки Из ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки Цикл
		Если ИдентификаторСтроки.КлассОбъекта = "Документы" Тогда
			Если ВерсионированиеОбъектов.ДокументПроводится(ИдентификаторСтроки.ТипОбъекта) Тогда
				ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении;
				ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
				                            ИдентификаторСтроки.ТипОбъекта,
				                            Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении);
			Иначе
				ИдентификаторСтроки.ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда;
				ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(
				                            ИдентификаторСтроки.ТипОбъекта,
				                            Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьВсегда);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// По выделенным строкам определяет какие из них являются именованиями
// справочников и возвращает массив
//
Функция СредиВыделенныхЭлементовЕстьСправочник()
	
	Для Каждого ИдентификаторСтроки Из ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки Цикл
		Если ИдентификаторСтроки.КлассОбъекта = "Справочники" Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// По выделенным строкам определяет какие из них являются именованиями
// документов и возвращает массив
//
Функция СредиВыделенныхДокументовЕстьНепроводящиеся()
	
	Для Каждого ИдентификаторСтроки Из ЭлементыФормы.РегистрСведенийСписок.ВыделенныеСтроки Цикл
		Если ИдентификаторСтроки.КлассОбъекта = "Документы" И Не ИдентификаторСтроки.Проводится Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ДействияФормыДействиеСправка(Кнопка)
	
	ОткрытьСправку(РегистрыСведений.НастройкаВерсионированияОбъектов);
	
КонецПроцедуры

Процедура РегистрСведенийСписокВариантВерсионированияПриИзменении(Элемент)
	
	СтрокаДерева = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные;
	
	ТипОбъекта             = СтрокаДерева.ТипОбъекта;
	ВариантВерсионирования = СтрокаДерева.ВариантВерсионирования;
	
	ВерсионированиеОбъектов.ЗаписатьНастройкуВерсионированияПоОбъекту(ТипОбъекта, ВариантВерсионирования);
	
КонецПроцедуры

