
Процедура ОбновитьПоставщиковУслуг() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПоступлениеТоваровУслуг.Контрагент,
		|	ПоступлениеТоваровУслугУслуги.Номенклатура КАК Услуга,
		|	ВЫБОР
		|		КОГДА СпрСтруктурныеЕдиницы.НомерТочки = 0
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|		ИНАЧЕ ПоступлениеТоваровУслугУслуги.ТорговаяТочка
		|	КОНЕЦ КАК ТорговаяТочка
		|ПОМЕСТИТЬ ИнформацияПоУслугам
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|		ПО ПоступлениеТоваровУслугУслуги.Ссылка = ПоступлениеТоваровУслуг.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК СпрСтруктурныеЕдиницы
		|		ПО ПоступлениеТоваровУслугУслуги.ТорговаяТочка = СпрСтруктурныеЕдиницы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО ПоступлениеТоваровУслугУслуги.Номенклатура = СпрНоменклатура.Ссылка
		|ГДЕ
		|	ПоступлениеТоваровУслуг.Дата МЕЖДУ &Дата1 И &Дата2
		|	И ПоступлениеТоваровУслуг.Проведен
		|	И СпрНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИнформацияПоУслугам.ТорговаяТочка,
		|	ИнформацияПоУслугам.Контрагент КАК Поставщик,
		|	СоответствиеУслугТипамУслугProRating.ТипУслуги
		|ПОМЕСТИТЬ ВсеУслуги
		|ИЗ
		|	ИнформацияПоУслугам КАК ИнформацияПоУслугам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеУслугТипамУслугProRating КАК СоответствиеУслугТипамУслугProRating
		|		ПО ИнформацияПоУслугам.Услуга = СоответствиеУслугТипамУслугProRating.Услуга
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ИнформацияПоУслугам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВсеУслуги.ТорговаяТочка,
		|	ВсеУслуги.Поставщик,
		|	ВсеУслуги.ТипУслуги,
		|	ИСТИНА КАК Работаем
		|ИЗ
		|	ВсеУслуги КАК ВсеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоставщикиУслугProRating КАК ПоставщикиУслугProRating
		|		ПО ВсеУслуги.ТорговаяТочка = ПоставщикиУслугProRating.ТорговаяТочка
		|			И ВсеУслуги.Поставщик = ПоставщикиУслугProRating.Поставщик
		|			И ВсеУслуги.ТипУслуги = ПоставщикиУслугProRating.ТипУслуги
		|ГДЕ
		|	ПоставщикиУслугProRating.Поставщик ЕСТЬ NULL");
	Запрос.УстановитьПараметр("Дата1", Дата("20180101"));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ТекущаяДата()));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда Возврат; КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ПоставщикиУслугProRating.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(РезультатЗапроса.Выгрузить());
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры
