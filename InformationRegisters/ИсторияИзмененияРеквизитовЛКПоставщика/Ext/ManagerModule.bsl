
//+++АК SHEP 2018.10.22 ИП-00018753.05
Функция ЗаполнитьДанныеДляИстории(Объект, СтрокаРеквизитов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИсторияИзмененияРеквизитов = Новый Структура;
	
	СтруктураДанныеБыли = Новый Структура(СтрокаРеквизитов);
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СтруктураДанныеБыли = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Ссылка, СтруктураДанныеБыли);
	Иначе
		// получаем значение заполнения по умолчанию
		МДОбъекта_Реквизиты = Объект.Метаданные().Реквизиты;
		Для Каждого КлючИЗначение Из СтруктураДанныеБыли Цикл
			ИмяПоляДляПроверкиИзменений = КлючИЗначение.Ключ;
			СтруктураДанныеБыли[ИмяПоляДляПроверкиИзменений] = МДОбъекта_Реквизиты[ИмяПоляДляПроверкиИзменений].ЗначениеЗаполнения;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из СтруктураДанныеБыли Цикл
		ИмяПоляДляПроверкиИзменений = КлючИЗначение.Ключ;
		Если Объект[ИмяПоляДляПроверкиИзменений] <> СтруктураДанныеБыли[ИмяПоляДляПроверкиИзменений] Тогда
			СтруктураИзменений = Новый Структура("ЗначениеБыло,ЗначениеСтало", СтруктураДанныеБыли[ИмяПоляДляПроверкиИзменений], Объект[ИмяПоляДляПроверкиИзменений]);
			ИсторияИзмененияРеквизитов.Вставить(ИмяПоляДляПроверкиИзменений, СтруктураИзменений);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИсторияИзмененияРеквизитов;
	
КонецФункции

//+++АК SHEP 2018.10.22 ИП-00018753.05
Процедура ЗаписатьИсторию(Ссылка, ИсторияИзмененияРеквизитов) Экспорт
	
	Если ИсторияИзмененияРеквизитов.Количество() > 0 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Для Каждого СтруктураИзменений Из ИсторияИзмененияРеквизитов Цикл
			ЗаписьРС = РегистрыСведений["ИсторияИзмененияРеквизитовЛКПоставщика"].СоздатьМенеджерЗаписи();
			ЗаписьРС.Период = ТекущаяДата();
			ЗаписьРС.Ссылка = Ссылка;
			ЗаписьРС.ИмяРеквизита = СтруктураИзменений.Ключ;
			ЗаписьРС.Автор = ПараметрыСеанса.ТекущийПользователь;
			ЗаписьРС.Контрагент = ПараметрыСеанса.ТекущийКонтрагент;
			ЗаполнитьЗначенияСвойств(ЗаписьРС, СтруктураИзменений.Значение);
			ЗаписьРС.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
