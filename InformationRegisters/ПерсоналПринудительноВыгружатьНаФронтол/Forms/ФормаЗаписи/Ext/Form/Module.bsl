
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Запись.Магазин) Тогда
		Запись.Магазин = ПараметрыСеанса.ТорговаяТочкаПоАйпи;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.ДатаДобавления) Тогда
		Запись.ДатаДобавления = ТекущаяДата();
	КонецЕсли;
	
	Если Не РольДоступна("ПолныеПрава") Тогда
		Элементы.Магазин.Доступность = Ложь;
		Элементы.ДатаДобавления.Доступность = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПерсоналККМНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) //+++АК mika 2018.02.07 ИП-00017859
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("РежимВыбора", Истина);
	ПараметрыВыбора.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыВыбора.Вставить("ЗакрыватьПослеВыбора", Истина);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборПерсоналККМ", ЭтаФорма);

	ОткрытьФорму("РегистрСведений.ПерсоналПринудительноВыгружатьНаФронтол.Форма.ФормаВыбораПерсоналККМ", ПараметрыВыбора, Запись.ПерсоналККМ,,ВариантОткрытияОкна.ОтдельноеОкно,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборПерсоналККМ(Результат, ДополнительныеПараметры) Экспорт  //+++АК mika 2018.02.07 ИП-00017859
	
	Если Результат <> Неопределено Тогда
		Запись.ПерсоналККМ = Результат.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерсоналККМАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) //+++АК mika 2018.02.07 ИП-00017859
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрДлина(СокрЛП(Текст))< 3 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = ПолучитьСписокВыбораПоЧастиТекста(Текст);

КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокВыбораПоЧастиТекста(Текст) //+++АК mika 2018.02.07 ИП-00017859
	
    Возврат ПолучитьДанныеВыбора(Тип("СправочникСсылка.ПерсоналККМ"), 
			Новый Структура("ДополнительныеПараметры, ТекстПоиска", "ПодборПоГруппамСотрудников", Текст));
	
КонецФункции



