//+++АК sole 2018.06.19 ИП-00018944
Функция ПолучитьТЗТарифы(пДата) Экспорт
	
	Перем Запрос, тзТарифы;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
"ВЫБРАТЬ
|	СтоимостьУслугПоДоставкеТовараНаТТСрезПоследних.Маршрут,
|	МАКСИМУМ(СтоимостьУслугПоДоставкеТовараНаТТСрезПоследних.ВариантРасчетаНДС) КАК ВариантРасчетаНДС,
|	МАКСИМУМ(СтоимостьУслугПоДоставкеТовараНаТТСрезПоследних.СтавкаНДС) КАК СтавкаНДС
|ИЗ
|	РегистрСведений.СтоимостьУслугПоДоставкеТовараНаТТ.СрезПоследних(&Дата, ) КАК СтоимостьУслугПоДоставкеТовараНаТТСрезПоследних
|ГДЕ
|	СтоимостьУслугПоДоставкеТовараНаТТСрезПоследних.Маршрут <> ЗНАЧЕНИЕ(Справочник.Маршруты.ПустаяСсылка)
|
|СГРУППИРОВАТЬ ПО
|	СтоимостьУслугПоДоставкеТовараНаТТСрезПоследних.Маршрут";
	
	Запрос.Параметры.Вставить("Дата", пДата);
	
	тзТарифы = Запрос.Выполнить().Выгрузить();
	
	Возврат тзТарифы;
КонецФункции

//+++АК sole 2018.07.06 ИП-00019019
Функция ПроверитьПраваНаЗапись(СписокТорговыхТочек) Экспорт
	
	ЕстьПравоРедактироватьВсеРегионы = УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РедактированиеСтоимостиУслугПоДоставкеТовараНаТТ_ВсеРегионы, Ложь);
	
	Если ЕстьПравоРедактироватьВсеРегионы Тогда
		Возврат Истина;
	КонецЕсли; 
	
	СписокРегионовДляРедактирования = Новый СписокЗначений();
	
	ЕстьПравоРедактироватьРегионМосква = УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РедактированиеСтоимостиУслугПоДоставкеТовараНаТТ_Москва, Ложь);
	ЕстьПравоРедактироватьРегионМосковскаяОбласть = УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РедактированиеСтоимостиУслугПоДоставкеТовараНаТТ_МосковскаяОбласть, Ложь);
	
	Если ЕстьПравоРедактироватьРегионМосква Тогда
		СписокРегионовДляРедактирования.Добавить(77);
	КонецЕсли;
	
	Если ЕстьПравоРедактироватьРегионМосковскаяОбласть Тогда
		СписокРегионовДляРедактирования.Добавить(50);
	КонецЕсли;
	
	Если СписокРегионовДляРедактирования.Количество() = 0 Тогда
		Сообщить("У Вас нет дополнительных прав для редактирования регистра сведений ""Стоимость услуг по доставке товара на ТТ""!", СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
"ВЫБРАТЬ
|	СтруктурныеЕдиницы.Регион.Наименование КАК НаименованиеРегиона
|
|ИЗ Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы	
|ГДЕ
|		НЕ СтруктурныеЕдиницы.Регион.Код В (&СписокРегионовДляРедактирования)
|	И	СтруктурныеЕдиницы.Ссылка В (&СписокТорговыхТочек)
|
| СГРУППИРОВАТЬ ПО СтруктурныеЕдиницы.Регион
|";
	Запрос.УстановитьПараметр("СписокРегионовДляРедактирования", СписокРегионовДляРедактирования);
	Запрос.УстановитьПараметр("СписокТорговыхТочек", СписокТорговыхТочек);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Истина;
	КонецЕсли;
	
	СообщениеОбОшибке = "";
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СообщениеОбОшибке = СообщениеОбОшибке + 
			"У Вас нет дополнительных прав для редактирования регистра сведений ""Стоимость услуг по доставке товара на ТТ"" для региона """ + Выборка.НаименованиеРегиона + """!" + Символы.ПС; 
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Сообщить(СообщениеОбОшибке, СтатусСообщения.Важное);	
	КонецЕсли;
	
	Возврат	Ложь;
КонецФункции
	

//