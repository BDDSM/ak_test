
//+++АК SHEP 20160712
Функция ПолучитьСледующийid_OK() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОбращенияПокупателей.id_OK КАК id_OK
		|ИЗ
		|	РегистрСведений.ОбращенияПокупателей КАК ОбращенияПокупателей
		|
		|УПОРЯДОЧИТЬ ПО
		|	id_OK УБЫВ");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Счётчик = 0;
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Счётчик = ВыборкаДетальныеЗаписи.id_OK;
	КонецЕсли;
	
	Возврат Счётчик + 1;
	
КонецФункции
//---АК SHEP 20160712

Функция ПолучитьПродактМенеджера(Запись) Экспорт
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата"			, ТекущаяДата());
	Запрос.УстановитьПараметр("Номенклатура"	, Запись.Номенклатура);
	Запрос.УстановитьПараметр("Производитель"	, Запись.Производитель);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Характеристика
	|ПОМЕСТИТЬ втХар
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО ХарактеристикиНоменклатуры.Ссылка = ЗначенияСвойствОбъектов.Объект
	|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Владелец = &Номенклатура
	|	И ЗначенияСвойствОбъектов.Значение = &Производитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Производитель КАК Производитель,
	|	втХар.Характеристика КАК Характеристика,
	|	РолиПользователейСоставРоли.Сотрудник,
	|	РолиПользователейСоставРоли.Сотрудник.Наименование КАК СотрудникНаименование,
	|	РолиПользователейСоставРоли.Ссылка
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРоли
	|		ЛЕВОЕ СОЕДИНЕНИЕ втХар КАК втХар
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ РолиПользователейСоставРоли.Ссылка.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеОбъектРольСрезПоследних1.Объект,
	|	СоответствиеОбъектРольСрезПоследних.РольПользователя,
	|	СоответствиеОбъектРольСрезПоследних1.ТипРоли
	|ПОМЕСТИТЬ втБрендМенеджеры
	|ИЗ
	|	РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(, ТипРоли = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ТипыРолейПользователя.БрендМенеджер)) КАК СоответствиеОбъектРольСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&Дата, ) КАК СоответствиеОбъектРольСрезПоследних1
	|		ПО СоответствиеОбъектРольСрезПоследних.Объект = СоответствиеОбъектРольСрезПоследних1.РольПользователя
	|ГДЕ
	|	СоответствиеОбъектРольСрезПоследних.Объект ССЫЛКА Справочник.РолиПользователей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РолиПользователейСоставРоли.Сотрудник,
	|	РолиПользователейСоставРоли.СотрудникНаименование,
	|	МАКСИМУМ(РолиПользователейСоставРоли.Ссылка) КАК Ссылка,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА СоответствиеОбъектРольСрезПоследних.Объект ССЫЛКА Справочник.Контрагенты
	|				ТОГДА 3
	|			КОГДА СоответствиеОбъектРольСрезПоследних.Объект ССЫЛКА Справочник.Номенклатура
	|				ТОГДА 2
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Приоритет
	|ПОМЕСТИТЬ втПриор
	|ИЗ
	|	вт КАК РолиПользователейСоставРоли
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.ТипыРолей КАК РолиПользователейТипыРолей
	|		ПО РолиПользователейСоставРоли.Ссылка = РолиПользователейТипыРолей.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втБрендМенеджеры КАК СоответствиеОбъектРольСрезПоследних
	|		ПО РолиПользователейСоставРоли.Ссылка = СоответствиеОбъектРольСрезПоследних.РольПользователя
	|			И (СоответствиеОбъектРольСрезПоследних.Объект = РолиПользователейСоставРоли.Характеристика
	|				ИЛИ РолиПользователейСоставРоли.Производитель = СоответствиеОбъектРольСрезПоследних.Объект)
	|ГДЕ
	|	РолиПользователейТипыРолей.ТипРоли = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ТипыРолейПользователя.БрендМенеджер)
	|
	|СГРУППИРОВАТЬ ПО
	|	РолиПользователейСоставРоли.Сотрудник,
	|	РолиПользователейСоставРоли.СотрудникНаименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(втПриор.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ втМаксПриор
	|ИЗ
	|	втПриор КАК втПриор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПриор.Сотрудник,
	|	втПриор.СотрудникНаименование,
	|	втПриор.Ссылка,
	|	втПриор.Приоритет
	|ИЗ
	|	втПриор КАК втПриор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМаксПриор КАК втМаксПриор
	|		ПО втПриор.Приоритет = втМаксПриор.Приоритет
	|
	|УПОРЯДОЧИТЬ ПО
	|	втПриор.Приоритет,
	|	втПриор.СотрудникНаименование";
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	МасФЛ = Новый Массив;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МасФЛ.Добавить(ВыборкаДетальныеЗаписи.Сотрудник);
	КонецЦикла;
	
	Возврат МасФЛ;
	
КонецФункции
