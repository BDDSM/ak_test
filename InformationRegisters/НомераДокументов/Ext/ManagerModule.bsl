
#Область ПрограммныйИнтерфейс

//+++АК LATV 2018.10.18 ИП-00020107
Функция ПолучитьИЗафиксироватьНовыйИдентификатор(ВидИдентификатора, Отказ = Ложь) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НомераДокументов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Вид", ВидИдентификатора);
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка.Заблокировать();
		
		Запись = РегистрыСведений.НомераДокументов.СоздатьМенеджерЗаписи();
		Запись.Вид = ВидИдентификатора;
		Запись.Прочитать();
		
		Запись.Номер = Запись.Номер + 1;
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить новый номер для вида идентификатора ""%1"" по причине: %2'")
			, ВидИдентификатора, ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Запись.Номер;

КонецФункции

#КонецОбласти
