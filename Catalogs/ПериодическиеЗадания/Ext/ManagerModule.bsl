
Функция ВыполнитьЗапрос(ТекстЗапроса, Параметры) Экспорт 
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Для Каждого СтрокаПараметров Из Параметры Цикл				
		Если СтрокаПараметров.ЭтоВыражение Тогда					
			РезультатВычисления = Неопределено;					
			Попытка 
				РезультатВычисления = Вычислить(СтрокаПараметров.ЗначениеПараметра);
			Исключение
			КонецПопытки;					
			Запрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, РезультатВычисления);
		Иначе					
			Запрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, СтрокаПараметров.ЗначениеПараметра);				
		КонецЕсли;				
	КонецЦикла;
	
	Возврат Запрос.Выполнить();			
			
КонецФункции

Процедура ВыполнитьПоПростойНастройке(ТекущийОбъект, ПолучателиРассылки, Параметры) Экспорт 

	Если НЕ ПустаяСтрока(ТекущийОбъект.ТекстЗапроса) И ПолучателиРассылки.Количество() > 0 Тогда
				
		РезультатЗапроса = ВыполнитьЗапрос(ТекущийОбъект.ТекстЗапроса, Параметры);			
		Если НЕ РезультатЗапроса.Пустой() ИЛИ ТекущийОбъект.ОтправлятьСообщениеВсегда Тогда 
			
			УчетнаяЗапись = ОбщиеПроцедуры.ПолучитьУчетнуюЗаписьДляРассылки();	
			Почта = Новый ИнтернетПочта;
			Профиль = УправлениеЭлектроннойПочтой.ПолучитьИнтернетПочтовыйПрофиль(УчетнаяЗапись);
			Письмо = Новый ИнтернетПочтовоеСообщение;
			
			Почта.Подключиться(Профиль);  
			Письмо.Тема 			= ТекущийОбъект.Наименование; 
			Письмо.Отправитель 		= УчетнаяЗапись.Логин;
			Письмо.ИмяОтправителя 	= УчетнаяЗапись.Логин;
			
			Получатели = Письмо.Получатели;
			ОтправитьСообщениеВТелеграм = Ложь;
			Для Каждого СтрокаПолучатель Из ПолучателиРассылки Цикл
				Если ЗначениеЗаполнено(СтрокаПолучатель.ЭлектронныйАдрес) Тогда
					Получатель = Получатели.Добавить();
					Получатель.Адрес           = СтрокаПолучатель.ЭлектронныйАдрес;
					Если ЗначениеЗаполнено(СтрокаПолучатель.ФизЛицо) Тогда 
						Получатель.ОтображаемоеИмя = СтрокаПолучатель.ФизЛицо.Наименование;
					КонецЕсли;
				КонецЕсли;
				ОтправитьСообщениеВТелеграм = СтрокаПолучатель.ИдТелеграм > 0 ИЛИ ОтправитьСообщениеВТелеграм;
			КонецЦикла;	
			
			ОтправитьПисьмо = Получатели.Количество() > 0;
			Если ОтправитьПисьмо = Ложь И ОтправитьСообщениеВТелеграм = Ложь Тогда 
				Возврат;
			КонецЕсли;
			
			ТабЗначений 		= РезультатЗапроса.Выгрузить();	
			КоличествоКолонок 	= ТабЗначений.Колонки.Количество();
			КоличествоСтрок 	= ТабЗначений.Количество();

			Если ОтправитьПисьмо Тогда
				
				Если КоличествоСтрок > 0 Тогда 
					ТаблицаХТМЛ = "<table cellpadding=""0"" cellspacing=""0"" border=""5"">
					|<tbody>
					|<tr>";
					
					//Формируем шапку
					МассивИменКолонок = Новый Массив;
					Для Счетчик = 1 по КоличествоКолонок Цикл
						ИмяКолонки = ТабЗначений.Колонки[Счетчик-1].Имя; 
						ТаблицаХТМЛ = ТаблицаХТМЛ + " 
						|<td><font face=""Courier New"">" + ИмяКолонки + "</font></td>";	
						МассивИменКолонок.Добавить(ИмяКолонки);
					КонецЦикла;
					ТаблицаХТМЛ = ТаблицаХТМЛ + "
					|</tr>";
					
					//Заполняем таблицу	
					ПунктПорядку = 0;
					Для каждого СтрокаТаблицы Из ТабЗначений Цикл		
						ТаблицаХТМЛ = ТаблицаХТМЛ + "
						|<tr>";
						Для Счетчик = 0 по КоличествоКолонок - 1 Цикл
							ТаблицаХТМЛ = ТаблицаХТМЛ + "
							|<td><font face=""Courier New"">" + СтрокаТаблицы[МассивИменКолонок[Счетчик]] + "</font></td>";			
						КонецЦикла;				
						ТаблицаХТМЛ = ТаблицаХТМЛ + "
						|</tr>";
					КонецЦикла;	
					ТаблицаХТМЛ = ТаблицаХТМЛ + " 
					|</tbody>
					|</table>";	
				Иначе
					ТаблицаХТМЛ = "";
				КонецЕсли;
				
				ТекстПисьмаОтправить = "<p>" +ТекущийОбъект.ТекстПисьма+ "</p>" 
				+ "<p><br><font size=""1"">" + " Отчет сформирован на основании настройки №"+ СокрЛП(ТекущийОбъект.Код)+ " справочника ""Периодические задания (рассылки)""." + "</font></p>";  //Текст письма	
				ТекстПисьмаОтправить = ТекстПисьмаОтправить + ТаблицаХТМЛ;
				
				Письмо.Тексты.Добавить(ТекстПисьмаОтправить, ТипТекстаПочтовогоСообщения.HTML);	
				Почта.Послать(Письмо);	
				Почта.Отключиться();
			КонецЕсли;
			
			//Сообщение в Telegram
			Если ОтправитьСообщениеВТелеграм Тогда 
				ТекстСообщения 		= СокрЛП(ТекущийОбъект.Наименование) + Символы.ПС;
				Если КоличествоСтрок > 0 Тогда
					ПерваяСтрока = ТабЗначений[0];
					Если КоличествоКолонок > 0 Тогда
						Для Счетчик = 1 по КоличествоКолонок Цикл
							ИмяКолонки = ТабЗначений.Колонки[Счетчик-1].Имя; 
							ТекстСообщения = ТекстСообщения + ИмяКолонки + ": " + СокрЛП(ПерваяСтрока[ИмяКолонки]) + ", ";
						КонецЦикла;
						Если КоличествоСтрок > 1 Тогда
							ТекстСообщения	= Лев(ТекстСообщения,СтрДлина(ТекстСообщения)-2) + ?(КоличествоСтрок > 1," ...",".");	
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;
				Для каждого СтрокаПолучатель Из ПолучателиРассылки Цикл 
					Если СтрокаПолучатель.ИдТелеграм > 0 Тогда
						Телеграм.СоздатьСообщение(5, СтрокаПолучатель.ИдТелеграм, ТекстСообщения, 0, 0);		
					КонецЕсли;
				КонецЦикла;		
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//+++АК LATV 2018.11.30 ИП-00020296
Функция ПолучателиРассылки(Ссылка) Экспорт

	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПериодическиеЗаданияПолучателиРассылки.ФизЛицо КАК ФизЛицо,
		|	ПериодическиеЗаданияПолучателиРассылки.НомерСтроки КАК НомерСтроки,
		|	ВЫРАЗИТЬ(ПериодическиеЗаданияПолучателиРассылки.ЭлектронныйАдрес КАК СТРОКА(250)) КАК ЭлектронныйАдрес,
		|	ПериодическиеЗаданияПолучателиРассылки.ИдТелеграм
		|ПОМЕСТИТЬ втАдресаИзЗадания
		|ИЗ
		|	Справочник.ПериодическиеЗадания.ПолучателиРассылки КАК ПериодическиеЗаданияПолучателиРассылки
		|ГДЕ
		|	ПериодическиеЗаданияПолучателиРассылки.Ссылка = &Задание
		|	И НЕ ЕСТЬNULL(ПериодическиеЗаданияПолучателиРассылки.ФизЛицо.ПометкаУдаления, ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втАдресаИзЗадания.НомерСтроки,
		|	ВЫРАЗИТЬ(КонтактнаяИнформация_АдресЭлПочты.Представление КАК СТРОКА(250)) КАК ЭлектронныйАдрес
		|ПОМЕСТИТЬ втКонтактнаяИнформация
		|ИЗ
		|	втАдресаИзЗадания КАК втАдресаИзЗадания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация_АдресЭлПочты
		|		ПО втАдресаИзЗадания.ФизЛицо = КонтактнаяИнформация_АдресЭлПочты.Объект
		|			И (КонтактнаяИнформация_АдресЭлПочты.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
		|			И (КонтактнаяИнформация_АдресЭлПочты.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailФизЛица))
		|			И ((ВЫРАЗИТЬ(КонтактнаяИнформация_АдресЭлПочты.Представление КАК СТРОКА(250))) <> """")
		|ГДЕ
		|	втАдресаИзЗадания.ЭлектронныйАдрес = """"
		|	И втАдресаИзЗадания.ФизЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втАдресаИзЗадания.ФизЛицо КАК ФизЛицо,
		|	ЕСТЬNULL(втАдресаИзЗадания.ФизЛицо.Наименование, """") КАК Представление,
		|	ВЫБОР
		|		КОГДА втАдресаИзЗадания.ЭлектронныйАдрес <> """"
		|			ТОГДА втАдресаИзЗадания.ЭлектронныйАдрес
		|		ИНАЧЕ ЕСТЬNULL(втКонтактнаяИнформация.ЭлектронныйАдрес, """")
		|	КОНЕЦ КАК Адрес,
		|	втАдресаИзЗадания.ИдТелеграм КАК ИдТелеграм
		|ИЗ
		|	втАдресаИзЗадания КАК втАдресаИзЗадания
		|		ЛЕВОЕ СОЕДИНЕНИЕ втКонтактнаяИнформация КАК втКонтактнаяИнформация
		|		ПО втАдресаИзЗадания.НомерСтроки = втКонтактнаяИнформация.НомерСтроки";
		
	Запрос.УстановитьПараметр("Задание", Ссылка);
	
	Получатели = Запрос.Выполнить().Выгрузить();
	Возврат Получатели;

КонецФункции
