
#Область ОбработчикиСобытийКомандФормы

//+++АК LATV 2018.10.01 ИП-00019985
&НаКлиенте
Процедура ОбновитьСправочник(Команда)

	ЗагрузитьВФоне();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Загрузка

//+++АК LATV 2018.10.01 ИП-00019985
&НаКлиенте
Процедура ЗагрузитьВФоне()

	Если НастройкаApi.Пустая() Тогда
		Сообщить("Выберите настройку API");
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения	= Истина;
	ПараметрыОжидания.ТекстСообщения	= НСтр("ru = 'Обновление справочника из Ветис'");
	ПараметрыОжидания.ОповещениеПользователя.Показать	= Истина;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение	= НСтр("ru = 'Обновление справочника из Ветис завершено'");
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьВФонеЗавершение", ЭтаФорма);
	
	ДлительнаяОперация = ЗагрузитьНаСервере();
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

//+++АК LATV 2018.10.01 ИП-00019985
&НаСервере
Функция ЗагрузитьНаСервере()

	// Выполняемый метод
	ИмяМетода = "Справочники.МЙ_ПродукцияВетис.ЗагрузитьСписокИзВетис";
	
	ПараметрыМетода = Новый Структура();
	ПараметрыМетода.Вставить("НастройкаApi", НастройкаAPI);
	
	// Запуск фонового задания
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыМетода, ПараметрыВыполнения);

КонецФункции

//+++АК LATV 2018.10.01 ИП-00019985
&НаКлиенте
Процедура ЗагрузитьВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда // Задание отменено
		Элементы.Список.Обновить();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		Если Результат.Сообщения <> Неопределено Тогда
			Для Каждого Сообщение Из Результат.Сообщения Цикл
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		Элементы.Список.Обновить();
		Возврат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
