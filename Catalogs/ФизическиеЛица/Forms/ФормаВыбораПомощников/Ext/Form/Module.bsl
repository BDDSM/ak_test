
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьДеревоПодчиненностиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазвернутьДеревоПодчиненности();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДерева

&НаКлиенте
Процедура СписокПомощниковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Новый Структура("ПомощникТУ", Элемент.ТекущиеДанные.Ссылка));

КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодчиненностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(Элемент.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Закрыть(Новый Структура("ПомощникТУ", Элемент.ТекущиеДанные.Ссылка));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПодчиненности.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено 
		И ТипЗнч(ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		Закрыть(Новый Структура("ПомощникТУ", ТекущиеДанные.Ссылка));

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьЗначение(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПодчиненности.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ОткрытьЗначение(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДеревоПодчиненностиСервер()
	
	ТекущееДерево = РеквизитФормыВЗначение("ДеревоПодчиненности");
	ТекущееДерево.Строки.Очистить();
	
	Запрос = Новый Запрос();
	
	//+++АК mika 2018.04.17 ИП-00018288
	//Запрос.Текст = ПолучитьТекстЗапросаДляПостроенияДерева();
	Запрос.Текст = ПолучитьТекстЗапросаДляПостроенияДереваПоРолям();
	//---АК mika ИП-00018288
	
	Запрос.УстановитьПараметр("ЦФО", Справочники.СтруктурныеЕдиницы.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаЦФО = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаЦФО.Следующий() Цикл
			
			СтрокаЦФО = ТекущееДерево.Строки.Добавить();
			СтрокаЦФО.Ссылка = ВыборкаЦФО.ЦФО;
			
			ВыборкаПомощник = ВыборкаЦФО.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПомощник.Следующий() Цикл
				
				СтрокаПомощник = СтрокаЦФО.Строки.Добавить();
				СтрокаПомощник.Ссылка = ВыборкаПомощник.ПомощникТУ;
				
				ВыборкаТорговаяТочка = ВыборкаПомощник.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаТорговаяТочка.Следующий() Цикл
					СтрокаТорговаяТочка = СтрокаПомощник.Строки.Добавить();
					СтрокаТорговаяТочка.Ссылка = ВыборкаТорговаяТочка.СтруктурнаяЕдиница;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ТекущееДерево, "ДеревоПодчиненности");
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстЗапросаДляПостроенияДерева()

	Возврат
	
	 "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 |	ЦФОСтруктурныхЕдиницСрезПоследних.ЦФО,
	 |	ЦФОСтруктурныхЕдиницСрезПоследних.СтруктурнаяЕдиница
	 |ПОМЕСТИТЬ ВТ_ЦФО
	 |ИЗ
	 |	РегистрСведений.ЦФОСтруктурныхЕдиниц.СрезПоследних(
	 |			&ТекущаяДата,
	 |			ВЫБОР
	 |				КОГДА &ЦФО <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	 |					ТОГДА ЦФО = &ЦФО
	 |				ИНАЧЕ ИСТИНА
	 |			КОНЕЦ) КАК ЦФОСтруктурныхЕдиницСрезПоследних
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	СоответствиеОбъектРольСрезПоследних.ТипРоли,
	 |	СоответствиеОбъектРольСрезПоследних.Объект,
	 |	СоответствиеОбъектРольСрезПоследних.РольПользователя,
	 |	СоответствиеОбъектРольСрезПоследних.ТипРолиID,
	 |	РолиПользователейСоставРоли.Сотрудник
	 |ПОМЕСТИТЬ ВТ_Помощники_ТУ_Соотв
	 |ИЗ
	 //+++ AK suvv 2018.06.08 ИП-00018376.01
	 //|	РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&ТекущаяДата, ТипРолиID = ""PomoshnikTerrUpravlyushego"") КАК СоответствиеОбъектРольСрезПоследних
	 |	РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&ТекущаяДата, ТипРолиID = ""PomoshnikTerrUpravlyushego"" или ТипРолиID = ""PomoshnikStorRozn"") КАК СоответствиеОбъектРольСрезПоследних
	 //--- AK suvv
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРоли
	 |		ПО СоответствиеОбъектРольСрезПоследних.РольПользователя = РолиПользователейСоставРоли.Ссылка
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВТ_ЦФО.ЦФО КАК ЦФО,
	 |	ВТ_Помощники_ТУ_Соотв.Сотрудник КАК ПомощникТУ,
	 |	ВТ_ЦФО.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	 |ИЗ
	 |	ВТ_ЦФО КАК ВТ_ЦФО
	 |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Помощники_ТУ_Соотв КАК ВТ_Помощники_ТУ_Соотв
	 |		ПО ВТ_ЦФО.СтруктурнаяЕдиница = ВТ_Помощники_ТУ_Соотв.Объект
	 |ГДЕ
	 |	ВТ_Помощники_ТУ_Соотв.Сотрудник <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	 |
	 |УПОРЯДОЧИТЬ ПО
	 |	ЦФО,
	 |	ПомощникТУ,
	 |	СтруктурнаяЕдиница
	 |ИТОГИ ПО
	 |	ЦФО,
	 |	ПомощникТУ,
	 |	СтруктурнаяЕдиница
	 |АВТОУПОРЯДОЧИВАНИЕ";

КонецФункции


&НаСервере
Функция ПолучитьТекстЗапросаДляПостроенияДереваПоРолям()

	//Определения "Группы управления" для помощника изменено с "фактического"(привязка по ЦФО структурных единиц) на привязку Управляющего(Руководитель отдела)
	//В регистре "Пользователи по ЦФО". Привязка определяется как: 
	//Помощник → Доступные магазины → Функциональналя роль помощника(Родитель или Родитель.Родитель для группы Управление Развитием) → Пользователь(Управляющий, согласно родителя функ. роли) → Пользователи ЦФО(Срез последник, с признаком Руководитель отдела).
	Возврат
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПользователиПоЦФОСрезПоследних.ЦФО,
	|	МАКСИМУМ(ПользователиПоЦФОСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_ПользователиПоЦФОДаты
	|ИЗ
	|	РегистрСведений.ПользователиПоЦФО.СрезПоследних(&ТекущаяДата, ) КАК ПользователиПоЦФОСрезПоследних
	|ГДЕ
	|	ПользователиПоЦФОСрезПоследних.РуководительОтдела
	|
	|СГРУППИРОВАТЬ ПО
	|	ПользователиПоЦФОСрезПоследних.ЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПользователиПоЦФО.Сотрудник,
	|	МАКСИМУМ(ПользователиПоЦФО.ЦФО) КАК ЦФО
	|ПОМЕСТИТЬ ВТ_РуководителиПоЦФО
	|ИЗ
	|	ВТ_ПользователиПоЦФОДаты КАК ВТ_ПользователиПоЦФОДаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПользователиПоЦФО КАК ПользователиПоЦФО
	|		ПО ВТ_ПользователиПоЦФОДаты.ЦФО = ПользователиПоЦФО.ЦФО
	|			И ВТ_ПользователиПоЦФОДаты.Период = ПользователиПоЦФО.Период
	|			И (ПользователиПоЦФО.РуководительОтдела)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПользователиПоЦФО.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РуководителиПоЦФО.ЦФО КАК ЦФО,
	|	ТабИтоговая.Помощник КАК ПомощникТУ,
	|	ТабИтоговая.ТорговаяТочка КАК СтруктурнаяЕдиница,
	|	Пользователи.Ссылка КАК УправляющийПользователь
	|ИЗ
	|	(ВЫБРАТЬ
	|		СоответствиеОбъектРольСрезПоследних.ТипРоли КАК ТипРоли,
	|		СоответствиеОбъектРольСрезПоследних.Объект КАК ТорговаяТочка,
	|		СоответствиеОбъектРольСрезПоследних.РольПользователя КАК РольПомощника,
	|		СоответствиеОбъектРольСрезПоследних.РольПользователя.Родитель КАК РольПользователяРодитель,
	|		СоответствиеОбъектРольСрезПоследних.РольПользователя.Родитель.Родитель КАК РольПользователяРодительРодитель,
	|		СоответствиеОбъектРольСрезПоследних.ТипРолиID КАК ТипРолиID,
	|		РолиПользователейСоставРоли.Сотрудник КАК Помощник,
	|		ВЫБОР
	|			КОГДА НЕ РолиПользователейСоставРолиРодительРодитель.Сотрудник ЕСТЬ NULL
	|				ТОГДА РолиПользователейСоставРолиРодительРодитель.Сотрудник
	|			ИНАЧЕ РолиПользователейСоставРолиРодитель.Сотрудник
	|		КОНЕЦ КАК УправляющийФизЛицо
	|	ИЗ
	|		Справочник.РолиПользователей.ТипыРолей КАК РолиПользователейТипыРолей
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРоли
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(
	|						&ТекущаяДата,
	//+++ AK suvv 2018.06.08 ИП-00018376.01
	//|						ТипРолиID = ""PomoshnikTerrUpravlyushego""
	|						(ТипРолиID = ""PomoshnikTerrUpravlyushego"" или ТипРолиID = ""PomoshnikStorRozn"") 
	//--- AK suvv
	|							И Объект.СтатусТорговойТочки <> ЗНАЧЕНИЕ(Перечисление.СтатусыТорговыхТочек.Закрыт)
	|							И Объект ССЫЛКА Справочник.СтруктурныеЕдиницы) КАК СоответствиеОбъектРольСрезПоследних
	|				ПО (СоответствиеОбъектРольСрезПоследних.РольПользователя = РолиПользователейСоставРоли.Ссылка)
	|			ПО РолиПользователейТипыРолей.Ссылка = РолиПользователейСоставРоли.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРолиРодитель
	|			ПО РолиПользователейТипыРолей.Ссылка.Родитель = РолиПользователейСоставРолиРодитель.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРолиРодительРодитель
	|			ПО РолиПользователейТипыРолей.Ссылка.Родитель.Родитель = РолиПользователейСоставРолиРодительРодитель.Ссылка
	|	ГДЕ
	|		РолиПользователейСоставРоли.Сотрудник.Активный
	//+++ AK suvv 2018.06.08 ИП-00018376.01
	//|		И РолиПользователейТипыРолей.ТипРоли.Код = ""PomoshnikTerrUpravlyushego"") КАК ТабИтоговая
	|		И (РолиПользователейТипыРолей.ТипРоли.Код = ""PomoshnikTerrUpravlyushego"" или РолиПользователейТипыРолей.ТипРоли.Код = ""PomoshnikStorRozn"")) КАК ТабИтоговая
	//--- AK suvv 
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РуководителиПоЦФО КАК ВТ_РуководителиПоЦФО
	|			ПО Пользователи.Ссылка = ВТ_РуководителиПоЦФО.Сотрудник
	|		ПО ТабИтоговая.УправляющийФизЛицо = Пользователи.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_РуководителиПоЦФО.ЦФО,
	|	ПомощникТУ,
	|	СтруктурнаяЕдиница
	|ИТОГИ ПО
	|	ЦФО,
	|	ПомощникТУ,
	|	СтруктурнаяЕдиница
	|АВТОУПОРЯДОЧИВАНИЕ";

КонецФункции

&НаКлиенте
Процедура РазвернутьДеревоПодчиненности()
	
	КоллекцияПервогоУровня = ДеревоПодчиненности.ПолучитьЭлементы();
	
	Для Каждого ЭлементПервогоУровня Из КоллекцияПервогоУровня Цикл
		РазвернутьСтрокиДерева(ЭлементПервогоУровня.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьСтрокиДерева(ИдентификаторСтроки)
	
	Элементы.ДеревоПодчиненности.Развернуть(ИдентификаторСтроки, Ложь);
	
КонецПроцедуры

#КонецОбласти

