
// Процедура обновляет даные пользователя ИБ, если изменился профиль
//
Процедура ОбновитьДанныеПользователейИБ()
	
	Если ЭтоНовый() 
		ИЛИ ЭтоГруппа 
		ИЛИ ПрофильПолномочийПользователя.Пустая() 
		ИЛИ ПрофильПолномочийПользователя = Ссылка.ПрофильПолномочийПользователя Тогда
		
		Возврат;
	КонецЕсли; 
	
	// Обновим данные пользователя ИБ
	СтруктураДанных = Новый Структура;
	
	МассивРолей = ПрофильПолномочийПользователя.СоставРолей.ВыгрузитьКолонку("ИмяРоли");
	СтруктураДанных.Вставить("СоставРолей", МассивРолей);
	СтруктураДанных.Вставить("ОсновнойИнтерфейс", ПрофильПолномочийПользователя.ОсновнойИнтерфейс);
	
	УправлениеПользователями.ИзменитьДанныеПользователяИБ(Код, СтруктураДанных);
	
КонецПроцедуры // ОбновитьДанныеПользователейИБ

Процедура ПриКопировании(ОбъектКопирования)
	
	Код = "";
	Наименование = "";
	ФизЛицо = Неопределено;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	ОбновитьДанныеПользователейИБ();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	Пользователи.Ссылка
	               |ИЗ
	               |	Справочник.Пользователи КАК Пользователи
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
	               |		ПО Пользователи.Ссылка = НастройкиПользователей.Пользователь
	               |			И (НастройкиПользователей.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновнойОтветственный))
	               |ГДЕ
	               |	ЕСТЬNULL(НастройкиПользователей.Значение, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	               |	И Пользователи.Ссылка = &Ссылка";
				   
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Запись = РегистрыСведений.НастройкиПользователей.СоздатьМенеджерЗаписи();
		Запись.Пользователь = Ссылка;
		Запись.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойОтветственный;
		Запись.Значение = Ссылка;
		Запись.Записать();
	КонецЕсли;	
	
КонецПроцедуры
