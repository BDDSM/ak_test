
Функция ПроверитьДублиТоваров(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекОбъект"		, ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Склад"			, ЭтотОбъект.Склад);
	Запрос.УстановитьПараметр("МассивТоваров"	, ЭтотОбъект.ГруппыНоменклатуры.ВыгрузитьКолонку("ГруппаНоменклатуры"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетчикиГруппыНоменклатуры.Ссылка.Наименование КАК Расчетчик,
	|	РасчетчикиГруппыНоменклатуры.ГруппаНоменклатуры КАК Товар
	|ИЗ
	|	Справочник.Расчетчики.ГруппыНоменклатуры КАК РасчетчикиГруппыНоменклатуры
	|ГДЕ
	|	РасчетчикиГруппыНоменклатуры.Ссылка.Склад = &Склад
	|	И РасчетчикиГруппыНоменклатуры.ГруппаНоменклатуры В(&МассивТоваров)
	|	И НЕ РасчетчикиГруппыНоменклатуры.Ссылка = &ТекОбъект
	|	И НЕ РасчетчикиГруппыНоменклатуры.Ссылка.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Сообщить("Товар """ + Выборка.Товар + """ уже указан в списке другого расчетчика (" + СокрЛП(Выборка.Расчетчик) + ")!");
		Отказ = Истина;
	КонецЦикла;
	
КонецФункции

Процедура ПередЗаписью(Отказ)
	
	//
	ПроверитьДублиТоваров(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Удаление планов продаж по позициям, которые были удалены из списка номенклатуры текущего расчетчика
	Если ЭтотОбъект.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	ТЧСтарая 	= ЭтотОбъект.Ссылка.ГруппыНоменклатуры;
	ТЧНовая 	= ЭтотОбъект.ГруппыНоменклатуры;
	
	МассивКУдалению = Новый Массив;
	Для Каждого СтрокаТЧ Из ТЧСтарая Цикл
		ТекНоменклатура = СтрокаТЧ.ГруппаНоменклатуры;
		Если НЕ ТЧНовая.Найти(ТекНоменклатура, "ГруппаНоменклатуры") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МассивКУдалению.Добавить(ТекНоменклатура);
	КонецЦикла;	
	
	Если МассивКУдалению.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	мПланыПродажПоДнямНедели = РегистрыСведений.ПланыПродажПоДнямНедели;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекДата"				, ТекущаяДата());
	Запрос.УстановитьПараметр("Расчетчик"			, ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("МассивНоменклатуры"	, МассивКУдалению);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПланыПродажПоДнямНедели.Дата КАК Дата,
	|	ПланыПродажПоДнямНедели.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.ПланыПродажПоДнямНедели КАК ПланыПродажПоДнямНедели
	|ГДЕ
	|	ПланыПродажПоДнямНедели.Расчетчик = &Расчетчик
	|	И ПланыПродажПоДнямНедели.Номенклатура В ИЕРАРХИИ(&МассивНоменклатуры)
	|	И НЕ ПланыПродажПоДнямНедели.Дата < &ТекДата
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланыПродажПоДнямНедели.Дата,
	|	ПланыПродажПоДнямНедели.Номенклатура";

	Выборка = Запрос.Выполнить().Выбрать(); 
	Пока Выборка.Следующий() Цикл

		НаборЗаписей = мПланыПродажПоДнямНедели.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Дата.Установить(Выборка.Дата);
		НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
		НаборЗаписей.Отбор.Расчетчик.Установить(ЭтотОбъект.Ссылка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
			Сообщить("Удаляются планы продаж по номенклатуре """ + СокрЛП(Выборка.Номенклатура.Наименование) + """");
			Попытка
				НаборЗаписей.Записать();
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
		Иначе   // по идее такого быть не должно, на всякий случай
			НаборЗаписей = Неопределено; // очистка памяти приложения 1С
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
