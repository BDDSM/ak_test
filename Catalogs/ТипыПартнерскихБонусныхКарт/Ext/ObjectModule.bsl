Перем ИсторияИзмененияРеквизитов; //+++АК ILIK 2018.07.19 ИП-00019030

//+++АК ILIK 2018.07.19 ИП-00019030
Процедура ПередЗаписью(Отказ)
	Если Не РольДоступна("ПолныеПрава")
	   И Не УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.МожетРедактироватьТипыПартнерскихБонусныхКарт, Ложь) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Недостаточно прав для записи элемента!", Отказ);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Отказ Тогда
		ИсторияИзмененияРеквизитов = Новый Структура;
		
		МассивПолейДляПроверкиИзменений = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Наименование,ПроцентСкидки,БезлимитнаяУстановкаЛП");
		Для Каждого ИмяПоляДляПроверкиИзменений Из МассивПолейДляПроверкиИзменений Цикл
			Если ЭтотОбъект[ИмяПоляДляПроверкиИзменений] <> Ссылка[ИмяПоляДляПроверкиИзменений] Тогда
				СтруктураИзменений = Новый Структура("ЗначениеБыло, ЗначениеСтало", Ссылка[ИмяПоляДляПроверкиИзменений], ЭтотОбъект[ИмяПоляДляПроверкиИзменений]);
				ИсторияИзмененияРеквизитов.Вставить(ИмяПоляДляПроверкиИзменений, СтруктураИзменений);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

//+++АК ILIK 2018.07.19 ИП-00019030
Процедура ПриЗаписи(Отказ)
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Отказ Тогда
		Для Каждого СтруктураИзменений Из ИсторияИзмененияРеквизитов Цикл
			ЗаписьРС = РегистрыСведений.ИсторияИзмененияРеквизитов.СоздатьМенеджерЗаписи();
			ЗаписьРС.Период = ТекущаяДата();
			ЗаписьРС.Ссылка = Ссылка;
			ЗаписьРС.ИмяРеквизита = СтруктураИзменений.Ключ;
			ЗаписьРС.Автор = ПараметрыСеанса.ТекущийПользователь;
			ЗаполнитьЗначенияСвойств(ЗаписьРС, СтруктураИзменений.Значение);
			ЗаписьРС.Записать();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
