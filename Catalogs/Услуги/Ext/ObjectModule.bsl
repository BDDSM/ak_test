
Перем СуммаУслугиИзменена; //+++АК SHEP 2017.11.24 ИП-00017268

Процедура ПередЗаписью(Отказ)
	
	//+++АК SHEP 2017.11.24 ИП-00017268
	Если ЗначениеЗаполнено(Ссылка) И ЭтотОбъект.СуммаНормативная <> Ссылка.СуммаНормативная Тогда
		СуммаУслугиИзменена = Истина;
	Иначе
		СуммаУслугиИзменена = Ложь;
	КонецЕсли;
	//---АК SHEP 2017.11.24
	 
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	//+++АК SHEP 2017.11.24 ИП-00017268
	// При смене нормативной суммы в элементе справочника "Услуги" автоматически проставлять во всех комплектациях за текущий год нормативную сумму на вкладке "Услуга". 
	Если СуммаУслугиИзменена Тогда
		ПроставитьСуммуВКомплектациях();
	КонецЕсли;
	//---АК SHEP 2017.11.24
	
КонецПроцедуры

//+++АК SHEP 2017.11.24 ИП-00017268
Процедура ПроставитьСуммуВКомплектациях()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КомплектацияМагазинаПоСделкамСПоставщиком.Ссылка
		|ИЗ
		|	Документ.КомплектацияМагазинаПоСделкамСПоставщиком КАК КомплектацияМагазинаПоСделкамСПоставщиком
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КомплектацияМагазинаПоСделкамСПоставщиком.Услуги КАК КомплектацияМагазинаПоСделкамСПоставщикомУслуги
		|		ПО КомплектацияМагазинаПоСделкамСПоставщиком.Ссылка = КомплектацияМагазинаПоСделкамСПоставщикомУслуги.Ссылка
		|ГДЕ
		|	КомплектацияМагазинаПоСделкамСПоставщиком.Дата МЕЖДУ &ДатаНач И &ДатаКон
		|	И КомплектацияМагазинаПоСделкамСПоставщикомУслуги.Услуга = &Услуга
		|	И КомплектацияМагазинаПоСделкамСПоставщикомУслуги.СуммаНормативная <> &СуммаНормативная");
	Запрос.УстановитьПараметр("ДатаНач", НачалоГода(ТекущаяДата()));
	Запрос.УстановитьПараметр("ДатаКон", КонецГода(ТекущаяДата()));
	Запрос.УстановитьПараметр("Услуга", Ссылка);
	Запрос.УстановитьПараметр("СуммаНормативная", СуммаНормативная);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда Возврат; КонецЕсли;
	
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		ДокументОбъект = ВыборкаЗапроса.Ссылка.ПолучитьОбъект();
		
		МассивСтрок = ДокументОбъект.Услуги.НайтиСтроки(Новый Структура("Услуга", Ссылка));
		Для Каждого СтрокаУслуга Из МассивСтрок Цикл
			СтрокаУслуга.СуммаНормативная = СуммаНормативная;
		КонецЦикла;
		
		ДокументОбъект.Записать();
		//Сообщить("Перезаписан документ " + Строка(ДокументОбъект.Ссылка));
		
	КонецЦикла;
	
	Сообщить("Изменена нормативная сумма в услугах комплектаций за текущий год");
	
КонецПроцедуры
