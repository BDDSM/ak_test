
Процедура ПриКопировании(ОбъектКопирования)
	
	НеАктуальная = Ложь;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если Не ЭтоНовый()
		И НЕ ОбменДанными.Загрузка Тогда
		Если НеАктуальная И (НЕ Ссылка.НеАктуальная) Тогда
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Планограмма", Ссылка);
			Запрос.Текст = "ВЫБРАТЬ
			               |	СтруктурныеЕдиницы.Ссылка
			               |ИЗ
			               |	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
			               |ГДЕ
			               |	СтруктурныеЕдиницы.Планограмма = &Планограмма";
						   
			МассивТТ = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			Если МассивТТ.Количество() > 0  Тогда
				ТекстСообщения = "Нельзя установить планограмму не активной. Она действует для следующих магазинов:";
				Для Каждого ЭлементМассива Из МассивТТ Цикл
					ТекстСообщения = ТекстСообщения + Символы.ПС + "   - " + ЭлементМассива;
				КонецЦикла;	
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИД) Тогда
		ИД = ОбщегоНазначенияСервер.ПолучитьНовыйУникальныйИдентификатор("Планограммы", "ИД");
	КонецЕсли;

	
КонецПроцедуры
