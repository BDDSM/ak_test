////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура итображает картинку, которая в данный момент находится в хранилище.
//
// Параметры:
//  Нет.
//
Процедура ОтображениеИзображения()
	
	Если Хранилище.Получить() = Неопределено Тогда
		ЭлементыФормы.ПолеИзображения.Картинка = Новый Картинка();
		Возврат;
	КонецЕсли;
	
	Если ВидДанных = Перечисления.ВидыДополнительнойИнформацииОбъектов.Изображение Тогда
		ЭлементыФормы.ПолеИзображения.Картинка = Хранилище.Получить();
	Иначе
		ЭлементыФормы.ПолеИзображения.Картинка = Новый Картинка();
	КонецЕсли; 
	
КонецПроцедуры // ОтображениеИзображения()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события ПередОткрытием формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// ИТРП Лавров С.В. 29.05.2008 - начало
	// Исправление ошибки при непосредственном открытии формы
	Если Объект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	// ИТРП Лавров С.В. - окончание
	ЭлементыФормы.НадписьОбъекта.Значение = Объект.Метаданные().Синоним + ":";
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	Если Объект = Неопределено ИЛИ Объект.Пустая() Тогда
		ТекущийЭлемент = ЭлементыФормы.Объект;
	Иначе
		ТекущийЭлемент = ЭлементыФормы.Наименование;
	КонецЕсли;
	
	ОтображениеИзображения();
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события ПередЗаписью формы.
// 
Процедура ПередЗаписью(Отказ)
	
	Если ВидДанных = Перечисления.ВидыДополнительнойИнформацииОбъектов.Изображение Тогда
		Если Хранилище.Получить() = Неопределено
			ИЛИ Хранилище.Получить().Вид = ВидКартинки.Пустая Тогда
			ОбщегоНазначения.ПредупреждениеОбОшибке("Выберите изображение.");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект = Неопределено ИЛИ Объект.Пустая() Тогда
		ОбщегоНазначения.ПредупреждениеОбОшибке("Укажите владельца изображения.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПослеЗаписи формы.
//
Процедура ПослеЗаписи()
	
	Если ЭтаФорма.РежимВыбора Тогда
		ОповеститьОВыборе(Ссылка);
	КонецЕсли;
	
КонецПроцедуры // ПослеЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ДЕЙСТВИЙ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ 

// Процедура - обработчик действия формы Изменить изображение.
// Процедура изменяет текущее изображение на выбираемое из диалога выбора файлов.
//
Процедура ДействияФормыИзменитьИзображение(Кнопка)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.ПредварительныйПросмотр = Истина;
	ДиалогОткрытияФайла.Заголовок = "Выберите файл с фотографией";
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	ДиалогОткрытияФайла.Фильтр = 
	"Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|" 
	+ "Все файлы (*.*)|*.*|"
	+ "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
	+ "Формат JPEG (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
	+ "Формат TIFF (*.tif)|*.tif|"
	+ "Формат GIF (*.gif)|*.gif|"
	+ "Формат PNG (*.png)|*.png|"
	+ "Формат icon (*.ico)|*.ico|"
	+ "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|"; // картинки
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		пФайл = Новый Файл(ДиалогОткрытияФайла.ПолноеИмяФайла);
		//Если Константы.МаксимальныйРазмерФайловПриложенийМб.Получить() = 0 
		//	ИЛИ пФайл.Размер() <= Константы.МаксимальныйРазмерФайловПриложенийМб.Получить() / 2048 Тогда	
			ВыбранноеФото = Новый Картинка(ДиалогОткрытияФайла.ПолноеИмяФайла,Ложь);
			ЭлементыФормы.ПолеИзображения.Картинка = ВыбранноеФото;
			Хранилище = Новый ХранилищеЗначения(ВыбранноеФото, Новый СжатиеДанных());
			ИмяФайла = ДиалогОткрытияФайла.ПолноеИмяФайла;
		//Иначе
		//	Возврат;
		//КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	ОтображениеИзображения();
	
КонецПроцедуры // ДействияФормыИзменитьИзображение()

// Процедура - обработчик действия формы Сохранить изображение.
// Процедура сохраняет текущее изображение в файл на диск.
//
Процедура ДействияФормыСохранитьИзображение(Кнопка)
	
	ЗаписываемаяКартинка = ЭлементыФормы.ПолеИзображения.Картинка;
	Если ЗаписываемаяКартинка.Вид <> ВидКартинки.Пустая Тогда
		
		ДиалогЗаписиФайла                             = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогЗаписиФайла.Заголовок                   = "Укажите имя файла";
		ДиалогЗаписиФайла.ПолноеИмяФайла              = "";
		ДиалогЗаписиФайла.Фильтр                      = ЗаписываемаяКартинка.ФильтрИменФайлов(); // картинки
		ДиалогЗаписиФайла.ПроверятьСуществованиеФайла = Истина;
		
		Если Не ДиалогЗаписиФайла.Выбрать() Тогда
			Возврат;
		КонецЕсли;
		
		Попытка
			ЗаписываемаяКартинка.Записать(ДиалогЗаписиФайла.ПолноеИмяФайла);
		Исключение
			ОбщегоНазначения.ПредупреждениеОбОшибке("Ошибка при записи файла: " + ДиалогЗаписиФайла.ПолноеИмяФайла + Символы.ПС
			+ ОписаниеОшибки() + Символы.ПС + "Файл не записан.");
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры // ДействияФормыСохранитьИзображение()

Процедура ОткрытьИзображение(Кнопка)
	Если ЗначениеЗаполнено(Хранилище) Тогда
		РаботаСФайлами.ОткрытьФайлы(Ссылка);                       
	КонецЕсли;
КонецПроцедуры
