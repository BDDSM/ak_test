
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("Режим", Режим);
	
	Если Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.ГруппыИЭлементы И Параметры.МножественныйВыбор = Истина Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоГруппа");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Истина;
		ЭлементОтбора.Использование = Истина;
	Иначе
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
	Если Режим = "С остатками по складу" Тогда
	
		Список.ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникНоменклатура.Наименование КАК Наименование,
		|	ВложенныйЗапрос.КоличествоОстаток
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеТиповРозничныхТочекИТорговыхМарок КАК СоответствиеТиповРозничныхТочекИТорговыхМарок
		|			ПО СправочникНоменклатура.ТорговаяМарка = СоответствиеТиповРозничныхТочекИТорговыхМарок.ТорговаяМарка
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|			ТоварыНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		|		ИЗ
		|			РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки) КАК ВложенныйЗапрос
		|		ПО СправочникНоменклатура.Ссылка = ВложенныйЗапрос.Номенклатура
		|ГДЕ
		|	СправочникНоменклатура.ПометкаУдаления = ЛОЖЬ
		|	И СправочникНоменклатура.ЭтоГруппа = ЛОЖЬ
		|	И Выбор когда &ЭтоПродавец ТОгда 
		|	ВЫбор когда СправочникНоменклатура.ЭтоГруппа Тогда Истина Иначе
		|	СоответствиеТиповРозничныхТочекИТорговыхМарок.ТипРозничнойТочки = &ТипРозничнойТочки Конец
		|	Иначе Истина Конец";
		
		Список.Параметры.УстановитьЗначениеПараметра("Склад",	Параметры.Склад);
		//Список.Параметры.УстановитьЗначениеПараметра("Дата",	Параметры.Дата);
		ЕстьОтборПоОстаткам	= Истина;
		ТолькоСОстатками	= Истина;
		СкладНаименование	= Строка(Параметры.Склад);
	
	ИначеЕсли Режим = "По товарному ассортименту"
			И Параметры.Свойство("ТорговаяТочка")
			И ЗначениеЗаполнено(Параметры.ТорговаяТочка) Тогда
		
		Список.ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникНоменклатура.Ссылка КАК Ссылка,
		|	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
		|	СправочникНоменклатура.Предопределенный КАК Предопределенный,
		|	СправочникНоменклатура.Родитель КАК Родитель,
		|	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
		|	СправочникНоменклатура.Код КАК Код,
		|	СправочникНоменклатура.Наименование КАК Наименование,
		|	СправочникНоменклатура.Артикул КАК Артикул,
		|	СправочникНоменклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиницаИзмерения
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныйАссортиментТочек.СрезПоследних(, ТорговаяТочка = &ТорговаяТочка) КАК ТоварныйАссортимент
		|		ПО (ТоварныйАссортимент.Номенклатура = СправочникНоменклатура.Ссылка)
		|			И (НЕ ТоварныйАссортимент.Выведена)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеТиповРозничныхТочекИТорговыхМарок КАК СоответствиеТиповРозничныхТочекИТорговыхМарок
		|			ПО СправочникНоменклатура.ТорговаяМарка = СоответствиеТиповРозничныхТочекИТорговыхМарок.ТорговаяМарка
		|ГДЕ
		|	НЕ СправочникНоменклатура.ПометкаУдаления
		|	И Выбор когда &ЭтоПродавец ТОгда 
		|	ВЫбор когда СправочникНоменклатура.ЭтоГруппа Тогда Истина Иначе
		|	СоответствиеТиповРозничныхТочекИТорговыхМарок.ТипРозничнойТочки = &ТипРозничнойТочки Конец
		|	Иначе Истина Конец";
		Список.Параметры.УстановитьЗначениеПараметра("ТорговаяТочка", Параметры.ТорговаяТочка);
		
	ИначеЕсли (Параметры.Свойство("Склад")
			И ЗначениеЗаполнено(Параметры.Склад)
			И Параметры.Склад.ВидСклада = Перечисления.ВидыСкладов.Оптовый)
			И НЕ Параметры.Склад.ЭтоЗонаОтгрузки Тогда
		
		Список.ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникНоменклатура.Ссылка КАК Ссылка,
		|	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
		|	СправочникНоменклатура.Предопределенный КАК Предопределенный,
		|	СправочникНоменклатура.Родитель КАК Родитель,
		|	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
		|	СправочникНоменклатура.Код КАК Код,
		|	СправочникНоменклатура.Наименование КАК Наименование,
		|	СправочникНоменклатура.Артикул КАК Артикул,
		|	СправочникНоменклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА СправочникНоменклатура.ЭтоГруппа
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ВЗ_Запрос.КоличествоОстаток, 0)
		|	КОНЕЦ КАК КоличествоОстаток
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			ВложенныйЗапрос.Ссылка КАК Ссылка,
		|			ВложенныйЗапрос.КоличествоОстаток КАК КоличествоОстаток
		|		ИЗ
		|			(ВЫБРАТЬ
		|				СправочникНоменклатура.Ссылка КАК Ссылка,
		|				ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
		|			ИЗ
		|				(ВЫБРАТЬ
		|					СправочникНоменклатура.Ссылка КАК Ссылка
		|				ИЗ
		|					РегистрСведений.ДоступностьТоваровНаСкладах КАК ДоступностьТоваровНаСкладах
		|						ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|						ПО ДоступностьТоваровНаСкладах.Номенклатура = СправочникНоменклатура.Ссылка
		|				ГДЕ
		|					ДоступностьТоваровНаСкладах.Склад = &Склад
		|				
		|				ОБЪЕДИНИТЬ ВСЕ
		|				
		|				ВЫБРАТЬ
		|					СправочникНоменклатура.Ссылка
		|				ИЗ
		|					РегистрСведений.ДоступностьНоменклатураВОперацияхОрдеров КАК ДоступностьТоваровНаСкладах
		|						ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|						ПО (ДоступностьТоваровНаСкладах.Номенклатура = СправочникНоменклатура.Ссылка
		|								ИЛИ ДоступностьТоваровНаСкладах.Номенклатура = СправочникНоменклатура.Родитель
		|								ИЛИ ДоступностьТоваровНаСкладах.Номенклатура = СправочникНоменклатура.Родитель.Родитель)
		|				ГДЕ
		|					ДоступностьТоваровНаСкладах.Пользователь = &Пользователь) КАК СправочникНоменклатура
		|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладахОстатки
		|					ПО (ТоварыНаСкладахОстатки.Номенклатура = СправочникНоменклатура.Ссылка)) КАК ВложенныйЗапрос) КАК ВЗ_Запрос
		|		ПО СправочникНоменклатура.Ссылка = ВЗ_Запрос.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеТиповРозничныхТочекИТорговыхМарок КАК СоответствиеТиповРозничныхТочекИТорговыхМарок
		|		ПО СправочникНоменклатура.ТорговаяМарка = СоответствиеТиповРозничныхТочекИТорговыхМарок.ТорговаяМарка
		|ГДЕ
		|	СправочникНоменклатура.ПометкаУдаления = ЛОЖЬ
		|	И Выбор когда &ЭтоПродавец ТОгда 
		|	ВЫбор когда СправочникНоменклатура.ЭтоГруппа Тогда Истина Иначе
		|	Выбор когда СправочникНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар) ТОгда
		|	СоответствиеТиповРозничныхТочекИТорговыхМарок.ТипРозничнойТочки = &ТипРозничнойТочки Иначе Истина Конец  Конец
		|	Иначе Истина Конец";

		
		Список.Параметры.УстановитьЗначениеПараметра("Склад"		, Параметры.Склад);
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь"	, ПараметрыСеанса.ТекущийПользователь);
		ЕстьОтборПоОстаткам	= Истина;
		
	Иначе
		
		Список.ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникНоменклатура.Ссылка КАК Ссылка,
		|	СправочникНоменклатура.ПометкаУдаления КАК ПометкаУдаления,
		|	СправочникНоменклатура.Предопределенный КАК Предопределенный,
		|	СправочникНоменклатура.Родитель КАК Родитель,
		|	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
		|	СправочникНоменклатура.Код КАК Код,
		|	СправочникНоменклатура.Наименование КАК Наименование,
		|	СправочникНоменклатура.Артикул КАК Артикул,
		|	СправочникНоменклатура.БазоваяЕдиницаИзмерения КАК БазоваяЕдиницаИзмерения
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеТиповРозничныхТочекИТорговыхМарок КАК СоответствиеТиповРозничныхТочекИТорговыхМарок
		|		ПО СправочникНоменклатура.ТорговаяМарка = СоответствиеТиповРозничныхТочекИТорговыхМарок.ТорговаяМарка
		|ГДЕ
		|	СправочникНоменклатура.ПометкаУдаления = ЛОЖЬ
		|	И Выбор когда &ЭтоПродавец ТОгда 
		|	ВЫбор когда СправочникНоменклатура.ЭтоГруппа Тогда Истина Иначе
		|	Выбор когда СправочникНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар) ТОгда
		|	СоответствиеТиповРозничныхТочекИТорговыхМарок.ТипРозничнойТочки = &ТипРозничнойТочки Иначе Истина Конец  Конец
		|	Иначе Истина Конец";
		
		ЕстьОтборПоОстаткам	= Ложь;
		
	КонецЕсли;
	
		//Если ЗначениеЗаполнено(ЗаказПоставщику) Тогда
		//	Список.Параметры.УстановитьЗначениеПараметра("ЗаказПоставщику",ЗаказПоставщику);
		//КонецЕсли;	
	//КонецЕсли;
	
	//+++АК KIRN 2018.03.20 ИП-00018103
	ОбщиеПроцедуры.НоменклатураФормаУпрПриСозданииНаСервере(ЭтаФорма, "Список");
	//---АК KIRN 

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Иногда при открытии нужно показывать только то, что есть на остатках
	//Если ТолькоСОстатками Тогда
	//	ФильтрТолькоСОстаткамиПриИзменении(Неопределено);
	//КонецЕсли;
	
	ИзменитьФильтр("КоличествоОстаток", ВидСравненияКомпоновкиДанных.Больше, 0, ТолькоСОстатками);
	//Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	Если Режим = "С остатками по складу" Тогда
		
		//Для наглядности
		ЭтаФорма.АвтоЗаголовок		= Ложь;
		Заголовок					= "Номенклатура. " + СкладНаименование;
	КонецЕсли;
	
	Элементы.ФильтрТолькоСОстатками.Видимость = ЕстьОтборПоОстаткам;
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрНаименованиеПриИзменении(Элемент)

	ИзменитьФильтр("Наименование", ВидСравненияКомпоновкиДанных.Содержит, Наименование, ЗначениеЗаполнено(Наименование));
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрТолькоСОстаткамиПриИзменении(Элемент)
	
	ИзменитьФильтр("КоличествоОстаток", ВидСравненияКомпоновкиДанных.Больше, 0, ТолькоСОстатками);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьФильтр(Имя, ВидСравнения, Значение, Использование)
	
	//Алгоритм позволяет фильтрам действовать
	//как независимо, так и совместно

	ЭлементОтбора = Неопределено;
	
	//Пытаемся найти элемент отбора
	Для Каждого Элемент Из Список.Отбор.Элементы Цикл
		Если Строка(Элемент.ЛевоеЗначение) = Имя Тогда
			ЭлементОтбора = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//Удаляем элемент, если такой есть
	Если ЭлементОтбора <> Неопределено Тогда
		Список.Отбор.Элементы.Удалить(ЭлементОтбора);
	КонецЕсли;
	
	Если Не Использование Тогда
		Возврат;
	КонецЕсли;
	
	//Создаем
	ЭлементОтбора					= Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных(Имя); 
	ЭлементОтбора.ВидСравнения		= ВидСравнения; 
	ЭлементОтбора.РежимОтображения	= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный; 
		
	ЭлементОтбора.ПравоеЗначение	= ?(ТипЗнч(Значение)=Тип("Строка"), СокрЛП(Значение), Значение);
	ЭлементОтбора.Использование		= Истина;
	
КонецПроцедуры

