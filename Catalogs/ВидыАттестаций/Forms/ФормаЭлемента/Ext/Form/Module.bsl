
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПараметрыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступностьЭлементовФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	УдалитьВременныеФайлыКлиент(Строка(КаталогВременныхФайлов) + "\" + Этаформа.УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДобавитьИнструкцию(Команда)
	
	Если ПроверитьВозможностьРаботыСИнструкциями() Тогда
		
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		
		ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Документ'; en = 'Document'") + "(*.doc, *.docx)|*.doc;*.docx";
		
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		
		ДиалогОткрытияФайла.Заголовок = "Выберите файл";
		
		НачатьПомещениеФайлов(Новый ОписаниеОповещения("ОбработатьВыборФайлаЗакрытие", ЭтаФорма), , ДиалогОткрытияФайла, Истина, УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьИнструкцию(Команда)
	
	Оповещение = Новый ОписаниеОповещения("УдалитьИнструкциюВопросЗавершение", ЭтаФорма);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Текущая инструкция будет удалена! Продолжить?'", "ru"), РежимДиалогаВопрос.ДаНет, 15 );
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьИнструкцию(Команда)
	
	ПолноеИмяФайла = "";
	
	СохранитьИнструкциюНаДискеСервер(КаталогВременныхФайлов, ПолноеИмяФайла); 
	
	Если ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
		ЗапуститьПриложение(ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОпевещений

&НаКлиенте
Процедура ОбработатьВыборФайлаЗакрытие(Результат, ДополнительныеПараметры)Экспорт 
	
	Если Результат <> Неопределено Тогда
		Для Каждого ИмяФайла Из Результат Цикл
			ОбработатьВыбранныйФайлКлиент(ИмяФайла);
		КонецЦикла;
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Файл не выбран!'; en = 'File not selected!'"))
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИнструкциюВопросЗавершение(Результат, ДополнительныеПараметры)Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьТекущуюИнструкциюСервер();
		ОбновитьДоступностьЭлементовФормыКлиент();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПараметрыФормы()
	
	КаталогВременныхФайлов = Аттестация.ПолучитьКаталогВременныхФайлов(); 

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьЭлементовФормыКлиент()

	ИнструкцияДобавлена = ЗначениеЗаполнено(Объект.ИмяФайла);
	
	Элементы.КомандаОткрытьИнструкцию.Доступность = ИнструкцияДобавлена;
	Элементы.КомандаУдалитьИнструкцию.Доступность = ИнструкцияДобавлена;

КонецПроцедуры

&НаКлиенте
Функция ПроверитьВозможностьРаботыСИнструкциями()

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, "Перед продолжением необходимо записать элемент!");
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВыбранныйФайлКлиент(Адрес)
	
	//Определение имени файла
	ИмяФайла = Адрес.Имя;
	
	ЭтоКаталог = Найти(ИмяФайла,"\");
	Пока ЭтоКаталог > 0 Цикл
		ИмяФайла = Сред(ИмяФайла, ЭтоКаталог+1, СтрДлина(ИмяФайла));
		ЭтоКаталог = Найти(ИмяФайла,"\");
	КонецЦикла;
	
	//Заполнение реквизитов объекта на сервере
	ОбработатьВыбранныйФайлСервер(ИмяФайла, Адрес.Хранение);
	
	ОбновитьДоступностьЭлементовФормыКлиент();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыбранныйФайлСервер(ИмяФайла, Хранение)
	
	Объект.ИмяФайла = ИмяФайла;
	
	ЭлементОбъект = РеквизитФормыВЗначение("Объект");
	 
	ЭлементОбъект.Инструкция = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Хранение), Новый СжатиеДанных(9));
	
	ЭлементОбъект.Записать();
	Модифицированность = Ложь;
	
	УдалитьИзВременногоХранилища(Хранение);
	
	ЗначениеВРеквизитФормы(ЭлементОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура УдалитьТекущуюИнструкциюСервер()
	
	ЭлементОбъект = РеквизитФормыВЗначение("Объект");
	
	ЭлементОбъект.Инструкция = Неопределено;
	ЭлементОбъект.ИмяФайла = "";
	
	ЭлементОбъект.Записать();
	Модифицированность = Ложь;
	
	ЗначениеВРеквизитФормы(ЭлементОбъект, "Объект");
	
КонецПроцедуры
	
&НаСервере
Процедура СохранитьИнструкциюНаДискеСервер(КаталогВременныхФайлов, ПолноеИмяФайла);

	ПолноеИмяФайла = Строка(КаталогВременныхФайлов) + "\" + Этаформа.УникальныйИдентификатор + "\" +
			Строка(Новый УникальныйИдентификатор) + ".doc";

	ЭлементОбъект = РеквизитФормыВЗначение("Объект");
	
    Аттестация.СохранитьФайлНаДискеИзХранилищаСервер(ЭлементОбъект.Инструкция, ПолноеИмяФайла, Объект.Ссылка);	
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВременныеФайлыКлиент(ВременныйКаталог)
	
	Попытка
		УдалитьФайлы(ВременныйКаталог);
	Исключение
	КонецПопытки
	
КонецПроцедуры // УдалитьВременныеФайлыHTMLКлиент()

#КонецОбласти

