
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОкончаниеВводаГостаТу" Тогда
		
		Список.Отбор.Элементы.Очистить();
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ПолучитьМассивГостовТу();
		
		Элементы.Список.Обновить();	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивГостовТу()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ГосударственныеСтандартыИТехническиеУсловияСостав.Ссылка
	|ИЗ
	|	Справочник.ГосударственныеСтандартыИТехническиеУсловия.Состав КАК ГосударственныеСтандартыИТехническиеУсловияСостав
	|ГДЕ
	|	ГосударственныеСтандартыИТехническиеУсловияСостав.Номенклатура В (&Номенклатура)");
	
	// Найдем всех родителей, т.к. в ГОСТах и ТУ могут указываться группы
	МассивЭлементов = НастройкаПравДоступа.ПолучитьМассивРодительскихЭлементов(Номенклатура); 
	
	// Добавим владельца характеристики
	МассивЭлементов.Добавить(Номенклатура);
	
	Запрос.УстановитьПараметр("Номенклатура", МассивЭлементов);  
	
	СписокГостов = Новый СписокЗначений;
	СписокГостов.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат СписокГостов;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Номенклатура")Тогда
		Номенклатура = Параметры.Номенклатура;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ЗначениеЗаполнено(Номенклатура)Тогда// И Не Копирование Тогда
		
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура();
		
		ЗначенияЗаполнения = Новый Структура("Родитель", Родитель);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		Если Копирование И ЗначениеЗаполнено(Элемент.ТекущаяСтрока) Тогда
			ПараметрыФормы.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("Номенклатура", Номенклатура);
		
		ОткрытьФорму("Справочник.ГосударственныеСтандартыИТехническиеУсловия.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

