
//+++АК VERT 2018.03.02 ИП-00017712
Функция ПолучитьСоединениеСбазойБух(Параметрыданных) Экспорт
	Перем ПодключениеУстановлено;
	Соединение = Неопределено;
	Если ПараметрыДанных <> Неопределено И ПараметрыДанных.Свойство("Соединение", Соединение) Тогда 
		ПодключениеУстановлено = Истина;
	Иначе
		Соединение = ОбменСБП2_0.ВернутьПодключениеКБазеБУ(ПодключениеУстановлено);	
		ПараметрыДанных.Вставить("Соединение", Соединение);
	КонецЕсли;
	
	Если Не ПодключениеУстановлено Тогда 
		ВызватьИсключение "Ошибка подключения к базе БУХ";
	КонецЕсли;
	
	Возврат Соединение;
КонецФункции

//+++АК VERT 2018.03.05 ИП-00017712
Функция ВозможностьПроверки() Экспорт
	Возврат ДопМодульСервер.ПолучитьЗначениеПраваПользователя("ВыполнениеПроверки", Ложь) ;
КонецФункции

//+++АК LATV 2018.08.07 ИП-00017712.02
// Заполнение структуры параметров по данным табличной части "Параметры"
//
// Возвращаемое значение:
//   Структура   - Ключ = Значение
//
Функция СтруктураПараметров(Проверка) Экспорт

	Если Не ЗначениеЗаполнено(Проверка) Тогда
		Возврат Новый Структура("ВидОтчета", "");
	КонецЕсли;
	
	ПараметрыДанных = Новый Структура;
	Для Каждого ЭлементПараметр Из Проверка.Параметры Цикл
		Если ЭлементПараметр.Выражение Тогда
			ПараметрыДанных.Вставить(ЭлементПараметр.Наименование, Вычислить(ЭлементПараметр.ЗначениеПараметра));
		Иначе
			ПараметрыДанных.Вставить(ЭлементПараметр.Наименование, ЭлементПараметр.ЗначениеПараметра);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыДанных.Вставить("НаименованиеПроверки",	Проверка.Наименование);
	ПараметрыДанных.Вставить("ВидОтчета",				Проверка.ВидОтчета);
	ПараметрыДанных.Вставить("Организация",				Проверка.Организация);
	
	// Период
	ПериодОтчета = Проверка.ПериодОтчета.Получить();
	Если ПериодОтчета = Неопределено Тогда
		ПериодОтчета = Новый СтандартныйПериод;
	КонецЕсли;
	ПараметрыДанных.Вставить("ПериодОтчета", ПериодОтчета);
	
	// Соответствия
	СоответствиеФин = Новый Соответствие;
	СоответствиеБух = Новый Соответствие;
	
	Если Проверка.СоответствиеЗначений.Количество() > 0 Тогда
		Для Каждого ЭлементСоответствия ИЗ Проверка.СоответствиеЗначений Цикл
			Если НЕ ПустаяСтрока(ЭлементСоответствия.Наименование) Тогда
				Если Не ПустаяСтрока(ЭлементСоответствия.ЗначениеВБазеБух) Тогда
					СоответствиеБух.Вставить(ЭлементСоответствия.ЗначениеВБазеБух, ЭлементСоответствия.Наименование);
				КонецЕсли;
				Если Не ПустаяСтрока(ЭлементСоответствия.ЗначениеПараметраФин) Тогда 
					СоответствиеФин.Вставить(ЭлементСоответствия.ЗначениеПараметраФин, ЭлементСоответствия.Наименование);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыДанных.Вставить("СоответствиеФин", СоответствиеФин);
	ПараметрыДанных.Вставить("СоответствиеБух", СоответствиеБух);
	
	// Счета
	СписокСчетовБухСумма	= Новый Массив;
	СписокСчетовБухРазность	= Новый Массив;
	Если ЗначениеЗаполнено(ПараметрыДанных.СчетБух) Тогда
		ВсеСчетаБух = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрыДанных.СчетБух);
		Для Каждого СчетБух Из ВсеСчетаБух Цикл
			Если Лев(СчетБух, 1) = "-" Тогда
				СписокСчетовБухРазность.Добавить(Сред(СчетБух, 2));
			Иначе
				СписокСчетовБухСумма.Добавить(СчетБух);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ПараметрыДанных.Вставить("СписокСчетовБухСумма",	СписокСчетовБухСумма);
	ПараметрыДанных.Вставить("СписокСчетовБухРазность",	СписокСчетовБухРазность);
	
	СписокСчетовФинСумма	= Новый Массив;
	СписокСчетовФинРазность	= Новый Массив;
	Если ЗначениеЗаполнено(ПараметрыДанных.СчетФин) Тогда
		ВсеСчетаФин = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПараметрыДанных.СчетФин);
		Для Каждого СчетФин Из ВсеСчетаФин Цикл
			Если Лев(СчетФин, 1) = "-" Тогда
				СписокСчетовФинРазность.Добавить(Сред(СчетФин, 2));
			Иначе
				СписокСчетовФинСумма.Добавить(СчетФин);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ПараметрыДанных.Вставить("СписокСчетовФинСумма",	СписокСчетовФинСумма);
	ПараметрыДанных.Вставить("СписокСчетовФинРазность",	СписокСчетовФинРазность);
	
	Возврат ПараметрыДанных;

КонецФункции
