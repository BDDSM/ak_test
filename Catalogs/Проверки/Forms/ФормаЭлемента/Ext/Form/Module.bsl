
#Область ОбработчикиСобытийФормы

//+++АК LATV 2018.06.08 ИП-00017712.01
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ЭтаФорма.ПериодОтчета = ТекущийОбъект.ПериодОтчета.Получить();

КонецПроцедуры

//+++АК LATV 2018.06.08 ИП-00017712.01
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Справочники.Проверки.ВозможностьПроверки() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У пользователя отсутствуют права на выполнение проверок",,,, Отказ);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьВидыОтчета();
	
	Если Не Параметры.ЗначениеКопирования.Пустая() Тогда
		ПериодОтчетаХранилище = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЗначениеКопирования, "ПериодОтчета");
		ЭтаФорма.ПериодОтчета = ПериодОтчетаХранилище.Получить();
	КонецЕсли;

КонецПроцедуры

//+++АК LATV 2018.06.08 ИП-00017712.01
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.ПериодОтчета = Новый ХранилищеЗначения(ЭтаФорма.ПериодОтчета);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

//+++АК LATV 2018.06.08 ИП-00017712.01
&НаКлиенте
Процедура ВидОтчетаПриИзменении(Элемент)

	ЗаполнитьДанныеОбработки();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.06.08 ИП-00017712.01
&НаСервере
Процедура ЗаполнитьВидыОтчета()

	Элементы.ВидОтчета.СписокВыбора.Очистить();
	
	ВидыОтчета = Отчеты.СравнениеОборотовМеждуБазами.ВидыОтчета();
	Для Каждого ВидОтчета Из ВидыОтчета Цикл
		Элементы.ВидОтчета.СписокВыбора.Добавить(ВидОтчета.Значение, ВидОтчета.Представление)
	КонецЦикла;

КонецПроцедуры

//+++АК LATV 2018.06.08 ИП-00017712.01
&НаСервере
Процедура ЗаполнитьДанныеОбработки()

	Если ПустаяСтрока(Объект.ВидОтчета) Тогда
		Объект.Параметры.Очистить();
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета = Отчеты.СравнениеОборотовМеждуБазами.ПараметрыОтчетаПоВиду(Объект.ВидОтчета);
	Для Каждого ПараметрОтчета Из ПараметрыОтчета Цикл
		
		Отбор = Новый Структура("Наименование", ПараметрОтчета.Имя);
		Если Объект.Параметры.НайтиСтроки(Отбор).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйПараметр = Объект.Параметры.Добавить();
		НовыйПараметр.Наименование		= ПараметрОтчета.Имя;
		НовыйПараметр.ЗначениеПараметра = ПараметрОтчета.Тип.ПривестиЗначение(НовыйПараметр.ЗначениеПараметра);
		НовыйПараметр.Описание			= ПараметрОтчета.Описание;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
