
//+++АК LATV 2018.06.10 ИП-00017712.01
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Если ПараметрКоманды.Количество() = 1 Тогда
		ПараметрыПроверки = ПараметрыПроверки(ПараметрКоманды[0]);
		ОткрытьФормуОтчета(ПараметрКоманды, ПараметрыВыполненияКоманды, ПараметрыПроверки);
		
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ПараметрКоманды",				ПараметрКоманды);
	ПараметрыОповещения.Вставить("ПараметрыВыполненияКоманды",	ПараметрыВыполненияКоманды);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаПараметров", ЭтотОбъект, ПараметрыОповещения);
	ОткрытьФорму("Справочник.Проверки.Форма.ФормаВводаПараметров",,,,,, Оповещение);

КонецПроцедуры

//+++АК LATV 2018.06.10 ИП-00017712.01
&НаКлиенте
Процедура ПослеВводаПараметров(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуОтчета(ДополнительныеПараметры.ПараметрКоманды, ДополнительныеПараметры.ПараметрыВыполненияКоманды, Результат);

КонецПроцедуры

//+++АК LATV 2018.06.10 ИП-00017712.01
&НаКлиенте
Процедура ОткрытьФормуОтчета(ПараметрКоманды, ПараметрыВыполненияКоманды, ПараметрыПроверки)

	ЗначенияЗаполнения = Новый Структура("ПериодОтчета, Организация, ТолькоРасхождения");
	ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, ПараметрыПроверки);
	ЗначенияЗаполнения.Вставить("Проверки", ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии",	Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения",		ЗначенияЗаполнения);
	
	ОткрытьФорму("Отчет.СравнениеОборотовМеждуБазами.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, Истина
		, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);

КонецПроцедуры

//+++АК LATV 2018.06.10 ИП-00017712.01
&НаСервере
Функция ПараметрыПроверки(Проверка)

	Возврат Справочники.Проверки.СтруктураПараметров(Проверка);

КонецФункции
