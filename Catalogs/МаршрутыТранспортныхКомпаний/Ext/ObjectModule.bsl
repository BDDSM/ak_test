
Процедура ПередЗаписью(Отказ)
	
	Если ЭтотОбъект.ЭтоНовый() Тогда
		ЭтотОбъект.ДатаСоздания = ТекущаяДата();
		ЭтотОбъект.Автор		= ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	#Область АК_ОтключенныйКод
	//Если НЕ ЭтотОбъект.ЕдиныйТариф Тогда
	//	Если НЕ ЭтотОбъект.Сумма = 0 Тогда
	//		ЭтотОбъект.Сумма = 0;
	//	КонецЕсли;
	//	Если НЕ ЭтотОбъект.ВариантРасчетаНДС = Перечисления.ВариантыРасчетаНДС.ПустаяСсылка() Тогда
	//		ЭтотОбъект.ВариантРасчетаНДС = Перечисления.ВариантыРасчетаНДС.ПустаяСсылка();
	//	КонецЕсли;
	//	Если НЕ ЭтотОбъект.СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка() Тогда
	//		ЭтотОбъект.СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
	//	КонецЕсли;
	//	Если НЕ ЭтотОбъект.СуммаНДС = 0 Тогда
	//		ЭтотОбъект.СуммаНДС = 0;
	//	КонецЕсли;
	//КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

Функция АК_ПолучитьПоследнийПериодТарифаНаДоставку()
	Перем Запрос;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
"ВЫБРАТЬ
|	МАКСИМУМ(ТарифыНаДоставкуПоМаршрутуСрезПоследних.Период) КАК Период
|ИЗ РегистрСведений.ТарифыНаДоставкуПоМаршруту.СрезПоследних(&Дата, Маршрут = &Маршрут ) КАК ТарифыНаДоставкуПоМаршрутуСрезПоследних
|";
	
	Запрос.Параметры.Вставить("Дата", ТекущаяДата());
	Запрос.Параметры.Вставить("Маршрут", ЭтотОбъект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Период = Выборка.Период;	
	Иначе
		Период = Дата(1,1,1);
	КонецЕсли;
	
	Возврат Период;
	
КонецФункции

//+++АК sole 2018.05.29 ИП-00018724.02
&НаСервере
Функция АК_ПолучитьТаблицуТарифовИзРегистра() Экспорт
	// Процедура вызывается из процедуры "АК_ЗаполнитьСведенияОТарифе", модуля формы
	
	Перем Запрос;
	
	Период = АК_ПолучитьПоследнийПериодТарифаНаДоставку();
	
	Запрос = Новый Запрос();
	Запрос.Текст =
"ВЫБРАТЬ
|	ТарифыНаДоставкуПоМаршруту.Период,
|	ТарифыНаДоставкуПоМаршруту.ВесОт,
|	ТарифыНаДоставкуПоМаршруту.ЕдиныйТариф,
|	ТарифыНаДоставкуПоМаршруту.Сумма,
|	ТарифыНаДоставкуПоМаршруту.ВариантРасчетаНДС,
|	ТарифыНаДоставкуПоМаршруту.СтавкаНДС,
|	ТарифыНаДоставкуПоМаршруту.СуммаНДС,
|	ТарифыНаДоставкуПоМаршруту.ЦенаЗаКг
|
|ИЗ РегистрСведений.ТарифыНаДоставкуПоМаршруту КАК ТарифыНаДоставкуПоМаршруту
|ГДЕ
|		ТарифыНаДоставкуПоМаршруту.Период = &Период
|	И	ТарифыНаДоставкуПоМаршруту.Маршрут = &Маршрут
|
|УПОРЯДОЧИТЬ ПО ТарифыНаДоставкуПоМаршруту.ВесОт
|";
	Запрос.Параметры.Вставить("Период", Период);	
	Запрос.Параметры.Вставить("Маршрут", ЭтотОбъект.Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//+++АК sole 2018.05.30 ИП-00018724.02
&НаСервере
Процедура АК_ОбновитьТарифыНаДоставкуПоМаршрутуЕслиНеобходимо(СтруктураТарифа) Экспорт
	
	// Процедура вызывается из процедуры "ПриЗаписиНаСервере", модуля формы
	
	Перем Запрос;
	
	Период = АК_ПолучитьПоследнийПериодТарифаНаДоставку();
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	БПР = Новый Структура();
	БПР.Вставить("Запрос", Запрос);
	БПР.Вставить("Период", Период);
	БПР.Вставить("СтруктураТарифа", СтруктураТарифа);
	
	ЕстьРазличия = АК_СравнитьТарифыНаДоставку(БПР);
	
	Если ЕстьРазличия Тогда
		
		Если
				НЕ РольДоступна("ПолныеПрава")
			И	РольДоступна("Перевозчик")  
			
		Тогда
			Сообщить("Для роли ""Перевозчик"" запрещено, обновление регистра сведений ""Тарифы на доставку по маршруту"" !");
		Иначе
			АК_ОбновитьТарифыНаДоставку(БПР);	
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры

//+++АК sole 2018.05.30 ИП-00018724.02
Функция АК_СравнитьТарифыНаДоставку(БПР)
	
	Перем Запрос;
	
	СтруктураТарифа = БПР.СтруктураТарифа;
	
	Запрос = БПР.Запрос;
	Запрос.Текст =
"ВЫБРАТЬ
|	&Период КАК Период,
|	&ЕдиныйТариф КАК ЕдиныйТариф,
|	&ВариантРасчетаНДС КАК ВариантРасчетаНДС,
|	&СтавкаНДС КАК СтавкаНДС,
|	т.ВесОт,
|	т.Сумма,
|	т.СуммаНДС,
|	т.ЦенаЗаКг,
|	&Перевозчик КАК Перевозчик
|
|		ПОМЕСТИТЬ втНовыйТариф
|
|ИЗ &Таблица КАК т
|;
|///////////////////////////////////////////////////////////
|ВЫБРАТЬ ПЕРВЫЕ 1
|	Истина КАК ЕстьРазличия 
|ИЗ втНовыйТариф
|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифыНаДоставкуПоМаршруту КАК ТарифыНаДоставкуПоМаршруту  ПО
|			ТарифыНаДоставкуПоМаршруту.Период = &ПериодДействующегоТарифа	
|		И	ТарифыНаДоставкуПоМаршруту.Маршрут = &Маршрут  
|       И	ТарифыНаДоставкуПоМаршруту.ЕдиныйТариф = втНовыйТариф.ЕдиныйТариф 
|       И	ТарифыНаДоставкуПоМаршруту.ВариантРасчетаНДС = втНовыйТариф.ВариантРасчетаНДС 
|       И	ТарифыНаДоставкуПоМаршруту.СтавкаНДС = втНовыйТариф.СтавкаНДС 
|       И	ТарифыНаДоставкуПоМаршруту.ВесОт = втНовыйТариф.ВесОт 
|       И	ТарифыНаДоставкуПоМаршруту.Сумма = втНовыйТариф.Сумма 
|       И	ТарифыНаДоставкуПоМаршруту.СуммаНДС = втНовыйТариф.СуммаНДС 
|       И	ТарифыНаДоставкуПоМаршруту.Перевозчик = втНовыйТариф.Перевозчик 
|       И	ТарифыНаДоставкуПоМаршруту.ЦенаЗаКг = втНовыйТариф.ЦенаЗаКг
|
|ГДЕ  ТарифыНаДоставкуПоМаршруту.Маршрут ЕСТЬ NULL
|";
	Запрос.Параметры.Вставить("ПериодДействующегоТарифа", БПР.Период);
	Запрос.Параметры.Вставить("Маршрут", ЭтотОбъект.Ссылка);
	Запрос.Параметры.Вставить("Период", СтруктураТарифа.Период);
	Запрос.Параметры.Вставить("ЕдиныйТариф", СтруктураТарифа.ЕдиныйТариф);
	Запрос.Параметры.Вставить("ВариантРасчетаНДС", СтруктураТарифа.ВариантРасчетаНДС);
	Запрос.Параметры.Вставить("СтавкаНДС", СтруктураТарифа.СтавкаНДС);
	Запрос.Параметры.Вставить("Перевозчик", ЭтотОбъект.Перевозчик);
	Запрос.Параметры.Вставить("Таблица", СтруктураТарифа.тзТарифы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЕстьРазличия = Выборка.ЕстьРазличия;	
	Иначе
		ЕстьРазличия = Ложь;
	КонецЕсли;
	
	Запрос.Параметры.Очистить();
	
	Возврат ЕстьРазличия;
	
КонецФункции

//+++АК sole 2018.05.30 ИП-00018724.02	
Процедура АК_ОбновитьТарифыНаДоставку(БПР)
	
	Перем Запрос;
	
	Период = БПР.СтруктураТарифа.Период;
	
	Если Период = БПР.Период Тогда
		Период = ТекущаяДата();	
	КонецЕсли;
	
	Запрос = БПР.Запрос;
	Запрос.Текст =	
"ВЫБРАТЬ
|	&Период КАК Период,
|	&Маршрут КАК Маршрут,
|	втНовыйТариф.ЕдиныйТариф КАК ЕдиныйТариф,
|	втНовыйТариф.ВариантРасчетаНДС КАК ВариантРасчетаНДС,
|	втНовыйТариф.СтавкаНДС КАК СтавкаНДС,
|	втНовыйТариф.ВесОт,
|	втНовыйТариф.Сумма,
|	втНовыйТариф.СуммаНДС,
|   втНовыйТариф.ЦенаЗаКг,
|	втНовыйТариф.Перевозчик
|
|ИЗ втНовыйТариф
|";
	
	Запрос.Параметры.Вставить("Период", Период);
	Запрос.Параметры.Вставить("Маршрут", ЭтотОбъект.Ссылка);	
	
	тзНовыйТариф = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из тзНовыйТариф Цикл
			
		НаборЗаписей = РегистрыСведений.ТарифыНаДоставкуПоМаршруту.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.Период.Установить(Стр["Период"]);
		НаборЗаписей.Отбор.Маршрут.Установить(Стр["Маршрут"]);
		НаборЗаписей.Отбор.ВесОт.Установить(Стр["ВесОт"]);
		НаборЗаписей.Отбор.ЕдиныйТариф.Установить(Стр["ЕдиныйТариф"]);
			
		Запись = НаборЗаписей.Добавить();
			
		ЗаполнитьЗначенияСвойств(Запись, Стр);
			
		НаборЗаписей.Записать();

	КонецЦикла;	
	
КонецПроцедуры

//