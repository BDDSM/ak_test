
&НаСервереБезКонтекста
Функция ПолучитьСписокАвтомобилей(ТекстВхождения)
	
	мСписок = Новый СписокЗначений;
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтрокаВхождения"	, "%" + ТекстВхождения + "%");
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 500
	|	Автомобили.Ссылка
	|ИЗ
	|	Справочник.Автомобили КАК Автомобили
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &СтрокаВхождения = ""%%""
	|				ТОГДА 1
	|			КОГДА Автомобили.Наименование ПОДОБНО &СтрокаВхождения
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ = 1
	|
	|УПОРЯДОЧИТЬ ПО
	|	Автомобили.Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мСписок.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат мСписок;
	
КонецФункции

&НаКлиенте
Процедура СтрокаВхожденияПриИзменении(Элемент)
	
	//
	ОтборНаФорме = ЭтаФорма.Список.Отбор;
	
	ПолеКомпоновкиСсылка = Новый ПолеКомпоновкиДанных("Ссылка");
	Если ОтборНаФорме.Элементы.Количество() = 0 Тогда
		ЭлементОтбора = ОтборНаФорме.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение 	= ПолеКомпоновкиСсылка;
		ЭлементОтбора.Использование 	= Ложь;
		ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение 	= ПредопределенноеЗначение("Справочник.Автомобили.ПустаяСсылка");
	Иначе
		Для Каждого ТекЭлементОтбора Из ОтборНаФорме.Элементы Цикл
			Если ТекЭлементОтбора.ЛевоеЗначение = ПолеКомпоновкиСсылка Тогда
				ЭлементОтбора = ТекЭлементОтбора;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если СтрДлина(СокрЛП(ЭтаФорма.СтрокаВхождения)) < 3 Тогда
		Если СтрДлина(СокрЛП(ЭтаФорма.СтрокаВхождения)) > 0 Тогда
			Сообщить("Строка поиска должна быть не менее 3 символов в длину!");
		КонецЕсли;
		ЭлементОтбора.Использование 	= Ложь;
		ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение 	= ПредопределенноеЗначение("Справочник.Автомобили.ПустаяСсылка");
    Иначе
		ЭлементОтбора.Использование 	= Истина;
		ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ПравоеЗначение 	= ПолучитьСписокАвтомобилей(СокрЛП(ЭтаФорма.СтрокаВхождения));
	КонецЕсли;
	
	//
	Элементы.Список.Обновить();
	
КонецПроцедуры
