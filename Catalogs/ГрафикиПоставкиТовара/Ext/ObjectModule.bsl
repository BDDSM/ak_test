
Процедура ЗаполнитьПредставлениеТоваров()
	
	Перем н, ТекПредставление;
	
	// запись значения реквизита "Представление товаров"
	н = 0;
	ТекПредставление = "";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", ЭтотОбъект.Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	Номенклатура.НаименованиеДляСМС
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&Товары)";
				   
	ТабКещ = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТЧ Из ЭтотОбъект.Товары Цикл
		
		СтрокаКеш = ТабКещ.Найти(СтрокаТЧ.Номенклатура, "Ссылка");
		Если СтрокаКеш = Неопределено Тогда
			Продолжить;
		КонецЕсли;	
		Если н = 1 Тогда
			ТекПредставление = ТекПредставление + ", "; 
		КонецЕсли;
		ТекПредставление = ТекПредставление + СокрЛП(СтрокаКеш.НаименованиеДляСМС);
		
		н = 1;
		
	КонецЦикла;
	
	ЭтотОбъект.ПредставлениеТоваров = ТекПредставление;
	
КонецПроцедуры

Процедура СоздатьПараметрыНоменклатурыДляЗаказа()
	
	Если ЭтотОбъект.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	РегПараметрыНоменклатурыДляЗаказа = РегистрыСведений.ПараметрыНоменклатурыДляЗаказа;
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Склад"			, ЭтотОбъект.СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("Производитель"	, ЭтотОбъект.Владелец);
	Запрос.УстановитьПараметр("МассивТоваров"	, ЭтотОбъект.Товары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Склад КАК Склад,
	|	СпрНоменклатура.Ссылка КАК Номенклатура,
	|	ЗначенияСвойствОбъектов.Объект КАК Характеристика
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))
	|			И (ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Объект КАК Справочник.ХарактеристикиНоменклатуры).Владелец = СпрНоменклатура.Ссылка)
	|			И (ЗначенияСвойствОбъектов.Значение = &Производитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыНоменклатурыДляЗаказа КАК ПараметрыНоменклатурыДляЗаказа
	|		ПО (ПараметрыНоменклатурыДляЗаказа.Склад = &Склад)
	|			И (ПараметрыНоменклатурыДляЗаказа.Номенклатура = СпрНоменклатура.Ссылка)
	|			И (ПараметрыНоменклатурыДляЗаказа.Характеристика = ЗначенияСвойствОбъектов.Объект)
	|ГДЕ
	|	СпрНоменклатура.Ссылка В(&МассивТоваров)
	|	И ПараметрыНоменклатурыДляЗаказа.Номенклатура ЕСТЬ NULL ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегПараметрыНоменклатурыДляЗаказа.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		
		МенеджерЗаписи.Записать();
		
		Сообщить("Созданы параметры номенклатуры для """ + Выборка.Номенклатура + """");
		
	КонецЦикла;
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ)
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Наименование) Тогда
		ЭтотОбъект.Наименование = Справочники.ГрафикиПоставкиТовара.ПолучитьНаименование(ЭтотОбъект.График);
	КонецЕсли;
	
	ЗаполнитьПредставлениеТоваров();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	СоздатьПараметрыНоменклатурыДляЗаказа();	
	
КонецПроцедуры
