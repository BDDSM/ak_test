
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если НЕ ПустаяСтрока(ИНН) Тогда
		Отказ=НЕ РегламентированнаяОтчетность.ИННСоответствуетТребованиям(ИНН, ?(СтрДлина(ИНН)=12,Перечисления.ЮрФизЛицо.ФизЛицо,Перечисления.ЮрФизЛицо.ЮрЛицо));
		Если Отказ Тогда
		   Сообщение = Новый СообщениеПользователю;
		   Сообщение.Текст = "ИНН не соответствует требованиям";
		   Сообщение.Поле  = "ИНН";
		   Сообщение.УстановитьДанные(ЭтотОбъект);
		   Сообщение.Сообщить();
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата=ТекущаяДата();
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Статус) тогда
		Статус=Справочники.АК_СтатусыПоступающихПредложений.Новое;
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения)=Тип("Структура") тогда
		Для Каждого Данные из ДанныеЗаполнения цикл
			ЭтотОбъект[Данные.Ключ]=Данные.Значение;
		КонецЦикла;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Статус) тогда
		Статус=Справочники.АК_СтатусыПоступающихПредложений.Новое;
	КонецЕсли;
КонецПроцедуры

