
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    УстановитьПривилегированныйРежим(Истина);
	
	Элементы.ОбразецКрасногоЦвета.Видимость=Ложь;
	КаталогФайнРидер = Константы.КаталогФайнРидер.Получить();
	
	Если Прав(КаталогФайнРидер, 1) <> "\" Тогда
		КаталогФайнРидер = КаталогФайнРидер + "\";
	КонецЕсли;	
	
	КаталогФайнРидер=СтрЗаменить(врег(КаталогФайнРидер),врег("\sticker\"),"");
	
	КаталогДок=КаталогФайнРидер+"\DOC\";
	
	КаталогФайнРидер=КаталогФайнРидер+"\draft\";
	
	
	
	ИсходныйФайл=Параметры.ИсходныйФайл;
	Расширение=Параметры.Расширение;
	                                        
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    КаталогНаДиске = Новый Файл(КаталогФайнРидер);
    Если НЕ КаталогНаДиске.Существует() Тогда
        Статус="Нет доступа к каталогу """+КаталогФайнРидер+"""";
		Элементы.Статус.ЦветТекста=Элементы.ОбразецКрасногоЦвета.ЦветТекста;
		возврат;
    КонецЕсли;	
	
	КаталогНаДиске = Новый Файл(КаталогДок);
    Если НЕ КаталогНаДиске.Существует() Тогда
       СоздатьКаталог(КаталогДок);
	КонецЕсли;
	
    КаталогНаДиске = Новый Файл(КаталогДок);
    Если НЕ КаталогНаДиске.Существует() Тогда
        Статус="Нет доступа к каталогу """+КаталогДок+"""";
		Элементы.Статус.ЦветТекста=Элементы.ОбразецКрасногоЦвета.ЦветТекста;
		возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИсходныйФайл) тогда
        Статус="Не указано имя файла";
		Элементы.Статус.ЦветТекста=Элементы.ОбразецКрасногоЦвета.ЦветТекста;
		возврат;
	КонецЕсли;
	
	Ид=Новый УникальныйИдентификатор;
	ФайлДляРаспознавания=КаталогФайнРидер+Ид+"."+Расширение;
	//ГотовыйФайл=КаталогДок+Ид+".DOCX";
	ШаблонПоискаФайла="*"+Ид+"*.DOCX";
	
	КопироватьФайл(ИсходныйФайл,ФайлДляРаспознавания);

	Статус=" Распознование";
	ПодключитьОбработчикОжидания("ПроверитьГотовность",1,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьГотовность()
	Если СтрДлина(Статус)>40 тогда
		Статус="Распознавание";
	Иначе 
		Статус="• "+Статус;
	КонецЕсли;
	
	Файлы=НайтиФайлы(КаталогДок,ШаблонПоискаФайла,Ложь);
	
	Если Файлы.Количество()>0 Тогда 
		
	    ФайлНаДиске = Файлы[0];
		
		Если ФайлНаДиске.Существует() Тогда
			
			ГотовыйФайл=ФайлНаДиске.ПолноеИмя;
			
			Статус="Загрузка данных...";
			ОтключитьОбработчикОжидания("ПроверитьГотовность");
			ПодключитьОбработчикОжидания("ЗагрузкаДанных",0.1,Истина);
			
		КонецЕсли;	
		
	КонецЕсли;
		
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузкаДанных()
	ОтключитьОбработчикОжидания("ПроверитьГотовность");

	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="Наименование";					СтрокаРеквизита.РеквизитТекст=СтрокаРеквизита.Реквизит;
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="Регион";                        СтрокаРеквизита.РеквизитТекст=СтрокаРеквизита.Реквизит;
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ЮридическийАдрес";              СтрокаРеквизита.РеквизитТекст="Юридический адрес";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="АдресПроизводства";				СтрокаРеквизита.РеквизитТекст="Адрес производства";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="КонтактноеЛицо";				СтрокаРеквизита.РеквизитТекст="Контактное лицо";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ДолжностьКонтактногоЛица";		СтрокаРеквизита.РеквизитТекст="Должность контактного лица";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="Телефон";                       СтрокаРеквизита.РеквизитТекст=СтрокаРеквизита.Реквизит;
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ЭлектроннаяПочта";              СтрокаРеквизита.РеквизитТекст="Электронная почта";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ИНН";							СтрокаРеквизита.РеквизитТекст=СтрокаРеквизита.Реквизит;
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="Руководитель";                  СтрокаРеквизита.РеквизитТекст=СтрокаРеквизита.Реквизит;
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ВидБизнеса";                    СтрокаРеквизита.РеквизитТекст="Вид Бизнеса";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ПродуктоваяКатегория";			СтрокаРеквизита.РеквизитТекст="Продуктовая категория";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ОбъемыПроизводстваРеализации";  СтрокаРеквизита.РеквизитТекст="Объемы производства/реализации";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="Ассортимент";                   СтрокаРеквизита.РеквизитТекст=СтрокаРеквизита.Реквизит;
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ТипыИВидыУпаковки";             СтрокаРеквизита.РеквизитТекст="Типы и виды упаковки";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ОсновныеКлиенты";               СтрокаРеквизита.РеквизитТекст="Основные клиенты";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ДоставкаВМоскву";               СтрокаРеквизита.РеквизитТекст="Доставка в Москву";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="ПроизводствоПодМаркойВВ";       СтрокаРеквизита.РеквизитТекст="Производство под маркой ВВ";
	СтрокаРеквизита=Результаты.Добавить();СтрокаРеквизита.Реквизит="Презентация";                   СтрокаРеквизита.РеквизитТекст=СтрокаРеквизита.Реквизит;
	
	СоответствиеПолей2=Новый Структура;
	СоответствиеПолей2.Вставить("delivery_moscow","ДоставкаВМоскву");
	СоответствиеПолей2.Вставить("vkusvill_label","ПроизводствоПодМаркойВВ");
	
	ПриложениеWord=Новый COMОбъект("Word.Application"); 
	ПриложениеWord.Documents.Open(ГотовыйФайл);
	Документ = ПриложениеWord.ActiveDocument();
	
	НомерРеквизита=0;
	Для НомерТаблицы = 1 по Документ.Tables.Count Цикл
		Таблица=Документ.Tables.Item(НомерТаблицы);
		Для НомерСтроки=1 по Таблица.Rows.Count цикл
			Если НомерТаблицы=1 и НомерСтроки=1 тогда
				Продолжить;
			КонецЕсли;
			Ячейка1=УдалитьНедопустимыеСимволыXML(строка(Таблица.Cell(НомерСтроки,1).Range.Text));
			Ячейка2=УдалитьНедопустимыеСимволыXML(Строка(Таблица.Cell(НомерСтроки,2).Range.Text));
			поз=Найти(Ячейка2,"[");
			Если Поз>0 Тогда 
				Ячейка2=сред(Ячейка2,Поз+1);
			КонецЕсли;
			поз=Найти(Ячейка2,"]");
			Если Поз>0 Тогда 
				Ячейка2=лев(Ячейка2,Поз-1);
			КонецЕсли;
			Ячейка2=СокрЛП(Ячейка2);
			//Результаты[НомерРеквизита].Распознано1=Ячейка1;
			Результаты[НомерРеквизита].Значение=Ячейка2;
			НомерРеквизита=НомерРеквизита+1;
			Если НомерРеквизита>=Результаты.Количество() тогда
				прервать;
			КонецЕсли;
		КонецЦикла;
		Если НомерРеквизита>=Результаты.Количество() тогда
			прервать;
		КонецЕсли;
	КонецЦикла;
	
	Документ.Close(); 
	ПриложениеWord.Quit();	
	
	Статус="Документ загружен";
	
	//предварительный просмотр полученных данных
	//Элементы.Результаты.Видимость=Истина;
	СоздатьЭлементСправочника("");
		
КонецПроцедуры


&НаКлиенте
Процедура СоздатьЭлементСправочника(Команда)
	АдресПроизводства="";
	ЮридическийАдрес="";
	
	ПараметрыЗаполнения = Новый Структура;
	Для Каждого СтрокаРеквизита из Результаты цикл
		Если СтрокаРеквизита.Реквизит="ДоставкаВМоскву" или СтрокаРеквизита.Реквизит="ПроизводствоПодМаркойВВ" тогда
			ПараметрыЗаполнения.Вставить(СтрокаРеквизита.Реквизит, Врег(Сокрлп(СтрокаРеквизита.Значение))="ДА");
		Иначе 
			ПараметрыЗаполнения.Вставить(СтрокаРеквизита.Реквизит,?(ЗначениеЗаполнено(СтрокаРеквизита.Значение),строка(СтрокаРеквизита.Значение),""));
		КонецЕсли;
		Если СтрокаРеквизита.Реквизит="ЮридическийАдрес" тогда
			ЮридическийАдрес=строка(СтрокаРеквизита.Значение);
		ИначеЕсли СтрокаРеквизита.Реквизит="АдресПроизводства" тогда
			АдресПроизводства=строка(СтрокаРеквизита.Значение);
		КонецЕсли;
	КонецЦикла;
	
	попытка
		ПараметрыЗаполнения.Вставить("КоординатыОпределены",Ложь);
		ПараметрыЗаполнения.Вставить("СтрокаКоординат","");
		Если НЕ ПустаяСтрока(ПараметрыЗаполнения.АдресПроизводства) тогда
			Адрес=АдресПроизводства;
		иначе
			Адрес=ЮридическийАдрес;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Адрес) тогда
			WinHttp = ПолучитьГеокодерЯндекс();
			ПараметрыЗаполнения.КоординатыОпределены=ПолучитьСтрокуКоординат(WinHttp,Адрес,ПараметрыЗаполнения.СтрокаКоординат);
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
 	ФормаЭлемента = ПолучитьФорму("Справочник.АК_ПоступающиеПредложения.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма.ВладелецФормы);
 	ФормаЭлемента.Открыть();
	
	попытка
		УдалитьФайлы(ГотовыйФайл);
	Исключение
	КонецПопытки;
	
	ЭтаФорма.Закрыть();
   
КонецПроцедуры

&НаКлиенте
Функция УдалитьНедопустимыеСимволыXML(СтрокаХМЛ)
  
 Позиция = НайтиНедопустимыеСимволыXML(СтрокаХМЛ);
 Пока Позиция <> 0 Цикл
  НедопустимыйСимвол = Сред(СтрокаХМЛ, Позиция,1);
  СтрокаХМЛ = СтрЗаменить(СтрокаХМЛ, НедопустимыйСимвол, "");
  Позиция = НайтиНедопустимыеСимволыXML(СтрокаХМЛ);
 КонецЦикла; 

 Возврат СтрокаХМЛ;
 КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьГеокодерЯндекс() экспорт
	
	Попытка
		
		Сервис = "geocode-maps.yandex.ru";
		Порт = 443;
		
	    WinHttp = Новый COMОбъект("WinHttp.WinHttpRequest.5.1");
	    WinHttp.Option(2,"utf-8");
	    WinHttp.Open("POST","https://geocode-maps.yandex.ru:443/1.x/",0);
	    WinHttp.SetRequestHeader("Accept-Language", "ru");
	    WinHttp.SetRequestHeader("Accept-Charset","utf-8");
	    WinHttp.setRequestHeader("Content-Language", "ru");
	    WinHttp.setRequestHeader("Content-Charset", "utf-8");
	    WinHttp.setRequestHeader("Content-type", "application/x-www-form-urlencoded; charset=utf-8");
		
		Возврат WinHttp;
		
	Исключение	
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция Координаты41(СтрокаКоординат,Реверс=Ложь) экспорт
	Поз=Найти(СтрокаКоординат,",");
	Если Поз=0 тогда
		Поз=Найти(СтрокаКоординат," ");
	КонецЕсли;        		
	
	Если Поз=0 тогда
		Возврат "";
	КонецЕсли;
	
	К1=лев(сокрлп(Лев(СтрокаКоординат,Поз-1)),20);
	К2=лев(сокрлп(Сред(СтрокаКоординат,Поз+1)),20);
	
	Если Реверс тогда
		Возврат К2+","+К1;
	иначе
		Возврат К1+","+К2;
	КонецЕсли;
КонецФункции


&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСтрокуКоординат(WinHttp,Адрес,СтрокаКоординат) экспорт
    WinHttp.Send("geocode="+Адрес);
    ОтветСервера = WinHttp.ResponseText();
	п1=найти(ОтветСервера,"<pos>");
	п2=найти(ОтветСервера,"</pos>");
	Если п1>0 и п2>п1 тогда
		СтрокаКоординат=Координаты41(сред(ОтветСервера,п1+5,п2-п1-5),Истина);
	    Возврат НЕ ПустаяСтрока(СтрокаКоординат);
	иначе
		СтрокаКоординат="";
		Возврат Ложь;
	КонецЕсли;
КонецФункции
