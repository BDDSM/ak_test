Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")Тогда
		
		пВладелец = Неопределено;
		ДанныеЗаполнения.Свойство("Владелец", пВладелец);
		ЕдиницаИзмерения = пВладелец.ЕдиницаХраненияОстатков;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	СпецификацияИзменена = Ложь;
	
	Если Не ЭтоНовый() Тогда
		
		// Изменились реквизиты шапки
		Если Владелец <> Ссылка.Владелец ИЛИ ЕдиницаИзмерения <> Ссылка.ЕдиницаИзмерения Тогда
			СпецификацияИзменена = Истина;
		КонецЕсли;
		
		// Изменился состав
		ТЗ = Состав.Выгрузить(,"Номенклатура, ЕдиницаИзмерения, Количество");
		
		Для Каждого Строка Из Ссылка.Состав Цикл
			НовСтр = ТЗ.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, Строка);
			НовСтр.Количество = -Строка.Количество;
		КонецЦикла;	
		
		ТЗ.Свернуть("Номенклатура, ЕдиницаИзмерения", "Количество");
		СпецификацияИзменена = ТЗ.Итог("Количество")<>0;
		
	КонецЕсли;
	
	Если СпецификацияИзменена Тогда
		ПроверитьЗаписиВРегистреСведенийСпецификацииПоставщиковВЗакрытомПериоде(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаписиВРегистреСведенийСпецификацииПоставщиковВЗакрытомПериоде(Отказ)
	
	СоответствиеГраницЗапрета = ПолныеПрава.ПолучитьГраницыЗапретаИзмененияДанных().Получить();
	
	Если СоответствиеГраницЗапрета <> Неопределено Тогда
		
		ДатаЗапрета = СоответствиеГраницЗапрета.Получить(Справочники.Организации.ПустаяСсылка());
		
		Если ДатаЗапрета <> Неопределено Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	СпецификацииПоставщиков.Спецификация
			|ИЗ
			|	РегистрСведений.СпецификацииПоставщиков КАК СпецификацииПоставщиков
			|ГДЕ
			|	СпецификацииПоставщиков.Период <= &ДатаЗапрета
			|	И СпецификацииПоставщиков.Спецификация = &Спецификация");
			Запрос.УстановитьПараметр("ДатаЗапрета", ДатаЗапрета);
			Запрос.УстановитьПараметр("Спецификация", Ссылка);
			
			Если Не Запрос.Выполнить().Пустой()Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нельзя изменять спецификацию, есть записи в закрытом периде! Запись не выполнена!",,,,Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
