
&НаКлиенте
Процедура Подбор(Команда)
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", Новый Структура("ЗакрыватьПриВыборе", Ложь), Элементы.Товары); 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДопДанныеПоТовару(Номенклатура)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СпрНоменклатура.Состав КАК Состав,
	               |	СпрНоменклатура.СрокГодности КАК СрокГодности,
	               |	СпрНоменклатура.Описание КАК Описание
	               |ИЗ
	               |	Справочник.Номенклатура КАК СпрНоменклатура
	               |ГДЕ
	               |	СпрНоменклатура.Ссылка = &Ссылка";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	СтруктураВозврат = Новый Структура("Состав, СрокГодности, Описание");
	Если Выборка.Следующий() Тогда
		СтруктураВозврат.Состав = СокрЛП(Выборка.Состав);
		СтруктураВозврат.СрокГодности = СокрЛП(Выборка.СрокГодности);
		ТекстОписание = СтрЗаменить(Выборка.Описание, "<BR>", Символы.ПС);
		ТекстОписание = СтрЗаменить(ТекстОписание, "<br>", Символы.ПС);
		ТекстОписание = СтрЗаменить(ТекстОписание, "%nbsp", Символы.ПС);
		НомерСмв = Найти(ТекстОписание, "<");
		НомерСмвЗакр = Найти(ТекстОписание, ">");
		Пока НомерСмв > 0 И НомерСмвЗакр > 0 Цикл
			ТекстОписание = Сред(ТекстОписание, 1, НомерСмв - 1) + Сред(ТекстОписание, НомерСмвЗакр + 1);
			НомерСмв = Найти(ТекстОписание, "<");
			НомерСмвЗакр = Найти(ТекстОписание, ">");
		КонецЦикла;	
		СтруктураВозврат.Описание = ТекстОписание;
	КонецЕсли;
	
	Возврат СтруктураВозврат;
	
КонецФункции	

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтрокаДоб = Объект.Товары.Добавить();
	СтрокаДоб.Номенклатура = ВыбранноеЗначение;
	ДопДанные = ПолучитьДопДанныеПоТовару(ВыбранноеЗначение);
	СтрокаДоб.Состав = ДопДанные.Состав;
	СтрокаДоб.СрокГодности = ДопДанные.СрокГодности;
	СтрокаДоб.Описание = ДопДанные.Описание;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Объект.ДатаНачалаДействия));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Объект.ДатаОкончанияДействия));
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СпискиТоп35Товаров.Наименование
	               |ИЗ
	               |	Справочник.СпискиТоп35Товаров КАК СпискиТоп35Товаров
	               |ГДЕ
	               |	(СпискиТоп35Товаров.ДатаНачалаДействия МЕЖДУ &ДатаНач И &ДатаКон
	               |			ИЛИ СпискиТоп35Товаров.ДатаОкончанияДействия МЕЖДУ &ДатаНач И &ДатаКон
	               |			ИЛИ &ДатаНач МЕЖДУ СпискиТоп35Товаров.ДатаНачалаДействия И СпискиТоп35Товаров.ДатаОкончанияДействия
	               |			ИЛИ &ДатаКон МЕЖДУ СпискиТоп35Товаров.ДатаНачалаДействия И СпискиТоп35Товаров.ДатаОкончанияДействия)
	               |	И СпискиТоп35Товаров.ПометкаУдаления = ЛОЖЬ
	               |	И СпискиТоп35Товаров.Ссылка <> &Ссылка";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Сообщить("Уже есть элемент с таким же периодом действия - " + Выборка.Наименование);
		Отказ = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	ДопДанные = ПолучитьДопДанныеПоТовару(ТекДанные.Номенклатура);
	ТекДанные.Состав = ДопДанные.Состав;
	ТекДанные.СрокГодности = ДопДанные.СрокГодности;
	ТекДанные.Описание = ДопДанные.Описание;
	
КонецПроцедуры
