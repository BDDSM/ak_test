
Перем ИсторияИзмененияРеквизитов; //+++АК SHEP 2018.10.22 ИП-00018753.05: сохраняем историю изменения реквизитов

Процедура ЗаполнитьТЧ_Аллергены()
	
	АллергеныСтрокой = "арахис;аспартам;горчица;диоксид серы (сульфиты);злаки, содержащие глютен;кунжут;люпин;моллюски;ракообразные;рыба;молоко (лактоза);орехи;сельдерей;соя;яйца";
	МассивАллергенов = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(АллергеныСтрокой, ";");
	Для Каждого Значение Из МассивАллергенов Цикл
		НоваяСтрокаТЧ = Аллергены.Добавить();
		НоваяСтрокаТЧ.Аллерген = Значение;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТЧ_ПредупреждающиеФразы()
	
	ПредупреждающиеФразыСтрокой = "содержит подсластители (сахарозаменители);содержит источник фенилаланина;возможно наличие косточек, будьте осторожны";
	МассивФраз = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ПредупреждающиеФразыСтрокой, ";");
	Для Каждого Значение Из МассивФраз Цикл
		НоваяСтрокаТЧ = ПредупреждающиеФразы.Добавить();
		НоваяСтрокаТЧ.Фраза = Значение;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ХарактеристикиНоменклатурыПоставщиков") Тогда
		
		//Владелец = ?(ЗначениеЗаполнено(ДанныеЗаполнения.ХарактеристикаНоменклатуры), ДанныеЗаполнения.ХарактеристикаНоменклатуры, ДанныеЗаполнения.Ссылка);
		Владелец = ДанныеЗаполнения.Ссылка;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,
			"Поставщик,Технолог,СоставПродуктаДляЭтикетки,СрокГодности,ТипСрокаГодности,ТемператураХраненияОт,ТемператураХраненияДо,ТипПозиционирования,ШтрихКод" +
			?(ЭтотОбъект.ТипПродукции = 0, ",ТипПродукции", "") + ",НаименованиеДляЦенника");
		ЭтотОбъект.НомерНормативногоДокумента = Строка(ДанныеЗаполнения.ГосударственныйСтандартТехническоеУсловие);
		ЭтотОбъект.ВидСтандартаКачества = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДанныеЗаполнения.ГосударственныйСтандартТехническоеУсловие, "ВидСтандартаКачества");
		//ЭтотОбъект.НаименованиеПродуктаПоТехническойДокументации = ЭтотОбъект.Наименование;
		ЭтотОбъект.ОсобенностиПродукта = ДанныеЗаполнения.ФишкаРазвёрнуто;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "ОсобенностиПроизводстваПродукта,ОсобенностиПродуктаПрочие,МассаНетто"); //+++АК SHEP 2018.10.23 ИП-00018753.05
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "ШиринаУпаковки,ВысотаУпаковки,ГлубинаУпаковки"); //+++АК SHEP 2018.12.10 ИП-00018753.05
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, "ВидТемпературыХранения"); //+++АК SHEP 2018.12.16 ИП-00018753.05
		
		ЭтоШтучныйТовар = (Найти(НРег(ДанныеЗаполнения.ЕдиницаИзмерения), "шт") > 0);
		//+++АК SHEP 2018.10.23 ИП-00018753.05: добавлено отдельное поле, заполнение чуть выше
		//ЭтотОбъект.МассаНетто = ?(ЭтоШтучныйТовар, Строка(ДанныеЗаполнения.Вес) + СокрЛП(ДанныеЗаполнения.ЕдиницаИзмеренияВеса), "");
		//---АК SHEP 2018.10.23
		
		// всё-таки нужно отдельные поля для срока годности
		//Если ЗначениеЗаполнено(ДанныеЗаполнения.ТипСрокаГодности) Тогда
		//	СрокГодности 	= ЧислоПрописью(ДанныеЗаполнения.СрокГодности, "НД=Ложь", ДанныеЗаполнения.ТипСрокаГодности.ПараметрыПрописиНаРусском);
		//	СрокГодности 	= СтрЗаменить(СрокГодности, " ", Символы.ПС);
		//	Если СтрЧислоСтрок(СрокГодности) > 0 Тогда
		//		СрокГодности = Формат(ДанныеЗаполнения.СрокГодности, "ЧГ=0") + " " + СтрПолучитьСтроку(СрокГодности, СтрЧислоСтрок(СрокГодности));
		//	КонецЕсли;
		//ИначеЕсли ЗначениеЗаполнено(ДанныеЗаполнения.СрокГодности) Тогда
		//	ЭтотОбъект.СрокГодности = Строка(ДанныеЗаполнения.СрокГодности);
		//КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЭтотОбъект.ТипПродукции <> 2 Тогда
		Если ЭтотОбъект.ПищеваяЦенность.Количество() = 0 Тогда Справочники.КартыДизайнПроектов.ЗаполнитьТЧ_ПищеваяЦенность(ЭтотОбъект); КонецЕсли;
		Если ЭтотОбъект.Аллергены.Количество() = 0 Тогда ЗаполнитьТЧ_Аллергены(); КонецЕсли;
		Если ЭтотОбъект.ПредупреждающиеФразы.Количество() = 0 Тогда ЗаполнитьТЧ_ПредупреждающиеФразы(); КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	//+++АК SHEP 2018.10.22 ИП-00018753.05
	Если ЗначениеЗаполнено(ЭтотОбъект.Владелец) И Наименование <> Строка(ЭтотОбъект.Владелец) Тогда
		Наименование = Строка(ЭтотОбъект.Владелец);
	КонецЕсли;
	
	ИсторияИзмененияРеквизитов = РегистрыСведений.ИсторияИзмененияРеквизитовЛКПоставщика.ЗаполнитьДанныеДляИстории(ЭтотОбъект, "ОсобенностиПродукта,СоставПродуктаДляЭтикетки,Статус");
	//---АК SHEP 2018.10.22
	
	
	//+++АК SHEP 2018.11.07 ИП-00018753.05: сохраняем данные по упаковке при передаче поставщиком
	Если ТипыУпаковки.Количество() > 0 И ЗначениеЗаполнено(Владелец) И Статус = ПредопределенноеЗначение("Перечисление.СтатусыКарточекТовараПоставщиков.НаПроверке") Тогда
		СпрХарактеристикиНоменклатурыСсылка =
			?(ТипЗнч(Владелец) = Тип("СправочникСсылка.ХарактеристикиНоменклатуры"), Владелец, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ХарактеристикаНоменклатуры"));
		Если ЗначениеЗаполнено(СпрХарактеристикиНоменклатурыСсылка) Тогда
			СпрХарактеристикиНоменклатурыОбъект = СпрХарактеристикиНоменклатурыСсылка.ПолучитьОбъект();
			Если СпрХарактеристикиНоменклатурыОбъект.ТипыУпаковки.Количество() = 0 Тогда
				СпрХарактеристикиНоменклатурыОбъект.ТипыУпаковки.Загрузить(ТипыУпаковки.Выгрузить());
				СпрХарактеристикиНоменклатурыОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//---АК SHEP 2018.11.07
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	//+++АК SHEP 2018.10.22 ИП-00018753.05: сохраняем историю изменения реквизитов
	Если ИсторияИзмененияРеквизитов.Количество() > 0 Тогда
		РегистрыСведений.ИсторияИзмененияРеквизитовЛКПоставщика.ЗаписатьИсторию(Ссылка, ИсторияИзмененияРеквизитов);
	КонецЕсли;
	//---АК SHEP 2018.10.22
	
КонецПроцедуры
