
Процедура ПередЗаписью(Отказ)
	
	Если Лев(Код, 1) = "8" и СтрДлина(СокрЛП(Код))=11 Тогда
		Код = "7" + Сред(Код, 2);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	//+++АК BARA 2017.12.04
	УстановитьПривилегированныйРежим(Истина);
	Если Отказ = Ложь и ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		
		Если ЭтотОбъект.Используется = Ложь или ЭтотОбъект.Заблокирован или ЭтотОбъект.ПометкаУдаления  Тогда

			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПривязкаТелефонов.Номер,
			|	ПривязкаТелефонов.Привязка,
			|	ПривязкаТелефонов.Назначение  КАК Назначение
			|ПОМЕСТИТЬ ВТ
			|ИЗ
			|	РегистрСведений.ПривязкаТелефонов.СрезПоследних КАК ПривязкаТелефонов
			|   Где  ПривязкаТелефонов.Номер = &НомерСим
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ.Привязка,
			|	ВТ.Номер.Код КАК Номер,
			|	ВТ.Назначение
			|ПОМЕСТИТЬ ВТ2
			|ИЗ
			|	ВТ КАК ВТ
			|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеТелефоны КАК ОсновныеТелефоны
			|		ПО ВТ.Привязка = ОсновныеТелефоны.Привязка
			|			И ВТ.Назначение = ОсновныеТелефоны.Назначение
			|  Где ВТ.Назначение = Значение(Справочник.НазначенияИспользованияSIMКарт.СлужебныйТелефон) 
			|	И (ЕСТЬNULL(ОсновныеТелефоны.Телефон, """") = """" ИЛИ  ОсновныеТелефоны.Телефон = ВТ.Номер  )
			|	И ТИПЗНАЧЕНИЯ(ВТ.Привязка) = ТИП(Справочник.ФизическиеЛица)
			|	И ВТ.Привязка.ЭтоГруппа = ЛОЖЬ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КонтактнаяИнформация.Представление,
			|	КонтактнаяИнформация.Объект,
			|	КонтактнаяИнформация.Тип,
			|	КонтактнаяИнформация.Вид,
			|	ВТ2.Привязка
			|ИЗ
			|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ2 КАК ВТ2
			|		ПО (ПОДСТРОКА(КонтактнаяИнформация.Представление, 2, 10) = ПОДСТРОКА(ВТ2.Номер, 2, 10))
			|ГДЕ
			|	КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
			|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонСлужебный)
			|	И ТИПЗНАЧЕНИЯ(КонтактнаяИнформация.Объект) = ТИП(Справочник.ФизическиеЛица) ";
			
			Запрос.УстановитьПараметр("НомерСим", ЭтотОбъект.Ссылка);
			
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				мз = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
				мз.Объект = Выборка.Объект;
				мз.Тип = Выборка.Тип;
				мз.Вид = Выборка.Вид;
				мз.Представление = Выборка.Представление;
				
				мз.Представление = "";
				Сообщить("Удалили из физ лица "+мз.Объект+" служебный номер.");
				мз.Удалить();
				
			КонецЦикла;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	//---АК BARA 2017.12.04
КонецПроцедуры
