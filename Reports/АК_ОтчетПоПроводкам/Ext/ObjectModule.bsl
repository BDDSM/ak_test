Перем СохраненнаяНастройка Экспорт;
Перем Расшифровки Экспорт;
Перем РежимРасшифровки Экспорт;
Перем СохранятьНастройкуОтчета Экспорт;

Перем мТекущийНаборПоказателей;

#Если Клиент Тогда
	
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Возврат Неопределено;
	
КонецФункции	

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	АК_СтандартныеОтчеты.ЗаполнитьДанныеОтчета(ЭтотОбъект);
	
КонецПроцедуры

Процедура СформироватьОтчет(Результат = Неопределено, ДанныеРасшифровки = Неопределено, ВыводВФормуОтчета = Истина, ВнешниеНаборыДанных = Неопределено, ВыводитьПолностью = Истина) Экспорт
	
	Результат.Очистить();
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ВыводЗаголовкаОтчета(ЭтотОбъект, Результат);
	Если ВыводитьПолностью Тогда
		ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных);
		КомпоновщикНастроек.Восстановить();
		НастройкаКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
		КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
		АК_СтандартныеОтчеты.ВывестиОтчет(ЭтотОбъект, Результат, ДанныеРасшифровки, ВыводВФормуОтчета, ВнешниеНаборыДанных, Истина, НастройкаКомпоновкиДанных);
	КонецЕсли; 
	ВыводПодписейОтчета(ЭтотОбъект, Результат);
	
	Если ВыводитьПолностью Тогда
		// Выполним дополнительную обработку Результата отчета
		ОбработкаРезультатаОтчета(Результат);
		
		// Сохраним настройки для Истории
		АК_СтандартныеОтчеты.СохранитьНастройкуДляИстории(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередВыводомОтчета(МакетКомпоновки) Экспорт
	
	ПоказателиОтчета = ДанныеОтчета.ПоказателиОтчета;
	
	ИсходныйТекстЗапроса         = МакетКомпоновки.НаборыДанных.Проводки.Запрос;
	ШаблонЗаменыЧтоЗаменить      = "Активность = ИСТИНА";
	ШаблонЗаменыНаЧтоЗаменить    = "";
	ШаблонЗаменыНаЧтоЗаменитьУпр  = "Сумма <> 0";
	ШаблонЗаменыНаЧтоЗаменитьКорр = "СуммаМСФО <> 0";
	ШаблонЗаменыНаЧтоЗаменитьМСФО = "(Сумма + СуммаМСФО) <> 0";
	ШаблонЗаменыНаЧтоЗаменитьКол = "КоличествоДт <> 0 ИЛИ КоличествоКт <> 0";
	КоличествоПоказателей        = 0;
	ВключитьСкобки               = Ложь;
	Для Каждого Показатель Из ПоказателиОтчета Цикл
		Если Показатель.Значение.Значение Тогда
			КоличествоПоказателей = КоличествоПоказателей + 1;
			ВключитьСкобки        = ВключитьСкобки ИЛИ (КоличествоПоказателей > 0);
		КонецЕсли;
		
		Если Показатель.Значение.Значение И Показатель.Значение.Использование Тогда
			Если Показатель.Ключ = "Упр" Тогда
				ШаблонЗамены = ШаблонЗаменыНаЧтоЗаменитьУпр;
			ИначеЕсли Показатель.Ключ = "Корр" Тогда
				ВключитьСкобки        = Истина;
				ШаблонЗамены = ШаблонЗаменыНаЧтоЗаменитьКорр;
			ИначеЕсли Показатель.Ключ = "МСФО" Тогда
				ВключитьСкобки        = Истина;
				ШаблонЗамены = ШаблонЗаменыНаЧтоЗаменитьМСФО;
			ИначеЕсли Показатель.Ключ = "ВалютнаяСумма" Тогда
				//+++АК sils 13.04.2018 ИП-00018366
				//ШаблонЗамены = СтрЗаменить(ШаблонЗаменыНаЧтоЗаменитьКорр, "СуммаКорр", "ВалютнаяСумма");
				ШаблонЗамены = СтрЗаменить(ШаблонЗаменыНаЧтоЗаменитьКол, "Количество", "ВалютнаяСумма");
				//---АК
				ВключитьСкобки        = Истина;
			ИначеЕсли Показатель.Ключ = "Количество" Тогда
				ШаблонЗамены = ШаблонЗаменыНаЧтоЗаменитьКол;
				ВключитьСкобки        = Истина;
			КонецЕсли;
			
			ШаблонЗаменыНаЧтоЗаменить = ШаблонЗаменыНаЧтоЗаменить + ?(КоличествоПоказателей = 1, " " + ШаблонЗамены, " ИЛИ " + ШаблонЗамены)
		КонецЕсли;
	КонецЦикла;
	
	Если СокрЛП(ШаблонЗаменыНаЧтоЗаменить) <> "" Тогда
		ШаблонЗаменыНаЧтоЗаменить = ?(ВключитьСкобки, " И (" + ШаблонЗаменыНаЧтоЗаменить + ")", " И " + ШаблонЗаменыНаЧтоЗаменить);
	КонецЕсли;
	
	КонечныйТекстЗапроса = СтрЗаменить(ИсходныйТекстЗапроса, ШаблонЗаменыЧтоЗаменить, ШаблонЗаменыЧтоЗаменить + ШаблонЗаменыНаЧтоЗаменить);
	МакетКомпоновки.НаборыДанных.Проводки.Запрос = КонечныйТекстЗапроса;
	
	// Если показатель один, то удалим столбик "Показатель"
	Если КоличествоПоказателей = 1 Тогда
		Для Каждого Макет Из МакетКомпоновки.Макеты Цикл
			Для Каждого СтрокаМакета Из Макет.Макет Цикл
				СтрокаМакета.Ячейки.Удалить(СтрокаМакета.Ячейки[4]);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	МакетПроводки = АК_СтандартныеОтчеты.ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, "Проводки", Истина);
	
	Если МакетПроводки.Количество() = 1 Тогда
		МакетПроводки = МакетПроводки[0];
		ДанныеОтчета.Вставить("МакетПроводок", МакетПроводки.Имя);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередВыводомЭлементаРезультата(МакетКомпоновки, ДанныеРасшифровки, ЭлементРезультата, Отказ = Ложь) Экспорт
	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ДоработатьКомпоновщикПередВыводом(ВнешниеНаборыДанных) Экспорт
	
	Если ЗначениеЗаполнено(НачалоПериода) Тогда
		ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(НачалоПериода));
	КонецЕсли;
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(КонецПериода));
	КонецЕсли;
	
	ТиповыеОтчеты.УстановитьПараметр(КомпоновщикНастроек, "ПС", Символы.ПС);

	//Если ЗначениеЗаполнено(Подразделение) Тогда
	//	ТиповыеОтчеты.ДобавитьОтбор(КомпоновщикНастроек, "Подразделение", Подразделение, ВидСравненияКомпоновкиДанных.ВИерархии);
	//КонецЕсли;
		
	ПоказателиОтчета = ДанныеОтчета.ПоказателиОтчета;
	
	ЛинияСплошная = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	
	МассивПоказателей = Новый Массив;
	МассивПоказателей.Добавить("Упр");
	МассивПоказателей.Добавить("Корр");
	МассивПоказателей.Добавить("МСФО");
	МассивПоказателей.Добавить("ВалютнаяСумма");
	МассивПоказателей.Добавить("Количество");
	
	ТекущийНаборПоказателей = "" + ПоказателиОтчета.Упр.Значение + ПоказателиОтчета.Корр.Значение + ПоказателиОтчета.МСФО.Значение + ПоказателиОтчета.ВалютнаяСумма.Значение + ПоказателиОтчета.Количество.Значение;
	Если ТекущийНаборПоказателей <> мТекущийНаборПоказателей Тогда
		
		МассивМакетов = Новый Массив;
		МассивМакетов.Добавить("ЗаголовокПодвал");
		МассивМакетов.Добавить("ПроводкиЗаголовок");	
		
		Для Каждого ЭлементМассива Из МассивМакетов Цикл
			СхемаКомпоновкиДанных.Макеты[ЭлементМассива].Макет = АК_СтандартныеОтчеты.ПолучитьКопиюОписанияМакета(СхемаКомпоновкиДанных.Макеты[ЭлементМассива + "Образец"].Макет);
			ОписаниеМакета = СхемаКомпоновкиДанных.Макеты[ЭлементМассива].Макет;
			МассивСтрокДляУдаления = Новый Массив;
			Индекс = 0;
			Для Каждого ЭлементПоказатель Из МассивПоказателей Цикл
				Если Не ПоказателиОтчета[ЭлементПоказатель].Значение Тогда 
					МассивСтрокДляУдаления.Добавить(ОписаниеМакета[Индекс]);
				КонецЕсли;
				Индекс = Индекс + 1;
			КонецЦикла;		
			
			Для Каждого Строка Из МассивСтрокДляУдаления Цикл
				ОписаниеМакета.Удалить(Строка);
			КонецЦикла;
			
			КоличествоСтрок = ОписаниеМакета.Количество();
			
			// Обвести область
			Если КоличествоСтрок > 0 Тогда
				Для Индекс = 0 По 10 Цикл
					ПоследняяСтрока = ?(ЭлементМассива = "ЗаголовокПодвал" И Индекс < 4, 0, КоличествоСтрок - 1);
					ПараметрГраницы = ТиповыеОтчеты.ПолучитьПараметр(ОписаниеМакета[0].Ячейки[Индекс].Оформление.Элементы, "СтильГраницы");
					ТиповыеОтчеты.УстановитьПараметр(ПараметрГраницы.ЗначенияВложенныхПараметров, "СтильГраницы.Сверху", ЛинияСплошная);
					ПараметрГраницы = ТиповыеОтчеты.ПолучитьПараметр(ОписаниеМакета[ПоследняяСтрока].Ячейки[Индекс].Оформление.Элементы, "СтильГраницы");
					ТиповыеОтчеты.УстановитьПараметр(ПараметрГраницы.ЗначенияВложенныхПараметров, "СтильГраницы.Снизу", ЛинияСплошная);	
				КонецЦикла;
			КонецЕсли;
			
			Для Индекс = 1 По КоличествоСтрок - 1 Цикл
				ОписаниеМакета[Индекс].Ячейки[0].Элементы.Очистить();	
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[0].Оформление.Элементы, "ОбъединятьПоВертикали", Истина);
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[0].Оформление.Элементы, "Расшифровка", Неопределено, Ложь);
				ОписаниеМакета[Индекс].Ячейки[1].Элементы.Очистить();
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[1].Оформление.Элементы, "ОбъединятьПоВертикали", Истина);
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[1].Оформление.Элементы, "Расшифровка", Неопределено, Ложь);
				ОписаниеМакета[Индекс].Ячейки[2].Элементы.Очистить();
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[2].Оформление.Элементы, "ОбъединятьПоВертикали", Истина);
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[2].Оформление.Элементы, "Расшифровка", Неопределено, Ложь);
				ОписаниеМакета[Индекс].Ячейки[3].Элементы.Очистить();
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[3].Оформление.Элементы, "ОбъединятьПоВертикали", Истина);
				ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[3].Оформление.Элементы, "Расшифровка", Неопределено, Ложь);
				Если ЭлементМассива = "ПроводкиЗаголовок" Тогда
					ОписаниеМакета[Индекс].Ячейки[5].Элементы.Очистить();
					ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[5].Оформление.Элементы, "ОбъединятьПоВертикали", Истина);
					ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[5].Оформление.Элементы, "Расшифровка", Неопределено, Ложь);
					ОписаниеМакета[Индекс].Ячейки[8].Элементы.Очистить();
					ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[8].Оформление.Элементы, "ОбъединятьПоВертикали", Истина);
					ТиповыеОтчеты.УстановитьПараметр(ОписаниеМакета[Индекс].Ячейки[8].Оформление.Элементы, "Расшифровка", Неопределено, Ложь);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		мТекущийНаборПоказателей = ТекущийНаборПоказателей;
	КонецЕсли;
	Если Не ПоказателиОтчета.Упр.Значение Тогда
		ГруппаОтборов = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтборов.Использование = Истина;
		ГруппаОтборов.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		Если ПоказателиОтчета.Корр.Значение Тогда
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "КоррДт", 0, ВидСравненияКомпоновкиДанных.НеРавно);
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "КоррКт", 0, ВидСравненияКомпоновкиДанных.НеРавно);
		КонецЕсли;
		Если ПоказателиОтчета.МСФО.Значение Тогда
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "МСФОДт", 0, ВидСравненияКомпоновкиДанных.НеРавно);
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "МСФОКт", 0, ВидСравненияКомпоновкиДанных.НеРавно);
		КонецЕсли;
		Если ПоказателиОтчета.ВалютнаяСумма.Значение Тогда
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "ВалютнаяСуммаДт", 0, ВидСравненияКомпоновкиДанных.НеРавно);
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "ВалютнаяСуммаКт", 0, ВидСравненияКомпоновкиДанных.НеРавно);
		КонецЕсли;
		Если ПоказателиОтчета.Количество.Значение Тогда
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "КоличествоДт", 0, ВидСравненияКомпоновкиДанных.НеРавно);
			ТиповыеОтчеты.ДобавитьОтбор(ГруппаОтборов, "КоличествоКт", 0, ВидСравненияКомпоновкиДанных.НеРавно);	
		КонецЕсли;	
	КонецЕсли;
	
	МассивОтборов = Новый Массив;
	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
			Если ЭлементОтбора.Использование И Найти(ЭлементОтбора.ЛевоеЗначение, "(доп. реквизит)") > 0 Тогда
				МассивОтборов.Добавить(ЭлементОтбора);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлементОтбора Из МассивОтборов Цикл
		ГруппаИЛИ = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		ТиповыеОтчеты.ДобавитьОтбор(ГруппаИЛИ, СтрЗаменить(ЭлементОтбора.ЛевоеЗначение, "Субконто", "СубконтоДт"), ЭлементОтбора.ПравоеЗначение, ЭлементОтбора.ВидСравнения); 
		ТиповыеОтчеты.ДобавитьОтбор(ГруппаИЛИ, СтрЗаменить(ЭлементОтбора.ЛевоеЗначение, "Субконто", "СубконтоКт"), ЭлементОтбора.ПравоеЗначение, ЭлементОтбора.ВидСравнения); 
		КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);	
	КонецЦикла;
	
	АК_СтандартныеОтчеты.ДобавитьОтборПоОрганизации(ЭтотОбъект);
	
КонецПроцедуры

Процедура ВыводЗаголовкаОтчета(ОтчетОбъект, Результат)
	
	ДополнительныеПараметры = Новый Структура("ВыводитьОписаниеЕдиницыИзмерения", 
		?(НастройкиФормы.Свойство("ВыводитьЕдиницуИзмерения"), НастройкиФормы.ВыводитьЕдиницуИзмерения, Ложь));
	АК_СтандартныеОтчеты.ВыводЗаголовкаОтчета(ОтчетОбъект, Результат, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ВыводПодписейОтчета(ОтчетОбъект, Результат)
	
	//ДополнительныеПараметры = Новый Структура("Период,Организация,ОтветственноеЛицо", 
	//	КонецПериода, 
	//	Организация, 
	//	Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаБухгалтерскиеРегистры);
	//		
	//АК_СтандартныеОтчеты.ВыводПодписейОтчета(ОтчетОбъект, Результат, ДополнительныеПараметры);
			
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(ОрганизацияВНачале = Истина) Экспорт 
	
	ЗаголовокОтчета = "Отчет по проводкам" + АК_СтандартныеОтчеты.ПолучитьПредставлениеПериода(ЭтотОбъект);

	Возврат ?(ОрганизацияВНачале, ЗаголовокОтчета, ЗаголовокОтчета + " " + АК_СтандартныеОтчеты.ПолучитьТекстОрганизация(ЭтотОбъект));
	
КонецФункции

Процедура ПолучитьСтруктуруПоказателейОтчета() Экспорт
	
	ПоказателиОтчета = АК_СтандартныеОтчеты.ПолучитьСтруктуруПоказателейОтчета(,, Ложь, Истина, Истина, Ложь);
	ДанныеОтчета.Вставить("ПоказателиОтчета", ПоказателиОтчета);

КонецПроцедуры

Процедура ОбработкаРезультатаОтчета(Результат)
	
	АК_СтандартныеОтчеты.ОбработкаРезультатаОтчета(ЭтотОбъект, Результат);

	// Зафиксируем заголовок отчета
	ВысотаЗаголовка = Результат.Области.Заголовок.Низ;
	Результат.ФиксацияСверху = ВысотаЗаголовка + 2;
	
КонецПроцедуры

// Для настройки отчета (расшифровка и др.)
Процедура Настроить() Экспорт
	
	ЗаполнитьНачальныеНастройки();
	
КонецПроцедуры

Процедура СохранитьНастройку() Экспорт
	
	Если СохранятьНастройкуОтчета Тогда	
		АК_СтандартныеОтчеты.СохранитьНастройку(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет параметры отчета по элементу справочника из переменной СохраненнаяНастройка.
Процедура ПрименитьНастройку() Экспорт
	
	Если СохраненнаяНастройка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = СохраненнаяНастройка.ХранилищеНастроек.Получить();
	Если РежимРасшифровки Тогда
		НастройкиФормы = СтруктураПараметров.НастройкиФормы;
	Иначе
		ТиповыеОтчеты.ПрименитьСтруктуруПараметровОтчета(ЭтотОбъект, СтруктураПараметров);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализацияОтчета() Экспорт
	
	АК_СтандартныеОтчеты.ИнициализацияОтчета(ЭтотОбъект);
	
КонецПроцедуры

Расшифровки = Новый СписокЗначений;

НастройкаПериода = Новый НастройкаПериода;

РежимРасшифровки = Ложь;

#КонецЕсли