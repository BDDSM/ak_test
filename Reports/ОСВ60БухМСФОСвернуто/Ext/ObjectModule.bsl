
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументРезультат.Очистить();
	
	СформироватьОтчет(ДокументРезультат);
	
КонецПроцедуры 

#КонецОбласти

#Область ФормированиеОтчета

//+++ AK suvv 10.08.2018 ИП-00019510
Процедура СформироватьОтчет(ДокументРезультат)
	
	ДанныеДляЗаполнения = ОборотыИОстаткиПоСчету60Бух();
	
	ЗаполнитьТабличныйДокумент(ДокументРезультат, ДанныеДляЗаполнения);
	
КонецПроцедуры //--- AK suvv

//+++ AK suvv 10.08.2018 ИП-00019510
Функция ПолучитьСоединениеСБазойБух()
	
	Перем ПодключениеУстановлено;
	
	Соединение = Неопределено;
	
	Соединение = ОбменСБП2_0.ВернутьПодключениеКБазеБУ(ПодключениеУстановлено);	
	
	Если Не ПодключениеУстановлено Тогда 
		ВызватьИсключение "Ошибка подключения к базе БУХ";
	КонецЕсли;
	
	Возврат Соединение;
	
КонецФункции //--- AK suvv

//+++ AK suvv 10.08.2018 ИП-00019510
Функция ОборотыИОстаткиПоСчету60Бух()
	
	НачалоПериода       = Дата(0001,01,01);
	КонецПериода        = Дата(0001,01,01);
	НазваниеОрганизации = "";
	
	Для Каждого ПользоватПоле Из ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если ПользоватПоле.Использование Тогда
			Если ТипЗнч(ПользоватПоле) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
				Если Строка(ПользоватПоле.Параметр) = "ПериодОтчета" Тогда
					НачалоПериода       = ПользоватПоле.Значение.ДатаНачала;
					КонецПериода        = ПользоватПоле.Значение.ДатаОкончания;
				ИначеЕсли Строка(ПользоватПоле.Параметр) = "Организация" Тогда
					НазваниеОрганизации = ПользоватПоле.Значение.Наименование;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Соединение = ПолучитьСоединениеСБазойБух();
	
	ОборотыИОстаткиВБухБазе = ПолучитьФактическиеОборотыВБухБазе(Соединение, НачалоПериода, КонецПериода, НазваниеОрганизации);
	
	Возврат ОборотыИОстаткиВБухБазе;
	
КонецФункции //--- AK suvv

//+++ AK suvv 10.08.2018 ИП-00019510
Функция ПолучитьФактическиеОборотыВБухБазе(Соединение, НачалоПериода, КонецПериода, НазваниеОрганизации)
	
	Запрос = Соединение.NewObject("Запрос");
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокКт,
	|	0 КАК СуммаНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокКт,
	|	0 КАК СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
	|ПОМЕСТИТЬ ВТ_ОстаткиИОбороты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщиками), , Организация.Наименование = &НазваниеОрганизации) КАК ХозрасчетныйОстаткиИОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Субконто1,
	|	0,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт,
	|	0,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамВыданным), , ) КАК ХозрасчетныйОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиИОбороты.Контрагент,
	|	СУММА(ВТ_ОстаткиИОбороты.СуммаНачальныйОстатокКт) КАК СуммаНачальныйОстатокКт,
	|	СУММА(ВТ_ОстаткиИОбороты.СуммаНачальныйОстатокДт) КАК СуммаНачальныйОстатокДт,
	|	СУММА(ВТ_ОстаткиИОбороты.СуммаКонечныйОстатокКт) КАК СуммаКонечныйОстатокКт,
	|	СУММА(ВТ_ОстаткиИОбороты.СуммаКонечныйОстатокДт) КАК СуммаКонечныйОстатокДт,
	|	СУММА(ВТ_ОстаткиИОбороты.СуммаОборотДт) КАК СуммаОборотДт,
	|	СУММА(ВТ_ОстаткиИОбороты.СуммаОборотКт) КАК СуммаОборотКт
	|ПОМЕСТИТЬ ВТ_ОстаткиИОборотыСгруппировано
	|ИЗ
	|	ВТ_ОстаткиИОбороты КАК ВТ_ОстаткиИОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОстаткиИОбороты.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиИОборотыСгруппировано.Контрагент.Наименование КАК Контрагент,
	|	ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокКт,
	|	ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокДт,
	|	ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокКт,
	|	ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокДт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокДт > ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокКт
	|			ТОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокДт - ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокКт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НачальныйОстатокСвернутоДт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокКт > ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокДт
	|			ТОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокКт - ВТ_ОстаткиИОборотыСгруппировано.СуммаНачальныйОстатокДт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НачальныйОстатокСвернутоКт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокДт > ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокКт
	|			ТОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокДт - ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокКт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КонечныйОстатокСвернутоДт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокКт > ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокДт
	|			ТОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокКт - ВТ_ОстаткиИОборотыСгруппировано.СуммаКонечныйОстатокДт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КонечныйОстатокСвернутоКт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотДт > ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотКт
	|			ТОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотДт - ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотКт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИтогоСуммаОборотДт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотКт > ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотДт
	|			ТОГДА ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотКт - ВТ_ОстаткиИОборотыСгруппировано.СуммаОборотДт
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИтогоСуммаОборотКт
	|ПОМЕСТИТЬ ВТ_ОстаткиИОборотыРасчитано
	|ИЗ
	|	ВТ_ОстаткиИОборотыСгруппировано КАК ВТ_ОстаткиИОборотыСгруппировано
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиИОборотыРасчитано.Контрагент,
	|	ВТ_ОстаткиИОборотыРасчитано.СуммаНачальныйОстатокКт,
	|	ВТ_ОстаткиИОборотыРасчитано.СуммаНачальныйОстатокДт,
	|	ВТ_ОстаткиИОборотыРасчитано.СуммаКонечныйОстатокКт,
	|	ВТ_ОстаткиИОборотыРасчитано.СуммаКонечныйОстатокДт,
	|	ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоДт,
	|	ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоКт,
	|	ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоДт,
	|	ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоКт,
	|	ВТ_ОстаткиИОборотыРасчитано.ИтогоСуммаОборотДт,
	|	ВТ_ОстаткиИОборотыРасчитано.ИтогоСуммаОборотКт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоДт = 0
	|				И ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоКт = 0
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоДт <> 0
	|						ТОГДА ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоДт * -1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		КОГДА ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоДт <> 0
	|				И ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоДт <> 0
	|			ТОГДА ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоДт - ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоДт
	|		ИНАЧЕ ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоДт - ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоДт
	|	КОНЕЦ КАК ИтогоСвернутыйОборотДт,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоДт = 0
	|				И ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоКт = 0
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоКт <> 0
	|						ТОГДА ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоКт * -1
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		КОГДА ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоКт <> 0
	|				И ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоКт <> 0
	|			ТОГДА ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоКт - ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоКт
	|		ИНАЧЕ ВТ_ОстаткиИОборотыРасчитано.КонечныйОстатокСвернутоКт - ВТ_ОстаткиИОборотыРасчитано.НачальныйОстатокСвернутоКт
	|	КОНЕЦ КАК ИтогоСвернутыйОборотКт
	|ПОМЕСТИТЬ ВТ_РасчитаныСвернутыеОбороты
	|ИЗ
	|	ВТ_ОстаткиИОборотыРасчитано КАК ВТ_ОстаткиИОборотыРасчитано
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасчитаныСвернутыеОбороты.Контрагент,
	|	ВТ_РасчитаныСвернутыеОбороты.СуммаНачальныйОстатокКт,
	|	ВТ_РасчитаныСвернутыеОбороты.СуммаНачальныйОстатокДт,
	|	ВТ_РасчитаныСвернутыеОбороты.СуммаКонечныйОстатокКт,
	|	ВТ_РасчитаныСвернутыеОбороты.СуммаКонечныйОстатокДт,
	|	ВТ_РасчитаныСвернутыеОбороты.НачальныйОстатокСвернутоДт,
	|	ВТ_РасчитаныСвернутыеОбороты.НачальныйОстатокСвернутоКт,
	|	ВТ_РасчитаныСвернутыеОбороты.КонечныйОстатокСвернутоДт,
	|	ВТ_РасчитаныСвернутыеОбороты.КонечныйОстатокСвернутоКт,
	|	ВТ_РасчитаныСвернутыеОбороты.ИтогоСуммаОборотДт,
	|	ВТ_РасчитаныСвернутыеОбороты.ИтогоСуммаОборотКт,
	|	ВТ_РасчитаныСвернутыеОбороты.ИтогоСвернутыйОборотДт,
	|	ВТ_РасчитаныСвернутыеОбороты.ИтогоСвернутыйОборотКт,
	|	ВЫБОР
	|		КОГДА ВТ_РасчитаныСвернутыеОбороты.ИтогоСуммаОборотДт - ВТ_РасчитаныСвернутыеОбороты.ИтогоСуммаОборотКт <> ВТ_РасчитаныСвернутыеОбороты.ИтогоСвернутыйОборотДт - ВТ_РасчитаныСвернутыеОбороты.ИтогоСвернутыйОборотКт
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОшибка
	|ИЗ
	|	ВТ_РасчитаныСвернутыеОбороты КАК ВТ_РасчитаныСвернутыеОбороты";
	
	Если ЗначениеЗаполнено(НачалоПериода) или ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоПериода", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КонецПериода", "");
	КонецЕсли;
	
	Если НазваниеОрганизации = "" Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организация.Наименование = &НазваниеОрганизации", "");
	Иначе
		Запрос.УстановитьПараметр("НазваниеОрганизации", НазваниеОрганизации);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	ФактическиеОборотыВБухБазе = ЗначениеИзКОМ(Соединение, РезультатЗапроса);
	
	Возврат ФактическиеОборотыВБухБазе;
	
КонецФункции //--- AK suvv

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьТабличныйДокумент(ТабличныйДокумент, ОстаткиИОборотыПоСчет60Бух)
	
	СхемаКД   = ЭтотОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки();	
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКД, Настройки);
	
	ВнешниеНаборыДанных = Новый Структура("ОстаткиИОборотыПоСчет60Бух", ОстаткиИОборотыПоСчет60Бух);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Функция ЗначениеИзКОМ(Соединение, Значение)
	
	Возврат ЗначениеИзСтрокиВнутр(Соединение.ЗначениеВСтрокуВнутр(Значение));
	
КонецФункции

#КонецОбласти
