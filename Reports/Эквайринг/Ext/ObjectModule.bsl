
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ДатаНачала = '00010101';
	ДатаОкончания = '00010101';
	Для Каждого ПользПоле Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ПользПоле) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")
			И Строка(ПользПоле.Параметр) = "Период" Тогда
			ДатаНачала = ПользПоле.Значение.ДатаНачала;
			ДатаОкончания = КонецДня(ПользПоле.Значение.ДатаОкончания);
		КонецЕсли;	
	КонецЦикла;
	
	//МассивТТ = Новый Массив();
	//ЕстьОтборТТ = Ложь;
	//Для Каждого ЭлементОтбор Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
	//	Если ЭлементОтбор.ПредставлениеПользовательскойНастройки = "Торговая точка" Тогда
	//		ОтборТТ = ПолучитьПользовательскуюНастройку(ЭлементОтбор.ИдентификаторПользовательскойНастройки);
	//		Если ОтборТТ <> Неопределено
	//			И ОтборТТ.Использование = Истина Тогда
	//			Если ОтборТТ.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
	//				ЕстьОтборТТ = Истина;
	//				МассивТТ.Добавить(ОтборТТ.ПравоеЗначение);
	//			КонецЕсли;	
	//			Если ОтборТТ.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
	//				ЕстьОтборТТ = Истина;
	//				Для Каждого ЭлементТТ Из ОтборТТ.ПравоеЗначение Цикл
	//					МассивТТ.Добавить(ЭлементТТ.Значение);
	//				КонецЦикла;	
	//			КонецЕсли;	
	//		КонецЕсли;	
	//	КонецЕсли;	
	//КонецЦикла;	
	
	ТаблицаДанных = Новый ТаблицаЗначений();
	ТаблицаДанных.Колонки.Добавить("ТорговаяТочка");
	ТаблицаДанных.Колонки.Добавить("Терминал");
	ТаблицаДанных.Колонки.Добавить("Документ");
	ТаблицаДанных.Колонки.Добавить("КомиссииБанка");
	ТаблицаДанных.Колонки.Добавить("Приход");
	ТаблицаДанных.Колонки.Добавить("Расход");
								
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаС", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаПо", ДатаОкончания);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЗ_Привязки.Терминал,
	               |	ВЗ_Привязки.Дата,
	               |	ВЗ_Привязки.ДатаПоРегистру,
	               |	ПривязкиОборудованияКРабочимМестам.РабочееМесто.СтруктурнаяЕдиница КАК ТорговаяТочка
	               |ПОМЕСТИТЬ ВТ_Привязки
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ВЗ_Даты.Терминал КАК Терминал,
	               |		ВЗ_Даты.Дата КАК Дата,
	               |		МАКСИМУМ(ПривязкиОборудованияКРабочимМестам.Период) КАК ДатаПоРегистру
	               |	ИЗ
	               |		(ВЫБРАТЬ
	               |			ПоступлениеВБанкЭквайрингРасшифровка.Терминал КАК Терминал,
	               |			НАЧАЛОПЕРИОДА(ПоступлениеВБанкЭквайрингРасшифровка.Ссылка.Дата, ДЕНЬ) КАК Дата
	               |		ИЗ
	               |			Документ.ПоступлениеВБанк.ЭквайрингРасшифровка КАК ПоступлениеВБанкЭквайрингРасшифровка
	               |		ГДЕ
	               |			ПоступлениеВБанкЭквайрингРасшифровка.Ссылка.Проведен
	               |			И ПоступлениеВБанкЭквайрингРасшифровка.Ссылка.Дата МЕЖДУ &ДатаС И &ДатаПо
	               |		
	               |		ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |		ВЫБРАТЬ
	               |			РасходИзБанкаЭквайрингРасшифровка.Терминал,
	               |			НАЧАЛОПЕРИОДА(РасходИзБанкаЭквайрингРасшифровка.Ссылка.Дата, ДЕНЬ)
	               |		ИЗ
	               |			Документ.РасходИзБанка.ЭквайрингРасшифровка КАК РасходИзБанкаЭквайрингРасшифровка
	               |		ГДЕ
	               |			РасходИзБанкаЭквайрингРасшифровка.Ссылка.Проведен
	               |			И РасходИзБанкаЭквайрингРасшифровка.Ссылка.Дата МЕЖДУ &ДатаС И &ДатаПо) КАК ВЗ_Даты
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПривязкиОборудованияКРабочимМестам КАК ПривязкиОборудованияКРабочимМестам
	               |			ПО ВЗ_Даты.Терминал = ПривязкиОборудованияКРабочимМестам.Терминал
	               |				И ВЗ_Даты.Дата >= ПривязкиОборудованияКРабочимМестам.Период
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ВЗ_Даты.Терминал,
	               |		ВЗ_Даты.Дата) КАК ВЗ_Привязки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПривязкиОборудованияКРабочимМестам КАК ПривязкиОборудованияКРабочимМестам
	               |		ПО ВЗ_Привязки.Терминал = ПривязкиОборудованияКРабочимМестам.Терминал
	               |			И ВЗ_Привязки.ДатаПоРегистру = ПривязкиОборудованияКРабочимМестам.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПоступлениеВБанкЭквайрингРасшифровка.Терминал,
	               |	ПоступлениеВБанкЭквайрингРасшифровка.СуммаДокумента КАК СуммаДокументаПриход,
	               |	ЕСТЬNULL(ВТ_Привязки.ТорговаяТочка, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК ТорговаяТочка,
	               |	ПоступлениеВБанкЭквайрингРасшифровка.СуммаКомиссииБанка,
	               |	ПоступлениеВБанкЭквайрингРасшифровка.Ссылка,
	               |	0 КАК СуммаДокументаРасход
	               |ПОМЕСТИТЬ ЭквайрингДокументы
	               |ИЗ
	               |	Документ.ПоступлениеВБанк.ЭквайрингРасшифровка КАК ПоступлениеВБанкЭквайрингРасшифровка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Привязки КАК ВТ_Привязки
	               |		ПО (НАЧАЛОПЕРИОДА(ПоступлениеВБанкЭквайрингРасшифровка.Ссылка.Дата, ДЕНЬ) = ВТ_Привязки.Дата)
	               |			И ПоступлениеВБанкЭквайрингРасшифровка.Терминал = ВТ_Привязки.Терминал
	               |ГДЕ
	               |	ПоступлениеВБанкЭквайрингРасшифровка.Ссылка.Проведен
	               |	И ПоступлениеВБанкЭквайрингРасшифровка.Ссылка.Дата МЕЖДУ &ДатаС И &ДатаПо
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	РасходИзБанкаЭквайрингРасшифровка.Терминал,
	               |	0,
	               |	ЕСТЬNULL(ВТ_Привязки.ТорговаяТочка, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)),
	               |	0,
	               |	РасходИзБанкаЭквайрингРасшифровка.Ссылка,
	               |	РасходИзБанкаЭквайрингРасшифровка.СуммаДокумента
	               |ИЗ
	               |	Документ.РасходИзБанка.ЭквайрингРасшифровка КАК РасходИзБанкаЭквайрингРасшифровка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Привязки КАК ВТ_Привязки
	               |		ПО (НАЧАЛОПЕРИОДА(РасходИзБанкаЭквайрингРасшифровка.Ссылка.Дата, ДЕНЬ) = ВТ_Привязки.Дата)
	               |			И РасходИзБанкаЭквайрингРасшифровка.Терминал = ВТ_Привязки.Терминал
	               |ГДЕ
	               |	РасходИзБанкаЭквайрингРасшифровка.Ссылка.Проведен
	               |	И РасходИзБанкаЭквайрингРасшифровка.Ссылка.Дата МЕЖДУ &ДатаС И &ДатаПо
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЭквайрингДокументы.Терминал,
	               |	ЭквайрингДокументы.СуммаДокументаПриход КАК Приход,
	               |	ЭквайрингДокументы.СуммаКомиссииБанка КАК КомиссииБанка,
	               |	ЭквайрингДокументы.Ссылка КАК Документ,
	               |	ЭквайрингДокументы.СуммаДокументаРасход КАК Расход,
	               |	ЭквайрингДокументы.ТорговаяТочка
	               |ИЗ
	               |	ЭквайрингДокументы КАК ЭквайрингДокументы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СтруктурныеЕдиницы.Ссылка КАК ТорговаяТочка,
	               |	ВЫБОР
	               |		КОГДА СтруктурныеЕдиницы.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Избенка)
	               |			ТОГДА СтруктурныеЕдиницы.Терминал
	               |		ИНАЧЕ ПривязкиОборудованияКРабочимМестамСрезПоследних.Терминал
	               |	КОНЕЦ КАК Терминал
	               |ИЗ
	               |	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПривязкиОборудованияКРабочимМестам.СрезПоследних(&ДатаПо, ) КАК ПривязкиОборудованияКРабочимМестамСрезПоследних
	               |		ПО СтруктурныеЕдиницы.Ссылка = ПривязкиОборудованияКРабочимМестамСрезПоследних.РабочееМесто.СтруктурнаяЕдиница
	               |ГДЕ
	               |	СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Розница)";
				   
	Результаты = Запрос.ВыполнитьПакет();
	Выборка = Результаты[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДанных.Добавить(), Выборка);
	КонецЦикла;	
	
	Выборка = Результаты[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокиТаб = ТаблицаДанных.НайтиСтроки(Новый Структура("ТорговаяТочка, Терминал", Выборка.ТорговаяТочка, Выборка.Терминал));
		Если СтрокиТаб.Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаДанных.Добавить(), Выборка);
		КонецЕсли;	
	КонецЦикла;	
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеДляФормирования", ТаблицаДанных);
	
	//Макет компоновки 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	
	//Компоновка данных
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	//Вывод результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
