
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтрокаПодключения = ОбщегоНазначения.ПолучитьСтрокуПодключенияКБухгалтерии();
	
	v82COMОбъект = Новый COMОбъект(ПолныеПрава.ПолучитьВерсиюКомОбъекта_Бух() + ".COMConnector");
	Попытка
		v82 = v82COMОбъект.Connect(СтрокаПодключения);
	Исключение
		ПодключениеУстановлено=Ложь;
		Сообщить("Не удалось подключиться к базе бухгалтерии!");
		Возврат;
	КонецПопытки;
	
	ЗапросБух = v82.NewObject("Запрос");
	ЗапросФин = Новый Запрос();
	
	ТабБух = Новый ТаблицаЗначений();
	ТабБух.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТабБух.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ТабБух.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы"));
	ТабБух.Колонки.Добавить("Префикс", Новый ОписаниеТипов("Число"));
	ТабБух.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	
	Для каждого Параметр из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл		
		Если СокрЛП(Параметр.Параметр)="Период" Тогда
			ЗапросБух.УстановитьПараметр("НачалоПериода", НачалоДня(Параметр.Значение.ДатаНачала));			
			ЗапросБух.УстановитьПараметр("КонецПериода", КонецДня(Параметр.Значение.ДатаОкончания));
			
			ЗапросФин.УстановитьПараметр("НачалоПериода", НачалоДня(Параметр.Значение.ДатаНачала));			
			ЗапросФин.УстановитьПараметр("КонецПериода", КонецДня(Параметр.Значение.ДатаОкончания));
		КонецЕсли;		
	КонецЦикла;
	
	ЗапросКеш = Новый Запрос();
	ЗапросКеш.Текст = "ВЫБРАТЬ
	                  |	СтруктурныеЕдиницы.Ссылка,
	                  |	СтруктурныеЕдиницы.НомерТочки,
	                  |	СтруктурныеЕдиницы.КодОПБух
	                  |ИЗ
	                  |	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы";
					  
	ТабКеш = ЗапросКеш.Выполнить().Выгрузить();
	
	ЗапросБух.УстановитьПараметр("Счет90_01_1", v82.ПланыСчетов.Хозрасчетный.НайтиПоКоду("90.01.1"));
	ЗапросБух.УстановитьПараметр("Счет50_01", v82.ПланыСчетов.Хозрасчетный.НайтиПоКоду("50.01"));
	ЗапросБух.УстановитьПараметр("Счет62_Р", v82.ПланыСчетов.Хозрасчетный.НайтиПоКоду("62.Р"));
	ЗапросБух.Текст = "ВЫБРАТЬ
						|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт.Наименование КАК Наименование,
						|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт.Код КАК Код,
						|	СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК Сумма,
						|	НачалоПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, День) КАК Период,
						|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт.Префикс КАК Префикс
						|ИЗ
						|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
						|			&НачалоПериода,
						|			&КонецПериода,
						|			СчетКт = &Счет90_01_1
						|				И СчетДт В (&Счет50_01, &Счет62_Р),
						|			,
						|			) КАК ХозрасчетныйДвиженияССубконто
						|
						|СГРУППИРОВАТЬ ПО
						|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт.Наименование,
						|	НачалоПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, День),
						|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт.Префикс,
						|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт.Код";
						
	Выборка = ЗапросБух.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаДоб = ТабБух.Добавить();
		СтрокаДоб.Период = НачалоДня(Выборка.Период);
		СтрокаДоб.Наименование = Выборка.Наименование;
		Попытка
			СтрокаДоб.Префикс = Число(Выборка.Префикс);
			СтрокаКеш = ТабКеш.Найти(СтрокаДоб.Префикс, "НомерТочки");
			Если СтрокаКеш <> Неопределено Тогда
				СтрокаДоб.Подразделение = СтрокаКеш.Ссылка;
			Иначе
				СтрокаКеш = ТабКеш.Найти(Выборка.Код, "КодОПБух");
				Если СтрокаКеш <> Неопределено Тогда
					СтрокаДоб.Подразделение = СтрокаКеш.Ссылка;
				КонецЕсли;	
			КонецЕсли;	
		Исключение
			СтрокаКеш = ТабКеш.Найти(Выборка.Код, "КодОПБух");
			Если СтрокаКеш <> Неопределено Тогда
				СтрокаДоб.Подразделение = СтрокаКеш.Ссылка;
			КонецЕсли;
		КонецПопытки;	
		СтрокаДоб.Сумма = Выборка.Сумма;
	КонецЦикла;
	
	ЗапросФин.УстановитьПараметр("Счет90_4", ПланыСчетов.Финансовый.НайтиПоКоду("90.4"));
	ЗапросФин.УстановитьПараметр("Счет50", ПланыСчетов.Финансовый.НайтиПоКоду("50"));
	ЗапросФин.УстановитьПараметр("Таб", ТабБух);
	ЗапросФин.Текст = "ВЫБРАТЬ
	                  |	Таб.Период,
	                  |	Таб.Наименование,
	                  |	Таб.Подразделение,
	                  |	Таб.Префикс,
	                  |	Таб.Сумма
	                  |ПОМЕСТИТЬ ВТ_Бух
	                  |ИЗ
	                  |	&Таб КАК Таб
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	                  |	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК Справочник.СтруктурныеЕдиницы).НомерТочки КАК НомерТочки,
	                  |	СУММА(ХозрасчетныйДвиженияССубконто.Сумма * ВЫБОР
	                  |			КОГДА ХозрасчетныйДвиженияССубконто.СчетДт = &Счет90_4
	                  |				ТОГДА -1
	                  |			ИНАЧЕ 1
	                  |		КОНЕЦ) КАК Сумма,
	                  |	НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, ДЕНЬ) КАК Период
	                  |ПОМЕСТИТЬ ВТ_Финансы
	                  |ИЗ
	                  |	РегистрБухгалтерии.Финансовый.ДвиженияССубконто(
	                  |			&НачалоПериода,
	                  |			&КонецПериода,
	                  |			СчетКт = &Счет90_4
	                  |				ИЛИ СчетДт = &Счет90_4
	                  |					И СчетКт = &Счет50,
	                  |			,
	                  |			) КАК ХозрасчетныйДвиженияССубконто
	                  |
	                  |СГРУППИРОВАТЬ ПО
	                  |	НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, ДЕНЬ),
	                  |	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК Справочник.СтруктурныеЕдиницы).НомерТочки,
	                  |	ХозрасчетныйДвиженияССубконто.СубконтоКт1
	                  |;
	                  |
	                  |////////////////////////////////////////////////////////////////////////////////
	                  |ВЫБРАТЬ
	                  |	ЕСТЬNULL(ВТ_Финансы.Период, ВТ_Бух.Период) КАК Дата,
	                  |	ВТ_Финансы.СубконтоКт1 КАК СтруктурнаяЕдиницаФин,
	                  |	ВТ_Бух.Наименование КАК СтруктурнаяЕдиницаБух,
	                  |	ВТ_Финансы.Сумма КАК СуммаВФинансах,
	                  |	ВТ_Бух.Сумма КАК СуммаВБухгалтерии
	                  |ИЗ
	                  |	ВТ_Финансы КАК ВТ_Финансы
	                  |		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Бух КАК ВТ_Бух
	                  |		ПО ВТ_Финансы.Период = ВТ_Бух.Период
	                  |			И ВТ_Финансы.СубконтоКт1 = ВТ_Бух.Подразделение";
					  
	ТабДанные = ЗапросФин.Выполнить().Выгрузить();
	
	СтандартнаяОбработка = Ложь;
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеДляФормирования", ТабДанные);
	
	//Макет компоновки 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	
	//Компоновка данных
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	//Вывод результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
