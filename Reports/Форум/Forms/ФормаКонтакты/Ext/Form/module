
////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////

Процедура ПриОткрытии()
	
	ЭлементыФормы.КонтекстноеМеню.Кнопки.кнВойтиОтПользователя.Доступность 		= АдминНаФоруме;
	ЭлементыФормы.КонтекстноеМеню.Кнопки.кнПодключитьсяКТермСессии.Доступность 	= АдминНаФоруме;
	ЭлементыФормы.КонтекстноеМеню.Кнопки.кнПользовательСправочника.Доступность 	= АдминНаФоруме;
	ЭлементыФормы.КонтекстноеМеню.Кнопки.кнСообщениеНаСервер.Доступность 		= АдминНаФоруме;
	ЭлементыФормы.КонтекстноеМеню.Кнопки.кнНаписатьПисьмо.Доступность 			= АдминНаФоруме;
	
	СформироватьОтчет();
	
	ПодключитьОбработчикОжидания("СетФокус", 0.1, Истина);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////

Процедура ФИОАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	СформироватьОтчет();
	
КонецПроцедуры

Процедура ФИОПриИзменении(Элемент)
	
	СформироватьОтчет();
	
КонецПроцедуры

Процедура ФИООчистка(Элемент, СтандартнаяОбработка)
	
	СформироватьОтчет();
	
КонецПроцедуры

Процедура ТПКонтактыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Пользователь = ВыбраннаяСтрока.Пользователь;
	
	Если Колонка.Имя = "Пользователь"
			И АдминНаФоруме Тогда
		
		Пользователь.ПолучитьФорму().Открыть();
		
	ИначеЕсли Колонка.Имя = "Емайл" Тогда
		
		Емайл = ВыбраннаяСтрока.Емайл;
		ТекстПисьма = ПолучитьТекстПисьма(Пользователь);
		СтрокаЗапуска = "mailto:" + Емайл + "?cc= &bcc= &subject=1С&body=" + ТекстПисьма;
		ЗапуститьПриложение(СтрокаЗапуска);
		
	ИначеЕсли Колонка.Имя = "Группа" Тогда
		
		ФИО = ВыбраннаяСтрока.Группа;
		СформироватьОтчет();
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// НАЖАТИЕ КНОПОК
////////////////////////////////////////////////////////////////////////////////

Процедура СформироватьОтчет()
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтрокаОтбора"		, "%" + СокрЛП(ЭлементыФормы.ФИО.Значение) + "%");
	Запрос.УстановитьПараметр("ИспользоватьОтбор"	, СокрЛП(ЭлементыФормы.ФИО.Значение) <> "");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.Родитель КАК Группа,
	|	НастройкиПользователейФорума.Емайл КАК Емайл,
	|	НастройкиПользователейФорума.ТелефонРабочий КАК ТелефонРабочий,
	|	НастройкиПользователейФорума.ТелефонСотовый КАК ТелефонСотовый
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|		ПО Пользователи.Ссылка = НастройкиПользователейФорума.Пользователь
	|ГДЕ
	|	(НЕ Пользователи.ПометкаУдаления)
	|	И (НЕ Пользователи.ЭтоГруппа)
	|	И ВЫБОР
	|			КОГДА &ИспользоватьОтбор
	|				ТОГДА Пользователи.Наименование ПОДОБНО &СтрокаОтбора
	|						ИЛИ Пользователи.Код ПОДОБНО &СтрокаОтбора
	|						ИЛИ Пользователи.Родитель.Наименование ПОДОБНО &СтрокаОтбора
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Пользователи.Родитель.Наименование,
	|	Пользователи.Наименование";
	Выборка = Запрос.Выполнить().Выбрать();
	
	//
	ТПКонтакты.Очистить();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТПКонтакты.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВойтиОтПользователя(Кнопка)
	
	ТекСтр = ЭлементыФормы.ТПКонтакты.ТекущаяСтрока;
	Если ТекСтр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//
	Логин = СокрЛП(ТекСтр.Пользователь.Код);
	ВходОтПользователя(Логин);
	
КонецПроцедуры

Процедура ПользовательСправочника(Кнопка)
	
	ТекСтр = ЭлементыФормы.ТПКонтакты.ТекущаяСтрока;
	Если ТекСтр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//
	ТекСтр.Пользователь.ПолучитьФорму().Открыть();
	
КонецПроцедуры

Процедура ПодключитьсяКТерминальнойСессии(Кнопка)
	
	ТекСтр = ЭлементыФормы.ТПКонтакты.ТекущаяСтрока;
	Если ТекСтр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//
	Логин = СокрЛП(ТекСтр.Пользователь.Код);
	ПодключитьсяКСессии(Логин);
	
КонецПроцедуры

Процедура СообщениеНаСервер(Кнопка)
	
	ТекСтр = ЭлементыФормы.ТПКонтакты.ТекущаяСтрока;
	Если ТекСтр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//
	врФайл = ПолучитьИмяВременногоФайла();
	ЗапТекст = Новый ЗаписьТекста(врФайл, КодировкаТекста.ANSI);
	Для каждого ВыдСтрока Из ЭлементыФормы.ТПКонтакты.ВыделенныеСтроки Цикл
		ЗапТекст.ЗаписатьСтроку(СокрЛП(ВыдСтрока.Пользователь.Код));
	КонецЦикла;
	ЗапТекст.Закрыть();
	ОтправитьСообщениеПоФайлу(врФайл, ТекСтр.Пользователь);
	
КонецПроцедуры

Процедура НаписатьПисьмо(Кнопка)
	
	ТекСтр = ЭлементыФормы.ТПКонтакты.ТекущаяСтрока;
	Если ТекСтр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Пользователь = ТекСтр.Пользователь;
	Емайл = ПолучитьЕмайлПользователя(Пользователь);
	Если Емайл <> Неопределено Тогда
		ТекстПисьма = ПолучитьТекстПисьма(Пользователь);
		СтрокаЗапуска = "mailto:" + Емайл + "?cc= &bcc= &subject=1С&body=" + ТекстПисьма;
		ЗапуститьПриложение(СтрокаЗапуска);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

Процедура СетФокус()
	
	ТекущийЭлемент = ЭлементыФормы.ФИО;
	
КонецПроцедуры

Функция КорректныйАдресЕмайл(СтрокаАдреса)
	
	Шаблон = ".+@.+\..+"; // регулярное выражение соответствует произвольному e-mail адресу
	
	RegExp = Новый COMОбъект("VBScript.RegExp"); // создаем объект для работы  с регулярными выражениями
	RegExp.MultiLine 	= Ложь;    // истина — текст многострочный, ложь — одна строка
	RegExp.Global 		= Истина;     // истина — поиск по всей строке, ложь — до первого совпадения
	RegExp.IgnoreCase 	= Истина; // истина — игнорировать регистр строки при поиске
	RegExp.Pattern 		= Шаблон;    // шаблон (регулярное выражение) для проверки  корректности e-mail
	Если RegExp.Test(СтрокаАдреса) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
