
////////////////////////////////////////////////////////////////////////////////
// ГЛОБАЛЬНЫЕ ПЕРЕМЕННЫЕ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////
Перем НомерСтраницы;
Перем мКолСтраниц;
Перем МассивНомеровСтраниц;
Перем ФормаЗаписиНастроекПользователяФорума;
Перем СтруктураСортировки;
Перем ФормаИсправлениеОшибок;


////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////

Процедура ПриОткрытии()
	
	ВосстановитьЗначенияФормы();
	
	Инициализация();
	
	Панель.Страницы[0].Значение = Новый Структура("Вопрос, НомерСтраницы", Неопределено, 0);
	ЭлементыФормы.ПанельСтраниц.Страницы[0].Значение = Новый Структура("Вопрос, НомерСтраницы", Неопределено, 0);
	
	ВывестиГлавную();
	
	Если НЕ ЗаполненыНастройкиПользователяФорума() Тогда
		// Выносить мозг до тех пор, пока сами не заполнят свои контакты :)
		ПодключитьОбработчикОжидания("ОткрытьФормуНастроекПользователяФорума"	, 120		, Ложь);
	КонецЕсли;
	ПодключитьОбработчикОжидания("ОбновлениеГлавнойСтраницы"					, 20 * 60	, Ложь);
	ПодключитьОбработчикОжидания("ПоказатьВопросыДляОзнакомления"				, 20 * 60	, Ложь);
	ПодключитьОбработчикОжидания("ПоказатьВопросыДляОзнакомленияПриОткрытии"	, 1			, Истина);
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанНовыйВопрос" Тогда
		НомерСтраницы = 1;
		ВывестиГлавную();
	КонецЕсли;
	
	Если ИмяСобытия = "ПросмотрОбязательногоВопросаФорума" Тогда
		ПоказатьОбсуждениеВопроса(Параметр);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////

Процедура ДокументСформирован(Элемент)
	
	ЭлементыФормы[Элемент.Имя].Документ.body.scroll = "auto";
	
КонецПроцедуры

Процедура ДокументСформирован_ИТС(Элемент)
	
	Если ЭлементыФормы[Элемент.Имя].Документ.URLUnencoded = "http://its.1c.ru/personal/profile.php" Тогда
		
		Если ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница <> ЭлементыФормы.ПанельСтраниц.Страницы[0] Тогда
			ЭлементыФормы.ПанельСтраниц.Страницы.Удалить(ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + Формат(ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы,"ЧГ=")]);
		КонецЕсли;
		Предупреждение("Нельзя менять регистрационные данные!", 30);
		//ДобавитьСтраницу_ИТС();
		//ЗавершитьРаботуСистемы(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВопросовНаСтраницеПриИзменении(Элемент)
	
	СохранитьЗначение("Форум_ВопросовНаСтранице", ВопросовНаСтранице);
	
	НомерСтраницы = 1;
	
	ВывестиГлавную();
	
КонецПроцедуры

Процедура ПолеHTMLДокументаonclick(Элемент, pEvtObj)
	
	htmlElement = pEvtObj.srcElement;
	
	//////////////////////////////
	Если ЭтоАдресВИнтернете(htmlElement) Тогда
		ОткрытьURL(htmlElement.href, htmlElement.innerHTML);
		Возврат;
	КонецЕсли;
	
	//////////////////////////////
	Попытка
		ID = htmlElement.ID;
		Если ID = "Add_Question" Тогда // Добавление нового вопроса
			Документы.ВопросыПользователей.ПолучитьФормуНовогоДокумента().Открыть();
		ИначеЕсли ID = "page_back" Тогда // Переход назад
		    Если НомерСтраницы > 1 Тогда
				НомерСтраницы = НомерСтраницы - 1;
				ВывестиГлавную();
			КонецЕсли;
		ИначеЕсли ID = "page_next" Тогда // Переход вперед
		    Если НомерСтраницы < мКолСтраниц Тогда
				НомерСтраницы = НомерСтраницы + 1;
				ВывестиГлавную();
			КонецЕсли;
		ИначеЕсли ID = "opendocument" Тогда	
			ДокВопрос = Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber);
			ДокВопрос.ПолучитьФорму().ОткрытьМодально();
			ПоказатьОбсуждениеВопроса(ДокВопрос);
		ИначеЕсли ID = "close_vopros" Тогда  // Снятие вопроса
			Если Вопрос("Снятием Вы подтверждаете, что разобрались с вопросом, продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				//ДокВопрос = Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber).ПолучитьОбъект();
				ДокВопрос = ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.Вопрос.ПолучитьОбъект();
				ДокВопрос.Снят = Истина;
				ДокВопрос.Записать();
				ПоказатьОбсуждениеВопроса(ДокВопрос.Ссылка);
			КонецЕсли;
		ИначеЕсли ID = "show_main" Тогда  // Показать главную
			СброситьВсеОтборы(Неопределено);
		ИначеЕсли ID = "open_efp" Тогда  // Открыть справочник "ВнешниеОбработки"
			ОткрытьВнешниеОтчеты();
		ИначеЕсли ID = "open_journal" Тогда  // Открыть журнал изменений
			Элем = Справочники.ВнешниеОбработки.НайтиПоНаименованию("Журнал регистрации");
			Если ЗначениеЗаполнено(Элем) Тогда
				ИмяФайла = ПолучитьИмяВременногоФайла();
				ДвоичныеДанные = Элем.ХранилищеВнешнейОбработки.Получить();
				ДвоичныеДанные.Записать(ИмяФайла);
				Форма = ВнешниеОбработки.ПолучитьФорму(ИмяФайла);
				Если Форма <> Неопределено Тогда
					Форма.Открыть();
				КонецЕсли;	
			КонецЕсли;
		ИначеЕсли ID = "refresh_page" Тогда  // Обновить страницу
			ДокВопрос = ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.Вопрос;
			ПоказатьОбсуждениеВопроса(ДокВопрос);
			УстановитьФокусВводаОтвета();
		ИначеЕсли ID = "update_news" Тогда  // Обновить новости
			ПоказатьНовости();
		ИначеЕсли ID = "close_page" Тогда  // Закрыть вкладку
			ЗакрытьВкладку(Неопределено);
		ИначеЕсли ID = "add_comment" Тогда  // Добавление нового комментария
			ТекстКомментария = ЭлементыФормы["Поле_" + Формат(ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы,"ЧГ=")].Документ.parentWindow.eval("GetTextComment()");
			Если СокрЛП(ТекстКомментария) = "" Тогда
				Предупреждение("Вы не ввели текст комментария!", 30);
			Иначе
				ДокВопрос = ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.Вопрос;
				ДобавитьКомментарий(ДокВопрос, ТекстКомментария);
				ПоказатьОбсуждениеВопроса(ДокВопрос.Ссылка);
			КонецЕсли;
			УстановитьФокусВводаОтвета();
		ИначеЕсли ID = "My_Options" Тогда  // Изменение настроек пользователя
			ФормаЗаписиНастроекПользователяФорума.Открыть();
		ИначеЕсли ID = "SITE_ITS" Тогда  // 
			ДобавитьСтраницу_ИТС();
		ИначеЕсли ID = "reglaments" Тогда  // Показать страницу с регламентами
			ПоказатьРегламенты();
		ИначеЕсли ID = "Forum_Manual" Тогда
			ПоказатьХелп();
		ИначеЕсли ID = "list_users" Тогда  // 
			ПолучитьФорму("ФормаКонтакты").Открыть();
		ИначеЕсли ID = "statistika" Тогда  // Статистика
			ПолучитьФорму("ФормаСтатистика").Открыть();
		ИначеЕсли ID = "clickmail" Тогда  // 
			ТекстПисьма = "";
			СтрокаЗапуска = "mailto:" + СокрЛП(htmlElement.email) + "?cc= &bcc= &subject=1С 8.2&body=" + ТекстПисьма;
			ЗапуститьПриложение(СтрокаЗапуска);
		ИначеЕсли ID = "mail_all" Тогда // Письмо всем пользователям
			НаписатьПисьмоВсемПользователям();
		ИначеЕсли ID = "sort_by_date" Тогда // Сортировка по дате создания вопроса
			СортировкаГлавной("ДатаВопроса");
		ИначеЕсли ID = "sort_by_active_date" Тогда // Сортировка по дате последней активности
			СортировкаГлавной("ДатаПоследнейАктивности");
		ИначеЕсли ID = "sort_by_autor" Тогда // Сортировка по автору
			СортировкаГлавной("Автор");
		ИначеЕсли ID = "sort_by_otvets" Тогда // Сортировка по количеству комментариев
			СортировкаГлавной("Ответов");
		ИначеЕсли ID = "podtverjdenie" Тогда //	
			Если Вопрос("Подтверждаете, что вопрос и все комментарии прочитаны?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда	
				ЗаписатьПодтверждение();
			КонецЕсли;
		ИначеЕсли Найти(ID,"page_") > 0 Тогда // Переход на другую страницу
			НомерСтраницы = Число(Сред(ID, Найти(ID,"_") + 1, СтрДлина(ID) - Найти(ID,"_")));
			ВывестиГлавную();
		ИначеЕсли Найти(ID,"vorp_") > 0 Тогда // Открыть обсуждение вопроса
			НомерВопроса = Сред(ID, Найти(ID,"_") + 1, СтрДлина(ID) - Найти(ID,"_"));
			ДокВопрос =  Документы.ВопросыПользователей.НайтиПоНомеру(НомерВопроса);
			ПоказатьОбсуждениеВопроса(ДокВопрос);
		ИначеЕсли Найти(ID,"user_") > 0 Тогда // Сведения об авторе вопроса
			КодПольз = Сред(ID, Найти(ID,"_") + 1, СтрДлина(ID) - Найти(ID,"_"));
			Польз = Справочники.Пользователи.НайтиПоКоду(КодПольз);
			Если АдминНаФоруме И ЗначениеЗаполнено(Польз) Тогда
				ДействиеНадПользователем(Польз);
			КонецЕсли;
		ИначеЕсли Найти(ID,"openObject_") > 0 ИЛИ Найти(ID,"openComentObject_") > 0 Тогда // Открыть форму прикрепленного объекта
			Если htmlElement.metaName = "ВопросыПользователей" Тогда
				ДокВопрос = Документы.ВопросыПользователей.ПолучитьСсылку(Новый УникальныйИдентификатор(htmlElement.uid));
				ПоказатьОбсуждениеВопроса(ДокВопрос);
			Иначе
				СтрокаЗапуска = "" + htmlElement.metaType + "[""" + htmlElement.metaName + """].ПолучитьСсылку(Новый УникальныйИдентификатор(""" + htmlElement.uid + """)).ПолучитьФорму().Открыть()";
				Выполнить(СтрокаЗапуска);
			КонецЕсли;
		ИначеЕсли Найти(ID,"openFile_") > 0 Тогда // Открыть прикрепленный файл
			ДокВопрос = Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber).ПолучитьОбъект();
			ДокВопрос.ПоказатьПрикрепленныйФайл(htmlElement.fileName);
		ИначеЕсли Найти(ID,"coment_change") > 0 Тогда // Изменение текста комментария
			НовыйТекст = ФорматироватьСтрокуДляКомментария(htmlElement.coment_text);
			Если ВвестиСтроку(НовыйТекст,"Изменение комментария",0,Истина) Тогда
				ДокВопрос 	= Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber);
				Период		= Дата(htmlElement.period);
				ИзменитьКомментарий(ДокВопрос, Период, НовыйТекст);
				ПоказатьОбсуждениеВопроса(ДокВопрос);
				УстановитьФокусВводаОтвета();
			КонецЕсли;
		ИначеЕсли Найти(ID,"addlinkcomment_") > 0 Тогда // Добавление ссылки к коментарию
			//Ном			= Сред(ID, Найти(ID,"_") + 1, СтрДлина(ID) - Найти(ID,"_")); // Номер объекта
			ДокВопрос 	= Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber);
		    Период		= Дата(htmlElement.period);
			ДобавитьСсылкуККомментарию(ДокВопрос, Период);
		ИначеЕсли Найти(ID,"addlinkvopros_") > 0 Тогда // Добавление ссылки другой вопрос форума
			ДокВопрос 	= Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber);
		    Период		= Дата(htmlElement.period);
			ДобавитьСсылкуНаДругойВопрос(ДокВопрос, Период);
		ИначеЕсли Найти(ID,"changelinkcomment") > 0 Тогда // Изменение ссылки к коментарию
			Ном			= Сред(ID, Найти(ID,"_") + 1, СтрДлина(ID) - Найти(ID,"_")); // Номер объекта
			ДокВопрос 	= Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber);
		    Период		= Дата(htmlElement.period);
			ДобавитьСсылкуККомментарию(ДокВопрос, Период, Ном);
		ИначеЕсли Найти(ID,"addfile_") > 0 Тогда // Добавить файл к коментарию
			Ном			= Сред(ID, Найти(ID,"_") + 1, СтрДлина(ID) - Найти(ID,"_")); // Номер объекта
			ДокВопрос 	= Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber);
			Период		= Дата(htmlElement.period);
			ПрикрепитьФайлККомментарию(ДокВопрос, Период);
		ИначеЕсли Найти(ID,"openComentFile_") > 0 Тогда // Открыть файл прикрепленный к комментарию
			ДокВопрос 	= Документы.ВопросыПользователей.НайтиПоНомеру(htmlElement.docNumber);
			Период		= Дата(htmlElement.period);
			ОткрытьФайлКомментария(ДокВопрос, Период);
		ИначеЕсли Найти(ID,"smile") > 0	Тогда
			ИмяПоля = "Поле_" + Формат(ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы,"ЧГ=");
			ЭлементыФормы[ИмяПоля].Документ.parentWindow.eval("insert_text_cursor('" + ID + "')");
			ЭлементыФормы[ИмяПоля].Документ.parentWindow.eval("hiddenLayer('PanelSML')");
		КонецЕсли;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	pEvtObj.returnValue = Ложь;
	
КонецПроцедуры

Процедура ПолеHTMLДокументаondblclick(Элемент, pEvtObj)
	//ЭлементыФормы.ПанельСтраниц.Страницы.Удалить(ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + Формат(ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы,"ЧГ=")]);
КонецПроцедуры

Процедура ПолеHTMLДокументаonhelp(Элемент, pEvtObj)
	
	Попытка
		htmlElement = pEvtObj.srcElement;
		procParam   = htmlElement.document.body.procParam;
		ВывестиГлавную_Поиск(procParam);
	Исключение
	КонецПопытки;
	
	pEvtObj.returnValue = Ложь;
	
КонецПроцедуры

Процедура ПолеHTMLonkeypress(Элемент, pEvtObj)
	
	// Обновление страницы по нажатию Ctrl+R
	Если pEvtObj.keyCode = 18 И pEvtObj.ctrlKey Тогда
		
		ТекЗнач = ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение;
		Если ТекЗнач.НомерСтраницы = 0 Тогда
			ВывестиГлавную();
		Иначе
			ПоказатьОбсуждениеВопроса(ТекЗнач.Вопрос);
		КонецЕсли;
		
	// Контакты: Ctrl+K
	ИначеЕсли pEvtObj.keyCode = 11 И pEvtObj.ctrlKey Тогда
		
		ПолучитьФорму("ФормаКонтакты").Открыть();
		
	ИначеЕсли pEvtObj.keyCode = 27 Тогда
		
		Если ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница <> ЭлементыФормы.ПанельСтраниц.Страницы[0] И Вопрос("Закрыть текущую вкладку?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			ЭлементыФормы.ПанельСтраниц.Страницы.Удалить(ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + Формат(ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы,"ЧГ=")]);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПанельСтраницПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	УстановитьВидимостьСтраниц();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// НАЖАТИЕ КНОПОК
////////////////////////////////////////////////////////////////////////////////

Процедура ЗакрытиеФорума(Элемент)
	
	Если АдминНаФоруме Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытьВкладку(Кнопка)
	
	Если ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница <> ЭлементыФормы.ПанельСтраниц.Страницы[0] Тогда
		Ном = ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы;
		ЭлементыФормы.ПанельСтраниц.Страницы.Удалить(ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + Формат(Ном,"ЧГ=")]);
		ЭлементыФормы.Удалить(ЭлементыФормы["Поле_" + Формат(Ном,"ЧГ=")]);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытьВсеВкладки(Кнопка)
	
	Кол = ЭлементыФормы.ПанельСтраниц.Страницы.Количество() - 1;
	Если Кол > 0 И Вопрос("Закрыть вкладки?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		Пока Кол > 0 Цикл
			НомерСтраницы = ЭлементыФормы.ПанельСтраниц.Страницы[Кол].Значение.НомерСтраницы;
			ЭлементыФормы.ПанельСтраниц.Страницы.Удалить(ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + НомерСтраницы]);
			ЭлементыФормы.Удалить(ЭлементыФормы["Поле_" + Формат(НомерСтраницы,"ЧГ=")]);
			Кол = Кол - 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьВсеВопросы(Элемент)
	
	НомерСтраницы = 1;
	ВывестиГлавную();
	
КонецПроцедуры

Процедура СброситьВсеОтборы(Элемент)
	
	ОтборПостроителя = ПостроительОтчета.Отбор;
	Для каждого Элем Из ОтборПостроителя Цикл
		Элем.Использование = Ложь;
	КонецЦикла;
	
	НомерСтраницы = 1;
	ВывестиГлавную();
	
КонецПроцедуры

Процедура ДобавитьВопрос()
	
	Документы.ВопросыПользователей.ПолучитьФормуНовогоДокумента().Открыть();
	
КонецПроцедуры

Процедура ОткрытьЖурналРегистрации()
	
	Элем = Справочники.ВнешниеОбработки.НайтиПоНаименованию("Журнал регистрации");
	Если ЗначениеЗаполнено(Элем) Тогда
		ИмяФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные = Элем.ХранилищеВнешнейОбработки.Получить();
		ДвоичныеДанные.Записать(ИмяФайла);
		Форма = ВнешниеОбработки.ПолучитьФорму(ИмяФайла);
		Если Форма <> Неопределено Тогда
			Форма.Открыть();
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьУниверсальныйЖурнал(Кнопка)
	
	Элем = Справочники.ВнешниеОбработки.НайтиПоНаименованию("Универсальный журнал документов");
	Если ЗначениеЗаполнено(Элем) Тогда
		ИмяФайла = ПолучитьИмяВременногоФайла();
		ДвоичныеДанные = Элем.ХранилищеВнешнейОбработки.Получить();
		ДвоичныеДанные.Записать(ИмяФайла);
		Форма = ВнешниеОбработки.ПолучитьФорму(ИмяФайла);
		Если Форма <> Неопределено Тогда
			Форма.Открыть();
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьТелефоны(Элемент)
	
	ПолучитьФорму("ФормаКонтакты").Открыть();
	
КонецПроцедуры

Процедура ОткрытьВнешниеОтчеты()
	
	Попытка
		Форма = Справочники.ВнешниеОбработки.ПолучитьФормуСписка();
	Исключение
		Сообщить("Ошибка, возможно в конфигурации нет справочника ""ВнешниеОбработки""", СтатусСообщения.Важное);
		Возврат;
	КонецПопытки;
	Форма.СправочникСписок.Отбор.ВидОбработки.ВидСравнения  = ВидСравнения.ВСписке;
	Форма.СправочникСписок.Отбор.ВидОбработки.Использование = Истина;
	СписокВидов = Новый СписокЗначений;
	СписокВидов.Добавить(Перечисления.ВидыДополнительныхВнешнихОбработок.Отчет);
	СписокВидов.Добавить(Перечисления.ВидыДополнительныхВнешнихОбработок.Обработка);
	Форма.СправочникСписок.Отбор.ВидОбработки.Значение = СписокВидов;
	Форма.Открыть();
	
КонецПроцедуры

Процедура ПоказатьРегламенты()
	
	ОтборПостроителя = ПостроительОтчета.Отбор;
	Для каждого Элем Из ОтборПостроителя Цикл
		Элем.Использование = Ложь;
	КонецЦикла;
	Если ОтборПостроителя.Найти("Категория") = Неопределено Тогда
		ОтборПостроителя.Добавить("Категория");
	КонецЕсли;
	ОтборПостроителя.Категория.Значение 		= Справочники.КатегорииВопросов.Регламенты;
	ОтборПостроителя.Категория.Использование 	= Истина;
	
	ВывестиГлавную();
	
КонецПроцедуры

Процедура ОтчетПоСтатистике(Кнопка)
	
	ПолучитьФорму("ФормаСтатистика").Открыть();
	
КонецПроцедуры

Процедура ПоказатьНастройки(Кнопка)
	
	ФормаЗаписиНастроекПользователяФорума.Открыть();
	
КонецПроцедуры

Процедура Справка(Кнопка)
	
	ПоказатьХелп();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

Процедура УстановитьВидимостьСтраниц()
	
	Для каждого Элем Из ЭлементыФормы Цикл
		Если Найти(Элем.Имя, "Поле_") > 0 Тогда
			ЭлементыФормы[Элем.Имя].Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	ИмяПоля = "Поле_" + ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы;
	Попытка
		ЭлементыФормы[ИмяПоля].Видимость = Истина;
		ТекущийЭлемент = ЭлементыФормы[ИмяПоля];
	Исключение
	КонецПопытки;
	
КонецПроцедуры

Процедура ОткрытьФормуНастроекПользователяФорума()
	
	Если ЗаполненыНастройкиПользователяФорума() Тогда
		ОтключитьОбработчикОжидания("ОткрытьФормуНастроекПользователяФорума");
	Иначе
		//ОчиститьСообщения();
		Сообщить("Заполните обязательные поля и это окно перестанет надоедать!", СтатусСообщения.Важное);
    	ФормаЗаписиНастроекПользователяФорума.Открыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеГлавнойСтраницы()
	
	//НомерСтраницы = 1;
	ОтборПостроителя = ПостроительОтчета.Отбор;
	Для каждого Элем Из ОтборПостроителя Цикл
		Элем.Использование = Ложь;
	КонецЦикла;
	
	ВывестиГлавную();
	
КонецПроцедуры

Процедура ПоказатьВопросыДляОзнакомления()
	
	врТЗ = ПолучитьТаблицуВопросовДляОзнакомления();
	Если врТЗ.Количество() > 0 Тогда
		ФормаОзнакомление = ПолучитьФорму("ФормаОзнакомление");
		ФормаОзнакомление.ЭлементыФормы.ТПВопросы.Значение = врТЗ;
		ФормаОзнакомление.ЭлементыФормы.ТПВопросы.СоздатьКолонки();
		ФормаОзнакомление.Открыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьВопросыДляОзнакомленияПриОткрытии()
	
	ПоказатьВопросыДляОзнакомления();
	
КонецПроцедуры

Процедура ЗаписатьПодтверждение()
	
	ТекПольз  = ПараметрыСеанса.ТекущийПользователь;
	
	ДокВопрос = ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.Вопрос;
	Набор = РегистрыСведений.ВопросыФорумаДляОзнакомления.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(ТекПольз);
	Набор.Прочитать();
	НовЗап = Набор.Добавить();
	НовЗап.Пользователь = ТекПольз;
	НовЗап.Вопрос = ДокВопрос;
	НовЗап.ДатаОзнакомления = ТекущаяДата();
	Набор.Записать();
	
	///////////////////////////////
	ЗаполнитьМассивВопросовДляОзнакомления();
	ПоказатьОбсуждениеВопроса(ДокВопрос);
	ПоказатьВопросыДляОзнакомления();
	
КонецПроцедуры

Процедура ВосстановитьЗначенияФормы()
	
	Попытка
		ВопросовНаСтранице = ВосстановитьЗначение("Форум_ВопросовНаСтранице");
		Если ВопросовНаСтранице = 0
				ИЛИ ВопросовНаСтранице = Неопределено Тогда
			ВопросовНаСтранице = 100;
		КонецЕсли;
	Исключение
		ВопросовНаСтранице = 100;
	КонецПопытки;
	
КонецПроцедуры

Функция ПолучитьСтрокуСоСтраницами()
	
	Разделитель = "<FONT color='#D5D5D5'> | </FONT>";
	Результат = ПолучитьТекстСсылки("page_back", "3399CC", " Пред.") + Разделитель;
	Для Ном = 1 по мКолСтраниц Цикл
		Если Ном = НомерСтраницы Тогда
			Результат = Результат + ПолучитьТекстСсылки("page_" + Ном, , Ном, Истина) + Разделитель;
		ИначеЕсли Ном >= НомерСтраницы - 4 И Ном <= НомерСтраницы + 4 Тогда
			Результат = Результат + ПолучитьТекстСсылки("page_" + Ном, "3399CC", Ном) + Разделитель;
		КонецЕсли;
	КонецЦикла;	
	
	Возврат Результат + ПолучитьТекстСсылки("page_next", "3399CC", " След.");
	
КонецФункции

Функция ПолучитьСтрокуАвтор(Пользователь, МассивАктивныхЛогинов, ПоказыватьЛогин = Ложь)
	
	Если НЕ ПоказыватьЛогин Тогда
		врСтр = СокрЛП(Пользователь);
	Иначе
		врСтр = СокрЛП(Пользователь.Код);
	КонецЕсли;
	
	Если ПользовательСейчасВБазе(Пользователь, МассивАктивныхЛогинов) Тогда
		СтрокаАвтор = "<FONT color='#008000'>" + врСтр + "</FONT>";
	Иначе
		СтрокаАвтор = "<FONT color='#808080'>" + врСтр + "</FONT>";
	КонецЕсли;
	
	Возврат СтрокаАвтор;
	
КонецФункции

Функция ПолучитьСтрокуСВопросами(ВыборкаВопросов)
	
	Результат = "";
	Счетчик = 1;
	//МассивАктивныхЛогинов = ПолучитьМассивАктивныхЛогинов();
	
	Пока ВыборкаВопросов.Следующий() Цикл
		Если Счетчик >= (НомерСтраницы - 1) * ВопросовНаСтранице + 1
				И Счетчик <= НомерСтраницы * ВопросовНаСтранице Тогда
			ЦветФона 	= ?(Счетчик % 2 = 1, "FFFFFF", "F4F4F4");
			ЦветКол		= ?(ВыборкаВопросов.Снят,"008000","D02090");
			Парам1	 = ?(Счетчик % 2 = 1, 0, 1);
			Результат = Результат +
				"<tr style='cursor:pointer;color:#3399CC;font-size:14pt;background-color:#" + ЦветФона + "'>" + 
					"<td ID = 'vorp_" + ВыборкаВопросов.НомерВопроса + "' onMouseOver=""lightOn('vorp_" + ВыборкаВопросов.НомерВопроса +
						"')"" onMouseOut=""lightOff('" + Парам1 + "')"" > <FONT color='#993300' size='3'> №" + ВыборкаВопросов.НомерВопроса +
						" (" + ВыборкаВопросов.Категория + ")" + ". </FONT> " + ВыборкаВопросов.Вопрос + "</td>" +
					"<td style='cursor:pointer;color:#3399CC;font-size:12pt;background-color:#" + ЦветФона + "' ID = 'user_" +
						СокрЛП(ВыборкаВопросов.АвторВопросаКод) + "'><FONT color='#808080'>" + ВыборкаВопросов.АвторВопроса + "</FONT></td>" +
					"<td align='center' style='color:#" + ЦветКол + ";font-size:11pt;background-color:#" + ЦветФона + "'> " +
						ВыборкаВопросов.Ответов + "</td>" + 
					"<td style='cursor:pointer;color:#3399CC;font-size:12pt;background-color:#" + ЦветФона + "'>" +
						Формат(ВыборкаВопросов.ДатаПоследнейАктивности, "ДФ='dd.MM.yyyy hh:mm'") + 
						"<br>" + СокрЛП(ВыборкаВопросов.ПоследнийАвторКод) + "</td></tr>";
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьРезультатПоиска(ВыборкаВопросов, СтрокаПоиска)
	
	Результат = "";
	Пока ВыборкаВопросов.Следующий() Цикл
		Результат = Результат + "<tr style='cursor:pointer;color:#3399CC;font-size:14pt;'>" + 
		"<td ID = 'vorp_" + ВыборкаВопросов.НомерВопроса + 
		"'  > <FONT color='#993300' size='3'> №" + 
		ВыборкаВопросов.НомерВопроса + " (" + ВыборкаВопросов.Категория + ")" + ". </FONT> " + 
		СтрЗаменить(ВыборкаВопросов.Вопрос, СтрокаПоиска,"<FONT color='#FF0000'>" + СтрокаПоиска + "</FONT>") + 
		"</td></tr>";
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНомерСтраницы()
	
	Макс = МассивНомеровСтраниц[0];
	Для каждого Элем Из МассивНомеровСтраниц Цикл
		Если Элем > Макс Тогда
			Макс = Элем;
		КонецЕсли;
	КонецЦикла;
	
	МассивНомеровСтраниц.Добавить(Макс + 1);
	
	Возврат Макс + 1;
	
КонецФункции

Процедура ВывестиГлавную()
	
	ПостроительОтчета.Параметры.Вставить("ТекДата", ТекущаяДата());
	ПостроительОтчета.Выполнить();
	ВыборкаВопросов = ПостроительОтчета.Результат.Выбрать();
	Если Цел(ВыборкаВопросов.Количество()/ВопросовНаСтранице) = ВыборкаВопросов.Количество() / ВопросовНаСтранице Тогда
		мКолСтраниц = Цел(ВыборкаВопросов.Количество() / ВопросовНаСтранице);
	Иначе
	    мКолСтраниц = Цел(ВыборкаВопросов.Количество() / ВопросовНаСтранице) + 1;
	КонецЕсли;
	ТекстНомеровСтраниц = ПолучитьСтрокуСоСтраницами();
	
	//////////////////////////////////////////////////////////////////
	ТекстГлавнойСтраницы = "
	|<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01//EN"" ""http://www.w3.org/TR/html4/strict.dtd"">
	|<HTML>
	|<head>
	|<meta http-equiv='Content-Type' content='text/html; charset=utf-8'>
	|<title>Форум</title>
    |</head>
	//|<BODY BACKGROUND='" + ЭлементыФормы.Поле_0.ПолучитьURL(Метаданные.ОбщиеКартинки.FonNY) + "'>
	|<BODY>
	|<style>
	|	a {color:#FB723B}
	|	a:hover {color:#3399CC;text-decoration:underline}
	|	table.submenu {border: 1px #0000f0 solid; visibility: hidden; position: absolute; left: 13; top: 35; cursor: pointer}
	|</style>
	|
	|<script type='text/javascript' language='javascript'>
	|var oldId;
	|var procParam;	
	|
	|function lightOn(elem) {
	|	oldId = elem;
	|	document.getElementById(elem).style.background = '#DAFFD3';
	|}
	|
	|
	|function lightOnColor(elem, mColor) {
	|	oldId = elem;
	|	document.getElementById(elem).style.background = mColor;
	|}
	|
	|function lightOffMenu() {
	|	document.getElementById(oldId).style.background = '#DAFFD3'
	|}
	|
	|
	|function lightOff(par1) {
	|	if (par1 == 0)
	|	  document.getElementById(oldId).style.background = '#FFFFFF'
	|	else
	|	  document.getElementById(oldId).style.background = '#F4F4F4'
	|}
	|
	|function hiddenLayer(elem) {
	|	document.getElementById(elem).style.visibility = 'hidden';
	|}
	|
	|function showLayer(elem) {
	|	document.getElementById(elem).style.visibility = 'visible';
	|}
	|
	|function Create1CProcess(pParam){ 
	|    var evt = document.createEventObject(); 
	|    document.body.procParam = pParam;
	|    document.body.fireEvent('onhelp', evt); 
	|} 	
	|
	|
	|function onEnter(){
	|	if(event.keyCode==13)
	|   	Create1CProcess(document.getElementById('searh_forum').value);
	|}
	|
	|</script>
	|
	|
	|<table border='0' frame='border' width='100%' cellpadding='12'>
	|<tr style='cursor:pointer;color:#000000;background-color:#F4F4F4'> 
	|<div style='color:#003366;' align='right'> Поиск: <input onkeypress='onEnter();' id='searh_forum' type='text' size='34' title='Поиск по номеру вопроса, по текстам вопросов и комментариям'> </div>
	|<th id='sort_by_date' title='Сортировка по дате создания вопросов' >Вопрос</th>  
	|<th id='sort_by_autor' width='10%' title='Сортировка по автору'>Автор</th>
	|<th id='sort_by_otvets' width='1%' title='Сортировка по количеству комментариев'>Ответы</th>
	|<th id='sort_by_active_date' width='20%' title='Сортировка по дате последней активности'>Обновление</th></tr>
	|" + ПолучитьСтрокуСВопросами(ВыборкаВопросов) + "
	|</table>
	|<br>
	|<div align='center'>Страницы: " + ТекстНомеровСтраниц + " </div>
	|</BODY>
	|</HTML>
	|";
	
	ЭлементыФормы.Поле_0.УстановитьТекст(ТекстГлавнойСтраницы);
	
	ПоказатьНовости();
	
КонецПроцедуры

Процедура ВывестиГлавную_Поиск(СтрокаПоиска)
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтрокаПоиска", "%"+СтрокаПоиска+"%");
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВопросыПользователей.Ссылка КАК Документ,
	|	ВопросыПользователей.Дата КАК ДатаВопроса,
	|	ВопросыПользователей.Категория КАК Категория,
	|	ВЫРАЗИТЬ(ВопросыПользователей.Вопрос КАК СТРОКА(1024)) КАК Вопрос,
	|	ВопросыПользователей.Номер КАК НомерВопроса,
	|	ВопросыПользователей.Автор КАК АвторВопроса,
	|	ВопросыПользователей.Автор.Код КАК АвторВопросаКод,
	|	ЕСТЬNULL(ОтветыНаВопросСрезПоследних.Период, ВопросыПользователей.Дата) КАК ПоследняяДата,
	|	ЕСТЬNULL(ОтветыНаВопросСрезПоследних.Пользователь, ВопросыПользователей.Автор) КАК ПоследнийАвтор,
	|	ЕСТЬNULL(ОтветыНаВопросСрезПоследних.Пользователь.Код, ВопросыПользователей.Автор.Код) КАК ПоследнийАвторКод,
	|	ЕСТЬNULL(Подсчет.Ответов, 0) КАК Ответов,
	|	ВопросыПользователей.Снят КАК Снят,
	|	ВЫБОР
	|		КОГДА ОтветыНаВопросСрезПоследних.Период ЕСТЬ NULL 
	|			ТОГДА ВопросыПользователей.Дата
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ОтветыНаВопросСрезПоследних.ДатаИзменения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА ОтветыНаВопросСрезПоследних.Период
	|				ИНАЧЕ ОтветыНаВопросСрезПоследних.ДатаИзменения
	|			КОНЕЦ
	|	КОНЕЦ КАК ДатаПоследнейАктивности,
	|	НастройкиПользователейФорума.ГруппаПользователя КАК ГруппаПользователя
	|ИЗ
	|	Документ.ВопросыПользователей КАК ВопросыПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветыНаВопрос.СрезПоследних КАК ОтветыНаВопросСрезПоследних
	|		ПО ВопросыПользователей.Ссылка = ОтветыНаВопросСрезПоследних.Вопрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОтветыНаВопрос.Вопрос КАК Вопрос,
	|			КОЛИЧЕСТВО(ВЫРАЗИТЬ(ОтветыНаВопрос.Комментарий КАК СТРОКА(1024))) КАК Ответов
	|		ИЗ
	|			РегистрСведений.ОтветыНаВопрос КАК ОтветыНаВопрос
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОтветыНаВопрос.Вопрос) КАК Подсчет
	|		ПО ВопросыПользователей.Ссылка = Подсчет.Вопрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветыНаВопрос КАК ОтветыНаВопрос
	|		ПО ВопросыПользователей.Ссылка = ОтветыНаВопрос.Вопрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|		ПО ВопросыПользователей.Автор = НастройкиПользователейФорума.Пользователь
	|ГДЕ
	|	(ВопросыПользователей.Вопрос ПОДОБНО &СтрокаПоиска
	|			ИЛИ ВопросыПользователей.Номер ПОДОБНО &СтрокаПоиска
	|			ИЛИ ОтветыНаВопрос.Комментарий ПОДОБНО &СтрокаПоиска)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПоследнейАктивности УБЫВ";
	ВыборкаВопросов = Запрос.Выполнить().Выбрать();
	
	ТекстГлавнойСтраницы = "
	|<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01//EN"" ""http://www.w3.org/TR/html4/strict.dtd"">
	|<HTML>
	|<head>
	|<meta http-equiv='Content-Type' content='text/html; charset=utf-8'>
    |</head>
	|<BODY>
	|
	|<table border='1' frame='border' width='100%' cellpadding='12'>
	|<tr style='cursor:pointer;color:#000000'>
	|<td><a ID= 'show_main' href=''>Показать все вопросы</a> <br> Результаты поиска фразы """ + СтрокаПоиска + """:</td>
	|</tr>
	|" + ПолучитьРезультатПоиска(ВыборкаВопросов, СтрокаПоиска) + "
	|</table>
	|<br>
	|</BODY>
	|</HTML>
	|";
	ЭлементыФормы.Поле_0.УстановитьТекст(ТекстГлавнойСтраницы);
	
КонецПроцедуры

Функция ПолучитьЦветНовости(НаДату)
	
	СтруктРез = Новый Структура("Дата,Цвет,ЕстьКартинка");
	ДлинаСуток = 86400;
	Если НачалоДня(НаДату) >= НачалоДня(ТекущаяДата()) Тогда
		СтруктРез.Дата = "";
		СтруктРез.Цвет = "#FF0000";
		СтруктРез.ЕстьКартинка = Истина;
	ИначеЕсли НачалоДня(НаДату) = НачалоДня(ТекущаяДата()) - ДлинаСуток Тогда
		СтруктРез.Дата = "";
		СтруктРез.Цвет = "#FF0000";
		СтруктРез.ЕстьКартинка = Истина;
	Иначе
		СтруктРез.Дата = Формат(НаДату, "ДФ=dd.MM.yy");
		СтруктРез.Цвет = "#6F6F6F";
		СтруктРез.ЕстьКартинка = Ложь;
	КонецЕсли;
	
	Возврат СтруктРез;
	
КонецФункции

Процедура ПоказатьНовости()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВопросыПользователей.Дата КАК Дата,
	|	ВопросыПользователей.Номер КАК Номер,
	|	(ВЫРАЗИТЬ(ВопросыПользователей.Вопрос КАК СТРОКА(45))) + "" ..."" КАК Вопрос,
	|	ВЫРАЗИТЬ(ВопросыПользователей.Вопрос КАК СТРОКА(1024)) КАК ПолныйТекстВопроса,
	|	ВопросыПользователей.Автор
	|ИЗ
	|	Документ.ВопросыПользователей КАК ВопросыПользователей
	|ГДЕ
	|	(НЕ ВопросыПользователей.ПометкаУдаления)
	|	И ВопросыПользователей.ПоказыватьВНовостях
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	СтрТаблицаНовостей = "";
	Пока Выборка.Следующий() Цикл
		ЦветНовости = ПолучитьЦветНовости(Выборка.Дата);
		СтрТаблицаНовостей = СтрТаблицаНовостей + "
		|<tr style='cursor: pointer'>
		|<td ID = 'vorp_" + Выборка.Номер + "'>" + ?(ЦветНовости.ЕстьКартинка, "<img ID = 'vorp_" + Выборка.Номер + "' src='" + ЭлементыФормы.Поле_0.ПолучитьURL(Метаданные.ОбщиеКартинки.Новости) + "' >","") + " <FONT COLOR=" + ЦветНовости.Цвет + ">" + ЦветНовости.Дата + "</FONT></td>
		|<td> <a href='' ID = 'vorp_" + Выборка.Номер + "' title='" + Выборка.ПолныйТекстВопроса + "' >" + Выборка.Вопрос + "</a></td>
		|</tr>
		|";
	КонецЦикла;
	СтрТаблицаНовостей = СтрТаблицаНовостей;
	
	//////////////////////////////////
	ТекстСтраницыНовостей = "
	|<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01//EN"" ""http://www.w3.org/TR/html4/strict.dtd"">
	|<HTML>
	|<head>
	|<meta http-equiv='Content-Type' content='text/html; charset=utf-8'>
	|<title>Форум</title>
    |</head>
	//|<BODY BACKGROUND='" + ЭлементыФормы.Поле_0.ПолучитьURL(Метаданные.ОбщиеКартинки.FonNY) + "'>
	|<BODY>
	|<style>
	|	a {color:#3399CC;text-decoration:none}
	|	a:hover {color:GREEN;text-decoration:underline}
	|	table.submenu {border: 1px #0000f0 solid; visibility: hidden; position: absolute; left: 13; top: 35; cursor: pointer}
	|</style>
	|
	|<FONT COLOR=GREEN><H2 ID='update_news' align=center style='cursor: pointer' title='Щелкните чтобы обновить раздел новостей'>Новости</H2></FONT>
	|
	|<table border cellpadding='3' cellspacing='0' bordercolor='#D3D3D3'>
	|<th>Дата</th>
	|<th>Описание</th>
	|" + СтрТаблицаНовостей + "
	|</table>
	|</HTML>       
	|<BODY>
	|";
	ЭлементыФормы.ПолеHTML_Новости.УстановитьТекст(ТекстСтраницыНовостей);
	
КонецПроцедуры

Процедура СортировкаГлавной(ИмяПоля)
	
	ПостроительОтчета.Порядок.Очистить();
	Если СтруктураСортировки.ИмяПоля = ИмяПоля Тогда
		Если СтруктураСортировки.Направление = НаправлениеСортировки.Возр Тогда
			СтруктураСортировки.Направление = НаправлениеСортировки.Убыв;
		Иначе
			СтруктураСортировки.Направление = НаправлениеСортировки.Возр;
		КонецЕсли;
	Иначе
		СтруктураСортировки.ИмяПоля 	= ИмяПоля;
		СтруктураСортировки.Направление = НаправлениеСортировки.Убыв;
	КонецЕсли;
	ПостроительОтчета.Порядок.Добавить(СтруктураСортировки.ИмяПоля,,, СтруктураСортировки.Направление);
	
	НомерСтраницы = 1;
	ВывестиГлавную();
	
КонецПроцедуры

Функция ПолучитьПоле(Документ, ДанныеДокумента)
	
	// Сначала ищем уже открытую страницу
	Для каждого Страница Из ЭлементыФормы.ПанельСтраниц.Страницы Цикл
		Если Страница.Значение.Вопрос = Документ Тогда
			Если ЭлементыФормы.Найти("Поле_" + Формат(Страница.Значение.НомерСтраницы,"ЧГ=")) <> Неопределено Тогда
				ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница = ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + Формат(Страница.Значение.НомерСтраницы,"ЧГ=")];
				Возврат ЭлементыФормы["Поле_" + Формат(Страница.Значение.НомерСтраницы,"ЧГ=")];
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	// Создаем новую страницу и новое поле 
	НомерТекущейСтраницы = ПолучитьНомерСтраницы();
	ИмяСтраницы = "Страница_" + НомерТекущейСтраницы;
	ИмяПоля 	= "Поле_" + НомерТекущейСтраницы;
	
	//////////////////////////////////////////////////
	Структура = Новый Структура("Вопрос, НомерСтраницы", Документ, НомерТекущейСтраницы);
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ВопросыПользователей") Тогда
		НовСтр = ЭлементыФормы.ПанельСтраниц.Страницы.Добавить("Страница_" + НомерТекущейСтраницы, "№" + СокрЛП(ДанныеДокумента.Номер) + "-" + СокрЛП(ДанныеДокумента.Логин), Структура, ); // БиблиотекаКартинок.ПользовательБезНеобходимыхСвойств
	ИначеЕсли ТипЗнч(Документ) = Тип("Строка") Тогда
		НовСтр = ЭлементыФормы.ПанельСтраниц.Страницы.Добавить("Страница_" + НомерТекущейСтраницы, Документ, Структура, );
	КонецЕсли;
	ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница = НовСтр;
	
	//////////////////////////////////////////////////
	Поле = ЭлементыФормы.Добавить(Тип("ПолеHTMLДокумента"), ИмяПоля, Истина, ЭлементыФормы.ПанельПолей);
	Поле.Верх = 52;
	Поле.Лево = 0;
	Поле.Ширина = ЭтаФорма.Ширина - 20;
	Поле.Высота = ЭтаФорма.Высота - 30;
	Поле.КонтекстноеМеню = ЭлементыФормы.КонтекстноеМенюБраузер;
	Поле.АвтоКонтекстноеМеню = Истина;
	
	Поле.УстановитьПривязку(ГраницаЭлементаУправления.Низ	, ЭлементыФормы.ПанельСтраниц, ГраницаЭлементаУправления.Низ);
	Поле.УстановитьПривязку(ГраницаЭлементаУправления.Право	, ЭлементыФормы.ПанельСтраниц, ГраницаЭлементаУправления.Право);
	Поле.УстановитьДействие("ДокументСформирован"	, Новый Действие("ДокументСформирован"));
	Поле.УстановитьДействие("onclick"				, Новый Действие("ПолеHTMLДокументаonclick"));
	Поле.УстановитьДействие("ondblclick"			, Новый Действие("ПолеHTMLДокументаondblclick"));
	Поле.УстановитьДействие("onhelp"				, Новый Действие("ПолеHTMLДокументаonhelp"));
	Поле.УстановитьДействие("onkeypress"			, Новый Действие("ПолеHTMLonkeypress"));
	
	//////////////////////////////////////////////////
	Возврат ЭлементыФормы[ИмяПоля];
	
КонецФункции

Процедура ОткрытьURL(URL, innerHTML)
	
	Для каждого Страница Из ЭлементыФормы.ПанельСтраниц.Страницы Цикл
		Если Страница.Значение.Вопрос = URL Тогда
			ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница = ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + Формат(Страница.Значение.НомерСтраницы,"ЧГ=")];
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// Создаем новую страницу и новое поле 
	НомерТекущейСтраницы = ПолучитьНомерСтраницы();
	ИмяСтраницы = "Страница_" + НомерТекущейСтраницы;
	ИмяПоля 	= "Поле_" + НомерТекущейСтраницы;
	
	//////////////////////////////////////////////////
	Структура = Новый Структура("Вопрос,НомерСтраницы", URL, НомерТекущейСтраницы);
	НовСтр = ЭлементыФормы.ПанельСтраниц.Страницы.Добавить("Страница_" + НомерТекущейСтраницы, Лев(innerHTML,30), Структура, );
	ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница = НовСтр;
	
	//////////////////////////////////////////////////
	Поле = ЭлементыФормы.Добавить(Тип("ПолеHTMLДокумента"), ИмяПоля, Истина, ЭлементыФормы.ПанельПолей);
	Поле.Верх = 4;
	Поле.Лево = 2;
	Поле.Ширина = ЭтаФорма.Ширина - 20;
	Поле.Высота = ЭтаФорма.Высота - 40;
	Поле.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ЭлементыФормы.ПанельСтраниц, ГраницаЭлементаУправления.Низ);
	Поле.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭлементыФормы.ПанельСтраниц, ГраницаЭлементаУправления.Право);
	
	ЭлементыФормы[ИмяПоля].Перейти(URL);
	
КонецПроцедуры

Процедура ДобавитьСтраницу_ИТС()
	
	URL 	= "http://its.1c.ru/auth.php";
	Логин 	= Справочники.НастройкиФорума.ПользовательСайтаИТС;
	Пароль 	= Справочники.НастройкиФорума.ПарольСайтаИТС;
	СтрокаЗапуска = URL + "?login=yes&backurl=/index.php&AUTH_FORM=Y&TYPE=AUTH&USER_LOGIN=" + Логин + "&USER_PASSWORD=" + Пароль + "";
	Для каждого Страница Из ЭлементыФормы.ПанельСтраниц.Страницы Цикл
		Если Страница.Значение.Вопрос = URL Тогда
			ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница = ЭлементыФормы.ПанельСтраниц.Страницы["Страница_" + Формат(Страница.Значение.НомерСтраницы,"ЧГ=")];
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	// Создаем новую страницу и новое поле 
	НомерТекущейСтраницы = ПолучитьНомерСтраницы();
	ИмяСтраницы = "Страница_" + НомерТекущейСтраницы;
	ИмяПоля 	= "Поле_" + НомерТекущейСтраницы;
	
	//////////////////////////////////////////////////
	Структура = Новый Структура("Вопрос,НомерСтраницы", URL, НомерТекущейСтраницы);
	НовСтр = ЭлементыФормы.ПанельСтраниц.Страницы.Добавить("Страница_" + НомерТекущейСтраницы, "ИТС", Структура, );
	ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница = НовСтр;
	
	//////////////////////////////////////////////////
	Поле = ЭлементыФормы.Добавить(Тип("ПолеHTMLДокумента"), ИмяПоля, Истина, ЭлементыФормы.ПанельПолей);
	Поле.Верх = 52;
	Поле.Лево = 0;
	Поле.Ширина = ЭтаФорма.Ширина - 20;
	Поле.Высота = ЭтаФорма.Высота - 30;
	Поле.КонтекстноеМеню = ЭлементыФормы.КонтекстноеМенюБраузер;
	//Поле.АвтоКонтекстноеМеню = Истина;
	Поле.УстановитьПривязку(ГраницаЭлементаУправления.Низ, ЭлементыФормы.ПанельСтраниц, ГраницаЭлементаУправления.Низ);
	Поле.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭлементыФормы.ПанельСтраниц, ГраницаЭлементаУправления.Право);
	Поле.УстановитьДействие("onkeypress", Новый Действие("ПолеHTMLonkeypress"));
	Поле.УстановитьДействие("ДокументСформирован", Новый Действие("ДокументСформирован_ИТС"));
	
	ЭлементыФормы[ИмяПоля].Перейти(СтрокаЗапуска);
	
	Состояние("");
	
КонецПроцедуры

Процедура УстановитьФокусВводаОтвета()
	
	ЭлементыФормы["Поле_" + Формат(ЭлементыФормы.ПанельСтраниц.ТекущаяСтраница.Значение.НомерСтраницы,"ЧГ=")].Документ.parentWindow.eval("SetFocusOtvet()");
	
КонецПроцедуры

Функция ПолучитьНомерНезаполненногоРеквизита(МенеджерЗаписи)
	
	Если МенеджерЗаписи.Выбран() Тогда
		Для Ном = 1 По 5 Цикл
			Если МенеджерЗаписи["СсылкаНаОбъект" + Ном] = Неопределено Тогда
				Возврат Ном;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ДобавитьСсылкуККомментарию(Документ, Период, НомерОбъекта = Неопределено)
	
	МенеджерЗаписи = РегистрыСведений.ОтветыНаВопрос.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период	= Период;
	МенеджерЗаписи.Вопрос 	= Документ;
	МенеджерЗаписи.Прочитать();
	Если НомерОбъекта = Неопределено Тогда
		НомерОбъекта = ПолучитьНомерНезаполненногоРеквизита(МенеджерЗаписи);
	КонецЕсли;
	Если НомерОбъекта = Неопределено Тогда
		Предупреждение("К одному комментарию можно прикрепить не более 5-ти объектов!", 10);
		Возврат;
	КонецЕсли;
	ИмяРеквизита = "СсылкаНаОбъект" + НомерОбъекта;
	Массив = Новый Массив;
	Для Каждого Док Из Метаданные.Документы Цикл
		Массив.Добавить(Тип("ДокументСсылка." + Док.Имя));
	КонецЦикла;	
	Для Каждого Справочник Из Метаданные.Справочники Цикл
		Массив.Добавить(Тип("СправочникСсылка." + Справочник.Имя));
	КонецЦикла;
	Для Каждого План Из Метаданные.ПланыВидовРасчета Цикл
		Массив.Добавить(Тип("ПланВидовРасчетаСсылка." + План.Имя));
	КонецЦикла;
	Для Каждого ПланСчетов Из Метаданные.ПланыСчетов Цикл
		Массив.Добавить(Тип("ПланСчетовСсылка." + ПланСчетов.Имя));
	КонецЦикла;
	// .. мне остальные не были нужны :)
	ВыбЗнач = Неопределено;
	Если МенеджерЗаписи.Выбран() Тогда
		ВыбЗнач = МенеджерЗаписи[ИмяРеквизита];
	КонецЕсли;
	Если ВвестиЗначение(ВыбЗнач, "Выберите тип объекта", Новый ОписаниеТипов(Массив)) Тогда
		МенеджерЗаписи[ИмяРеквизита] = ВыбЗнач;
		МенеджерЗаписи.Записать();
		ПоказатьОбсуждениеВопроса(Документ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьСсылкуНаДругойВопрос(Документ, Период, НомерОбъекта = Неопределено)
	
	МенеджерЗаписи = РегистрыСведений.ОтветыНаВопрос.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период	= Период;
	МенеджерЗаписи.Вопрос 	= Документ;
	МенеджерЗаписи.Прочитать();
	Если НомерОбъекта = Неопределено Тогда
		НомерОбъекта = ПолучитьНомерНезаполненногоРеквизита(МенеджерЗаписи);
	КонецЕсли;
	Если НомерОбъекта = Неопределено Тогда
		Предупреждение("К одному комментарию можно прикрепить не более 5-ти объектов!", 10);
		Возврат;
	КонецЕсли;
	ИмяРеквизита = "СсылкаНаОбъект" + НомерОбъекта;
	
	ВыбЗнач = Документы.ВопросыПользователей.ПолучитьФормуВыбора().ОткрытьМодально();
	Если ВыбЗнач <> Неопределено Тогда
		МенеджерЗаписи[ИмяРеквизита] = ВыбЗнач;
		МенеджерЗаписи.Записать();
		ПоказатьОбсуждениеВопроса(Документ);
		Если ВыбЗнач = Документ Тогда
			Сообщить("А вообще, нет смысла добавлять ссылку на вопрос в обсуждении этого же вопроса :)", СтатусСообщения.Информация);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрикрепитьФайлККомментарию(Документ, Период)
	
	ЗначениеВозврата = ВыбратьФайл();
	
	МенеджерЗаписи = РегистрыСведений.ОтветыНаВопрос.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период	= Период;
	МенеджерЗаписи.Вопрос 	= Документ;
	МенеджерЗаписи.Прочитать();
	Если ЗначениеВозврата = Неопределено Тогда
		МенеджерЗаписи.ИмяФайла = "";
		МенеджерЗаписи.Файл 	= Неопределено;
	Иначе
		СтруктураВозврата = ПолучитьИнформациюОФайле(ЗначениеВозврата);
		Если ЗначениеВозврата <> Неопределено Тогда
			МаксРазмер = Справочники.НастройкиФорума.ОграничениеНаРазмерФайлов_МБ.Наименование;
			Попытка
				МаксРазмер = Число(МаксРазмер);
			Исключение
				МаксРазмер = 0;
			КонецПопытки;
			Если МаксРазмер <> 0 И СтруктураВозврата.Размер > МаксРазмер Тогда
				Предупреждение("Ограничение на размер файлов: " + МаксРазмер + " МБ", 10);
				Возврат;
			КонецЕсли;
			МенеджерЗаписи.ИмяФайла = СтруктураВозврата.ИмяФайла;
			МенеджерЗаписи.Файл = Новый ХранилищеЗначения(Новый ДвоичныеДанные(СтруктураВозврата.ПолноеИмяФайла), Новый СжатиеДанных(9));
		Иначе	
			МенеджерЗаписи.ИмяФайла = "";
			МенеджерЗаписи.Файл 	= Неопределено;
		КонецЕсли;
	КонецЕсли;
	МенеджерЗаписи.Записать();
	
	///////////////////////////////////////////
	ПоказатьОбсуждениеВопроса(Документ);
	
КонецПроцедуры

Процедура ПоказатьОбсуждениеВопроса(Документ)
	
	ДанныеДокумента	= ПолучитьДанныеДокумента(Документ);
	Поле 			= ПолучитьПоле(Документ, ДанныеДокумента);
	
	ТекстСтраницы = "
	|<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01//EN"" ""http://www.w3.org/TR/html4/strict.dtd"">
	|<HTML>
	|<head>
	|<meta http-equiv='Content-Type' content='text/html; charset=utf-8'>
	|<title></title>
    |</head>
	|
	|<style>
	|	a {color:#3399CC;text-decoration:none}
	|	a:hover {color:#FB723B;text-decoration:underline}
	|	table.submenu {border: 1px #000000 solid; background-color: #FFFFFF; visibility: hidden; position: absolute; left: 1; cursor: pointer}
	|</style>
	|
	|<script type='text/javascript' language='javascript'>
	|
	|var procType;
	|var procParam;
	|var procComment;
	|
	|var oldId;
	|
	|function lightOn(elem) {
	|	oldId = elem;
	|	document.getElementById(elem).style.background = '#CCFFCC';
	|}
	|
	|function lightOff() {
	|	document.getElementById(oldId).style.background = '#FFFFFF';
	|}
	|
	|
	|function lightOnColor(elem, mColor) {
	|	oldId = elem;
	|	document.getElementById(elem).style.background = mColor;
	|}
	|
	|function Create1CProcess(pType,pParam,procComment){
	|	var evt = document.createEventObject();
	|	document.body.procType 		= pType;
	|	document.body.procParam 	= pParam;
	|	document.body.procComment 	= procComment;
	|	document.body.fireEvent('onhelp', evt);
	|}
	|
	|// fucking IE :)
	|function insert_text_cursor(sText){
	|	var obj = document.getElementById('comment'); 
	|	if (document.selection){
	|		obj.focus();
	|  		sel = document.selection.createRange();
	|		sel.text = sText;
	|	}
	|}
	|
	|
	|function SetFocusOtvet(){
	|	document.getElementById('comment').focus();
	|}
	|
	|
	|function GetTextComment(){
	|	return document.getElementById('comment').value;
	|}
	|
	|
	|function CommentQuote(elemID) {
	|	document.getElementById('comment').value = document.getElementById(elemID).coment_Quote;
	|	SetFocusOtvet();
	|}
	|
	|function CommentName(sText) {
	|	document.getElementById('comment').value = sText;
	|	SetFocusOtvet();
	|}
	|
	|
	|function SetCommentSelText(){
	|	var txt = '';
	|	if (window.getSelection){
	|		txt = window.getSelection();
	|    }else if (document.getSelection){
	|		txt = document.getSelection();
	|    }else if (document.selection){
	|    	txt = document.selection.createRange().text;
	|    }
	|    document.getElementById('comment').value = txt;
	|}	
	|
	|
	|function MyRepl(tag,s){
	|	return '<' + tag + '>' + s + '</' + tag + '>';
	|}
	|
	|function replaceSelectedText(tag,cbFunc){	
	|	var obj = document.getElementById('comment');
	|	obj.focus();
	|	if (document.selection){
	|		var s = document.selection.createRange(); 
	|		if (s.text){
	| 			eval('s.text='+cbFunc+'(tag,s.text);');
	| 			s.select();
	| 			return true;
	|		}
	|	}
	|	else if (typeof(obj.selectionStart)=='number'){ 
	|  		if (obj.selectionStart!=obj.selectionEnd){
	|    		var start = obj.selectionStart;
	|    		var end = obj.selectionEnd;
	|    		eval('var rs = '+cbFunc+'(tag,obj.value.substr(start,end-start));');
	|    		obj.value = obj.value.substr(0,start)+rs+obj.value.substr(end);
	|    		obj.setSelectionRange(end,end);
	|  		}
	|  	return true;
	|	}
	|return false;
	|}	
	|
	|
	|function InsertHyperLink(){
	|	var addr = prompt('Введите адрес: ', 'http://');
	|   var desc = prompt('Введите описание: ', '');
	|	if (desc == ''){
	|		desc = addr;
	|	}
	|	insert_text_cursor('<A HREF=""' + addr + '"">' + desc + '</A>');
	|}
	|
	|
	|function SelectTextColor() {
	|	var obj = document.getElementById('colorbox');
 	|	obj.style.background = colorbox.value;
	|	insert_text_cursor('<FONT COLOR=""#' + colorbox.value + '""> </FONT>');
	|}
	|
	|
	|function showLayer(elem) {
	|	document.getElementById(elem).style.visibility = 'visible';
	|}
	|
	|function hiddenLayer(elem) {
	|	document.getElementById(elem).style.visibility = 'hidden';
	|	lightOff();
	|}
	|
	|</script>
	|
	//|<BODY BACKGROUND='" + ЭлементыФормы.Поле_0.ПолучитьURL(Метаданные.ОбщиеКартинки.FonNY) + "'>
	|<BODY>
	|<table border='10' frame='border' width='100%' cellpadding='20' cellspacing='0' bordercolor='#F0F0F0'>
	|<tr align='left' style='cursor:pointer;color:#3399CC;font-size:12;background-color:#FFFFFF'> 
	|<td>
	|<FONT color='#993300' size='3'> №" + ДанныеДокумента.Номер + " (" + ДанныеДокумента.Категория + ")" +
		". </FONT> <B><a href='' ID='user_" + СокрЛП(ДанныеДокумента.Логин) + "' >" + ДанныеДокумента.Автор +
		"</a></B>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<FONT color='#999999'>" + Формат(Документ.Дата, "ДФ='dd.MM.yyyy hh:mm'") +
		"</FONT> &nbsp;&nbsp;&nbsp " + ДанныеДокумента.ГруппаПользователя + " <br> 
	|<FONT color='#000000'>" + ДанныеДокумента.Вопрос + "</FONT><br><br>" + 
	ПолучитьСтрокуПрикрепленныхОбъектовКВопросу(Документ, ДанныеДокумента) + 
	"
	|
	|" + ?(ДанныеДокумента.КоличествоСсылок = 0, "<FONT color='#FF0000' size='4'><B><div id='Forum_Manual'>Вопросы без ссылок на документы рассматриваются в последнюю очередь</div></B></FONT>", "") + "
	|</td>
	|" + ПолучитьТекстСнятияВопроса(ДанныеДокумента) + " 
	|" + ПолучитьСтрокуСКомментариями(Документ, ДанныеДокумента) + "
	|</table>
	|
	|<br>
	|" + ПолучитьПолеВводаОтвета(ДанныеДокумента) + "
	|</BODY>
	|</HTML>
	|";
	Поле.УстановитьТекст(ТекстСтраницы);
	
	ТекущийЭлемент = Поле;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОСНОВНАЯ ЧАСТЬ 
////////////////////////////////////////////////////////////////////////////////
НомерСтраницы 			= 1;
МассивНомеровСтраниц 	= Новый Массив;
МассивНомеровСтраниц.Добавить(0);
ФормаЗаписиНастроекПользователяФорума =
	РегистрыСведений.НастройкиПользователейФорума.ПолучитьФормуРедактированияЗаписи("ФормаЗаписи", ЭтаФорма, "ФормаЗаписиНастроекПользователяФорума");
СтруктураСортировки = Новый Структура;
СтруктураСортировки.Вставить("ИмяПоля"		, "ДатаПоследнейАктивности");
СтруктураСортировки.Вставить("Направление"	, НаправлениеСортировки.Убыв);
