
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТабДок = РасшифроватьОтчет(Расшифровка);
	
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ФиксацияСверху = 3;
	ТабДок.Показать();
	
КонецПроцедуры

&НаСервере
Функция РасшифроватьОтчет(Расшифровка)
	
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	//ДатаОтчета = Данные.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаОтчета")).Значение.Дата;
	ДатаОтчета = ТекущаяДата();

	Поля = ИнфокомТиповыеОтчеты.ПолучитьМассивПолейРасшифровки(Расшифровка, Данные);
	
	НоменклатураОтбор = Неопределено;
	СкладОтбор = Неопределено;
	ХарактеристикаОтбор = Неопределено;
	Для Каждого ПолеРасшифровки Из Поля Цикл
		Если ТипЗнч(ПолеРасшифровки) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
			Если ПолеРасшифровки.Поле = "Номенклатура" Тогда
				НоменклатураОтбор = ПолеРасшифровки.Значение;
			КонецЕсли;
			Если ПолеРасшифровки.Поле = "Склад" Тогда
				СкладОтбор = ПолеРасшифровки.Значение;
			КонецЕсли;
			Если ПолеРасшифровки.Поле = "Характеристика" Тогда
				ХарактеристикаОтбор = ПолеРасшифровки.Значение;
			КонецЕсли;
			Если ПолеРасшифровки.Поле = "Дата" Тогда
				ДатаОтчета = ПолеРасшифровки.Значение;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	
	ТаблицаДанных = Отчеты.НехваткаТовараПересорт.ПолучитьДанныеДляОтчета(ДатаОтчета, НоменклатураОтбор, СкладОтбор);
	
	Макет = Отчеты.НехваткаТовараПересорт.ПолучитьМакет("Расшифровка");
	ТабДок = Новый ТабличныйДокумент();
	
	Область = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(Область);
	
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		Если ЗначениеЗаполнено(СкладОтбор)
			И СтрокаДанных.Склад <> СкладОтбор Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(НоменклатураОтбор)
			И СтрокаДанных.Номенклатура <> НоменклатураОтбор Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ХарактеристикаОтбор)
			И СтрокаДанных.Характеристика <> ХарактеристикаОтбор Тогда
			Продолжить;
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("Строка");
		Область.Параметры.Заполнить(СтрокаДанных);
		ТабДок.Вывести(Область);
	КонецЦикла;	
	
	Возврат ТабДок;
	
КонецФункции
