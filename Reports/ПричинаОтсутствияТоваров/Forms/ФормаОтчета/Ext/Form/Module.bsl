
&НаКлиенте
Процедура ПоставщикиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытияФормы = Новый Структура("Поставщики, Номенклатура", Поставщики, ПолучитьАналоги(Номенклатура, Отчет.НеПоказыватьАналоги));
	ОткрытьФорму(СтроковыеФункцииКлиентСервер.ПолучитьПолноеИмяФормы(ЭтаФорма.ИмяФормы, "ФормаВыбораПоставщиков"), ПараметрыОткрытияФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ВсяНоменклатура = ПолучитьАналоги(Номенклатура, Отчет.НеПоказыватьАналоги);
	Отчет.Номенклатура.ЗагрузитьЗначения(ВсяНоменклатура.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикиПриИзменении(Элемент)
	
	Отчет.Поставщики.Очистить();
	Для Каждого ЭлементСписка Из Поставщики Цикл
		Отчет.Поставщики.Добавить(ЭлементСписка.Значение);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКоординатыПоТТСервер(ТТ)
	
	Отчет.КоординатыШирота = ТТ.КоординатыШирота;
	Отчет.КоординатыДолгота = ТТ.КоординатыДолгота;
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьКоординатыПоТТ(Команда)
	
	ТТ = ОткрытьФормуМодально("Справочник.СтруктурныеЕдиницы.ФормаВыбора");
	
	Если ЗначениеЗаполнено(ТТ) Тогда
		ЗаполнитьКоординатыПоТТСервер(ТТ);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАналоги(СписокНоменклатуры, НеПоказыватьАналоги)
	
	Если НеПоказыватьАналоги Тогда
		Возврат СписокНоменклатуры;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СпрНоменклатура.Ссылка КАК Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|ГДЕ
		|	СпрНоменклатура.Ссылка В (&СписокНоменклатуры)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АналогиНоменклатурыТовары.Номенклатура
		|ИЗ
		|	Справочник.АналогиНоменклатуры.Товары КАК АналогиНоменклатурыТовары
		|ГДЕ
		|	АналогиНоменклатурыТовары.Ссылка В
		|			(ВЫБРАТЬ
		|				АналогиНоменклатурыТовары.Ссылка
		|			ИЗ
		|				Справочник.АналогиНоменклатуры.Товары КАК АналогиНоменклатурыТовары
		|			ГДЕ
		|				АналогиНоменклатурыТовары.Номенклатура В (&СписокНоменклатуры)
		|				И НЕ АналогиНоменклатурыТовары.Ссылка.ПометкаУдаления
		|				И АналогиНоменклатурыТовары.Ссылка.ПолностьюЗаменяемыйТовар)
		|	И НЕ АналогиНоменклатурыТовары.Номенклатура.ПометкаУдаления");
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	Результат = Новый СписокЗначений;
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Результат.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
