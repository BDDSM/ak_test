
Функция ПолучитьТаблицуИсправлений(мДатаНачала, мДатаОкончания,
						ЕстьОтборТТ, МассивТТ, ЕстьОтборРасчетчик, МассивРасчетчиков, ЕстьОтборПродавец, МассивПродавцов) Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений();
	ТаблицаДанных.Колонки.Добавить("Дата"				, Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ТорговаяТочка"		, Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы"));
	ТаблицаДанных.Колонки.Добавить("Расчетчик"			, Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ТаблицаДанных.Колонки.Добавить("Продавец"			, Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("СуммаКорректировки"	, Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Если НЕ (ЗначениеЗаполнено(мДатаНачала)
				И ЗначениеЗаполнено(мДатаОкончания)
				И НЕ мДатаОкончания < мДатаНачала) Тогда
		Возврат ТаблицаДанных;	
	КонецЕсли;
	
	
	///////////////////////////////////////////////
	// Соединение с [SMS_IZBENKA].[dbo].[checks] //
	///////////////////////////////////////////////
	ВремТаблица = Новый ТаблицаЗначений();
	ВремТаблица.Колонки.Добавить("Дата"			, Новый ОписаниеТипов("Дата"));
	ВремТаблица.Колонки.Добавить("ТорговаяТочка", Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы"));
	ВремТаблица.Колонки.Добавить("Сумма"		, Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	СтрокаНомеровТТ = "9999999";
	Если ЕстьОтборТТ Тогда
		Для Каждого ТекТТ Из МассивТТ Цикл
			СтрокаНомеровТТ = СтрокаНомеровТТ + ", " + Формат(ТекТТ.НомерТочки, "ЧГ=0");
		КонецЦикла;
	КонецЕсли;
	
	ADOСоединение = Новый COMОбъект("ADODB.Connection");
	ADOСоединение.ConnectionTimeOut = 0;
	ADOСоединение.CommandTimeOut    = 0;
	ADOСоединение.ConnectionString  = ОбменСAccess.ПолучитьСтрокуСоединения("SMS_IZBENKA");
	ADOСоединение.Open();
	
	ТекстЗапроса =
	"SELECT
	|	Checks.CloseDate as CloseDate,
	|	Checks.ShopNo as ShopNo,
	|	Checks.BaseSum AS BaseSum
	|FROM
	|	[SMS_IZBENKA].[dbo].[checks] AS Checks (nolock)
	|WHERE
	|	Checks.JournType = 3
	|	AND Checks.CloseDate >= CONVERT(DATETIME, '" + Формат(мДатаНачала, "ДФ='yyyy-MM-dd HH:mm:ss'") +
	"', 20) AND Checks.CloseDate <= CONVERT(DATETIME, '" + Формат(КонецДня(мДатаОкончания), "ДФ='yyyy-MM-dd HH:mm:ss'") + "', 20)" +
		?(ЕстьОтборТТ, "
	|	AND Checks.ShopNo IN (" + СтрокаНомеровТТ + ")", "");
	
	Выборка = ADOСоединение.Execute(ТекстЗапроса);
	
	СпрТорговыеТочки = Справочники.СтруктурныеЕдиницы;
	
	Если НЕ Выборка.EOF() Тогда
		
		Выборка.MoveFirst();
		
		Пока НЕ Выборка.EOF() Цикл
			
			// поиск ТТ по номеру на СКЛ
			ТекТТ = СпрТорговыеТочки.НайтиПоРеквизиту("НомерТочки", Выборка.Fields("ShopNo").Value);
			Если ТекТТ.Пустая() Тогда
				Выборка.MoveNext();
				Продолжить;
			КонецЕсли;
			
			//отбойник, не совсем понятно, почему там 0 бывает
			ТекСуммаКорректировки = Выборка.Fields("BaseSum").Value;
			Если ТекСуммаКорректировки = 0 Тогда
				Выборка.MoveNext();
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ВремТаблица.Добавить();
			НоваяСтрока.Дата 			= НачалоДня(Выборка.Fields("CloseDate").Value);
			НоваяСтрока.ТорговаяТочка 	= ТекТТ;
			НоваяСтрока.Сумма 			= ТекСуммаКорректировки;
			
			Выборка.MoveNext();
			
		КонецЦикла;
		
	КонецЕсли;
	ВремТаблица.Свернуть("Дата, ТорговаяТочка", "Сумма");
	
	ADOСоединение.Close();
	
	
	/////////////////////
	// Основной запрос //
	/////////////////////
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВремТаблица"		, ВремТаблица);
	Запрос.УстановитьПараметр("ДатаНачала"		, мДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания"	, КонецДня(мДатаОкончания));
	Запрос.УстановитьПараметр("НеОтбиратьТТ"			, НЕ ЕстьОтборТТ);
	Запрос.УстановитьПараметр("МассивТТ"				, МассивТТ);
	Запрос.УстановитьПараметр("НеОтбиратьРасчетчиков"	, НЕ ЕстьОтборРасчетчик);
	Запрос.УстановитьПараметр("МассивРасчетчиков"		, МассивРасчетчиков);
	Запрос.УстановитьПараметр("НеОтбиратьПродавцов"		, НЕ ЕстьОтборПродавец);
	Запрос.УстановитьПараметр("МассивПродавцов"			, МассивПродавцов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВремТаблица.Дата КАК Дата,
	|	ВремТаблица.ТорговаяТочка КАК ТорговаяТочка,
	|	ВремТаблица.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТКорректировки
	|ИЗ
	|	&ВремТаблица КАК ВремТаблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ЛистУчетаПродавцы.Ссылка.Дата, ДЕНЬ) КАК Дата,
	|	ЛистУчетаПродавцы.Ссылка.ТорговаяТочка КАК ТорговаяТочка,
	|	ЛистУчетаПродавцы.Ссылка.Расчетчик КАК Расчетчик,
	|	ЛистУчетаПродавцы.Продавец КАК Продавец,
	|	ЕСТЬNULL(ВТКорректировки.Сумма, 0) КАК СуммаКорректировки
	|ИЗ
	|	Документ.ЛистУчета.Продавцы КАК ЛистУчетаПродавцы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКорректировки КАК ВТКорректировки
	|		ПО (ВТКорректировки.Дата = НАЧАЛОПЕРИОДА(ЛистУчетаПродавцы.Ссылка.Дата, ДЕНЬ))
	|			И (ВТКорректировки.ТорговаяТочка = ЛистУчетаПродавцы.Ссылка.ТорговаяТочка)
	|ГДЕ
	|	ЛистУчетаПродавцы.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И (&НеОтбиратьТТ
	|			ИЛИ ЛистУчетаПродавцы.Ссылка.ТорговаяТочка В (&МассивТТ))
	|	И (&НеОтбиратьРасчетчиков
	|			ИЛИ ЛистУчетаПродавцы.Ссылка.Расчетчик В (&МассивРасчетчиков))
	|	И (&НеОтбиратьПродавцов
	|			ИЛИ ЛистУчетаПродавцы.Продавец В (&МассивПродавцов))
	|	И НЕ ЛистУчетаПродавцы.Ссылка.ПометкаУдаления
	|	И НЕ ВТКорректировки.ТорговаяТочка ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКорректировки";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаДанных;
	
КонецФункции
