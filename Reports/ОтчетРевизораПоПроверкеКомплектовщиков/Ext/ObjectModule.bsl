//+++АК sole 2018.09.14 ИП-00019532
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Перем ВнешниеДанные;
	
	СтандартнаяОбработка = Ложь;
	
	
	тзРезультат = СформироватьТаблицуРезультата();	
	
	ВнешниеДанные = Новый Структура("тзРезультат", тзРезультат);
	
	Настройки = ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
      
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеДанные, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

//+++АК sole 2018.09.14 ИП-00019532
Функция СформироватьТаблицуРезультата()
	
	Перем БПР, Запрос, Период, НачалоПериода, ОкончаниеПериода; 
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	Период = ПолучитьПараметрПользователя("Период");
	
	НачалоПериода = Период.Значение.ДатаНачала;
	ОкончаниеПериода = Период.Значение.ДатаОкончания;
	
	БПР = Новый Структура();
	БПР.Вставить("Запрос", Запрос);
	БПР.Вставить("НачалоПериода", НачалоПериода);
	БПР.Вставить("ОкончаниеПериода", ОкончаниеПериода);
	
	СформироватьКоличествоКоробокСИнтерваламиДат(БПР);
	тзРезультат = СформироватьТзРезультат(БПР); 
	
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
	Возврат тзРезультат
КонецФункции

//+++АК sole 2018.09.14 ИП-00019532
Процедура СформироватьКоличествоКоробокСИнтерваламиДат(БПР)
	
	Перем Запрос, тз;
	
	Запрос = БПР.Запрос;
	Запрос.Текст =
"ВЫБРАТЬ
|	0 КАК Ид,
|	0 КАК СледИд,
|	КоличествоВКоробкеСрезПоследних.Период,
|	КоличествоВКоробкеСрезПоследних.СтруктурнаяЕдиница,
|	КоличествоВКоробкеСрезПоследних.Номенклатура,
|	КоличествоВКоробкеСрезПоследних.Характеристика,
|	КоличествоВКоробкеСрезПоследних.Количество
|
|ИЗ РегистрСведений.КоличествоВКоробке.СрезПоследних(&НачалоПериода) КАК КоличествоВКоробкеСрезПоследних
|ГДЕ
|		КоличествоВКоробкеСрезПоследних.Активность
|
|ОБЪЕДИНИТЬ
|
|ВЫБРАТЬ
|	0 КАК Ид,
|	0 КАК СледИд,
|	КоличествоВКоробке.Период,
|	КоличествоВКоробке.СтруктурнаяЕдиница,
|	КоличествоВКоробке.Номенклатура,
|	КоличествоВКоробке.Характеристика,
|	КоличествоВКоробке.Количество
|
|ИЗ РегистрСведений.КоличествоВКоробке КАК КоличествоВКоробке
|ГДЕ
|		КоличествоВКоробке.Активность
|	И	КоличествоВКоробке.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
|
|УПОРЯДОЧИТЬ ПО
|	КоличествоВКоробкеСрезПоследних.СтруктурнаяЕдиница,
|	КоличествоВКоробкеСрезПоследних.Номенклатура,
|	КоличествоВКоробкеСрезПоследних.Характеристика,
|	КоличествоВКоробкеСрезПоследних.Период
|
|";
	Запрос.УстановитьПараметр("НачалоПериода", БПР.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", БПР.ОкончаниеПериода);
	
	тз = Запрос.Выполнить().Выгрузить();
	
	Инд = 1;
	
	Для Каждого Стр Из тз  Цикл
		Стр.Ид = Инд;
		Инд = Инд + 1;
		Стр.СледИД = Инд;
	КонецЦикла;
		
	Запрос.Текст =
"ВЫБРАТЬ
|	т.Ид,
|	т.СледИд,
|	т.Период,
|	т.СтруктурнаяЕдиница,
|	т.Номенклатура,
|	т.Характеристика,
|	т.Количество
|
|		ПОМЕСТИТЬ вт
|
|ИЗ &Таблица КАК т
|
|;
|///////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	вт1.Период КАК ДатаС,
|	ЕСТЬNULL(вт2.Период, ДАТАВРЕМЯ(3099,12,31)) КАК ДатаДо,
|	вт1.СтруктурнаяЕдиница,
|	вт1.Номенклатура,
|	вт1.Характеристика,
|	вт1.Количество
|
|		ПОМЕСТИТЬ втКоличествоВКоробкеСИнтерваламиДат
|
|ИЗ вт КАК вт1
|	ЛЕВОЕ СОЕДИНЕНИЕ вт КАК вт2 ПО
|			вт2.Ид = вт1.СледИд
|  		И	вт2.СтруктурнаяЕдиница = вт1.СтруктурнаяЕдиница
|       И	вт2.Номенклатура = вт1.Номенклатура
|       И	вт2.Характеристика = вт1.Характеристика
|
|ИНДЕКСИРОВАТЬ ПО
|	вт1.СтруктурнаяЕдиница,
|	вт1.Номенклатура,
|	вт1.Характеристика
|
|;
|///////////////////////////////////////////////////////////
|УНИЧТОЖИТЬ вт
|";
	Запрос.УстановитьПараметр("Таблица", тз);	
	Запрос.Выполнить();
	Запрос.Параметры.Очистить();
	
КонецПроцедуры

//+++АК sole 2018.09.14 ИП-00019532
Функция СформироватьТзРезультат(БПР)
	
	Перем Запрос, тзРезультат;
	
	Запрос = БПР.Запрос;
	Запрос.Текст =
"ВЫБРАТЬ
|	РезультатыПроверкиРевизора.Документ КАК РасходныйОрдер,
|	РезультатыПроверкиРевизора.Документ.Организация КАК Организация, 
|	РезультатыПроверкиРевизора.Документ.Склад КАК Склад,
|	РезультатыПроверкиРевизора.Документ.Получатель КАК ТорговаяТочка,
|	РезультатыПроверкиРевизора.Документ.Ревизор КАК Ревизор,
|	РезультатыПроверкиРевизора.Номенклатура,
|	РезультатыПроверкиРевизора.Характеристика,
|	РезультатыПроверкиРевизора.Проверено КАК ПровереноШтук,
|	ЕСТЬNULL(КоличествоВКоробке.Количество, 0) КАК КоличествоВКоробке,
|   0 КАК ПровереноКоробок
|	
|ИЗ РегистрСведений.РезультатыПроверкиРевизора КАК РезультатыПроверкиРевизора
|	ЛЕВОЕ СОЕДИНЕНИЕ втКоличествоВКоробкеСИнтерваламиДат КАК КоличествоВКоробке ПО
|			РезультатыПроверкиРевизора.Документ.Дата >= КоличествоВКоробке.ДатаС
|		И	РезультатыПроверкиРевизора.Документ.Дата < КоличествоВКоробке.ДатаДо
|		И	КоличествоВКоробке.СтруктурнаяЕдиница = РезультатыПроверкиРевизора.Документ.Склад.Владелец
|		И	КоличествоВКоробке.Номенклатура = РезультатыПроверкиРевизора.Номенклатура
|		И	КоличествоВКоробке.Характеристика = РезультатыПроверкиРевизора.Характеристика
|		
|ГДЕ 
|		Не РезультатыПроверкиРевизора.Документ.ПометкаУдаления
|	И	РезультатыПроверкиРевизора.Документ.Проведен
|	И	РезультатыПроверкиРевизора.Документ.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода	
|";
	
	Запрос.УстановитьПараметр("НачалоПериода", БПР.НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", БПР.ОкончаниеПериода);
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из тзРезультат Цикл
		
		Если Стр.КоличествоВКоробке = 0 Тогда
			Продолжить;	
		КонецЕсли;
		
		КоличествоКоробок = Цел(Стр.ПровереноШтук/Стр.КоличествоВКоробке);
		Остаток = Стр.ПровереноШтук - КоличествоКоробок * Стр.КоличествоВКоробке;
		
		Если Остаток/Стр.КоличествоВКоробке >= 0.5 Тогда
			КоличествоКоробок = КоличествоКоробок + 1;	
		КонецЕсли;
		
		Стр.ПровереноКоробок = КоличествоКоробок;
		
	КонецЦикла;
	
	Возврат тзРезультат;
	
КонецФункции

//+++АК sole 2018.09.14 ИП-00019532
Функция ПолучитьПараметрПользователя(ИмяПараметра)
	
	Параметр = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	ПараметрПользователя = ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(Параметр.ИдентификаторПользовательскойНастройки);
	
	Возврат ПараметрПользователя;
	
КонецФункции
