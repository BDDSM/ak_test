Функция СФормироватьПисьмоДляОтправки(ТекущийОтчет, АдресПолучателя, ПредставлениеПолучателя, ПараметрыОтправки)

	Кому = Новый Массив;
	Кому.Добавить(Новый Структура("Адрес, Представление", АдресПолучателя, ПредставлениеПолучателя));
	
	Копия = Новый Массив;
	
	ПараметрыПисьма = Новый Структура();
	ПараметрыПисьма.Вставить("Важность",	ВажностьИнтернетПочтовогоСообщения.Обычная);
	ПараметрыПисьма.Вставить("ТипТекста",	ТипТекстаПочтовогоСообщения.HTML);
	ПараметрыПисьма.Вставить("Тема",		Метаданные().Синоним);
	ПараметрыПисьма.Вставить("Тело",		ПоместитьОтчетВТелоПисьма(ТекущийОтчет));
	ПараметрыПисьма.Вставить("Кому",		Кому);
	ПараметрыПисьма.Вставить("Копия",		Копия);
	//ПараметрыПисьма.Вставить("Вложения",	ПоместитьОтчетВоВложенияПисьма(ТекущийОтчет));
	
	Возврат ПараметрыПисьма;

КонецФункции

Функция ПоместитьОтчетВТелоПисьма(ТекущийОтчет)

	ИмяФайлаHTML = ПолучитьИмяВременногоФайла("html");
	ТекущийОтчет.Записать(ИмяФайлаHTML, ТипФайлаТабличногоДокумента.HTML4);
	
	ТекстДляЧтения = Новый ЧтениеТекста(ИмяФайлаHTML);
	ВесьТекст = ТекстДляЧтения.Прочитать();
	ТекстДляЧтения.Закрыть();
	УдалитьФайлы(ИмяФайлаHTML);
	
	ВесьТекст = СтрЗаменить(ВесьТекст, "#ccc085 1px solid","#ccc085 2px solid");
	
	ВесьТекст = ЗаменитьНедопустимыеСимволыXML(ВесьТекст);
	ТекстСообщения = СтрЗаменить(ВесьТекст, Символы.ПС, "");
	
	Возврат ТекстСообщения;

КонецФункции

Функция ПоместитьОтчетВоВложенияПисьма(ТекущийОтчет)

	ИмяФайлаВложения = ПолучитьИмяВременногоФайла("xls");
	ТекущийОтчет.Записать(ИмяФайлаВложения, ТипФайлаТабличногоДокумента.XLS);
	
	Возврат ИмяФайлаВложения;

КонецФункции

Функция ЗаменитьНедопустимыеСимволыXML(Знач Текст, СимволЗамены = "")

	#Если Не ВебКлиент Тогда
		ПозицияНачала = 1;
		Пока Истина Цикл
			Позиция = НайтиНедопустимыеСимволыXML(Текст, ПозицияНачала);
			Если Позиция = 0 Тогда
				Прервать;
			КонецЕсли;
			Если Позиция > 1 Тогда
				НедопустимыйСимвол = Сред(Текст, Позиция - 1, 1);
				Если НайтиНедопустимыеСимволыXML(НедопустимыйСимвол) > 0 Тогда
					Текст = СтрЗаменить(Текст, НедопустимыйСимвол, СимволЗамены);
				КонецЕсли;
			КонецЕсли;
			НедопустимыйСимвол = Сред(Текст, Позиция, 1);
			Если НайтиНедопустимыеСимволыXML(НедопустимыйСимвол) > 0 Тогда
				Текст = СтрЗаменить(Текст, НедопустимыйСимвол, СимволЗамены);
			КонецЕсли;
			ПозицияНачала = Позиция + 1;
		КонецЦикла;
	#КонецЕсли
	
	Возврат Текст;

КонецФункции

Процедура ОтправитьПисьма(ПисьмаДляОтправки)

	УчетнаяЗапись = ОбщиеПроцедуры.ПолучитьУчетнуюЗаписьДляРассылки();
	
	Профиль = УправлениеЭлектроннойПочтой.ПолучитьИнтернетПочтовыйПрофиль(УчетнаяЗапись);
	
	ДанныеУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетнаяЗапись, "АдресЭлектроннойПочты, Наименование");
	
	Почта = Новый ИнтернетПочта;
	Почта.Подключиться(Профиль);
	
	Для Каждого ПараметрыПисьма Из ПисьмаДляОтправки Цикл
		Письмо = Новый ИнтернетПочтовоеСообщение;
		
		Письмо.Тема				= ПараметрыПисьма.Тема;
		Письмо.ИмяОтправителя	= СокрЛП(ДанныеУчетнойЗаписи.Наименование);
		Письмо.Отправитель		= СокрЛП(ДанныеУчетнойЗаписи.АдресЭлектроннойПочты);
		
		Для Каждого СтрокаПолучатель Из ПараметрыПисьма.Кому Цикл
			Получатель = Письмо.Получатели.Добавить();
			Получатель.Адрес			= СтрокаПолучатель.Адрес;
			Получатель.ОтображаемоеИмя	= СтрокаПолучатель.Представление;
		КонецЦикла;
		
		Для Каждого СтрокаПолучатель Из ПараметрыПисьма.Копия Цикл
			Получатель = Письмо.Копии.Добавить();
			Получатель.Адрес			= СтрокаПолучатель.Адрес;
			Получатель.ОтображаемоеИмя	= СтрокаПолучатель.Представление;
		КонецЦикла;
		
		//Письмо.Вложения.Добавить(ПараметрыПисьма.Вложения,"Отчет"); 
		ТекстСообщения = Письмо.Тексты.Добавить();
		ТекстСообщения.Текст		= ПараметрыПисьма.Тело;
		ТекстСообщения.ТипТекста	= ПараметрыПисьма.ТипТекста;
		
		Почта.Послать(Письмо);
	КонецЦикла;
	
	Почта.Отключиться();

КонецПроцедуры

Процедура СформироватьВРежимеРассылки(ДокументРезультат, ДанныеРасшифровки)

	ПисьмаДляОтправки = Новый Массив;
	
	// Установка параметров
	ПараметрыОтправки = Новый Структура("Получатели", Новый Массив);
	Получатели = Неопределено;
	Если КомпоновщикНастроек.ФиксированныеНастройки.ДополнительныеСвойства.Свойство("Получатели", Получатели) Тогда
		ПараметрыОтправки.Получатели = УправлениеЭлектроннойПочтой.ЭлектронныеАдресаФизическихЛиц(Получатели);
	КонецЕсли;
	
	// Формирование отчета
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	Для каждого Элемент Из ПараметрыОтправки.Получатели Цикл
		ПредставлениеПолучателя	= Элемент.Представление;
		СтрокаАдресПолучателя	= Элемент.Адрес;
		Если Не ПустаяСтрока(СтрокаАдресПолучателя) Тогда
			ПараметрыПисьма = СФормироватьПисьмоДляОтправки(ДокументРезультат, СтрокаАдресПолучателя, ПредставлениеПолучателя, ПараметрыОтправки);
			ПисьмаДляОтправки.Добавить(ПараметрыПисьма);
		КонецЕсли;
	КонецЦикла;
	
	КомпоновщикНастроек.ФиксированныеНастройки.ДополнительныеСвойства.Удалить("РежимРассылки");
	
	// Отправка
	Если ПисьмаДляОтправки.Количество() > 0 Тогда
		ОтправитьПисьма(ПисьмаДляОтправки);
	КонецЕсли;

КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	// Рассылка отчета
	Если КомпоновщикНастроек.ФиксированныеНастройки.ДополнительныеСвойства.Свойство("РежимРассылки") Тогда
		СтандартнаяОбработка = Ложь;
		СформироватьВРежимеРассылки(ДокументРезультат, ДанныеРасшифровки);
	КонецЕсли;

КонецПроцедуры