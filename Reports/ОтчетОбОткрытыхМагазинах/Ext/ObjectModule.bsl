
//+++ AK suvv 20.08.2018 ИП-00019353
Функция ПолучитьНаборДанныхОткрытыеМагазины()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода" , КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("ТекДата",       ТекущаяДата());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтруктурныеЕдиницы.Ссылка КАК СтруктурнаяЕдиница,
	|	СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей КАК ДатаОткрытия
	|ПОМЕСТИТЬ ВТ_СЕОткрытыеЗаВыбранныйПериод
	|ИЗ
	|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	|ГДЕ
	|	СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей МЕЖДУ &НачалоПериода И &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтруктурныеЕдиницы.Ссылка) КАК КоличествоВсего
	|ПОМЕСТИТЬ ВТ_ОбщаяНумерация
	|ИЗ
	|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СЕОткрытыеЗаВыбранныйПериод КАК ВТ_СЕОткрытыеЗаВыбранныйПериод
	|		ПО СтруктурныеЕдиницы.Ссылка = ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница
	|ГДЕ
	|	СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей <= &КонецПериода
	|	И СтруктурныеЕдиницы.СтатусТорговойТочки <> ЗНАЧЕНИЕ(Перечисление.СтатусыТорговыхТочек.Закрыт)
	|	И СтруктурныеЕдиницы.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)
	|	И СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ЕСТЬNULL(ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница, """") = """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей, МЕСЯЦ) КАК Месяц,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтруктурныеЕдиницы.Ссылка) КАК КоличествоОткрытых
	|ПОМЕСТИТЬ ВТ_НумерацияПоМесяцам
	|ИЗ
	|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СЕОткрытыеЗаВыбранныйПериод КАК ВТ_СЕОткрытыеЗаВыбранныйПериод
	|		ПО СтруктурныеЕдиницы.Ссылка = ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница
	|ГДЕ
	|	СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей < &КонецПериода
	|	И СтруктурныеЕдиницы.СтатусТорговойТочки <> ЗНАЧЕНИЕ(Перечисление.СтатусыТорговыхТочек.Закрыт)
	|	И СтруктурныеЕдиницы.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)
	|	И СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ЕСТЬNULL(ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница, """") = """"
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(СтруктурныеЕдиницы.ДатаПродажиПерваяТысячаРублей, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница,
	|	ЕСТЬNULL(УправляющиеРозницей.РольПользователя, ЗНАЧЕНИЕ(Справочник.РолиПользователей.ПустаяСсылка)) КАК РольУправляющийПоРознице,
	|	ЕСТЬNULL(СпециалистыПоОткрытию.РольПользователя, ЗНАЧЕНИЕ(Справочник.РолиПользователей.ПустаяСсылка)) КАК РольСпециалистПоОткрытию,
	|	ЕСТЬNULL(РуководителиПоСтроительству.РольПользователя, ЗНАЧЕНИЕ(Справочник.РолиПользователей.ПустаяСсылка)) КАК РольРуководительПоСтроительству,
	|	ВТ_СЕОткрытыеЗаВыбранныйПериод.ДатаОткрытия
	|ПОМЕСТИТЬ ВТ_СЕСРолями
	|ИЗ
	|	ВТ_СЕОткрытыеЗаВыбранныйПериод КАК ВТ_СЕОткрытыеЗаВыбранныйПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&ТекДата, ТипРолиID = ""UpravlyayushchiiPoRoznice"") КАК УправляющиеРозницей
	|		ПО ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница = УправляющиеРозницей.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&ТекДата, ТипРолиID = ""SpecialistPoOtkrytiyu"") КАК СпециалистыПоОткрытию
	|		ПО ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница = СпециалистыПоОткрытию.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&ТекДата, ТипРолиID = ""RukovoditelPoStroitelstvu"") КАК РуководителиПоСтроительству
	|		ПО ВТ_СЕОткрытыеЗаВыбранныйПериод.СтруктурнаяЕдиница = РуководителиПоСтроительству.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница,
	|	ЕСТЬNULL(СоставРолиУправляющийПоРознице.Сотрудник, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК УправляющийРозницей,
	|	ЕСТЬNULL(СоставРолиСпециалистПоОткрытию.Сотрудник, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК СпециалистПоОткрытию,
	|	ЕСТЬNULL(СоставРолиРуководительПоСтроительству.Сотрудник, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК РуководительПоСтроительству,
	|	ЕСТЬNULL(ВТ_СЕСРолями.СтруктурнаяЕдиница.Территория, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК МенеджерПоСтройке,
	|	ЕСТЬNULL(ВТ_СЕСРолями.ДатаОткрытия, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОткрытия,
	|	ЕСТЬNULL(ВТ_ОбщаяНумерация.КоличествоВсего, 0) КАК КоличествоВсего,
	|	ЕСТЬNULL(ВТ_НумерацияПоМесяцам.КоличествоОткрытых, 0) КАК КоличествоОткрытыхЗаМесяц,
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница.ПлощадьПодсобныхПомещений + ВТ_СЕСРолями.СтруктурнаяЕдиница.ТорговаяПлощадь КАК ОбщаяПлощадь,
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница.ТорговаяПлощадь КАК ТорговаяПлощадь,
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница.ФактическаяПлощадь КАК ФактическаяПлощадь,
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница.ПлощадьПодсобныхПомещений КАК ПлощадьПодсобныхПомещений,
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница.ПлощадьНеиспользуемая КАК ПлощадьНеиспользуемая,
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница.ПлощадьСубаренды КАК ПлощадьСубаренды,
	|	ВЫБОР
	|		КОГДА ВТ_СЕСРолями.СтруктурнаяЕдиница.ФактическаяДатаПередачи <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РАЗНОСТЬДАТ(ВТ_СЕСРолями.СтруктурнаяЕдиница.ФактическаяДатаПередачи, ВТ_СЕСРолями.СтруктурнаяЕдиница.ДатаПродажиПерваяТысячаРублей, ДЕНЬ)
	|		ИНАЧЕ ""не заполнена дата факт.передачи""
	|	КОНЕЦ КАК КоличествоДнейНаОткрытие
	|ИЗ
	|	ВТ_СЕСРолями КАК ВТ_СЕСРолями
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК СоставРолиУправляющийПоРознице
	|		ПО ВТ_СЕСРолями.РольУправляющийПоРознице = СоставРолиУправляющийПоРознице.Ссылка
	|			И (СоставРолиУправляющийПоРознице.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК СоставРолиСпециалистПоОткрытию
	|		ПО ВТ_СЕСРолями.РольСпециалистПоОткрытию = СоставРолиСпециалистПоОткрытию.Ссылка
	|			И (СоставРолиСпециалистПоОткрытию.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбщаяНумерация КАК ВТ_ОбщаяНумерация
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК СоставРолиРуководительПоСтроительству
	|		ПО ВТ_СЕСРолями.РольРуководительПоСтроительству = СоставРолиРуководительПоСтроительству.Ссылка
	|			И (СоставРолиРуководительПоСтроительству.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НумерацияПоМесяцам КАК ВТ_НумерацияПоМесяцам
	|		ПО (НАЧАЛОПЕРИОДА(ВТ_СЕСРолями.ДатаОткрытия, МЕСЯЦ) = ВТ_НумерацияПоМесяцам.Месяц)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОткрытия,
	|	ВТ_СЕСРолями.СтруктурнаяЕдиница.Наименование";
	
	ОткрытыеМагазины = Запрос.Выполнить().Выгрузить();
	ОткрытыеМагазины.Колонки.Добавить("НомерПоПорядкуЗаМесяц");
	ОткрытыеМагазины.Колонки.Добавить("НомерПоПорядкуОбщий");
	
	НомерПоПорядкуЗаМесяц = 1;
	
	Для Каждого Стр из ОткрытыеМагазины Цикл
		
		Стр.НомерПоПорядкуЗаМесяц = Стр.КоличествоОткрытыхЗаМесяц + НомерПоПорядкуЗаМесяц;
		Стр.НомерПоПорядкуОбщий   = Стр.КоличествоВсего + НомерПоПорядкуЗаМесяц;	
		
		НомерПоПорядкуЗаМесяц = НомерПоПорядкуЗаМесяц + 1;
		
	КонецЦикла;
	
	ОткрытыеМагазины.Колонки.Удалить("КоличествоВсего");
	ОткрытыеМагазины.Колонки.Удалить("КоличествоОткрытыхЗаМесяц");
	
	Возврат ОткрытыеМагазины;
	
КонецФункции //--- AK suvv

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументРезультат.Очистить();
	
	СформироватьОтчет(ДокументРезультат, ДанныеРасшифровки);
	
КонецПроцедуры

//+++ AK suvv 20.08.2018 ИП-00019353
Процедура СформироватьОтчет(ТабличныйДокумент, ДанныеРасшифровки = неопределено) Экспорт
	
	СхемаКД					= ЭтотОбъект.СхемаКомпоновкиДанных;
	КомпоновщикНастроекКД	= КомпоновщикНастроек;
	
	ЗаголовокОтчета = "Отчет об открытых магазинах за период с " + Формат(НачалоПериода, "ДФ=dd.MM.yyyy") + " по " + Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	КомпоновщикНастроекКД.Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("Заголовок", ЗаголовокОтчета);
	
	ОткрытыеМагазины = ПолучитьНаборДанныхОткрытыеМагазины();
	
	ВнешниеНаборыДанных = Новый Структура("ОткрытыеМагазины", ОткрытыеМагазины);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКД, КомпоновщикНастроекКД.ПолучитьНастройки(), ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры //--- AK suvv
