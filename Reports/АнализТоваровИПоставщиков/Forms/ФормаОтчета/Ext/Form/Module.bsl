
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет.Периодичность = "По неделям";
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураРасшифровки = Расшифровать(Расшифровка);
	
	Если Не ЗначениеЗаполнено(СтруктураРасшифровки.Товар)
		И НЕ ЗначениеЗаполнено(СтруктураРасшифровки.Поставщик) Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений();
	СписокЗначений.Добавить("Расшифровать жалобы");
	СписокЗначений.Добавить("Расшифровать включения");
	СписокЗначений.Добавить("Расшифровать нарушения в лабораторных протоколах");
	Если ЗначениеЗаполнено(СтруктураРасшифровки.Поставщик) Тогда
		СписокЗначений.Добавить("Расшифровать маркер аудита");
	КонецЕсли;	
	
	ВыбранныйЭлемент = СписокЗначений.ВыбратьЭлемент();
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Если ВыбранныйЭлемент.Значение = "Расшифровать жалобы" Тогда
			СтруктураРасшифровки.Вставить("Тип", "Жалобы");
			ОткрытьФорму("Отчет.АнализТоваровИПоставщиков.Форма.ФормаРасшифровка", СтруктураРасшифровки);
		ИначеЕсли ВыбранныйЭлемент.Значение = "Расшифровать включения" Тогда
			СтруктураРасшифровки.Вставить("Тип", "Включения");
			ОткрытьФорму("Отчет.АнализТоваровИПоставщиков.Форма.ФормаРасшифровка", СтруктураРасшифровки);	
		ИначеЕсли ВыбранныйЭлемент.Значение = "Расшифровать нарушения в лабораторных протоколах" Тогда
			СтруктураРасшифровки.Вставить("Тип", "Нарушения");
			ОткрытьФорму("Отчет.АнализТоваровИПоставщиков.Форма.ФормаРасшифровка", СтруктураРасшифровки);
		ИначеЕсли ВыбранныйЭлемент.Значение = "Расшифровать маркер аудита" Тогда
			Док = ПолучитьРасшифровкуМаркера(СтруктураРасшифровки.Поставщик);
			Если ЗначениеЗаполнено(Док) Тогда
				ОткрытьЗначение(Док);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьРасшифровкуМаркера(Производитель)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Производитель", Производитель);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Ссылка
	               |ИЗ
	               |	Справочник.Контрагенты КАК Контрагенты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ВЗ_Макс.Производитель КАК Производитель,
	               |			МАКСИМУМ(ЗаданиеАудитору.Маркер) КАК Маркер,
	               |			ЗаданиеАудитору.Ссылка КАК Ссылка
	               |		ИЗ
	               |			(ВЫБРАТЬ
	               |				МАКСИМУМ(ЗаданиеАудитору.ДатаАудита) КАК ДатаАудита,
	               |				ЗаданиеАудитору.Производитель КАК Производитель
	               |			ИЗ
	               |				Документ.ЗаданиеАудитору КАК ЗаданиеАудитору
	               |			ГДЕ
	               |				ЗаданиеАудитору.ПометкаУдаления = Ложь
	               |				И ЗаданиеАудитору.ДатаАудита > ДАТАВРЕМЯ(1, 1, 1)
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				ЗаданиеАудитору.Производитель) КАК ВЗ_Макс
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеАудитору КАК ЗаданиеАудитору
	               |				ПО ВЗ_Макс.ДатаАудита = ЗаданиеАудитору.ДатаАудита
	               |					И ВЗ_Макс.Производитель = ЗаданиеАудитору.Производитель
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ВЗ_Макс.Производитель,
	               |			ЗаданиеАудитору.Ссылка) КАК ВложенныйЗапрос
	               |		ПО Контрагенты.Ссылка = ВложенныйЗапрос.Производитель
	               |ГДЕ
	               |	ВЫБОР
	               |			КОГДА Контрагенты.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |				ТОГДА Контрагенты.ГоловнойКонтрагент
	               |			ИНАЧЕ Контрагенты.Ссылка
	               |		КОНЕЦ = &Производитель";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе	
		Возврат Неопределено;
	КонецЕсли;	
	
КонецФункции	

&НаСервере
Функция Расшифровать(Расшифровка)
	
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	СтруктураПараметры = Новый Структура("ДатаНач, ДатаКон, Товар, Поставщик");
	
	ПоляРасшифровки = ТиповыеОтчеты.ПолучитьМассивПолейРасшифровки(Расшифровка, Данные);
	
	ДатаРасшифровки = '00010101';
	Поставщик = Неопределено;
	Товар = Неопределено;
	Для Каждого ЭлементРасшифровка Из ПоляРасшифровки Цикл
		Если ТипЗнч(ЭлементРасшифровка) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
			Если НРег(ЭлементРасшифровка.Поле) = "дата" Тогда
				ДатаРасшифровки = ЭлементРасшифровка.Значение;
			ИначеЕсли НРег(ЭлементРасшифровка.Поле) = "товар" Тогда
				СтруктураПараметры.Товар = ЭлементРасшифровка.Значение;
			ИначеЕсли НРег(ЭлементРасшифровка.Поле) = "поставщик" Тогда
				СтруктураПараметры.Поставщик = ЭлементРасшифровка.Значение;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;
	
	Если Отчет.Периодичность = "По неделям" Тогда
		СтруктураПараметры.ДатаНач = ДатаРасшифровки;
		СтруктураПараметры.ДатаКон = КонецНедели(ДатаРасшифровки);
	Иначе
		СтруктураПараметры.ДатаНач = ДатаРасшифровки;
		СтруктураПараметры.ДатаКон = КонецМесяца(ДатаРасшифровки);
	КонецЕсли;	
	
	Возврат СтруктураПараметры;	
КонецФункции


