
//+++АК MIND 2018.01.15 добавляю возможность утверждать доки сразу из отчета 
&НаСервере
Функция ПолучитьВыделенныеДокументыНаСервере(ПоВсемуДокументу = Ложь)
	
	РасшифровкаДанные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	Если ПоВсемуДокументу Тогда
		СтрокаОт = 1;
		СтрокаДо = Результат.ВысотаТаблицы;
		КолонкаОт = 1;
		КолонкаДо = Результат.ШиринаТаблицы;
	Иначе
		СтрокаОт = Элементы.Результат.ТекущаяОбласть.Верх;
		СтрокаДо = Элементы.Результат.ТекущаяОбласть.Низ;
		КолонкаОт = Элементы.Результат.ТекущаяОбласть.Лево;
		КолонкаДо = Элементы.Результат.ТекущаяОбласть.Право;
	КонецЕсли;	
	
	МассивДокументы = Новый Массив();
	Для ТекСтрока = СтрокаОт По СтрокаДо Цикл
		Для ТекКолонка = КолонкаОт По КолонкаДо Цикл
			Расшифровка = Результат.Область(ТекСтрока, ТекКолонка, ТекСтрока, ТекКолонка).Расшифровка;
			Если Расшифровка <> Неопределено Тогда
				Для Каждого Поле Из РасшифровкаДанные.Элементы[Расшифровка].ПолучитьПоля() Цикл
					Если ТипЗнч(Поле) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
						Если ТипЗнч(Поле.Значение) = Тип("ДокументСсылка.РасходныйОрдерСклад") Тогда
							МассивДокументы.Добавить(Поле.Значение);
						КонецЕсли;	
					КонецЕсли;	
				КонецЦикла;	
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
	Возврат МассивДокументы;
	
КонецФункции

//+++АК MIND 2018.01.15 
&НаКлиенте
Процедура УстановитьУтвержденоВВыделенныеДокументы(Команда)
	МассивДоки = ПолучитьВыделенныеДокументыНаСервере();
	Если МассивДоки.Количество() = 0 Тогда
		Ответ = Вопрос("В выделенной области документа не найдены документы. Выполнить поиск по всему отчету?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			МассивДоки = ПолучитьВыделенныеДокументыНаСервере(Истина);
		КонецЕсли;	
	КонецЕсли;
	
	Если МассивДоки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не найдены документы");
	Иначе
		ОткрытьФорму("Отчет.ОтчетРасхождениеДанныхСкладаИФинСлужбы.Форма.УтверждениеДокументов", Новый Структура("МассивДокументы", МассивДоки));
	КонецЕсли;	
	
КонецПроцедуры
