//+++АК sole 2018.10.02 ИП-00019934
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Перем ВнешниеДанные;
	
	СтандартнаяОбработка = Ложь;
	
	
	тзРезультат = СформироватьТаблицуРезультата();	
	
	ВнешниеДанные = Новый Структура("тзРезультат", тзРезультат);
	
	Настройки = ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
      
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеДанные, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

//+++АК sole 2018.10.02 ИП-00019934
Функция СформироватьТаблицуРезультата()
	
	БПР = СоздатьБлокПараметров();
	
	СформироватьСтоимостьДоставкиНаТТСИнтерваламиДат(БПР);
	тзРезультат = СформироватьТзРезультат(БПР); 
	
	БПР.Запрос.МенеджерВременныхТаблиц.Закрыть();
	
	Возврат	тзРезультат;
	
КонецФункции

//+++АК sole 2018.10.02 ИП-00019934
Функция СоздатьБлокПараметров()
	
	БПР = Новый Структура();
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	БПР.Вставить("Запрос", Запрос);
	
	Период = ПолучитьПараметрПользователя("Период");
	ДатаНачала = Период.Значение.ДатаНачала;
	ДатаОкончания = Период.Значение.ДатаОкончания;
	
	Организация = ПолучитьПараметрПользователя("Организация");
	Перевозчик = ПолучитьПараметрПользователя("Перевозчик");
	ПричинаПеревозки = ПолучитьПараметрПользователя("ПричинаПеревозки");
	
	БПР.Вставить("ДатаНачала", ДатаНачала);
	БПР.Вставить("ДатаОкончания", ДатаОкончания);
	БПР.Вставить("Организация", Организация.Значение);
	БПР.Вставить("Перевозчик", Перевозчик.Значение);
	БПР.Вставить("ПричинаПеревозки", ПричинаПеревозки.Значение);
	
	Возврат БПР;
	
КонецФункции

//+++АК sole 2018.10.02 ИП-00019934
Процедура СформироватьСтоимостьДоставкиНаТТСИнтерваламиДат(БПР)
	
	Перем Запрос;
	
	Запрос = БПР.Запрос;
	Запрос.Текст =
"ВЫБРАТЬ
|	0 КАК ИД,
|	0 КАК СледИД,
|	СтоимостьУслугПоДоставкеТовараНаТТ.Период,
|	СтоимостьУслугПоДоставкеТовараНаТТ.Контрагент,
|	СтоимостьУслугПоДоставкеТовараНаТТ.ТТ,
|	СтоимостьУслугПоДоставкеТовараНаТТ.ГруппаДоставкиНоменклатуры,
|	СтоимостьУслугПоДоставкеТовараНаТТ.Маршрут,
|	СтоимостьУслугПоДоставкеТовараНаТТ.Ставка,
|	СтоимостьУслугПоДоставкеТовараНаТТ.НаличиеДопТарифа,
|	СтоимостьУслугПоДоставкеТовараНаТТ.СтавкаДопТарифа,
|	СтоимостьУслугПоДоставкеТовараНаТТ.ВариантРасчетаНДС,
|	СтоимостьУслугПоДоставкеТовараНаТТ.СтавкаНДС,
|	СтоимостьУслугПоДоставкеТовараНаТТ.Организация
|
|ИЗ РегистрСведений.СтоимостьУслугПоДоставкеТовараНаТТ КАК СтоимостьУслугПоДоставкеТовараНаТТ	
|
|УПОРЯДОЧИТЬ ПО	
|	СтоимостьУслугПоДоставкеТовараНаТТ.Контрагент,
|	СтоимостьУслугПоДоставкеТовараНаТТ.ТТ,
|	СтоимостьУслугПоДоставкеТовараНаТТ.ГруппаДоставкиНоменклатуры,
|	СтоимостьУслугПоДоставкеТовараНаТТ.Маршрут,
|	СтоимостьУслугПоДоставкеТовараНаТТ.Период
|";
	
	тз = Запрос.Выполнить().Выгрузить();
	
	Инд = 1;
	
	Для Каждого Стр Из тз Цикл
		Стр.ИД = Инд;
		Инд = Инд + 1;
		Стр.СледИД = Инд;
	КонецЦикла;
	
	Запрос.Текст =
"ВЫБРАТЬ
|	т.ИД,
|	т.СледИД,
|	т.Период,
|	т.Контрагент,
|	т.ТТ,
|	т.ГруппаДоставкиНоменклатуры,
|	т.Маршрут,
|	т.Ставка,
|	т.НаличиеДопТарифа,
|	т.СтавкаДопТарифа,
|	т.ВариантРасчетаНДС,
|	т.СтавкаНДС,
|	т.Организация
|
|		ПОМЕСТИТЬ вт
|
|ИЗ &Таблица КАК т
|ИНДЕКСИРОВАТЬ ПО
|	т.Контрагент,
|	т.ТТ,
|	т.ГруппаДоставкиНоменклатуры,
|	т.Маршрут
|;
|///////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	вт1.Период КАК ДатаС,
|	ЕСТЬNULL(вт2.Период, ДАТАВРЕМЯ(3099,12,31)) КАК ДатаДо,
|	вт1.Контрагент,
|	вт1.ТТ,
|	вт1.ГруппаДоставкиНоменклатуры,
|	вт1.Маршрут,
|	вт1.Ставка,
|	вт1.НаличиеДопТарифа,
|	вт1.СтавкаДопТарифа,
|	вт1.ВариантРасчетаНДС,
|	вт1.СтавкаНДС,
|	вт1.Организация
|
|		ПОМЕСТИТЬ втСтоимостьДоставкиНаТТСИнтерваламиДат
|
|ИЗ вт КАК вт1
|	ЛЕВОЕ СОЕДИНЕНИЕ вт КАК вт2 ПО
|			вт2.Контрагент = вт1.Контрагент
|		И	вт2.ТТ = вт1.ТТ
|		И	вт2.ГруппаДоставкиНоменклатуры = вт1.ГруппаДоставкиНоменклатуры
|		И	вт2.Маршрут = вт1.Маршрут
|		И	вт2.ИД = вт1.СледИД
|
|ИНДЕКСИРОВАТЬ ПО
|	вт1.Контрагент,
|	вт1.ТТ,
|	вт1.ГруппаДоставкиНоменклатуры,
|	вт1.Маршрут
|;
|///////////////////////////////////////////////////////////
|УНИЧТОЖИТЬ вт;
|";
	Запрос.УстановитьПараметр("Таблица", тз);
	Запрос.Выполнить();
	Запрос.Параметры.Очистить();
	
КонецПроцедуры

//+++АК sole 2018.10.02 ИП-00019934
Функция СформироватьТзРезультат(БПР)
	
	Перем Запрос;
	
	Запрос = БПР.Запрос;
	Запрос.Текст =
"ВЫБРАТЬ
|	МаршрутныйЛистРасходныеОрдера.Ссылка КАК МаршрутныйЛист,
|	МаршрутныйЛистРасходныеОрдера.Документ.Получатель КАК ТорговаяТочка
|ПОМЕСТИТЬ втСписок
|ИЗ
|	Документ.МаршрутныйЛист.РасходныеОрдера КАК МаршрутныйЛистРасходныеОрдера
|ГДЕ
|	НЕ МаршрутныйЛистРасходныеОрдера.Ссылка.ПометкаУдаления
|	И МаршрутныйЛистРасходныеОрдера.Ссылка.Проведен
|	И МаршрутныйЛистРасходныеОрдера.Ссылка.ВидПеревозки = ЗНАЧЕНИЕ(Справочник.АК_ВидыПеревозки.ДоставкаНаТТ)
|	И МаршрутныйЛистРасходныеОрдера.Ссылка.Дата >= &ДатаНачала
|	И МаршрутныйЛистРасходныеОрдера.Ссылка.Дата <= &ДатаОкончания
|	И МаршрутныйЛистРасходныеОрдера.Ссылка.Организация = &Организация
|	И МаршрутныйЛистРасходныеОрдера.Ссылка.Перевозчик = &Перевозчик
|	И (МаршрутныйЛистРасходныеОрдера.Ссылка.ПричинаПеревозки = &ПричинаПеревозки
|			ИЛИ &ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
|				И МаршрутныйЛистРасходныеОрдера.Ссылка.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
|
|СГРУППИРОВАТЬ ПО
|	МаршрутныйЛистРасходныеОрдера.Ссылка,
|	МаршрутныйЛистРасходныеОрдера.Документ.Получатель
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	НАЧАЛОПЕРИОДА(втСписок.МаршрутныйЛист.Дата, ДЕНЬ) КАК Дата,
|	ДЕНЬ(втСписок.МаршрутныйЛист.Дата) КАК ЧислоМесяца,
|	втСписок.ТорговаяТочка,
|	втСписок.ТорговаяТочка.ТипРозничнойТочки КАК ТипРозничнойТочки,
|	втСписок.ТорговаяТочка.Город КАК Город,
|	втСтоимостьДоставкиНаТТСИнтерваламиДат.СтавкаДопТарифа КАК СуммаДопТарифа,
|	ВЫБОР
|		КОГДА втСписок.МаршрутныйЛист.ПричинаПеревозки В (ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка), ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка))
|			ТОГДА ВЫБОР
|					КОГДА втСтоимостьДоставкиНаТТСИнтерваламиДат.НаличиеДопТарифа
|						ТОГДА ЕСТЬNULL(втСтоимостьДоставкиНаТТСИнтерваламиДат.Ставка, 0) + ЕСТЬNULL(втСтоимостьДоставкиНаТТСИнтерваламиДат.СтавкаДопТарифа, 0)
|					ИНАЧЕ ЕСТЬNULL(втСтоимостьДоставкиНаТТСИнтерваламиДат.Ставка, 0)
|				КОНЕЦ
|		ИНАЧЕ ЕСТЬNULL(ЦенаДопоставкиНаТТ.Сумма, 0)
|	КОНЕЦ КАК Сумма,
|	1 КАК КоличествоРейсов,
//+++АК PISH 2018.11.01 ИП-00020119
|	втСписок.МаршрутныйЛист
//---АК PISH
|ИЗ
|	втСписок КАК втСписок
|		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьДоставкиНаТТСИнтерваламиДат КАК втСтоимостьДоставкиНаТТСИнтерваламиДат
|		ПО (втСписок.МаршрутныйЛист.ПричинаПеревозки В (ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка), ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)))
|			И (втСтоимостьДоставкиНаТТСИнтерваламиДат.Маршрут = втСписок.МаршрутныйЛист.Маршрут)
|			И (втСтоимостьДоставкиНаТТСИнтерваламиДат.Маршрут <> ЗНАЧЕНИЕ(Справочник.Маршруты.ПустаяСсылка))
|			И (втСтоимостьДоставкиНаТТСИнтерваламиДат.ТТ = втСписок.ТорговаяТочка)
|			И втСписок.МаршрутныйЛист.Дата >= втСтоимостьДоставкиНаТТСИнтерваламиДат.ДатаС
|			И втСписок.МаршрутныйЛист.Дата < втСтоимостьДоставкиНаТТСИнтерваламиДат.ДатаДо
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦенаДопоставкиНаТТ КАК ЦенаДопоставкиНаТТ
|		ПО (ЦенаДопоставкиНаТТ.Дата = НАЧАЛОПЕРИОДА(втСписок.МаршрутныйЛист.Дата, ДЕНЬ))
|			И (ЦенаДопоставкиНаТТ.Маршрут = втСписок.МаршрутныйЛист.Маршрут)
|			И (ЦенаДопоставкиНаТТ.ПричинаПеревозки = втСписок.МаршрутныйЛист.ПричинаПеревозки)
|			И (ЦенаДопоставкиНаТТ.ТорговаяТочка = втСписок.ТорговаяТочка)";
		Запрос.УстановитьПараметр("ДатаНачала", БПР.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", БПР.ДатаОкончания);
		Запрос.УстановитьПараметр("Организация", БПР.Организация);
		Запрос.УстановитьПараметр("Перевозчик", БПР.Перевозчик);
		Запрос.УстановитьПараметр("ПричинаПеревозки", БПР.ПричинаПеревозки);
		тзРезультат = Запрос.Выполнить().Выгрузить();
		
	    Возврат тзРезультат;
		
КонецФункции

//+++АК sole 2018.09.14 ИП-00019532
Функция ПолучитьПараметрПользователя(ИмяПараметра)
	
	Параметр = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	ПараметрПользователя = ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(Параметр.ИдентификаторПользовательскойНастройки);
	
	Возврат ПараметрПользователя;
	
КонецФункции

