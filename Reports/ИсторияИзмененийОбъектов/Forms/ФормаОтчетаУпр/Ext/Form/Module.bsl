
Процедура СформироватьТаблицуВерсий() Экспорт
	
	СписокВерсий.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Отчет.СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Отчет.СсылкаНаОбъект);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВерсииОбъектов.НомерВерсии КАК НомерВерсии,
	|	ВерсииОбъектов.АвторВерсии,
	|	ВерсииОбъектов.ДатаВерсии,
	|	ВерсииОбъектов.СохраненоВФайл
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.Объект = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерВерсии";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокВерсий.Добавить();
		НоваяСтрока.НомерВерсии = Выборка.НомерВерсии;
		НоваяСтрока.АвторВерсии = Выборка.АвторВерсии;
		НоваяСтрока.ДатаВерсии  = Выборка.ДатаВерсии;
		НоваяСтрока.СохраненоВФайл  = Выборка.СохраненоВФайл;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет.СсылкаНаОбъект = Параметры.СсылкаНаОбъект;
	Если Параметры.ЗаполнятьСписокВерсий Тогда
		СформироватьТаблицуВерсий();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	Объект = РеквизитФормыВЗначение("Отчет");
	ТабДок = Новый ТабличныйДокумент();
	
	ТекДанные = СписокВерсий.НайтиПоИдентификатору(Элементы.СписокВерсий.ТекущаяСтрока);
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекДанные.НомерВерсии) Тогда
		Список = Новый СписокЗначений;
		Список.Добавить(ТекДанные.НомерВерсии);
		Объект.СформироватьОтчет(ТабДок, Список);
	Иначе
		Сообщить("Для продолжения необходимо выбрать версию объекта");
	КонецЕсли;
	
	ТЧОтчета.Очистить();
	ТЧОтчета.Вывести(ТабДок);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьВерсиюОбъекта()
	
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаОбъектПриИзменении(Элемент)
	
	СформироватьТаблицуВерсий();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВерсийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВерсию(Команда)
	ОткрытьВерсиюОбъекта();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СформироватьТаблицуВерсий();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСравнение()
	
	Объект = РеквизитФормыВЗначение("Отчет");
	ТабДок = Новый ТабличныйДокумент();
	
	ВыделенныеСтроки = Элементы.СписокВерсий.ВыделенныеСтроки;
	
	Список = Новый СписокЗначений;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Список.Добавить(СписокВерсий.НайтиПоИдентификатору(ВыделеннаяСтрока).НомерВерсии);
	КонецЦикла;
	
	Если Список.Количество() = 2 Тогда
		Объект.СформироватьОтчет(ТабДок, Список);
	КонецЕсли;
	
	ТЧОтчета.Очистить();
	ТЧОтчета.Вывести(ТабДок);
	
КонецПроцедуры	

&НаКлиенте
Процедура СравнитьВерсии(Команда)
	
	Если Элементы.СписокВерсий.ВыделенныеСтроки.Количество() <> 2 Тогда
		Предупреждение("Для сравнения необходимо выбрать две версии",, "Сравнение версий");
	Иначе
		СформироватьСравнение();
	КонецЕсли;	
	
КонецПроцедуры

