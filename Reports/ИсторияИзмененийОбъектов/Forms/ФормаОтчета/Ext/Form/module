
Процедура СформироватьТаблицуВерсий() Экспорт //изменения Кудяков Г.А.   13 октября 2009 г. используется в иерархии документов
	
	СписокВерсий.Очистить();
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВерсииОбъектов.НомерВерсии КАК НомерВерсии,
	|	ВерсииОбъектов.АвторВерсии,
	|	ВерсииОбъектов.ДатаВерсии,
	|	ВерсииОбъектов.СохраненоВФайл
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.Объект = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерВерсии";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокВерсий.Добавить();
		НоваяСтрока.НомерВерсии = Выборка.НомерВерсии;
		НоваяСтрока.АвторВерсии = Выборка.АвторВерсии;
		НоваяСтрока.ДатаВерсии  = Выборка.ДатаВерсии;
		НоваяСтрока.СохраненоВФайл  = Выборка.СохраненоВФайл;
	КонецЦикла;
	
КонецПроцедуры

Процедура СписокВерсийВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ОткрытьВерсиюОбъекта();
	
КонецПроцедуры

Процедура ОткрытьВерсиюОбъекта()
	
	Если ЭлементыФормы.СписокВерсий.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементыФормы.СписокВерсий.ТекущиеДанные.НомерВерсии) Тогда
		Список = Новый СписокЗначений;
		Список.Добавить(ЭлементыФормы.СписокВерсий.ТекущиеДанные.НомерВерсии);
		СформироватьОтчет(ЭлементыФормы.ТЧОтчета, Список);
	Иначе
		Сообщить("Для продолжения необходимо выбрать версию объекта");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыДействиеПоказатьОбъект(Кнопка)
	
	ОткрытьВерсиюОбъекта();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыДействиеОтчетПоИзменениям(Кнопка)
	
	ВыделенныеСтроки = ЭлементыФормы.СписокВерсий.ВыделенныеСтроки;
	
	Список = Новый СписокЗначений;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Список.Добавить(ВыделеннаяСтрока.НомерВерсии);
	КонецЦикла;
	
	Если Список.Количество() = 2 Тогда
		СформироватьОтчет(ЭлементыФормы.ТЧОтчета, Список);
	Иначе
		Предупреждение("Для сравнения необходимо выбрать две версии",, "Сравнение версий");
	КонецЕсли;
	
КонецПроцедуры

Процедура СсылкаПриИзменении(Элемент)
	
	СформироватьТаблицуВерсий();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыДействиеОбновитьСписокВерсий(Кнопка)
	
	СформироватьТаблицуВерсий();
	
КонецПроцедуры

Процедура СписокВерсийПриАктивизацииСтроки(Элемент)
	
	Если ЭлементыФормы.СписокВерсий.ВыделенныеСтроки.Количество() > 1 Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ДействиеПоказатьОбъект.Доступность = Ложь;
		ЭлементыФормы.КоманднаяПанель1.Кнопки.Восстановление.Доступность = Ложь;
	Иначе
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ДействиеПоказатьОбъект.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

//Востановление версий 
//
Процедура КоманднаяПанель1ОткрытьВерсию(Кнопка)
	
	Объект = Неопределено;
	Если ВосстановитьВерсиюОбъекта(Объект, ЭлементыФормы.СписокВерсий.ТекущиеДанные) Тогда
		Форма = Объект.ПолучитьФорму();
		Форма.Открыть();
		Форма.Модифицированность = Объект.Модифицированность();
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель1СохранитьВерсию(Кнопка)
	
	Если НЕ РольДоступна("ПолныеПрава") Тогда
		Предупреждение("Данная функция доступна только администратору", 10);
		Возврат;
	КонецЕсли;
	
	Объект = Неопределено;
	Если ВосстановитьВерсиюОбъекта(Объект, ЭлементыФормы.СписокВерсий.ТекущиеДанные) Тогда
		Если Объект.Модифицированность() Тогда
			Объект.ОбменДанными.Загрузка = Истина;
			Объект.Записать();
			Сообщить("Изменения записаны в объект", СтатусСообщения.Информация);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры
