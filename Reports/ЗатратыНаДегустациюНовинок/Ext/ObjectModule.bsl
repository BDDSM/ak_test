
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ПараметрыДанныхКомпоновщика = КомпоновщикНастроек.Настройки.ПараметрыДанных;
	ПараметрыДанныхКомпоновщика.УстановитьЗначениеПараметра("Склад", РегистрыСведений.СоответствиеТиповЗаданийТехнологаМагазинамИСкладов.ВернутьСкладПеремещения("ЗадачаНаДегустациюНовинки"));
	
КонецПроцедуры

Процедура ОтправитьОтчётПоЭлектронке() Экспорт
Перем КомпоновщикНастроек;
	
	СКД = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	
	ВыбранныйВариант = СКД.ВариантыНастроек.Найти("РассылкаУведомлений");
	Если ВыбранныйВариант = Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(ВыбранныйВариант.Настройки);
	КонецЕсли;
	
	НастройкиКомпоновщика = КомпоновщикНастроек.Настройки;
	НашаСтруктураКомпоновщика = НастройкиКомпоновщика.Структура[0];
	НастройкиКомпоновщика.ПараметрыВывода.УстановитьЗначениеПараметра("РасположениеРесурсов", РасположениеРесурсовКомпоновкиДанных.Горизонтально);
	НастройкиКомпоновщика.ПараметрыВывода.УстановитьЗначениеПараметра("АвтоПозицияРесурсов", АвтоПозицияРесурсовКомпоновкиДанных.ПослеВсехПолей);
	//НашаСтруктураКомпоновщика.ПараметрыВывода.УстановитьЗначениеПараметра("РасположениеИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	
	ПериодОтчёта = Новый СтандартныйПериод(ВариантСтандартногоПериода.ПрошлыйМесяц);
	ПараметрыДанныхКомпоновщика = НастройкиКомпоновщика.ПараметрыДанных;
	ПараметрыДанныхКомпоновщика.УстановитьЗначениеПараметра("Период", ПериодОтчёта);
	//ПараметрыДанныхКомпоновщика.УстановитьЗначениеПараметра("Склад", РегистрыСведений.СоответствиеТиповЗаданийТехнологаМагазинамИСкладов.ВернутьСкладПеремещения("ЗадачаНаДегустациюНовинки"));
	ЗначениеПараметра = ПараметрыДанныхКомпоновщика.Элементы.Добавить();
	ЗначениеПараметра.Параметр = Новый ПараметрКомпоновкиДанных("Склад");
	ЗначениеПараметра.Использование = Истина;
	ЗначениеПараметра.Значение = РегистрыСведений.СоответствиеТиповЗаданийТехнологаМагазинамИСкладов.ВернутьСкладПеремещения("ЗадачаНаДегустациюНовинки");
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, КомпоновщикНастроек.ПолучитьНастройки(), , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;	
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ,, Истина);
	
	// подготовим таблицу и процессор вывода результата СКД в таблицу
	ТаблицаРезультатов = Новый ТаблицаЗначений;
	ПроцессорВыводаВДЗ = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;	
	ПроцессорВыводаВДЗ.УстановитьОбъект(ТаблицаРезультатов);	
	ТаблицаРезультатов = ПроцессорВыводаВДЗ.Вывести(ПроцессорКомпоновкиДанных);
	
	СтруктураПолей = Новый Структура;
	Для Каждого КолонкаДЗ Из ТаблицаРезультатов.Колонки Цикл СтруктураПолей.Вставить(КолонкаДЗ.Имя, КолонкаДЗ.Заголовок); КонецЦикла;
	Если СтруктураПолей.Свойство("АдресЭП") Тогда СтруктураПолей.Удалить("АдресЭП"); КонецЕсли;
	
	ТекстХТМЛ = "<HTML>
		|<HEAD>
		|<META HTTP-EQUIV='Content-Type' CONTENT='text/html; CHARSET=utf-8'>
		|<TITLE></TITLE></HEAD><BODY>
		|
		|<TABLE width='100%' border='1' cellspacing='0'>
		|<THEAD align='center'>
		|<TR>";
	
	Для Каждого КлючИЗначение Из СтруктураПолей Цикл
		ТекстХТМЛ = ТекстХТМЛ + "
		|	<TD><B>" + КлючИЗначение.Значение + "</B></TD>";
	КонецЦикла;
	
	ТекстХТМЛ = ТекстХТМЛ + "
		|</TR>
		|</THEAD>
		|<TBODY>
		|";
	
	ТемаПисьма = "Затраты на дегустацию новинок за " + ПредставлениеПериода(ПериодОтчёта.ДатаНачала, ПериодОтчёта.ДатаОкончания);
	
	ЭлАдреса = РегистрыСведений.АК_ГруппыРассылки.ПолучитьМассивЭлАдресовПоГруппеРассылки(Справочники.АК_ГруппыРассылки.ЗатратыНаДегустациюНовинок);
	ТелоПисьма = ТекстХТМЛ;
	
	Для Каждого ВыборкаДетальныеЗаписи Из ТаблицаРезультатов Цикл
		
		ТелоПисьма = ТелоПисьма + "<TR>";
		Для Каждого КлючИЗначение Из СтруктураПолей Цикл
			ТелоПисьма = ТелоПисьма + Символы.ПС + Символы.Таб + "<TD>" + ВыборкаДетальныеЗаписи[КлючИЗначение.Ключ] + "</TD>";
		КонецЦикла;
		ТелоПисьма = ТелоПисьма + Символы.ПС + "</TR>" + Символы.ПС;
		
	КонецЦикла;
	
	ТелоПисьма = ТелоПисьма + "</TBODY></TABLE></BODY></HTML>";
	Документы.ЗаданиеТехнологаМагазинам.ОтправитьСообщение(ТемаПисьма, ТелоПисьма, ЭлАдреса, "Затраты на дегустацию новинок");
	
КонецПроцедуры
