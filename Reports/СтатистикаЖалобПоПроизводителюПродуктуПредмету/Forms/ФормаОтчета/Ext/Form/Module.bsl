
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	КлючЗаписи = ПолучитьЗначениеРасшифровки(Расшифровка);
	Если КлючЗаписи <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("РегистрСведений.ОбращенияПокупателей.ФормаЗаписи", Новый Структура("Ключ", КлючЗаписи));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеРасшифровки(Расшифровка)
	
	КоллекцияРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	КоллекцияПолей = КоллекцияРасшифровки.Элементы[Расшифровка].ПолучитьПоля();
	
	Если КоллекцияПолей.Количество() Тогда
		
		Если КоллекцияПолей[0].Поле = "id_OK" Тогда
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ОбращенияПокупателей.id_OK,
				|	ОбращенияПокупателей.GUID_Загрузки,
				|	ОбращенияПокупателей.ДатаДок
				|ИЗ
				|	РегистрСведений.ОбращенияПокупателей КАК ОбращенияПокупателей
				|ГДЕ
				|	ОбращенияПокупателей.id_OK = &id_OK");
			Запрос.УстановитьПараметр("id_OK", КоллекцияПолей[0].Значение);
			
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда Возврат Неопределено; КонецЕсли;
			
			ВыборкаЗапроса = РезультатЗапроса.Выбрать();
			ВыборкаЗапроса.Следующий();
			
			СтруктураКлючЗаписи = Новый Структура("id_OK,GUID_Загрузки,ДатаДок");
			ЗаполнитьЗначенияСвойств(СтруктураКлючЗаписи, ВыборкаЗапроса);
			
			КлючЗаписи = РегистрыСведений.ОбращенияПокупателей.СоздатьКлючЗаписи(СтруктураКлючЗаписи);
			
			Возврат КлючЗаписи;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции
