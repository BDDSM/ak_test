
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

//+++ AK suvv 2018.08.24 ИП-00019211
&НаКлиенте
Процедура ПериодФормированияОтчетаПриИзменении(Элемент)
	
	ПриИзмененииГодаИПериодичности();
	
КонецПроцедуры //--- AK suvv

//+++ AK suvv 2018.08.24 ИП-00019211
&НаКлиенте
Процедура ГодФормированияОтчетаПриИзменении(Элемент)
	
	ПриИзмененииГодаИПериодичности(); 
	
КонецПроцедуры //--- AK suvv 

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	УстановитьСохраненныеПараметры();
	СформироватьОтчет();
	
КонецПроцедуры 

#КонецОбласти

#Область Расшифровка

//+++ AK suvv 2018.08.24 ИП-00019211
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если (Прав(Элемент.ТекущаяОбласть.Имя,1) = "2" или Прав(Элемент.ТекущаяОбласть.Имя,1) = "3" или Прав(Элемент.ТекущаяОбласть.Имя,1) = "4") и ВидОтчета = 1 ИЛИ 
		Прав(Элемент.ТекущаяОбласть.Имя,1) = "3" и ВидОтчета = 2 Тогда
		ИмяРазделаДляРасшифровки = Результат.Область(Лев(Элемент.ТекущаяОбласть.Имя, СтрДлина(Элемент.ТекущаяОбласть.Имя)-1) + "1").Текст;
	Иначе
		Возврат;
	КонецЕсли;
	
	ВывестиТаблицуРасшифровка(ИмяРазделаДляРасшифровки);
	
КонецПроцедуры //--- AK suvv

//+++ AK suvv 2018.08.24 ИП-00019211
&НаСервере
Функция СформироватьТабДокРасшифровкуКратко(ИмяРазделаДляРасшифровки)
	
	СтруктураОтбора = Новый Структура("ИтоговыйРаздел_1", ИмяРазделаДляРасшифровки);
	
	РезультатРасшифровкиБух = РеквизитФормыВЗначение("Отчет").РезультатРасшифровкиБух.Выгрузить().Скопировать(СтруктураОтбора);
	РезультатРасшифровкиФин = РеквизитФормыВЗначение("Отчет").РезультатРасшифровкиФин.Выгрузить().Скопировать(СтруктураОтбора);
	
	Если РезультатРасшифровкиБух.Количество() = 0 и РезультатРасшифровкиФин.Количество() = 0 Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РезультатРасшифровкиФин", РезультатРасшифровкиФин);
	Запрос.УстановитьПараметр("РезультатРасшифровкиБух", РезультатРасшифровкиБух);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РезультатРасшифровкиБух.ОрганизацияНаименование,
	|	РезультатРасшифровкиБух.ИтоговыйРаздел_1,
	|	РезультатРасшифровкиБух.СтатьяДР,
	|	РезультатРасшифровкиБух.Сумма,
	|	РезультатРасшифровкиБух.Аналитика_1
	|ПОМЕСТИТЬ ВТ_РезультатРасшифровкиБух
	|ИЗ
	|	&РезультатРасшифровкиБух КАК РезультатРасшифровкиБух
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатРасшифровкиФин.ОрганизацияНаименование,
	|	РезультатРасшифровкиФин.ИтоговыйРаздел_1,
	|	РезультатРасшифровкиФин.Аналитика_1,
	|	РезультатРасшифровкиФин.СуммаМСФО,
	|	РезультатРасшифровкиФин.СтатьяДР
	|ПОМЕСТИТЬ ВТ_РезультатРасшифровкиФин
	|ИЗ
	|	&РезультатРасшифровкиФин КАК РезультатРасшифровкиФин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РезультатРасшифровкиБух.ОрганизацияНаименование,
	|	ВТ_РезультатРасшифровкиБух.ИтоговыйРаздел_1,
	|	ВТ_РезультатРасшифровкиБух.СтатьяДР,
	|	ВТ_РезультатРасшифровкиБух.Сумма КАК СуммаБух,
	|	0 КАК СуммаФин,
	|	ВТ_РезультатРасшифровкиБух.Аналитика_1
	|ПОМЕСТИТЬ ВТ_Объединение
	|ИЗ
	|	ВТ_РезультатРасшифровкиБух КАК ВТ_РезультатРасшифровкиБух
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_РезультатРасшифровкиФин.ОрганизацияНаименование,
	|	ВТ_РезультатРасшифровкиФин.ИтоговыйРаздел_1,
	|	ВТ_РезультатРасшифровкиФин.СтатьяДР,
	|	0,
	|	ВТ_РезультатРасшифровкиФин.СуммаМСФО,
	|	ВТ_РезультатРасшифровкиФин.Аналитика_1
	|ИЗ
	|	ВТ_РезультатРасшифровкиФин КАК ВТ_РезультатРасшифровкиФин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Объединение.ОрганизацияНаименование КАК ОрганизацияНаименование,
	|	ВТ_Объединение.ИтоговыйРаздел_1,
	|	ВТ_Объединение.СтатьяДР КАК СтатьяДР,
	|	СУММА(ВТ_Объединение.СуммаБух) КАК СуммаБух,
	|	СУММА(ВТ_Объединение.СуммаФин) КАК СуммаФин,
	|	СУММА(ВТ_Объединение.СуммаФин - ВТ_Объединение.СуммаБух) КАК Разница,
	|	ВТ_Объединение.Аналитика_1
	|ПОМЕСТИТЬ ВТ_СгруппированноеОбъединение
	|ИЗ
	|	ВТ_Объединение КАК ВТ_Объединение
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Объединение.ИтоговыйРаздел_1,
	|	ВТ_Объединение.ОрганизацияНаименование,
	|	ВТ_Объединение.СтатьяДР,
	|	ВТ_Объединение.Аналитика_1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СгруппированноеОбъединение.ОрганизацияНаименование КАК ОрганизацияНаименование,
	|	ВТ_СгруппированноеОбъединение.ИтоговыйРаздел_1,
	|	ВТ_СгруппированноеОбъединение.СтатьяДР КАК СтатьяДР,
	|	ВТ_СгруппированноеОбъединение.СуммаБух КАК СуммаБух,
	|	ВТ_СгруппированноеОбъединение.СуммаФин КАК СуммаФин,
	|	ВТ_СгруппированноеОбъединение.Разница КАК Разница,
	|	ВЫБОР
	|		КОГДА ВТ_СгруппированноеОбъединение.СуммаБух <> 0
	|			ТОГДА ВТ_СгруппированноеОбъединение.Аналитика_1
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК СтатьяДР_Бух,
	|	ВЫБОР
	|		КОГДА ВТ_СгруппированноеОбъединение.СуммаФин <> 0
	|			ТОГДА ВТ_СгруппированноеОбъединение.Аналитика_1
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК СтатьяДР_Фин
	|ИЗ
	|	ВТ_СгруппированноеОбъединение КАК ВТ_СгруппированноеОбъединение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОрганизацияНаименование,
	|	ВТ_СгруппированноеОбъединение.СтатьяДР
	|ИТОГИ
	|	СУММА(СуммаБух),
	|	СУММА(СуммаФин),
	|	СУММА(Разница)
	|ПО
	|	ОрганизацияНаименование,
	|	СтатьяДР";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.НачатьАвтогруппировкуСтрок();
	
	МакетРасшифровка = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("РасшифровкаКратко");
	
	ОбластьШапка    = МакетРасшифровка.ПолучитьОбласть("Шапка");
	ОбластьСтрока   = МакетРасшифровка.ПолучитьОбласть("Строка");
	ОбластьИтог     = МакетРасшифровка.ПолучитьОбласть("Итог");
	ОбластьСтатьяДР = МакетРасшифровка.получитьОбласть("СтатьяДР");
	ВыборкаПоОрг    = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОрганизацияНаименование");
	
	Пока ВыборкаПоОрг.Следующий() Цикл
		
		ИтогСуммаФин = ВыборкаПоОрг.СуммаФин;
		ИтогСуммаБух = ВыборкаПоОрг.СуммаБух;
		ИтогоРазница = ВыборкаПоОрг.Разница;
		
		ОбластьШапка.Параметры.Организация = ВыборкаПоОрг.ОрганизацияНаименование;
		ТабДок.Вывести(ОбластьШапка, 1);
		
		ВыборкаПоСтатьямДР = ВыборкаПоОрг.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяДР");
		Пока ВыборкаПоСтатьямДР.Следующий() Цикл
			
			ОбластьСтатьяДР.Параметры.Заполнить(ВыборкаПоСтатьямДР);
			ТабДок.Вывести(ОбластьСтатьяДР, 2);
			
			Выборка = ВыборкаПоСтатьямДР.Выбрать();
			Пока Выборка.Следующий() Цикл
				ОбластьСтрока.Параметры.Заполнить(Выборка);
				ТабДок.Вывести(ОбластьСтрока, 3);
			КонецЦикла;
			
		КонецЦикла;
		
		ОбластьИтог.Параметры.ИтогоСуммаБух = ИтогСуммаБух;
		ОбластьИтог.Параметры.ИтогоСуммаФин = ИтогСуммаФин; 
		ОбластьИтог.Параметры.ИтогоРазница  = ИтогоРазница;
		ТабДок.Вывести(ОбластьИтог, 1);
		
	КонецЦикла;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	ТабДок.ПоказатьУровеньГруппировокСтрок(1);
	Возврат ТабДок;
	
КонецФункции //--- AK suvv

//+++ AK suvv 2018.08.24 ИП-00019211
&НаСервере
Функция СформироватьТабДокРасшифровкуПодробно(ИмяРазделаДляРасшифровки)
	
	СтруктураОтбора = Новый Структура("ИмяРаздела", ИмяРазделаДляРасшифровки);
	
	ТаблицаРасшифровка = РеквизитФормыВЗначение("Отчет").РезультатРасшифровки.Выгрузить().Скопировать(СтруктураОтбора);
	ТаблицаРасшифровка.Свернуть("НазваниеОрганизации, СтатьяДР", "Сумма");	
	
	Если ТаблицаРасшифровка.Количество() = 0 Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	//получение разделов макета
	МакетРасшифровка    = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("РасшифровкаПодробно");
	
	ОбластьШапкаОснова  = МакетРасшифровка.ПолучитьОбласть("Шапка|Основа");
	ОбластьШапкаОрг     = МакетРасшифровка.ПолучитьОбласть("Шапка|Организация");
	ОбластьШапкаВсего   = МакетРасшифровка.ПолучитьОбласть("Шапка|Всего");
	
	ОбластьСтрокаОснова = МакетРасшифровка.ПолучитьОбласть("Строка|Основа");
	ОбластьСтрокаОрг    = МакетРасшифровка.ПолучитьОбласть("Строка|Организация");
	ОбластьСтрокаВсего  = МакетРасшифровка.ПолучитьОбласть("Строка|Всего");
	
	ОбластьИтогОснова   = МакетРасшифровка.ПолучитьОбласть("Итог|Основа");
	ОбластьИтогОрг      = МакетРасшифровка.ПолучитьОбласть("Итог|Организация");
	ОбластьИтогВсего    = МакетРасшифровка.ПолучитьОбласть("Итог|Всего");
	
	//вывод шапки
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Вывести(ОбластьШапкаОснова);
	
	ТаблицаРасшифровкаОрг = ТаблицаРасшифровка.Скопировать();
	ТаблицаРасшифровкаОрг.Свернуть("НазваниеОрганизации");
	ТаблицаРасшифровкаОрг.Сортировать("НазваниеОрганизации Возр");
	МассивОрганизаций = ТаблицаРасшифровкаОрг.ВыгрузитьКолонку("НазваниеОрганизации");
	Для Каждого Орг Из МассивОрганизаций Цикл
		ОбластьШапкаОрг.Параметры.НазваниеОрганизации = Орг;
		ТабДок.Присоединить(ОбластьШапкаОрг);
	КонецЦикла; 
	
	ТабДок.Присоединить(ОбластьШапкаВсего);
	
	//вывод строк 
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РезультатРасшифровки", ТаблицаРасшифровка);
	Запрос.Текст = "ВЫБРАТЬ
	|	РезультатРасшифровки.НазваниеОрганизации,
	|	РезультатРасшифровки.СтатьяДР,
	|	РезультатРасшифровки.Сумма
	|ПОМЕСТИТЬ ВТ_РезультатРасшифровки
	|ИЗ
	|	&РезультатРасшифровки КАК РезультатРасшифровки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_РезультатРасшифровки.СтатьяДР
	|ПОМЕСТИТЬ ВТ_СтатьиДР
	|ИЗ
	|	ВТ_РезультатРасшифровки КАК ВТ_РезультатРасшифровки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_РезультатРасшифровки.НазваниеОрганизации
	|ПОМЕСТИТЬ ВТ_Организации
	|ИЗ
	|	ВТ_РезультатРасшифровки КАК ВТ_РезультатРасшифровки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Организации.НазваниеОрганизации,
	|	ВТ_СтатьиДР.СтатьяДР
	|ПОМЕСТИТЬ ВТ_ВсеОргИСтатьиДР
	|ИЗ
	|	ВТ_СтатьиДР КАК ВТ_СтатьиДР,
	|	ВТ_Организации КАК ВТ_Организации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВсеОргИСтатьиДР.НазваниеОрганизации КАК НазваниеОрганизации,
	|	ВТ_ВсеОргИСтатьиДР.СтатьяДР КАК СтатьяДР,
	|	ЕСТЬNULL(ВТ_РезультатРасшифровки.Сумма, 0) КАК Сумма
	|ИЗ
	|	ВТ_ВсеОргИСтатьиДР КАК ВТ_ВсеОргИСтатьиДР
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РезультатРасшифровки КАК ВТ_РезультатРасшифровки
	|		ПО ВТ_ВсеОргИСтатьиДР.НазваниеОрганизации = ВТ_РезультатРасшифровки.НазваниеОрганизации
	|			И ВТ_ВсеОргИСтатьиДР.СтатьяДР = ВТ_РезультатРасшифровки.СтатьяДР
	|
	|УПОРЯДОЧИТЬ ПО
	|	НазваниеОрганизации
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	СтатьяДР";
	
	ВыборкаПоСтатьямДР = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяДР");
	
	Пока ВыборкаПоСтатьямДР.Следующий() Цикл
		СтатьяДР = ВыборкаПоСтатьямДР.СтатьяДР;
		СуммаПоВсемОргПоСтатье = ВыборкаПоСтатьямДР.Сумма; 
		ОбластьСтрокаОснова.Параметры.СтатьяДР = СтатьяДР;
		ТабДок.Вывести(ОбластьСтрокаОснова);
		
		Выборка = ВыборкаПоСтатьямДР.Выбрать();
		Пока Выборка.Следующий() Цикл
			Орг = Выборка.НазваниеОрганизации;
			Сумма = Выборка.Сумма;
			ОбластьСтрокаОрг.Параметры.Сумма = Сумма;
			ТабДок.Присоединить(ОбластьСтрокаОрг);			
		КонецЦикла;
		
		ОбластьСтрокаВсего.Параметры.Сумма = СуммаПоВсемОргПоСтатье;
		ТабДок.Присоединить(ОбластьСтрокаВсего);
		
	КонецЦикла;
	
	//вывод итогов
	ТабДок.Вывести(ОбластьИтогОснова);
	СуммаИтого = 0;
	ТаблицаСуммыПоОрг = ТаблицаРасшифровка.Скопировать();
	ТаблицаСуммыПоОрг.Свернуть("НазваниеОрганизации", "Сумма");
	ТаблицаСуммыПоОрг.Сортировать("НазваниеОрганизации Возр");
	Для Каждого Стр из ТаблицаСуммыПоОрг Цикл
		ОбластьИтогОрг.Параметры.СуммаПоОрг = Стр.Сумма;
		ТабДок.Присоединить(ОбластьИтогОрг);
		СуммаИтого = СуммаИтого + Стр.Сумма;
	КонецЦикла;
	ОбластьИтогВсего.Параметры.СуммаИтого = СуммаИтого;
	ТабДок.Присоединить(ОбластьИтогВсего);
	
	Возврат ТабДок;
	
КонецФункции //--- AK suvv

//+++ AK suvv 2018.08.24 ИП-00019211
&НаКлиенте
Процедура ВывестиТаблицуРасшифровка(ИмяРазделаДляРасшифровки)
	
	ТабДок = неопределено;
	
	Если ВидОтчета = 1 Тогда 
		Если ИмяРазделаДляРасшифровки = "ДОХОДЫ" ИЛИ ИмяРазделаДляРасшифровки = "РАСХОДЫ" или ИмяРазделаДляРасшифровки = "Результаты операционной деятельности" Тогда 
			ТабДок = СформироватьТабДокРасшифровкуКратко(ИмяРазделаДляРасшифровки);
		КонецЕсли;
	Иначе
		ТабДок = СформироватьТабДокРасшифровкуПодробно(ИмяРазделаДляРасшифровки);
	КонецЕсли;
	
	Если ТабДок <> Неопределено Тогда 
		ТабДок.Защита              = Ложь;
		ТабДок.ОтображатьСетку     = Ложь;
		ТабДок.ОтображатьЗаголовки = Ложь;
		ТабДок.Показать("Расшифровка раздела " + ИмяРазделаДляРасшифровки);
	КонецЕсли;
	
КонецПроцедуры //--- AK suvv

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++ AK suvv 2018.08.24 ИП-00019211
&НаКлиенте
Процедура УстановитьСохраненныеПараметры()
	
	УстановитьПараметрОтчета("Организации",  Организации);
	УстановитьПараметрОтчета("ПериодОтчета", ПериодОтчета);
	УстановитьПараметрОтчета("ВидОтчета",    ВидОтчета);
	
КонецПроцедуры //--- AK suvv

&НаКлиенте
Процедура УстановитьПараметрОтчета(ИмяПараметра, ЗначениеПараметра)
	
	Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(ИмяПараметра, ЗначениеПараметра);
	
КонецПроцедуры 

&НаКлиенте
Процедура СформироватьОтчет()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	ПодключитьОбработчикОжидания("СформироватьОтчетОтложенно", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетОтложенно()
	
	СкомпоноватьРезультат(РежимКомпоновкиРезультата.Фоновый);
	
КонецПроцедуры 

//+++ AK suvv 2018.08.24 ИП-00019211
&НаКлиенте
Процедура ПриИзмененииГодаИПериодичности()
	
	Если ГодФормированияОтчета = "" Тогда
		Возврат;
	КонецЕсли;
	
	ДатаДляОпределенияПериода = Дата(Число(ГодФормированияОтчета),01,01);
	
	ПериодОтчета.ДатаНачала = НачалоГода(ДатаДляОпределенияПериода);
	
	Если ПериодФормированияОтчета = 1 Тогда 
		ПериодОтчета.ДатаОкончания = КонецМесяца(ДобавитьМесяц(ДатаДляОпределенияПериода,2));
	ИначеЕсли ПериодФормированияОтчета = 2 Тогда 
		ПериодОтчета.ДатаОкончания = КонецМесяца(ДобавитьМесяц(ДатаДляОпределенияПериода,5));
	ИначеЕсли ПериодФормированияОтчета = 3 Тогда 
		ПериодОтчета.ДатаОкончания = КонецМесяца(ДобавитьМесяц(ДатаДляОпределенияПериода,8));
	ИначеЕсли ПериодФормированияОтчета = 4 Тогда 
		ПериодОтчета.ДатаОкончания = КонецМесяца(ДобавитьМесяц(ДатаДляОпределенияПериода,11));
	КонецЕсли;
	
КонецПроцедуры //--- AK suvv

#КонецОбласти

