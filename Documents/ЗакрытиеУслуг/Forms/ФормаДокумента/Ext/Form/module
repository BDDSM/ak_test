
Процедура ПериодРегистрацииПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ПериодРегистрации) Тогда
		ПериодРегистрации = НачалоМесяца(ПериодРегистрации);
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если ЭтоНовый() Тогда
		Если Не ЗначениеЗаполнено(ПериодРегистрации) Тогда
			ПериодРегистрации = НачалоМесяца(ТекущаяДата());
		КонецЕсли;
		
		Если СчетаУчета.Количество() = 0 Тогда
			СчетаУчета.Добавить().Счет = ПланыСчетов.Финансовый.ПрочаяЗадолженность; //60.4
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ДействияФормыСтруктураПодчиненности(Кнопка)
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
КонецПроцедуры

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ОсновныеДействияФормыНастройка(Кнопка)

	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФорма(Ложь, Ссылка, ЭтотОбъект);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

Процедура ЗаполнитьДокумент(Кнопка)
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация");
		Отказ = Истина;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПериодРегистрации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан период регистрации");
		Отказ = Истина;
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	Если (Услуги.Количество() > 0) Тогда
		Если Вопрос("Перед заполнением табличная часть будей очищена. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Услуги.Очистить();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	спСчета = Новый СписокЗначений;
	спСчета.ЗагрузитьЗначения(СчетаУчета.ВыгрузитьКолонку("Счет"));
	Запрос.УстановитьПараметр("спСчета", спСчета);
	Запрос.Текст = "ВЫБРАТЬ
	|	РасходИзБанка.Ссылка КАК Документ,
	|	РасходИзБанка.Контрагент,
	|	РасходИзБанка.СуммаДокумента КАК Сумма,
	|	РасходИзБанка.СчетУчетаРасчетовСКонтрагентом КАК СчетКт,
	|	РасходИзБанка.ЦФО,
	|	РасходИзБанка.СтатьяДвиженияДенежныхСредств КАК СтатьяДДС
	//+++АК SUVV 2018.02.28 ИП-00017941
	//|   СоответствияСтатейСчетов.СтатьяДР
	|ПОМЕСТИТЬ ВТ_РасходыИзБанка
	//---АК SUVV
	|ИЗ
	|	Документ.РасходИзБанка КАК РасходИзБанка
	//+++АК SUVV 2018.02.28 ИП-00017941
	//|           ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияСтатейСчетов КАК СоответствияСтатейСчетов
	//|	        ПО РасходИзБанка.СтатьяДвиженияДенежныхСредств = СоответствияСтатейСчетов.СтатьяДДС
	//|		            И РасходИзБанка.СчетУчетаРасчетовСКонтрагентом = СоответствияСтатейСчетов.Счет
	//---АК SUVV
	|ГДЕ
	|	РасходИзБанка.Организация = &Организация
	|	И РасходИзБанка.Дата >= &НачалоПериода
	|	И РасходИзБанка.Дата <= &КонецПериода
	|	И РасходИзБанка.Проведен
	|	И РасходИзБанка.СчетУчетаРасчетовСКонтрагентом В ИЕРАРХИИ(&спСчета)
	|	И РасходИзБанка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ОплатаПоставщику)
	|	И НЕ РасходИзБанка.Контрагент.РаспределениеЗатратПоТТ
	|	И (РасходИзБанка.Контрагент = &Контрагент
	|			ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	//+++АК SUVV 2018.02.28 ИП-00017941
	//|УПОРЯДОЧИТЬ ПО
	//|РасходИзБанка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасходыИзБанка.Документ,
	|	ВТ_РасходыИзБанка.Контрагент,
	|	ВТ_РасходыИзБанка.Сумма,
	|	ВТ_РасходыИзБанка.СчетКт,
	|	ВТ_РасходыИзБанка.ЦФО,
	|	ВТ_РасходыИзБанка.СтатьяДДС,
	|	МАКСИМУМ(СоответствияСтатейСчетов.Период) КАК МаксПериод
	|ПОМЕСТИТЬ ВТ_РасходыИзБанкаСМаксПериодом
	|ИЗ
	|	ВТ_РасходыИзБанка КАК ВТ_РасходыИзБанка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияСтатейСчетов КАК СоответствияСтатейСчетов
	|		ПО ВТ_РасходыИзБанка.СтатьяДДС = СоответствияСтатейСчетов.СтатьяДДС
	|			И ВТ_РасходыИзБанка.СчетКт = СоответствияСтатейСчетов.Счет
	|			И ВТ_РасходыИзБанка.Документ.Дата >= СоответствияСтатейСчетов.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РасходыИзБанка.СчетКт,
	|	ВТ_РасходыИзБанка.СтатьяДДС,
	|	ВТ_РасходыИзБанка.Документ,
	|	ВТ_РасходыИзБанка.Контрагент,
	|	ВТ_РасходыИзБанка.ЦФО,
	|	ВТ_РасходыИзБанка.Сумма
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасходыИзБанкаСМаксПериодом.Документ,
	|	ВТ_РасходыИзБанкаСМаксПериодом.Контрагент,
	|	ВТ_РасходыИзБанкаСМаксПериодом.Сумма,
	|	ВТ_РасходыИзБанкаСМаксПериодом.СчетКт,
	|	ВТ_РасходыИзБанкаСМаксПериодом.ЦФО,
	|	ВТ_РасходыИзБанкаСМаксПериодом.СтатьяДДС,
	|	СоответствияСтатейСчетов.СтатьяДР
	|ИЗ
	|	ВТ_РасходыИзБанкаСМаксПериодом КАК ВТ_РасходыИзБанкаСМаксПериодом
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияСтатейСчетов КАК СоответствияСтатейСчетов
	|		ПО ВТ_РасходыИзБанкаСМаксПериодом.СтатьяДДС = СоответствияСтатейСчетов.СтатьяДДС
	|			И ВТ_РасходыИзБанкаСМаксПериодом.СчетКт = СоответствияСтатейСчетов.Счет
	|			И ВТ_РасходыИзБанкаСМаксПериодом.МаксПериод = СоответствияСтатейСчетов.Период";
	//---АК SUVV

	Услуги.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры
