
&НаСервере
Процедура ЗаполнитьПризнакИспользованияХарактеристики()
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.Товары,
		Новый Структура(
			"ЗаполнитьПризнакХарактеристикиИспользуются, ЗаполнитьПризнакСкладируемый",
			Новый Структура("Номенклатура", "ХарактеристикиИспользуются"),
			Новый Структура("Номенклатура", "Складируемая")));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;
		Если НЕ ЗначениеЗаполнено(Объект.Склад) Тогда
			Объект.Склад = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "ОсновнойСклад");
		КонецЕсли;
		Объект.Дата = ТекущаяДата();
				
	КонецЕсли;
	
	Элементы.Дата.ТолькоПросмотр = НЕ (РольДоступна("ПолныеПрава") ИЛИ РольДоступна("Финансист") ИЛИ РольДоступна("ФинансовыйДиректор"));
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НастройкаПравДоступа.ОпределитьДоступностьВозможностьИзмененияДокументаПоДатеЗапрета(Объект.Ссылка.ПолучитьОбъект(), ЭтаФорма);
	КонецЕсли;
	
	ЗаполнитьПризнакИспользованияХарактеристики();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьПризнакИспользованияХарактеристики();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьПризнакИспользованияХарактеристики();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЕдиницаПоКлассификатору"	, СтруктураДанные.Номенклатура.БазоваяЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Номенклатура"			, СтруктураДанные.Номенклатура);
			
	Если НЕ СтруктураДанные.Номенклатура.НеВедетсяУчетПоХарактеристикам Тогда
		
							 
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ХарактеристикиНоменклатуры.Ссылка КАК Характеристика,
		|	ХарактеристикиНоменклатуры.ХарактеристикаФасовка
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|ГДЕ
		|	ХарактеристикиНоменклатуры.Владелец = &Номенклатура
		|	И НЕ ХарактеристикиНоменклатуры.Неактивная
		|	И НЕ ХарактеристикиНоменклатуры.ПометкаУдаления
		|	И ХарактеристикиНоменклатуры.ХарактеристикаФасовка <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
		
		ТабРезультат = Запрос.Выполнить().Выгрузить();
		Если ТабРезультат.Количество() = 1 Тогда
			Если Объект.ВидОперации = Перечисления.ВидыОперацийФасовки.Фасовка Тогда
				СтруктураДанные.Характеристика = ТабРезультат[0].ХарактеристикаФасовка;
				СтруктураДанные.ХарактеристикаПолучено = ТабРезультат[0].Характеристика;
			Иначе	
				СтруктураДанные.Характеристика = ТабРезультат[0].Характеристика;
				СтруктураДанные.ХарактеристикаПолучено = ТабРезультат[0].ХарактеристикаФасовка;
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	ЕдиницыИзмерения.Владелец = &Номенклатура
	|	И ЕдиницыИзмерения.ЕдиницаПоКлассификатору = &ЕдиницаПоКлассификатору
	|	И НЕ ЕдиницыИзмерения.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураДанные.Вставить("ЕдиницаИзмерения", Выборка.ЕдиницаИзмерения);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция НеВедетсяУчетПоХарактеристикам(мНоменклатура)
	
	Возврат мНоменклатура.НеВедетсяУчетПоХарактеристикам;
	
КонецФункции

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	Если НЕ СтрокаТабличнойЧасти.Номенклатура = ПолучитьВладельцаРеквизитаСервер(СтрокаТабличнойЧасти.Характеристика) Тогда
		СтрокаТабличнойЧасти.Характеристика 	= ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	Если НЕ СтрокаТабличнойЧасти.Номенклатура = ПолучитьВладельцаРеквизитаСервер(СтрокаТабличнойЧасти.ХарактеристикаПолучено) Тогда
		СтрокаТабличнойЧасти.ХарактеристикаПолучено 	= ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	Если НЕ СтрокаТабличнойЧасти.Номенклатура = ПолучитьВладельцаРеквизитаСервер(СтрокаТабличнойЧасти.ЕдиницаИзмерения) Тогда
		СтрокаТабличнойЧасти.ЕдиницаИзмерения 	= ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка");
	КонецЕсли;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("ВидОперации", 	Объект.ВидОперации);
	СтруктураДанные.Вставить("Номенклатура", 	СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", 	?(НеВедетсяУчетПоХарактеристикам(СтрокаТабличнойЧасти.Номенклатура), ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"), СтрокаТабличнойЧасти.Характеристика));
	СтруктураДанные.Вставить("ХарактеристикаПолучено", 	?(НеВедетсяУчетПоХарактеристикам(СтрокаТабличнойЧасти.Номенклатура), ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"), СтрокаТабличнойЧасти.ХарактеристикаПолучено));
	СтруктураДанные.Вставить("Период", 			Объект.Дата);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Характеристика		= СтруктураДанные.Характеристика;
	СтрокаТабличнойЧасти.ХарактеристикаПолучено		= СтруктураДанные.ХарактеристикаПолучено;
	СтрокаТабличнойЧасти.ЕдиницаИзмерения 	= СтруктураДанные.ЕдиницаИзмерения;
	СтрокаТабличнойЧасти.Количество 		= 1;
	
	ЗаполнитьПризнакИспользованияХарактеристики();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтчетСервер()
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	СКД = Документы.ФасовкаСклад.ПолучитьМакет("СКД_ИзмененияДвижений");
	НастройкиСКД = СКД.НастройкиПоУмолчанию;
	НастройкиСКД.ПараметрыДанных.УстановитьЗначениеПараметра("Ссылка", Объект.Ссылка);
	НастройкиСКД.ПараметрыДанных.УстановитьЗначениеПараметра("Склад", Объект.Склад);
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, НастройкиСКД, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ,ДанныеРасшифровки);
	
	ОтчетИзмененияДвижений.Очистить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ОтчетИзмененияДвижений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьОтчет(Команда)
	
	ОбновитьОтчетСервер();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВладельцаРеквизитаСервер(мРеквизит)
	
	Возврат мРеквизит.Владелец;
	
КонецФункции

&НаКлиенте
Процедура ПанельЗакладкиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница.Имя = "СтраницаИзменения" Тогда
		ОбновитьОтчетСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ХарактеристикаИзменениеНаСервере(ТекСтрока)
	
	ТекДанные = Объект.Товары.НайтиПоИдентификатору(ТекСтрока);
	Если ЗначениеЗаполнено(ТекДанные.Характеристика) Тогда
		Если Объект.ВидОперации = Перечисления.ВидыОперацийФасовки.Расфасовка Тогда
			Если ЗначениеЗаполнено(ТекДанные.Характеристика.ХарактеристикаФасовка) Тогда
				ТекДанные.ХарактеристикаПолучено = ТекДанные.Характеристика.ХарактеристикаФасовка;
			КонецЕсли;	
		Иначе
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("ХарактеристикаФасовка", ТекДанные.Характеристика);
			Запрос.Текст = "ВЫБРАТЬ
			               |	ХарактеристикиНоменклатуры.Ссылка
			               |ИЗ
			               |	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
			               |ГДЕ
			               |	ХарактеристикиНоменклатуры.ХарактеристикаФасовка = &ХарактеристикаФасовка";
						   
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ТекДанные.ХарактеристикаПолучено = Выборка.Ссылка;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;				   
	
КонецПроцедуры

&НаСервере
Процедура ХарактеристикаПолученоИзменениеНаСервере(ТекСтрока)
	
	ТекДанные = Объект.Товары.НайтиПоИдентификатору(ТекСтрока);
	Если ЗначениеЗаполнено(ТекДанные.ХарактеристикаПолучено) Тогда
		Если Объект.ВидОперации = Перечисления.ВидыОперацийФасовки.Фасовка Тогда
			Если ЗначениеЗаполнено(ТекДанные.ХарактеристикаПолучено.ХарактеристикаФасовка) Тогда
				ТекДанные.Характеристика = ТекДанные.ХарактеристикаПолучено.ХарактеристикаФасовка;
			КонецЕсли;	
		Иначе
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("ХарактеристикаФасовка", ТекДанные.ХарактеристикаПолучено);
			Запрос.Текст = "ВЫБРАТЬ
			               |	ХарактеристикиНоменклатуры.Ссылка
			               |ИЗ
			               |	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
			               |ГДЕ
			               |	ХарактеристикиНоменклатуры.ХарактеристикаФасовка = &ХарактеристикаФасовка";
						   
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ТекДанные.Характеристика = Выборка.Ссылка;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;				   
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ХарактеристикаИзменениеНаСервере(Элементы.Товары.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПолученоПриИзменении(Элемент)
	
	ХарактеристикаПолученоИзменениеНаСервере(Элементы.Товары.ТекущаяСтрока);
	
КонецПроцедуры
