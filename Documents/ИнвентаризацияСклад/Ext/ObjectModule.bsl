
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	//+++АК SHEP 20170512 ИП-00015361, ИП-00015398.01
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаданиеНаДегустациюНовинки") Тогда
		
		ЭтотОбъект.ДокументОснование = ДанныеЗаполнения;
		ЭтотОбъект.Автор = глЗначениеПеременной("глТекущийПользователь");
		ЭтотОбъект.Склад = РегистрыСведений.СоответствиеТиповЗаданийТехнологаМагазинамИСкладов.ВернутьСкладПеремещения("ЗадачаНаДегустациюНовинки");
		ЭтотОбъект.Комментарий = Строка(ДанныеЗаполнения);
		НоваяСтрокаТЧ = ЭтотОбъект.Товары.Добавить();
		НоваяСтрокаТЧ.Номенклатура = ДанныеЗаполнения.Номенклатура;
		НоваяСтрокаТЧ.Характеристика = ДанныеЗаполнения.ХарактеристикаНоменклатуры;
		НоваяСтрокаТЧ.ЕдиницаИзмерения = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДанныеЗаполнения.Номенклатура, "ЕдиницаХраненияОстатков");
		НоваяСтрокаТЧ.Количество = 0;
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаданиеТехнологаНаДегустацию") Тогда
		
		ЭтотОбъект.ДокументОснование = ДанныеЗаполнения;
		ЭтотОбъект.Автор = глЗначениеПеременной("глТекущийПользователь");
		ЭтотОбъект.Склад = РегистрыСведений.СоответствиеТиповЗаданийТехнологаМагазинамИСкладов.ВернутьСкладПеремещения("ЗадачаТехнологаНаДегустацию");
		ЭтотОбъект.Комментарий = Строка(ДанныеЗаполнения);
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ОценкиЗадачТехнологаНаДегустацию.Номенклатура,
			|	ОценкиЗадачТехнологаНаДегустацию.ХарактеристикаНоменклатуры КАК Характеристика,
			|	ОценкиЗадачТехнологаНаДегустацию.ЗаданиеТехнологаНаДегустацию.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ОценкиЗадачТехнологаНаДегустацию.ДатаПроизводства,
			|	0 КАК Количество
			|ИЗ
			|	РегистрСведений.ОценкиЗадачТехнологаНаДегустацию КАК ОценкиЗадачТехнологаНаДегустацию
			|ГДЕ
			|	ОценкиЗадачТехнологаНаДегустацию.ЗаданиеТехнологаНаДегустацию = &ЗаданиеТехнологаНаДегустацию
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	ЗаданиеТехнологаНаДегустацию.Номенклатура,
			|	ЗаданиеТехнологаНаДегустацию.ХарактеристикаНоменклатуры,
			|	ЗаданиеТехнологаНаДегустацию.ЕдиницаИзмерения,
			|	ДАТАВРЕМЯ(1, 1, 1),
			|	0
			|ИЗ
			|	Документ.ЗаданиеТехнологаНаДегустацию КАК ЗаданиеТехнологаНаДегустацию
			|ГДЕ
			|	ЗаданиеТехнологаНаДегустацию.Ссылка = &ЗаданиеТехнологаНаДегустацию");
		Запрос.УстановитьПараметр("ЗаданиеТехнологаНаДегустацию", ДанныеЗаполнения.Ссылка);
		
		ЭтотОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	//---АК SHEP 20170512
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ЕстьПравоОтменять = УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РазрешитьОтменуСкладскихДокументов, Ложь);
		Если НЕ ЕстьПравоОтменять Тогда
			Отказ = Истина;
			Сообщить("У вас нет права отменять складские документы");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	//при первом проведении надо установить текущую дату
	Если НЕ ЭтотОбъект.ДополнительныеСвойства.Свойство("ОтложенноеПроведение") Тогда
		Если НЕ Ссылка.Проведен И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Дата = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;	
	
	Если ПоНесколькимСкладам Тогда
		Склад = Справочники.Склады.ПустаяСсылка();
	КонецЕсли;
	
	ПроверитьНаличиеДублейВТабличнойЧасти(Отказ);
	
	//+++ АК bara 17124 31.10.2017
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Склад"				, ЭтотОбъект.Склад);
	Запрос.УстановитьПараметр("НаДату"				, ?(ЭтотОбъект.Ссылка.Пустая(), ЭтотОбъект.Дата, ЭтотОбъект.Ссылка.МоментВремени()));
	Запрос.УстановитьПараметр("Таб"					, ЭтотОбъект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("ПоНесколькимСкладам"	, ЭтотОбъект.ПоНесколькимСкладам);			   
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таб.Номенклатура,
	|	Таб.Характеристика,
	|	Таб.ДатаПроизводства,
	|	Таб.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ПоНесколькимСкладам
	|			ТОГДА Таб.Склад
	|		ИНАЧЕ &Склад
	|	КОНЕЦ КАК Склад
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&Таб КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика,
	|	ТоварыНаСкладахОстатки.ДатаПроизводства,
	|	ТоварыНаСкладахОстатки.ЕдиницаИзмерения,
	|	ТоварыНаСкладахОстатки.Склад,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&НаДату,
	|			(Склад, Номенклатура, Характеристика, ДатаПроизводства, ЕдиницаИзмерения) В
	|				(ВЫБРАТЬ
	|					Таб.Склад,
	|					Таб.Номенклатура,
	|					Таб.Характеристика,
	|					Таб.ДатаПроизводства,
	|					Таб.ЕдиницаИзмерения
	|				ИЗ
	|					ВТ_Товары КАК Таб)) КАК ТоварыНаСкладахОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Товары";
	
	ТабТовары = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаб Из ЭтотОбъект.Товары Цикл
		СтрокиОстаток = ТабТовары.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, ДатаПроизводства, ЕдиницаИзмерения, Склад", СтрокаТаб.Номенклатура, СтрокаТаб.Характеристика, СтрокаТаб.ДатаПроизводства, СтрокаТаб.ЕдиницаИзмерения, ?(ЭтотОбъект.ПоНесколькимСкладам, СтрокаТаб.Склад, ЭтотОбъект.Склад)));
		Если СтрокиОстаток.Количество() > 0 Тогда
			УчетноеКоличество = СтрокиОстаток[0].КоличествоОстаток;
			СтрокаТаб.Расхождение = СтрокаТаб.Количество - УчетноеКоличество ;
		Иначе
			СтрокаТаб.Расхождение = СтрокаТаб.Количество - 0 ;
		КонецЕсли;	
	КонецЦикла;	
	//--- АК bara 17124 31.10.2017
	
	//+++AK GREK 10.12.2018 ИП-00020678
	ДополнитьОстаткамиСоСкладовРезерваИМ(Отказ);
	//---
	
КонецПроцедуры

//+++АК LATV 2018.06.23 ИП-00018971
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	УстановитьПривилегированныйРежим(Истина);
	
	ОтразитьДвиженияТоварыНаСкладах(Отказ);
	ОтразитьДвиженияПоРегиструБухгалтерии(Отказ);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ДокументОснование = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка);
	
	Если Запрос.Выполнить().Пустой() = Ложь Тогда 
		Сообщить("На основании инвентаризации существует документ реализации, отмена проведения не возможна.");
		Отказ = Истина;		
	КонецЕсли;	
	
КонецПроцедуры

//+++АК LATV 2018.06.14 ИП-00018971
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если Не ПоНесколькимСкладам
	   И Справочники.Склады.ДвиженияЗаблокированы(Склад) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Движения по складу %1 заблокированы! Проведение невозможно.'"), Склад);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Склад",, Отказ);
	КонецЕсли;

	//+++shae 2018.08.07 ИП-00019236  
	СкладыСервер.ПроверитьНаСоответствиеОрганизацииЗакупки(ЭтотОбъект.Организация, ЭтотОбъект.Товары.Выгрузить(), Отказ);		
	//---shae 2018.08.07 ИП-00019236  
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.06.14 ИП-00018971
Процедура ОтразитьДвиженияТоварыНаСкладах(Отказ)

	Движения.ТоварыНаСкладах.Записать(); //+++АК MIND 2018.01.19 сразу запишем движения, чтобы последующий запрос отработал правильно
										 //а то имеем эффект разных движений при перепроведении, странно, что я раньше не заметил этого
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаДату",	Новый Граница(Дата - 1, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Дата", 	Дата);
	Запрос.УстановитьПараметр("Склад", 	Склад);
	Запрос.УстановитьПараметр("Ссылка",	Ссылка);
	Запрос.УстановитьПараметр("ПоНесколькимСкладам",	ПоНесколькимСкладам);
	Запрос.УстановитьПараметр("АвторИзменений",		 	ПараметрыСеанса.ТекущийПользователь);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Склад,
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика,
	|	ТоварыНаСкладахОстатки.ДатаПроизводства,
	|	ТоварыНаСкладахОстатки.ЕдиницаИзмерения,
	|	-ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ОбщаяТаблица
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&НаДату,
	|			(Склад, Номенклатура, Характеристика, ДатаПроизводства, ЕдиницаИзмерения) В
	|				(ВЫБРАТЬ
	|					ВЫБОР
	|						КОГДА &ПоНесколькимСкладам
	|							ТОГДА Товары.Склад
	|						ИНАЧЕ &Склад
	|					КОНЕЦ КАК Склад,
	|					Товары.Номенклатура,
	|					Товары.Характеристика,
	|					Товары.ДатаПроизводства,
	|					Товары.ЕдиницаИзмерения
	|				ИЗ
	|					Документ.ИнвентаризацияСклад.Товары КАК Товары
	|				ГДЕ
	|					Товары.Ссылка = &Ссылка)) КАК ТоварыНаСкладахОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ПоНесколькимСкладам
	|			ТОГДА ИнвентаризацияСкладТовары.Склад
	|		ИНАЧЕ &Склад
	|	КОНЕЦ,
	|	ИнвентаризацияСкладТовары.Номенклатура,
	|	ВЫБОР
	|		КОГДА &ПоНесколькимСкладам
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИнвентаризацияСкладТовары.Номенклатура.НеВедетсяУчетПоХарактеристикам
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				ИНАЧЕ ИнвентаризацияСкладТовары.Характеристика
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ИнвентаризацияСкладТовары.ДатаПроизводства,
	|	ИнвентаризацияСкладТовары.ЕдиницаИзмерения,
	|	СУММА(ИнвентаризацияСкладТовары.Количество)
	|ИЗ
	|	Документ.ИнвентаризацияСклад.Товары КАК ИнвентаризацияСкладТовары
	|ГДЕ
	|	ИнвентаризацияСкладТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА &ПоНесколькимСкладам
	|			ТОГДА ИнвентаризацияСкладТовары.Склад
	|		ИНАЧЕ &Склад
	|	КОНЕЦ,
	|	ИнвентаризацияСкладТовары.Номенклатура,
	|	ИнвентаризацияСкладТовары.Характеристика,
	|	ИнвентаризацияСкладТовары.ДатаПроизводства,
	|	ИнвентаризацияСкладТовары.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ПоНесколькимСкладам
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИнвентаризацияСкладТовары.Номенклатура.НеВедетсяУчетПоХарактеристикам
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				ИНАЧЕ ИнвентаризацияСкладТовары.Характеристика
	|			КОНЕЦ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ВЫБОР
	|		КОГДА СУММА(ОбщаяТаблица.Количество) < 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ОбщаяТаблица.Склад,
	|	ОбщаяТаблица.Номенклатура,
	|	ОбщаяТаблица.Характеристика,
	|	ОбщаяТаблица.ДатаПроизводства,
	|	ОбщаяТаблица.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА СУММА(ОбщаяТаблица.Количество) < 0
	|			ТОГДА -СУММА(ОбщаяТаблица.Количество)
	|		ИНАЧЕ СУММА(ОбщаяТаблица.Количество)
	|	КОНЕЦ КАК Количество,
	|	&АвторИзменений
	|ИЗ
	|	ОбщаяТаблица КАК ОбщаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбщаяТаблица.Склад,
	|	ОбщаяТаблица.Номенклатура,
	|	ОбщаяТаблица.Характеристика,
	|	ОбщаяТаблица.ДатаПроизводства,
	|	ОбщаяТаблица.ЕдиницаИзмерения
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОбщаяТаблица.Количество) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОбщаяТаблица";
	
	//+++AK GREK 10.12.2018 ИП-00020678
	Если НЕ ПоНесколькимСкладам И Склад.Код = "000000316" Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	&Склад КАК Склад,
		               |	ИнвентаризацияСкладТовары.Номенклатура КАК Номенклатура,
		               |	ВЫБОР
		               |		КОГДА ИнвентаризацияСкладТовары.Номенклатура.НеВедетсяУчетПоХарактеристикам
		               |			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		               |		ИНАЧЕ ИнвентаризацияСкладТовары.Характеристика
		               |	КОНЕЦ КАК Характеристика,
		               |	ИнвентаризацияСкладТовары.ДатаПроизводства,
		               |	ИнвентаризацияСкладТовары.ЕдиницаИзмерения,
		               |	СУММА(ИнвентаризацияСкладТовары.Расхождение) КАК Количество
		               |ПОМЕСТИТЬ ОбщаяТаблица
		               |ИЗ
		               |	Документ.ИнвентаризацияСклад.Товары КАК ИнвентаризацияСкладТовары
		               |ГДЕ
		               |	ИнвентаризацияСкладТовары.Ссылка = &Ссылка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ИнвентаризацияСкладТовары.Номенклатура,
		               |	ИнвентаризацияСкладТовары.Характеристика,
		               |	ИнвентаризацияСкладТовары.ДатаПроизводства,
		               |	ИнвентаризацияСкладТовары.ЕдиницаИзмерения,
		               |	ВЫБОР
		               |		КОГДА &ПоНесколькимСкладам
		               |			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		               |		ИНАЧЕ ВЫБОР
		               |				КОГДА ИнвентаризацияСкладТовары.Номенклатура.НеВедетсяУчетПоХарактеристикам
		               |					ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		               |				ИНАЧЕ ИнвентаризацияСкладТовары.Характеристика
		               |			КОНЕЦ
		               |	КОНЕЦ,
		               |	ВЫБОР
		               |		КОГДА ИнвентаризацияСкладТовары.Номенклатура.НеВедетсяУчетПоХарактеристикам
		               |			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		               |		ИНАЧЕ ИнвентаризацияСкладТовары.Характеристика
		               |	КОНЕЦ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	&Дата КАК Период,
		               |	ВЫБОР
		               |		КОГДА СУММА(ОбщаяТаблица.Количество) < 0
		               |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		               |		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		               |	КОНЕЦ КАК ВидДвижения,
		               |	ОбщаяТаблица.Склад,
		               |	ОбщаяТаблица.Номенклатура,
		               |	ОбщаяТаблица.Характеристика,
		               |	ОбщаяТаблица.ДатаПроизводства,
		               |	ОбщаяТаблица.ЕдиницаИзмерения,
		               |	ВЫБОР
		               |		КОГДА СУММА(ОбщаяТаблица.Количество) < 0
		               |			ТОГДА -СУММА(ОбщаяТаблица.Количество)
		               |		ИНАЧЕ СУММА(ОбщаяТаблица.Количество)
		               |	КОНЕЦ КАК Количество,
		               |	&АвторИзменений
		               |ИЗ
		               |	ОбщаяТаблица КАК ОбщаяТаблица
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ОбщаяТаблица.Склад,
		               |	ОбщаяТаблица.Номенклатура,
		               |	ОбщаяТаблица.Характеристика,
		               |	ОбщаяТаблица.ДатаПроизводства,
		               |	ОбщаяТаблица.ЕдиницаИзмерения
		               |
		               |ИМЕЮЩИЕ
		               |	СУММА(ОбщаяТаблица.Количество) <> 0
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |УНИЧТОЖИТЬ ОбщаяТаблица";
	КонецЕсли;
	//---
	
	// Пишется разность между учетным и фактическим количеством
	Движения.ТоварыНаСкладах.Загрузить(Запрос.Выполнить().Выгрузить()); 
	Движения.ТоварыНаСкладах.Записать();

КонецПроцедуры

//+++АК LATV 2018.06.14 ИП-00018971
Процедура ОтразитьДвиженияПоРегиструБухгалтерии(Отказ)

	ВыполнитьДвиженияПоРегиструБухгалтерии = Ложь;
	ДополнительныеСвойства.Свойство("ОтложенноеПроведение", ВыполнитьДвиженияПоРегиструБухгалтерии); // Для совместимости
	Если ВыполнитьДвиженияПоРегиструБухгалтерии <> Истина Тогда
		ВыполнитьДвиженияПоРегиструБухгалтерии = ДопМодульСервер.ПолучитьЗначениеПраваПользователя("ФормированиеДвиженийВРБУНепосредственно", Ложь);
	КонецЕсли;
	
	Если ВыполнитьДвиженияПоРегиструБухгалтерии Тогда
		ДвиженияДокумента = Документы.ИнвентаризацияСклад.ДвиженияДокумента(Ссылка);
		Если ДвиженияДокумента.Финансовый = Неопределено Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Движения.Финансовый.Записывать = Истина;
		Движения.Финансовый.Загрузить(ДвиженияДокумента.Финансовый);
	Иначе
		РегистрыСведений.ОтложенныеДвиженияДокументовПоБухРегистру.ДобавитьДокументВОчередь(Ссылка, Отказ);
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьНаличиеДублейВТабличнойЧасти(Отказ)
	
	Если ПоНесколькимСкладам Тогда
		ПроверяемыеРеквизиты = "Номенклатура, Характеристика, Склад, ДатаПроизводства";
	Иначе
		ПроверяемыеРеквизиты = "Номенклатура, Характеристика, ДатаПроизводства";
	КонецЕсли;
	
	ТаблицаДокумента = Товары.Выгрузить();
	ТаблицаДокумента.Колонки.Добавить("КвоПроверкаДублей"); 
	ТаблицаДокумента.ЗаполнитьЗначения(1, "КвоПроверкаДублей");
	
	ТаблицаДокумента.Свернуть(ПроверяемыеРеквизиты,"КвоПроверкаДублей");
	
	ЕстьОшибки = Ложь;
	
	Для Каждого ТекущаяСтрока Из ТаблицаДокумента Цикл 
		Если ТекущаяСтрока.КвоПроверкаДублей > 1 Тогда
			Если ПоНесколькимСкладам Тогда
				Сообщить("В табличной части существуют строки с одинаковой номенклатурой: " + ТекущаяСтрока.Номенклатура + " и складом: " + ТекущаяСтрока.Склад);
			Иначе
				Сообщить("В табличной части существуют строки с одинаковой номенклатурой: " + ТекущаяСтрока.Номенклатура + " и характеристикой: " + ТекущаяСтрока.Характеристика);
			КонецЕсли;
			ЕстьОшибки = Истина;
		КонецЕсли; 
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Инвентаризация не записана!", Отказ);
	КонецЕсли;
	
КонецПроцедуры

//+++AK GREK 10.12.2018 ИП-00020678
Процедура ДополнитьОстаткамиСоСкладовРезерваИМ(Отказ)
	Если ПоНесколькимСкладам ИЛИ Склад.Код <> "000000316" Тогда
		Возврат;
	КонецЕсли;		   
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таб.Номенклатура,
	|	Таб.Характеристика,
	|	Таб.ДатаПроизводства,
	|	Таб.ЕдиницаИзмерения,
	|	&Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&Таб КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК СкладРезерва,
	|	Склады.Владелец КАК ИнтернетМагазин,
	|	&Склад КАК СкладРаспред
	|ПОМЕСТИТЬ ВТ_Склады
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	(&ДляВсехИнтернетМагазинов
	|			ИЛИ Склады.Владелец В (&ИнтрнетМагазины))
	|	И Склады.ЭтоСкладРезерваИнтернетМагазина = ИСТИНА
	|	И Склады.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0)) КАК КоличествоОстаток,
	|	ВТ_Товары.Номенклатура,
	|	ВТ_Товары.Характеристика,
	|	ВТ_Товары.ДатаПроизводства,
	|	ВТ_Товары.ЕдиницаИзмерения,
	|	ВТ_Товары.Склад
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&Дата,
	|				Склад В
	|					(ВЫБРАТЬ
	|						Таб.СкладРезерва
	|					ИЗ
	|						ВТ_Склады КАК Таб)) КАК ТоварыНаСкладахОстатки
	|		ПО ВТ_Товары.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|			И ВТ_Товары.Характеристика = ТоварыНаСкладахОстатки.Характеристика
	|			И ВТ_Товары.ДатаПроизводства = ТоварыНаСкладахОстатки.ДатаПроизводства
	|			И ВТ_Товары.ЕдиницаИзмерения = ТоварыНаСкладахОстатки.ЕдиницаИзмерения
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Товары.Номенклатура,
	|	ВТ_Товары.Характеристика,
	|	ВТ_Товары.ДатаПроизводства,
	|	ВТ_Товары.ЕдиницаИзмерения,
	|	ВТ_Товары.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Склады";
	
	Запрос.УстановитьПараметр("Склад", ЭтотОбъект.Склад);
	Запрос.УстановитьПараметр("Дата", ?(ЭтотОбъект.Ссылка.Пустая(), ЭтотОбъект.Дата, ЭтотОбъект.Ссылка.МоментВремени()));
	Запрос.УстановитьПараметр("Таб", ЭтотОбъект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("ДляВсехИнтернетМагазинов", Истина);
	Запрос.УстановитьПараметр("ИнтрнетМагазины", "");
	
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		Возврат;
	КонецЕсли;
	ТЗ_ОстаткиНаСкладахРезерваДляИМ = Рез.Выгрузить();
	
	Для Каждого СтрТовары Из ЭтотОбъект.Товары Цикл
		СтрокиОстаток = ТЗ_ОстаткиНаСкладахРезерваДляИМ.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, ДатаПроизводства, ЕдиницаИзмерения, Склад", СтрТовары.Номенклатура, СтрТовары.Характеристика, СтрТовары.ДатаПроизводства, СтрТовары.ЕдиницаИзмерения, ЭтотОбъект.Склад));
		Если СтрокиОстаток.Количество() > 0 Тогда
			СтрТовары.Расхождение = СтрТовары.Расхождение - СтрокиОстаток[0].КоличествоОстаток ;
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

#КонецОбласти
