Перем мВалютаРегламентированногоУчета Экспорт;

#Если Клиент Тогда

Функция ПечатьДоверенности(НазваниеМакета)

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Доверенность.Номер КАК НомерДокумента,
	|	Доверенность.Дата КАК ДатаДокумента,
	|	Доверенность.Организация КАК Руководители,
	|	Доверенность.Организация,
	|	Доверенность.ФизЛицо,
	|	Доверенность.ФизЛицо.Наименование КАК ФамилияИмяОтчествоДоверенного,
	|	Доверенность.БанковскийСчетОрганизации КАК БанковскийСчет,
	|	Доверенность.Контрагент КАК Поставщик,
	|	Доверенность.НаПолучениеОт КАК ПоставщикПредставление,
	|	Доверенность.ДатаДействия КАК СрокДействия,
	|	Доверенность.ПоДокументу КАК РеквизитыДокументаНаПолучение,
	|	Доверенность.Товары.(
	|		НомерСтроки КАК Номер,
	|		НаименованиеТовара КАК Ценности,
	|		НаименованиеТовара КАК ЦенностиПредставление,
	|		ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
	|		ЕдиницаПоКлассификатору.Представление КАК ЕдиницаИзмеренияПредставление,
	|		Количество
	|	)
	|ИЗ
	|	Документ.Доверенность КАК Доверенность
	|ГДЕ
	|	Доверенность.Ссылка = &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер";
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ВыборкаСтрокТовары = Шапка.Товары.Выбрать();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_М2";

	//+++АК sils 22.02.2018 ИП-00017897
	//Макет = ПолучитьОбщийМакет("М2");
	Макет = ПолучитьМакет("М2");
	//---АК

	//+++АК sils 22.02.2018 ИП-00017897
	//ДанныеОФизЛице = ОбщегоНазначения.ДанныеФизЛица(Организация, ФизЛицо, Дата);
	ДанныеОФизЛице = РегистрыСведений.ФИОФизЛиц.СрезПоследних(Дата, Новый Структура("ФизЛицо",ФизЛицо));
	ПаспортныеДанные = РегистрыСведений.ПаспортныеДанныеФизЛиц.ПолучитьПоследнее(Дата,
																				Новый Структура("ФизЛицо", ФизЛицо));
	Если ПаспортныеДанные.ДокументКемВыдан = "" и ПаспортныеДанные.ДокументНомер = "" и 
		ПаспортныеДанные.ДокументСерия = "" и ЗначениеЗаполнено(ФизЛицо) Тогда
		КомСоединениеС_ЗУП = ОбменС_ЗУП.ПолучитьСоединениеСЗУПом();
		Запрос = КомСоединениеС_ЗУП.NewObject("Запрос");
		Запрос.УстановитьПараметр("Код",	ФизЛицо.Код);	
		Запрос.Текст = "ВЫБРАТЬ
		|	ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо.Код,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо.Наименование,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи
		|ИЗ
		|	РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних КАК ПаспортныеДанныеФизЛицСрезПоследних
		|ГДЕ
		|	ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо.Код = &Код";
		РезультатФизЛица = Запрос.Выполнить().Выбрать();
		Если РезультатФизЛица.Следующий() Тогда
			ПаспортныеДанные = РезультатФизЛица; 
		КонецЕсли;
	КонецЕсли;
	//---АК
	
	//+++АК sils 22.02.2018 ИП-00017897
	//ФамилияИмяОтчествоДоверенного = СокрЛП(ДанныеОФизЛице.Фамилия) + " " + СокрЛП(ДанныеОФизЛице.Имя) + " " + СокрЛП(ДанныеОФизЛице.Отчество);
	//Должность                    = СокрЛП(ДанныеОФизЛице.Должность);
	Если ДанныеОФизЛице.Количество() > 0 Тогда
		ФамилияИмяОтчествоДоверенного = СокрЛП(ДанныеОФизЛице[0].Фамилия) + " " + СокрЛП(ДанныеОФизЛице[0].Имя) + " " + СокрЛП(ДанныеОФизЛице[0].Отчество);
	Иначе
		ФамилияИмяОтчествоДоверенного = "";
	КонецЕсли;
	Должность                    = СокрЛП(ФизЛицо.Должность);
	//---АК

	Если НазваниеМакета = "М2" тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Отрез");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.НомерДокумента = ОбщегоНазначения.ПолучитьНомерНаПечать(ЭтотОбъект);
		ОбластьМакета.Параметры.ФИОДоверенного = "" + ?(ПустаяСтрока(Должность), "", Должность + ", " + Символы.ПС) + (ФамилияИмяОтчествоДоверенного);
		ТабДокумент.Вывести(ОбластьМакета);
		НазваниеФормы = "Типовая межотраслевая форма № М-2";
	Иначе
		НазваниеФормы = "Типовая межотраслевая форма № М-2а";
	КонецЕсли;

	Руководители = ОбщегоНазначения.ОтветственныеЛица(Организация, Дата);
	Руководитель = Руководители.РуководительПредставление;
	Бухгалтер    = Руководители.ГлавныйБухгалтерПредставление;

	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента,, Шапка.БанковскийСчет);

	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	Если НазваниеМакета = "М2" тогда
		ОбластьМакета.Параметры.ОКУД = "0315001";
	Иначе
		ОбластьМакета.Параметры.ОКУД = "0315002";
	КонецЕсли;
	
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.НомерДокумента               = ОбщегоНазначения.ПолучитьНомерНаПечать(ЭтотОбъект);
	ОбластьМакета.Параметры.НазваниеФормы                = НазваниеФормы;
	ОбластьМакета.Параметры.ОрганизацияПредставление     = ФормированиеПечатныхФорм.ОписаниеОрганизации(
		СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,ИНН,КПП,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.РеквизитыСчета               = ФормированиеПечатныхФорм.ОписаниеОрганизации(
		СведенияОбОрганизации, "НомерСчета,Банк,БИК,КоррСчет,");
	ОбластьМакета.Параметры.РеквизитыПотребителя         = ФормированиеПечатныхФорм.ОписаниеОрганизации(
		СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,ИНН,КПП,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.РеквизитыПлательщика         = ФормированиеПечатныхФорм.ОписаниеОрганизации(
		СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,ИНН,КПП,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.ОрганизацияКодПоОКПО         = СведенияОбОрганизации.КодПоОКПО;
	//+++АК sils 22.02.2018 ИП-00017897
	//ОбластьМакета.Параметры.ПаспортСерия                 = ДанныеОФизЛице.ДокументСерия;
	//ОбластьМакета.Параметры.ПаспортНомер                 = ДанныеОФизЛице.ДокументНомер;
	//ОбластьМакета.Параметры.ПаспортВыдан                 = ДанныеОФизЛице.ДокументКемВыдан;
	//ОбластьМакета.Параметры.ПаспортДатаВыдачи            = ДанныеОФизЛице.ДокументДатаВыдачи;
	ОбластьМакета.Параметры.ПаспортСерия                 = ПаспортныеДанные.ДокументСерия;
	ОбластьМакета.Параметры.ПаспортНомер                 = ПаспортныеДанные.ДокументНомер;
	ОбластьМакета.Параметры.ПаспортВыдан                 = ПаспортныеДанные.ДокументКемВыдан;
	ОбластьМакета.Параметры.ПаспортДатаВыдачи            = ПаспортныеДанные.ДокументДатаВыдачи;
	//---АК
	ОбластьМакета.Параметры.ФамилияИмяОтчествоДоверенного = ФамилияИмяОтчествоДоверенного;
	ОбластьМакета.Параметры.ДолжностьДоверенного         = Должность;
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Строка");

	Пока ВыборкаСтрокТовары.Следующий() Цикл

		ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
		ОбластьМакета.Параметры.КоличествоПрописью = ?(ВыборкаСтрокТовары.Количество = 0,
		                                               "",
		                                               Строка(ВыборкаСтрокТовары.Количество) + " (" + 
		                                               ФормированиеПечатныхФорм.КоличествоПрописью(ВыборкаСтрокТовары.Количество) + ")");
		ТабДокумент.Вывести(ОбластьМакета);

	КонецЦикла;

	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(Шапка);

	Если ЗначениеЗаполнено(Руководитель) Тогда
		ОбластьМакета.Параметры.ФИОРуководителя       = Руководитель;
		ОбластьМакета.Параметры.Руководитель          = Руководитель;
	КонецЕсли;

	Если ЗначениеЗаполнено(Бухгалтер) Тогда
		ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = Бухгалтер;
		ОбластьМакета.Параметры.ГлавныйБухгалтер      = Бухгалтер;
	КонецЕсли;

	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьДоверенности()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт

	Если ИмяМакета = "М2" или ИмяМакета = "М2а" Тогда
		
		ТабДокумент = ПечатьДоверенности(ИмяМакета);
		
	КонецЕсли;

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, ЭтотОбъект.Метаданные().Представление()), НепосредственнаяПечать);

КонецПроцедуры // Печать()

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("М2,М2а","Доверенность (М-2)","Доверенность (М-2а)");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
