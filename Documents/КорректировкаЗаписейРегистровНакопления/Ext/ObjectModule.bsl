Перем ТаблицаПартийПередЗаписью Экспорт;

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ТипЗнч(ОбъектКопирования) <> Тип("ДокументОбъект.КорректировкаЗаписейРегистровНакопления") Тогда
		Возврат;
	КонецЕсли; 
	
	СпецПризнакДокумента = 0;
	
	Для каждого Набор Из ОбъектКопирования.Движения Цикл
		
		Набор.Прочитать();
		Если Набор.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НаборТекущегоОбъекта = Движения[Набор.Метаданные().Имя];
		
		Для каждого ЗаписьНабора Из Набор Цикл
		
			НоваяЗапись = НаборТекущегоОбъекта.Добавить();
			НоваяЗапись.Активность  = ЗаписьНабора.Активность;
			Попытка
				Если НаборТекущегоОбъекта.Метаданные().ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
					НоваяЗапись.ВидДвижения = ЗаписьНабора.ВидДвижения;
				КонецЕсли;
			Исключение
			КонецПопытки;
			НоваяЗапись.Период      = ТекущаяДата();
			
			Для каждого Измерение Из Набор.Метаданные().Измерения Цикл
				НоваяЗапись[Измерение.Имя] = ЗаписьНабора[Измерение.Имя];
			КонецЦикла; 
			Для каждого Ресурс Из Набор.Метаданные().Ресурсы Цикл
				НоваяЗапись[Ресурс.Имя] = ЗаписьНабора[Ресурс.Имя];
			КонецЦикла; 
			Для каждого Реквизит Из Набор.Метаданные().Реквизиты Цикл
				НоваяЗапись[Реквизит.Имя] = ЗаписьНабора[Реквизит.Имя];
			КонецЦикла; 
		
		КонецЦикла; 
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ПометкаУдаления 
		И НЕ Ссылка.ПометкаУдаления Тогда
		//* Помечается на удаление ранее активный документ
		Для Каждого НаборДвижений Из Движения Цикл
			НаборДвижений.Прочитать();
			ДвиженияМодифицированы = Ложь;
			Для Каждого Движение Из НаборДвижений Цикл
				Если Движение.Активность <> Ложь Тогда
					Движение.Активность = Ложь;
					ДвиженияМодифицированы = Истина;
				КонецЕсли;
			КонецЦикла;
			Если ДвиженияМодифицированы Тогда
				НаборДвижений.Записать();
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли НЕ ПометкаУдаления
		И Ссылка.ПометкаУдаления Тогда
		//* Ранее помеченный на удаление документ восстанавливается
		Для Каждого НаборДвижений Из Движения Цикл
			НаборДвижений.Прочитать();
			ДвиженияМодифицированы = Ложь;
			Для Каждого Движение Из НаборДвижений Цикл
				Если Движение.Активность <> Истина Тогда
					Движение.Активность = Истина;
					ДвиженияМодифицированы = Истина;
				КонецЕсли;
			КонецЦикла;
			Если ДвиженияМодифицированы Тогда
				НаборДвижений.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

