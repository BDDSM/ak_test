
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Приход не контролируем
	Если ВидОперации = Перечисления.АК_ВидыОперацииАктаПриемаПередачиТМЦСотруднику.Передача Тогда
		//+++АК sils 16.05.2018 ИП-00018407
		Если не ЗначениеЗаполнено(ДокументОснование) Тогда
			СкладОфис = Константы.СкладРеклМатериалов_Офис.Получить();
			Если ЗначениеЗаполнено(СкладОфис) Тогда
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("Дата", Дата);
				Запрос.УстановитьПараметр("СкладОфис", СкладОфис);
				Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				               |	ТоварыНаСкладахОстатки.Номенклатура,
				               |	ТоварыНаСкладахОстатки.Характеристика,
				               |	ТоварыНаСкладахОстатки.ЕдиницаИзмерения,
				               |	ТоварыНаСкладахОстатки.ДатаПроизводства,
				               |	СУММА(ТоварыНаСкладахОстатки.КоличествоОстаток) КАК ОстатокНаСкладе
				               |ИЗ
				               |	РегистрНакопления.ТоварыНаСкладах.Остатки(
				               |			&Дата,
				               |			Склад = &СкладОфис
				               |				И Номенклатура.РекламныйМатериал = ИСТИНА) КАК ТоварыНаСкладахОстатки
				               |
				               |СГРУППИРОВАТЬ ПО
				               |	ТоварыНаСкладахОстатки.Номенклатура,
				               |	ТоварыНаСкладахОстатки.Характеристика,
				               |	ТоварыНаСкладахОстатки.ЕдиницаИзмерения,
				               |	ТоварыНаСкладахОстатки.ДатаПроизводства";
				ТЗ = Запрос.Выполнить().Выгрузить();
				
				Движения.ТоварыНаСкладах.Записывать = Истина;
				Движения.ТоварыНаСкладах.Очистить();
				
				Для Каждого ТекСтрокаТМЦ Из ТМЦ Цикл
					//+++АК sils 24.08.2018 ИП-00018407
					Если не ТекСтрокаТМЦ.Номенклатура.РекламныйМатериал Тогда
						Продолжить;
					КонецЕсли;
					//---АК
					НайдСтрока = ТЗ.НайтиСтроки(Новый Структура("Номенклатура", ТекСтрокаТМЦ.Номенклатура));
					Если НайдСтрока.Количество() = 0 Тогда
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = "ТМЦ " + ТекСтрокаТМЦ.Номенклатура + " отсутствует на складе ""Офис""!";
						Сообщение.Сообщить();
						Отказ = Истина;
						Продолжить;
					КонецЕсли;
					ТекСтр = НайдСтрока[0];
					Если ТекСтр.ОстатокНаСкладе < ТекСтрокаТМЦ.Количество Тогда
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = "Недостаточно ТМЦ " + ТекСтрокаТМЦ.Номенклатура + ". На складе ""Офис"" " + ТекСтр.ОстатокНаСкладе + " " + ТекСтр.ЕдиницаИзмерения;
						Сообщение.Сообщить();
						Отказ = Истина;
						Продолжить;
					КонецЕсли;
					Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
					Движение.Период = Дата;
					Движение.Склад = СкладОфис;
					Движение.Номенклатура = ТекСтрокаТМЦ.Номенклатура;
					Движение.Характеристика = ТекСтр.Характеристика;
					Движение.ЕдиницаИзмерения = ТекСтр.ЕдиницаИзмерения;
					Движение.ДатаПроизводства = ТекСтр.ДатаПроизводства;
					Движение.Количество = ТекСтрокаТМЦ.Количество;
					Движение.АвторИзменений = ПараметрыСеанса.ТекущийПользователь;
				КонецЦикла;    
			КонецЕсли;
		Иначе
			СкладКавказ = Справочники.Склады.НайтиПоКоду("000001227");
		КонецЕсли;
		//---АК
		
		Движения.ТМЦПереданныеСотруднику.Записывать = Истина;
		Движения.ТМЦПереданныеСотруднику.Очистить();
		
		Для Каждого ТекСтрокаТМЦ Из ТМЦ Цикл
			Движение = Движения.ТМЦПереданныеСотруднику.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Сотрудник = Сотрудник;
			Движение.Номенклатура = ТекСтрокаТМЦ.Номенклатура;
			Движение.Количество = ТекСтрокаТМЦ.Количество;
			
			//+++АК sils 16.05.2018 ИП-00018407
			Если ТекСтрокаТМЦ.Номенклатура.РекламныйМатериал Тогда
				Если не ЗначениеЗаполнено(ДокументОснование) Тогда
					Движение.Склад = СкладОфис;
				Иначе
					Движение.Склад = СкладКавказ;
				КонецЕсли;
			КонецЕсли;
			//---АК
		КонецЦикла;    
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	АК_АктПриемаПередачиТМЦСотрудникуТМЦ.Номенклатура КАК Номенклатура,
			|	СУММА(АК_АктПриемаПередачиТМЦСотрудникуТМЦ.Количество) КАК Количество
			|ПОМЕСТИТЬ ТМЦ
			|ИЗ
			|	Документ.АК_АктПриемаПередачиТМЦСотруднику.ТМЦ КАК АК_АктПриемаПередачиТМЦСотрудникуТМЦ
			|ГДЕ
			|	АК_АктПриемаПередачиТМЦСотрудникуТМЦ.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	АК_АктПриемаПередачиТМЦСотрудникуТМЦ.Номенклатура
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТМЦ.Номенклатура,
			|	ТМЦ.Количество
			|ИЗ
			|	ТМЦ КАК ТМЦ";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Движения.ТМЦПереданныеСотруднику.Очистить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Движение = Движения.ТМЦПереданныеСотруднику.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Сотрудник = Сотрудник;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Количество = ВыборкаДетальныеЗаписи.Количество;
		КонецЦикла;     		

		Движения.ТМЦПереданныеСотруднику.Записывать = Истина;
		Движения.Записать();       		
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТМЦПереданныеСотрудникуОстатки.Сотрудник,
			|	ТМЦПереданныеСотрудникуОстатки.Номенклатура,
			|	ПРЕДСТАВЛЕНИЕ(ТМЦПереданныеСотрудникуОстатки.Номенклатура) КАК НоменклатураПредставление,
			|	-ТМЦПереданныеСотрудникуОстатки.КоличествоОстаток КАК КоличествоОстаток
			|ИЗ
			|	РегистрНакопления.ТМЦПереданныеСотруднику.Остатки(
			|			&МоментВремени,
			|			Номенклатура В
			|				(ВЫБРАТЬ
			|					ТМЦ.Номенклатура КАК Номенклатура
			|				ИЗ
			|					ТМЦ КАК ТМЦ)) КАК ТМЦПереданныеСотрудникуОстатки
			|ГДЕ
			|	ТМЦПереданныеСотрудникуОстатки.КоличествоОстаток < 0";
		
		ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
		Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			Отказ = Истина;
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "ТМЦ " + ВыборкаДетальныеЗаписи.НоменклатураПредставление + " недостаточно в количестве " + ВыборкаДетальныеЗаписи.КоличествоОстаток + " шт.";
				Сообщение.Сообщить(); 
			КонецЦикла;
			
		КонецЕсли; 
		
		//+++АК sils 16.05.2018 ИП-00018407
		Если не Отказ и ВидОперации = Перечисления.АК_ВидыОперацииАктаПриемаПередачиТМЦСотруднику.ВозвратНаСклад Тогда
			//+++АК sils 16.05.2018 ИП-00018407
			СкладОфис = Константы.СкладРеклМатериалов_Офис.Получить();
			Если ЗначениеЗаполнено(СкладОфис) Тогда
				Движения.ТоварыНаСкладах.Записывать = Истина;
				Движения.ТоварыНаСкладах.Очистить();
				
				Для Каждого ТекСтрокаТМЦ Из ТМЦ Цикл
					Если ТекСтрокаТМЦ.Номенклатура.РекламныйМатериал Тогда
						Продолжить;
					КонецЕсли;
					Движение = Движения.ТоварыНаСкладах.ДобавитьПриход();
					Движение.Период = Дата;
					Движение.Склад = СкладОфис;
					Движение.Номенклатура = ТекСтрокаТМЦ.Номенклатура;
					Движение.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
					Движение.ЕдиницаИзмерения = ПолучитьБазовуюЕдиницуИзмерения(ТекСтрокаТМЦ.Номенклатура);
					Движение.Количество = ТекСтрокаТМЦ.Количество;
					Движение.АвторИзменений = ПараметрыСеанса.ТекущийПользователь;
				КонецЦикла;    
			КонецЕсли;
		КонецЕсли; 
		//---АК
	КонецЕсли; 
	
	
КонецПроцедуры

//+++АК sils 16.05.2018 ИП-00018407
Функция ПолучитьБазовуюЕдиницуИзмерения(мНоменклатура)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЕдиницаПоКлассификатору"	, мНоменклатура.БазоваяЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Номенклатура"			, мНоменклатура);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	ЕдиницыИзмерения.Владелец = &Номенклатура
	|	И ЕдиницыИзмерения.ЕдиницаПоКлассификатору = &ЕдиницаПоКлассификатору
	|	И НЕ ЕдиницыИзмерения.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ЕдиницаИзмерения;
	КонецЕсли;
	
	Возврат Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	
КонецФункции

