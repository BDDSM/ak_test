
&НаСервере
Процедура ЗагрузитьДанныеСервер()
	
	Объект.Начисления.Очистить();
	Если Объект.ВидНачисленияБонуса = Перечисления.ВидыНачисленийБонуса.Ежедневный Тогда
		ТабДанные = Документы.НачислениеБонусовПокупателям.ПолучитьЕжедневныеБонусы(Объект.ПериодЗагрузкиС, Объект.Дата);
	Иначе
		ТабДанные = Документы.НачислениеБонусовПокупателям.ПолучитьБонусыЗаМесяц(Объект.Дата);
	КонецЕсли;
	
	Объект.Начисления.Загрузить(ТабДанные);
	
КонецПроцедуры	

&НаКлиенте
Процедура НастроитьФорму()
	
	Если Объект.ВидНачисленияБонуса = ПредопределенноеЗначение("Перечисление.ВидыНачисленийБонуса.Ежедневный") Тогда
		Элементы.ГруппаЕжедневная.Видимость = Истина;
		Элементы.ГруппаМесячная.Видимость = Ложь;
	Иначе
		Элементы.ГруппаЕжедневная.Видимость = Ложь;
		Элементы.ГруппаМесячная.Видимость = Истина;
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(Объект.ВидНачисленияБонуса) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заполните вид начисления бонуса", Объект, "ВидНачисленияБонуса",, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	Если Объект.Начисления.Количество() > 0 Тогда
		Ответ = Вопрос("Таблица начислений будет очищена перед заполнением. Продолжить?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	ЗагрузитьДанныеСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МесяцРедактирования = Формат(Объект.Дата, "ДФ='MMMM yyyy'");
	НастроитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидНачисленияБонусаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцРедактированияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Дата = КонецМесяца(ДобавитьМесяц(ТекущаяДата(), -1));
		Объект.ПериодЗагрузкиС = НачалоМесяца(ДобавитьМесяц(ТекущаяДата(), -1));
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцРедактированияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.Дата = КонецМесяца(ДобавитьМесяц(Объект.Дата, Направление));
	Объект.ПериодЗагрузкиС = НачалоМесяца(Объект.Дата);
	МесяцРедактирования = Формат(Объект.Дата, "ДФ='MMMM yyyy'");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидНачисленияБонусаПриИзменении(Элемент)
	
	НастроитьФорму();
	
	Если Объект.ВидНачисленияБонуса = ПредопределенноеЗначение("Перечисление.ВидыНачисленийБонуса.Месячный") Тогда
		Объект.ПериодЗагрузкиС = НачалоМесяца(Объект.Дата);
	КонецЕсли;	
	
КонецПроцедуры
