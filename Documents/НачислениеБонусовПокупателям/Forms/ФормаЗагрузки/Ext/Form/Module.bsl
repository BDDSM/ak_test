
&НаСервере
Процедура ЗагрузитьДанныеСервер(ДатаОбработки)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
		               |	НачислениеБонусовПокупателям.Ссылка
		               |ИЗ
		               |	Документ.НачислениеБонусовПокупателям КАК НачислениеБонусовПокупателям
		               |ГДЕ
		               |	НачислениеБонусовПокупателям.ПометкаУдаления = ЛОЖЬ
		               |	И НачислениеБонусовПокупателям.ВидНачисленияБонуса = &ВидНачисленияБонуса
		               |	И НАЧАЛОПЕРИОДА(НачислениеБонусовПокупателям.Дата, МЕСЯЦ) = &Дата";
				   
	Запрос.УстановитьПараметр("Дата", НачалоМесяца(ДатаМесяц));
	
	
	Запрос.УстановитьПараметр("ВидНачисленияБонуса", ВидНачисления);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
	Иначе	
		ДокОбъект = Документы.НачислениеБонусовПокупателям.СоздатьДокумент();
		ДокОбъект.Дата = КонецМесяца(ДатаМесяц);
		ДокОбъект.ВидНачисленияБонуса = ВидНачисления;
	КонецЕсли;
	
	Если ВидНачисления = Перечисления.ВидыНачисленийБонуса.Месячный Тогда
		ТабДанные = Документы.НачислениеБонусовПокупателям.ПолучитьБонусыЗаМесяц(ДатаОбработки);
	Иначе	
		ТабДанные = Документы.НачислениеБонусовПокупателям.ПолучитьЕжедневныеБонусы(ДатаОбработки);
	КонецЕсли;
	
	ДокОбъект.Начисления.Загрузить(ТабДанные);
	ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(ВидНачисления) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заполните вид начисления бонуса", , "ВидНачисленияБонуса",, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьДанныеСервер(ДатаМесяц);
	
	Оповестить("ОбновитьНачисленияБонусов");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ДатаМесяц = КонецМесяца(ДобавитьМесяц(ТекущаяДата(), -1));
	МесяцКЗагрузке = Формат(ДатаМесяц, "ДФ='MMMM yyyy'");
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцКЗагрузкеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцКЗагрузкеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДатаМесяц = КонецМесяца(ДобавитьМесяц(ДатаМесяц, Направление));
	МесяцКЗагрузке = Формат(ДатаМесяц, "ДФ='MMMM yyyy'");
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидНачисления = Перечисления.ВидыНачисленийБонуса.Месячный;
	
КонецПроцедуры


