
&НаКлиенте
Процедура ОтборУтверждающийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//+++АК mika 2018.09.14 ИП-00019442 Исправление ошибки 
	//Запрос = Новый Запрос(
	//"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	//|	ПраваПользователейНаРедактированиеГрафиков.Пользователь
	//|ИЗ
	//|	РегистрСведений.ПраваПользователейНаРедактированиеГрафиков КАК ПраваПользователейНаРедактированиеГрафиков");
	//
	//МассивПользователей = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
    МассивПользователей = ПолучитьМассивПользователейУтверждающихСервер();
	//---АК mika 
	
	СписокПользователей = Новый СписокЗначений;
	СписокПользователей.ЗагрузитьЗначения(МассивПользователей);
	
	Отбор = Новый Структура("Ссылка", СписокПользователей);
	ПараметрыОткрытия = Новый Структура("Отбор", Отбор);
	
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборУтверждающийПриИзменении(Элемент)
	
	УстановитьОтборы();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//+++АК kats 2018.03.15 17479
	Если ЭтоРуководительПодразделения() Тогда
		Утверждающий=ПараметрыСеанса.ТекущийПользователь;
		Элементы.ОтборУтверждающий.ТолькоПросмотр=Истина;
	Иначе
		Ответственный=ПараметрыСеанса.ТекущийПользователь;
		Элементы.ОтборОтветственный.ТолькоПросмотр=Истина;
	КонецЕсли;
	
	//+++АК ILIK 2018.10.01 ИП-00019987
	//Если РольДоступна("ПолныеПрава") ИЛИ РольДоступна("ВыплатаЗарплаты") Тогда
	Если РольДоступна("ВыплатаЗарплаты") Тогда
	//---АК ILIK
		Элементы.ОтборУтверждающий.ТолькоПросмотр=Ложь;
		Элементы.ОтборОтветственный.ТолькоПросмотр=Ложь;
	//+++АК ILIK 2018.10.01 ИП-00019987
	ИначеЕсли РольДоступна("ПолныеПрава") Тогда
		Элементы.ОтборУтверждающий.ТолькоПросмотр=Ложь;
	//---АК ILIK
	КонецЕсли;
	
	Если УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РазрешитьРедактированиеПремийДругихОтветственных, Ложь) тогда
		Элементы.ОтборУтверждающий.ТолькоПросмотр=Ложь;
		Элементы.ОтборОтветственный.ТолькоПросмотр=Ложь;
	КонецЕсли;
	//---АК kats 2018.03.15 17479
	
	УстановитьОтборы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборы()
	
	Список.Параметры.УстановитьЗначениеПараметра("Утверждающий", Утверждающий);
	Список.Параметры.УстановитьЗначениеПараметра("ЕстьОтборПоУтверждающему", ЗначениеЗаполнено(Утверждающий));
	
	//+++АК kats 2018.03.15 17479
	Список.Параметры.УстановитьЗначениеПараметра("Ответственный", Ответственный);
	Список.Параметры.УстановитьЗначениеПараметра("ЕстьОтборПоОтветственному", ЗначениеЗаполнено(Ответственный));
	//---АК kats 2018.03.15 17479
	
	Список.Параметры.УстановитьЗначениеПараметра("НачалоМесяца", НачалоМесяца(Месяц));
	Список.Параметры.УстановитьЗначениеПараметра("КонецМесяца", КонецМесяца(Месяц));
	Список.Параметры.УстановитьЗначениеПараметра("ЕстьОтборДате", ЗначениеЗаполнено(Месяц));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцОчистка(Элемент, СтандартнаяОбработка)
	
	Месяц = Дата(1,1,1);
	УстановитьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачальноеЗначение = ?(ЗначениеЗаполнено(Месяц), Месяц, НачалоМесяца(ТекущаяДата()));
	
	СписокВыбора = Новый СписокЗначений;
	НачалоТекущегоГода = НачалоГода(НачальноеЗначение);
	НачалоПрошлогоГода = НачалоГода(НачалоТекущегоГода - 1);
	СписокВыбора.Добавить(НачалоПрошлогоГода, (Формат(НачалоПрошлогоГода, "ДФ='yyyy'") + "..."));
	НачалоМесяцаЗаполнения = НачалоТекущегоГода;
	ЭлементПоУмолчанию = Неопределено;
	Для а = 1 По 12 Цикл
		ДобавленныйЭлемент = СписокВыбора.Добавить(НачалоМесяцаЗаполнения, РаботаСДиалогами.ДатаКакМесяцПредставление(НачалоМесяцаЗаполнения));
		Если НачальноеЗначение = НачалоМесяцаЗаполнения Тогда
			ЭлементПоУмолчанию = ДобавленныйЭлемент;
		КонецЕсли; 
		НачалоМесяцаЗаполнения = ДобавитьМесяц(НачалоМесяцаЗаполнения, 1);
	КонецЦикла;
	НачалоСледующегоГода = КонецГода(НачалоТекущегоГода) + 1;
	СписокВыбора.Добавить(НачалоСледующегоГода, (Формат(НачалоСледующегоГода, "ДФ='yyyy'") + "..."));
	
	ВыбранныйЭлемент = ЭтаФорма.ВыбратьИзСписка(СписокВыбора, Элемент, ЭлементПоУмолчанию);
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Месяц = ВыбранныйЭлемент.Значение;
	МесяцСтрокой  = РаботаСДиалогами.ДатаКакМесяцПредставление(Месяц);
	
	УстановитьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтчетПоПремиям(Команда)
	ОткрытьФорму("Отчет.Премии.Форма.ФормаОтчета");
КонецПроцедуры

//+++АК kats 2018.03.15 17479
&НаСервере
Функция ЭтоРуководительПодразделения()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПраваПользователейНаРедактированиеГрафиков.Подразделение
	               |ИЗ
	               |	РегистрСведений.ПраваПользователейНаРедактированиеГрафиков КАК ПраваПользователейНаРедактированиеГрафиков
	               |ГДЕ
	               |	ПраваПользователейНаРедактированиеГрафиков.Пользователь = &Пользователь";
				   
	Результат = Запрос.Выполнить();
	Возврат (НЕ Результат.Пустой());
	
КонецФункции
//---АК kats 2018.03.15 17479

//+++АК kats 2018.03.15 17479
&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	УстановитьОтборы();
КонецПроцедуры
//---АК kats 2018.03.15 17479

// Возвращает массив пользователей, которых можно выбирать в качестве Утверждающих
//
// Возвращаемое значение:
//   <Тип.Массив>   - Массив пользователей
//
&НаСервереБезКонтекста
Функция ПолучитьМассивПользователейУтверждающихСервер() //+++АК mika 2018.09.14 ИП-00019442
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПраваПользователейНаРедактированиеГрафиков.Пользователь
	|ИЗ
	|	РегистрСведений.ПраваПользователейНаРедактированиеГрафиков КАК ПраваПользователейНаРедактированиеГрафиков");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Пользователь");
	Иначе
		Возврат Новый Массив();
	КонецЕсли;
	
КонецФункции // ПолучитьМассивПользователейУтверждающихСервер()
