
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//
	Если Не Утверждено Тогда
		Возврат;
	КонецЕсли; 
	
	//
	Для Каждого СтрокаСостава Из Состав Цикл
		
		Запись = Движения.Премии.Добавить();
		Запись.Период 		= Дата;
		Запись.ФизЛицо 		= СтрокаСостава.ФизЛицо;
		Запись.Сумма 		= СтрокаСостава.Сумма;
		Запись.СуммаСНДФЛ 	= СтрокаСостава.Сумма/0.87;
		Запись.Комментарий  = СтрокаСостава.Комментарий;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	//
	СуммаДокумента = Состав.Итог("Сумма");
	ВыплаченнаяСумма = Состав.Итог("ВыплаченнаяСумма");
	
	//
	//Если Состав.Количество() > 0 Тогда
	//	Комментарий = Состав[0].Комментарий;
	//КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Утверждено = Ложь;
	//+++ АК Pans 2017.11.09  ИП-00017174
	Для каждого СтрокаТЧ1 Из Состав Цикл
		СтрокаТЧ1.ВыплаченнаяСумма = 0;
		СтрокаТЧ1.ДатаВыплаты = Неопределено;
	КонецЦикла;
	//--- АК Pans 2017.11.09  ИП-00017174
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) //+++АК mika 2018.12.10 ИП-00020673
	
	ПроверитьАктуальностьСотрудников(Отказ);
	
КонецПроцедуры

// Выполняет проверку наличия и корректности признака "Активный" у физ. лиц  
// Параметры:
//  Отказ  - <Тип.Булево> - Признак отказа
//
Процедура ПроверитьАктуальностьСотрудников(Отказ) //+++АК mika 2018.12.10 ИП-00020673
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ *
	|ПОМЕСТИТЬ ВТ_Состав
	|ИЗ
	|	&Состав КАК Таб_Состав
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПремииСостав.НомерСтроки,
	|	НачислениеПремииСостав.ФизЛицо
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	ВТ_Состав КАК НачислениеПремииСостав
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб_Итоговая.НомерСтроки КАК НомерСтроки,
	|	Таб_Итоговая.ФизЛицоКод КАК Код,
	|	Таб_Итоговая.ФизЛицо,
	|	Таб_Итоговая.ФизЛицоАктивный КАК Активный,
	|	ВЫБОР
	|		КОГДА Таб_Итоговая.ДатаУвольнения = &ПустаяДата
	|			ТОГДА ""Нет (есть неувольенные сотрудники)""
	|		ИНАЧЕ ""Да""
	|	КОНЕЦ КАК СотрудникУволен,
	|	Таб_Итоговая.ДатаУвольнения
	|ИЗ
	|	(ВЫБРАТЬ
	|		МИНИМУМ(ВТ.НомерСтроки) КАК НомерСтроки,
	|		ВТ.ФизЛицо.Код КАК ФизЛицоКод,
	|		ВТ.ФизЛицо КАК ФизЛицо,
	|		МИНИМУМ(ВТ.ФизЛицо.Активный) КАК ФизЛицоАктивный,
	|		МИНИМУМ(ЕСТЬNULL(СотрудникиОрганизаций.ДатаУвольнения, &ПустаяДата)) КАК ДатаУвольнения
	|	ИЗ
	|		ВТ КАК ВТ
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|			ПО ВТ.ФизЛицо = СотрудникиОрганизаций.Физлицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ.ФизЛицо.Код,
	|		ВТ.ФизЛицо) КАК Таб_Итоговая
	|ГДЕ
	|	(НЕ Таб_Итоговая.ФизЛицоАктивный
	|			ИЛИ Таб_Итоговая.ФизЛицоАктивный
	|				И Таб_Итоговая.ДатаУвольнения <> &ПустаяДата
	|				И Таб_Итоговая.ДатаУвольнения < НАЧАЛОПЕРИОДА(&ДатаДокумента, МЕСЯЦ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Запрос.УстановитьПараметр("Состав", Состав.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		ШаблонУведомления = "Строка: %1, Код: %2, Физ. лицо: %3, Активный: %4, Сотрудник уволен: %5" + Символы.ПС;
		СтрокаУведомления = "";
		
		Пока Выборка.Следующий() Цикл
			СтрокаУведомления = СтрокаУведомления + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУведомления,
				Выборка.НомерСтроки, Выборка.Код, Выборка.ФизЛицо, Выборка.Активный, Выборка.СотрудникУволен); 
		КонецЦикла;
		
		Если ЗначениеЗаполнено(СтрокаУведомления) Тогда
			СтрокаУведомления = СтрЗаменить("В документе есть неактивные сотрудники: 
			                                |СтрокаУведомления
											|Проверьте карточки физ. лиц., сотрудников организаций и удалите при необходимости ""Неактивных/Уволенных"" сотрудников из документа!
											|(провести/перепровести документы с неактивными физ. лицами могут только пользователи с ролью ""Полные права"")", "СтрокаУведомления", СтрокаУведомления);
			
			//В ТЗ указан безусловный запрет, но делать его не нужно, поскольку это будет приводить к блокировке 
			//записи/проведения в ряде ситуаций (например при увольнении сотрудника в рамках текущего месяца, или утверждении через еmail)
			Если РольДоступна("ПолныеПрава") Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаУведомления);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаУведомления,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
			
	КонецЕсли;

КонецПроцедуры

