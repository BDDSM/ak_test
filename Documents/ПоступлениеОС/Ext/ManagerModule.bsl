
Функция ПечатьОС14(Ссылка) Экспорт
	
	Макет = ПолучитьОбщийМакет("ОС14");
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ИмяПараметровПечати = "ПараметрыПечати_ПоступлениеТоваровУслуг_ОС14";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеОС.Дата КАК ДатаДокумента,
	|	ПоступлениеОС.Номер КАК НомерДокумента,
	|	ПоступлениеОС.Организация КАК ОрганизацияПолучатель,
	|	ПоступлениеОС.Организация.КодПоОКПО КАК ОрганизацияПолучательКодПоОКПО,
	|	ПоступлениеОС.Склад КАК МестоПриема,
	|	ПоступлениеОС.Склад.Представление КАК МестоПриемаНаименование,
	|	ИСТИНА КАК УчитыватьНДС,
	|	ИСТИНА КАК СуммаВключаетНДС,
	|	0 КАК СуммаДокумента,
	|	ПоступлениеОС.Контрагент КАК ОрганизацияПоставщик,
	|	ПоступлениеОС.Контрагент КАК ОрганизацияГрузоотправитель,
	|	"""" КАК ОрганизацияПоставщикКодПоОКПО,
	|	"""" КАК Основание,
	|	ПоступлениеОС.НомерВходящегоДокумента КАК ОснованиеНомер,
	|	ПоступлениеОС.ДатаВходящегоДокумента КАК ОснованиеДата,
	|	ПоступлениеОС.ЦФО КАК ПодразделениеОрганизации,
	|	ВЫРАЗИТЬ(ПоступлениеОС.ЦФО.Наименование КАК СТРОКА(200)) КАК ПодразделениеНаименование,
	|	ЛОЖЬ КАК РасчетыВВалюте
	|ИЗ
	|	Документ.ПоступлениеОС КАК ПоступлениеОС
	|ГДЕ
	|	ПоступлениеОС.Ссылка = &Ссылка";
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();

	ОбластьШапка  = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	ОрганизацияПолучательНаименование = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияПолучатель, Шапка.ДатаДокумента), "НаименованиеДляПечатныхФорм,");
	ОрганизацияПолучательТелефоны = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияПолучатель, Шапка.ДатаДокумента), "Телефоны,");
	ОрганизацияПолучательБанковскиеРеквизиты = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияПолучатель, Шапка.ДатаДокумента), "НомерСчета,Банк,БИК,КоррСчет,");
	ОрганизацияПоставщикНаименование = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияПоставщик, Шапка.ДатаДокумента), "ПолноеНаименование,");
	ОрганизацияПоставщикТелефоны = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияПоставщик, Шапка.ДатаДокумента), "Телефоны,");
	ОрганизацияПоставщикБанковскиеРеквизиты = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияПоставщик, Шапка.ДатаДокумента), "НомерСчета,Банк,БИК,КоррСчет,");
	ОрганизацияГрузоотправительНаименование = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияГрузоотправитель, Шапка.ДатаДокумента), "НаименованиеДляПечатныхФорм,");
	ОрганизацияГрузоотправительБанковскиеРеквизиты = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(
		УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
			Шапка.ОрганизацияГрузоотправитель, Шапка.ДатаДокумента), "Телефоны,НомерСчета,Банк,БИК,КоррСчет,");
	
	ОбластьШапка.Параметры.Заполнить(Шапка);
	ОбластьШапка.Параметры.ОрганизацияПолучательНаименование               = ОрганизацияПолучательНаименование;
	ОбластьШапка.Параметры.ОрганизацияПолучательТелефоны                   = ОрганизацияПолучательТелефоны;
	ОбластьШапка.Параметры.ОрганизацияПолучательБанковскиеРеквизиты        = ОрганизацияПолучательБанковскиеРеквизиты;
	ОбластьШапка.Параметры.ОрганизацияПоставщикНаименование                = ОрганизацияПоставщикНаименование;
	ОбластьШапка.Параметры.ОрганизацияПоставщикТелефоны                    = ОрганизацияПоставщикТелефоны;
	ОбластьШапка.Параметры.ОрганизацияПоставщикБанковскиеРеквизиты         = ОрганизацияПоставщикБанковскиеРеквизиты;
	ОбластьШапка.Параметры.ОрганизацияГрузоотправительНаименование         = ОрганизацияГрузоотправительНаименование;
	ОбластьШапка.Параметры.ОрганизацияГрузоотправительБанковскиеРеквизиты  = ОрганизацияГрузоотправительБанковскиеРеквизиты;

	ПодразделениеОтветственныхЛиц = Шапка.ПодразделениеОрганизации;
	
	ОтветственныеЛица = ОбщегоНазначения.ОтветственныеЛица(
		Шапка.ОрганизацияПолучатель, Шапка.ДатаДокумента, ПодразделениеОтветственныхЛиц);

	ОбластьШапка.Параметры.РуководительДолжность    = ОтветственныеЛица.РуководительДолжность;
	ОбластьШапка.Параметры.РуководительНаименование = ОтветственныеЛица.РуководительПредставление;
	ТабДокумент.Вывести(ОбластьШапка);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеОСНоменклатура.Ссылка,
	|	ПоступлениеОСНоменклатура.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеОСНоменклатура.ОсновноеСредство КАК Оборудование,
	|	ВЫРАЗИТЬ(ПоступлениеОСНоменклатура.ОсновноеСредство.Наименование КАК СТРОКА(1000)) КАК ОборудованиеНаименование,
	|	ПоступлениеОСНоменклатура.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	1 КАК Коэффициент,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС) КАК СтавкаНДС,
	|	1 КАК Количество,
	|	1 КАК ФактическиПринято,
	|	ПоступлениеОСНоменклатура.Сумма КАК СтоимостьЗаЕдиницу,
	|	ПоступлениеОСНоменклатура.Сумма КАК Сумма,
	|	0 КАК СуммаНДС,
	|	ПоступлениеОСНоменклатура.Сумма КАК СуммаВВалютеДокумента,
	|	0 КАК СуммаНДСВВалютеДокумента
	|ИЗ
	|	Документ.ПоступлениеОС.Номенклатура КАК ПоступлениеОСНоменклатура
	|ГДЕ
	|	ПоступлениеОСНоменклатура.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоступлениеОСНоменклатура.НомерСтроки";
	ТаблицаПоОборудованию = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаОборудование Из ТаблицаПоОборудованию Цикл

		ОбластьСтрока.Параметры.Заполнить(СтрокаОборудование);
		ОбластьСтрока.Параметры.ОборудованиеНаименование = СокрЛП(СтрокаОборудование.ОборудованиеНаименование);
		
		Если Шапка.УчитыватьНДС Тогда
			Если Шапка.СуммаВключаетНДС Тогда
				ОбластьСтрока.Параметры.СтоимостьВсего     = СтрокаОборудование.Сумма - СтрокаОборудование.СуммаНДС;
				ОбластьСтрока.Параметры.СтоимостьЗаЕдиницу = ?(СтрокаОборудование.Количество > 0,
															  (СтрокаОборудование.Сумма - СтрокаОборудование.СуммаНДС) / СтрокаОборудование.Количество,
															   СтрокаОборудование.Сумма - СтрокаОборудование.СуммаНДС);
			Иначе
				ОбластьСтрока.Параметры.СтоимостьВсего     = СтрокаОборудование.Сумма;
				ОбластьСтрока.Параметры.СтоимостьЗаЕдиницу = СтрокаОборудование.СтоимостьЗаЕдиницу;
			КонецЕсли;
		Иначе
			ОбластьСтрока.Параметры.СтоимостьВсего = СтрокаОборудование.Сумма;
		КонецЕсли;

		ТабДокумент.Вывести(ОбластьСтрока);

	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ГлавныйБухгалтерНаименование = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
	ТабДокумент.Вывести(ОбластьМакета);

	ТабДокумент.ПолеСнизу 			= 5;
	ТабДокумент.ПолеСверху 			= 5;	
	ТабДокумент.АвтоМасштаб 		= Истина;
	ТабДокумент.ОтображатьСетку 	= Ложь;
	ТабДокумент.Защита 				= Ложь;
	ТабДокумент.ТолькоПросмотр 		= Ложь;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДокумент;
	
КонецФункции