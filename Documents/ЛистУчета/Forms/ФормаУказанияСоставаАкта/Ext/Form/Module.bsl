
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТабЕстьДанные = Новый ТаблицаЗначений();
	ТабЕстьДанные.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТабЕстьДанные.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	
	Если ТипЗнч(Параметры.ДанныеАкта) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из Параметры.ДанныеАкта Цикл
			СтрокаДоб = ТабЕстьДанные.Добавить();
			СтрокаДоб.Номенклатура = ЭлементМассива.Номенклатура;
			СтрокаДоб.Количество = ЭлементМассива.Количество;
		КонецЦикла;	
	КонецЕсли;
	
	ПричинаСписания = Параметры.ПричинаСписания;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ЛистУчета", Параметры.ЛистСсылка);
	Запрос.УстановитьПараметр("Таб", ТабЕстьДанные);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Таб.Номенклатура,
	               |	Таб.Количество
	               |ПОМЕСТИТЬ ВТ_Данные
	               |ИЗ
	               |	&Таб КАК Таб
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЗ_Запрос.Номенклатура,
	               |	СУММА(ВЗ_Запрос.Количество) КАК Количество
	               |ИЗ
	               |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |		ДвиженияТоваровПоЛистамУчета.Номенклатура КАК Номенклатура,
	               |		0 КАК Количество
	               |	ИЗ
	               |		РегистрНакопления.ДвиженияТоваровПоЛистамУчета КАК ДвиженияТоваровПоЛистамУчета
	               |	ГДЕ
	               |		ДвиженияТоваровПоЛистамУчета.Регистратор = &ЛистУчета
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ВТ_Данные.Номенклатура,
	               |		ВТ_Данные.Количество
	               |	ИЗ
	               |		ВТ_Данные КАК ВТ_Данные) КАК ВЗ_Запрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЗ_Запрос.Номенклатура
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВЗ_Запрос.Номенклатура.Наименование";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаДоб = ТоварныйСостав.Добавить();
		СтрокаДоб.Номенклатура = Выборка.Номенклатура;
		СтрокаДоб.Количество = Выборка.Количество;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ЗначениеЗаполнено(ПричинаСписания) Тогда
		Предупреждение("Заполните причина списания");
		Возврат;
	КонецЕсли;	
	
	МассивВозврат = Новый Массив();
	Для Каждого СтрокаТаб Из ТоварныйСостав Цикл
		Если СтрокаТаб.Количество <> 0 Тогда
			МассивВозврат.Добавить(Новый Структура("ПричинаСписания, Номенклатура, Количество", ПричинаСписания, СтрокаТаб.Номенклатура, СтрокаТаб.Количество));
		КонецЕсли;	
	КонецЦикла;	
	
	Закрыть(МассивВозврат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПричинаСписанияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	Элемент.СписокВыбора.Добавить("Поломка оборудования");
	
КонецПроцедуры
