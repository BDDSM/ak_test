
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	НастройкаОтраженияОперацийВУчетеСрезПоследних.ЦФО КАК ЦФО,
	|	НастройкаОтраженияОперацийВУчетеСрезПоследних.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	НастройкаОтраженияОперацийВУчетеСрезПоследних.Счет КАК Счет
	|ИЗ
	|	РегистрСведений.НастройкаОтраженияОперацийВУчете.СрезПоследних(&Дата, ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.Резервы_СписаниеДепозитов)) КАК НастройкаОтраженияОперацийВУчетеСрезПоследних");
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	ВыбокаНастройки = Запрос.Выполнить().Выбрать();
	
	Если НЕ ВыбокаНастройки.Следующий() ИЛИ НЕ ЗначениеЗаполнено(ВыбокаНастройки.Счет) ИЛИ НЕ ЗначениеЗаполнено(ВыбокаНастройки.ЦФО) ИЛИ НЕ ЗначениеЗаполнено(ВыбокаНастройки.СтатьяДоходовРасходов)  Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнены настройки отражения в учете для операции резерва по депозитам!", Отказ);
		Возврат;
	КонецЕсли;   
	
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВложенныйЗапрос.Контрагент,
	                      |	ВложенныйЗапрос.ДоговорКонтрагента,
	                      |	ВложенныйЗапрос.Документ,
	                      |	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма
	                      |ПОМЕСТИТЬ ВТ_Документ
	                      |ИЗ
	                      |	(ВЫБРАТЬ
	                      |		РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Контрагент КАК Контрагент,
	                      |		РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.ДоговорКонтрагента КАК ДоговорКонтрагента,
	                      |		РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Документ КАК Документ,
	                      |		РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Сумма КАК Сумма
	                      |	ИЗ
	                      |		Документ.РезервПоДепозитамАренды.СписаниеНеиспользованногоРезерва КАК РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва
	                      |	ГДЕ
	                      |		РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Ссылка = &Ссылка
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ
	                      |		РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Контрагент,
	                      |		РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.ДоговорКонтрагента,
	                      |		РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Документ,
	                      |		РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Сумма
	                      |	ИЗ
	                      |		Документ.РезервПоДепозитамАренды.СписаниеБезвозвратныхДепозитов КАК РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов
	                      |	ГДЕ
	                      |		РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВложенныйЗапрос.Контрагент,
	                      |	ВложенныйЗапрос.ДоговорКонтрагента,
	                      |	ВложенныйЗапрос.Документ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Документ.Контрагент,
	                      |	ВТ_Документ.ДоговорКонтрагента,
	                      |	ВТ_Документ.Документ,
	                      |	ВТ_Документ.Сумма,
	                      |	ЕСТЬNULL(ФинансовыйОстатки.СуммаОстатокКт, 0) КАК ОстатокПоСчету
	                      |ИЗ
	                      |	ВТ_Документ КАК ВТ_Документ
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Финансовый.Остатки(&Дата, Счет = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.РезервНаСписаниеДепозитов), , Организация = &Организация) КАК ФинансовыйОстатки
	                      |		ПО ВТ_Документ.Контрагент = ФинансовыйОстатки.Субконто1
	                      |			И ВТ_Документ.ДоговорКонтрагента = ФинансовыйОстатки.Субконто2
	                      |			И ВТ_Документ.Документ = ФинансовыйОстатки.Субконто3
	                      |ГДЕ
	                      |	ВТ_Документ.Сумма > ЕСТЬNULL(ФинансовыйОстатки.СуммаОстатокКт, 0)");
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначения.СообщитьОбОшибке("Контрагент: " + Выборка.Контрагент + ". " + Выборка.Документ + ". Не хватает остатка резерва для списания. Сумма по документу: " + Формат(Выборка.Сумма,"ЧДЦ=2; ЧН=") + ". Остаток резерва: " + Формат(Выборка.ОстатокПоСчету,"ЧДЦ=2; ЧН="),  Отказ);		
	КонецЦикла;   
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	//  
	Движения.Финансовый.Записывать = Истина;
	Движения.Финансовый.Очистить();
	
	Запрос.Текст = "ВЫБРАТЬ
	|	РезервПоДепозитамАрендыНачислениеРезерва.Контрагент,
	|	РезервПоДепозитамАрендыНачислениеРезерва.ДоговорКонтрагента,
	|	РезервПоДепозитамАрендыНачислениеРезерва.Документ,
	|	РезервПоДепозитамАрендыНачислениеРезерва.Документ.ОбъектАренды.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	РезервПоДепозитамАрендыНачислениеРезерва.Сумма
	|ИЗ
	|	Документ.РезервПоДепозитамАренды.НачислениеРезерва КАК РезервПоДепозитамАрендыНачислениеРезерва
	|ГДЕ
	|	РезервПоДепозитамАрендыНачислениеРезерва.Ссылка = &Ссылка";
	
	ТаблицаНачислениеРезерва = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из ТаблицаНачислениеРезерва Цикл
		Проводка = Движения.Финансовый.Добавить();
		Проводка.Организация = Организация;
		Проводка.Период = Дата;
		
		Проводка.СчетДт = ВыбокаНастройки.Счет;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Строка.СтруктурнаяЕдиница);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, ВыбокаНастройки.СтатьяДоходовРасходов);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, ВыбокаНастройки.ЦФО);
		
		Проводка.СчетКт = ПланыСчетов.Финансовый.РезервНаСписаниеДепозитов;                                                                                    
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Строка.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, Строка.ДоговорКонтрагента);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, Строка.Документ);
		
		Проводка.Сумма = Строка.Сумма;
		Проводка.Содержание = "Начисление резерва по депозитам аренды";		
	КонецЦикла;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Контрагент,
	               |	РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.ДоговорКонтрагента,
	               |	РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Документ,
	               |	РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Документ.ОбъектАренды.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	               |	РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Сумма
	               |ИЗ
	               |	Документ.РезервПоДепозитамАренды.СписаниеНеиспользованногоРезерва КАК РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва
	               |ГДЕ
	               |	РезервПоДепозитамАрендыСписаниеНеиспользованногоРезерва.Ссылка = &Ссылка";
	
	ТаблицаСписаниеНеиспользованногоРезерва = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из ТаблицаСписаниеНеиспользованногоРезерва Цикл
		Проводка = Движения.Финансовый.Добавить();
		Проводка.Организация = Организация;
		Проводка.Период = Дата;
		
		Проводка.СчетДт = ПланыСчетов.Финансовый.РезервНаСписаниеДепозитов;                                                                                    
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Строка.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, Строка.ДоговорКонтрагента);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, Строка.Документ);
		
		Проводка.СчетКт = ВыбокаНастройки.Счет;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Строка.СтруктурнаяЕдиница);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, ВыбокаНастройки.СтатьяДоходовРасходов);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, ВыбокаНастройки.ЦФО);
		
		Проводка.Сумма = Строка.Сумма;
		Проводка.Содержание = "Списание неиспользованного резерва по депозитам аренды";		
	КонецЦикла;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Контрагент,
	               |	РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.ДоговорКонтрагента,
	               |	РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Документ,
	               |	РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Документ.ОбъектАренды.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	               |	РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Сумма
	               |ИЗ
	               |	Документ.РезервПоДепозитамАренды.СписаниеБезвозвратныхДепозитов КАК РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов
	               |ГДЕ
	               |	РезервПоДепозитамАрендыСписаниеБезвозвратныхДепозитов.Ссылка = &Ссылка";
	
	ТаблицаСписаниеБезвозвратныхДепозитов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из ТаблицаСписаниеБезвозвратныхДепозитов Цикл
		Проводка = Движения.Финансовый.Добавить();
		Проводка.Организация = Организация;
		Проводка.Период = Дата;
		
		Проводка.СчетДт = ПланыСчетов.Финансовый.РезервНаСписаниеДепозитов;                                                                                    
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Строка.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, Строка.ДоговорКонтрагента);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, Строка.Документ);
		
		Проводка.СчетКт = ПланыСчетов.Финансовый.ОбеспечительныйВзнос;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Организация);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, Строка.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, Строка.СтруктурнаяЕдиница);
		
		Проводка.Сумма = Строка.Сумма;
		Проводка.Содержание = "Списание безвозвратного депозита в счет резерва";		
	КонецЦикла;
	
КонецПроцедуры




