
//+++АК SHEP 2018.08.31 ИП-00019388 Создана новая форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураДляЗаполнения = Новый Структура("ДокументОснование,Магазин");
	ЗаполнитьЗначенияСвойств(СтруктураДляЗаполнения, Параметры);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураДляЗаполнения);
	
	ВремТаб = "";
	Если Параметры.Свойство("ТоварыДляЗаполнения", ВремТаб) Тогда
		Для Каждого СтрокаТЧ Из ВремТаб Цикл
			ЗаполнитьЗначенияСвойств(ТоварыДляЗаполнения.Добавить(), СтрокаТЧ);
		КонецЦикла;
		
		СписокВыбораНоменклатуры = Элементы.Номенклатура.СписокВыбора;
		Для Каждого СтрокаНоменклатуры Из ТоварыДляЗаполнения Цикл
			СписокВыбораНоменклатуры.Добавить(СтрокаНоменклатуры.Номенклатура);
		КонецЦикла;
		
		Если ТоварыДляЗаполнения.Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(ЭтаФорма, ТоварыДляЗаполнения[0]);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ОтветственныеЛица", ВремТаб) Тогда
		Для Каждого СтрокаТЧ Из ВремТаб Цикл
			ЗаполнитьЗначенияСвойств(ОтветственныеЛица.Добавить(), СтрокаТЧ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	
	ДатаОтправки = ТекущаяДата();
	Если НЕ ПроверитьЗаполнение() Тогда Возврат; КонецЕсли;
	
	СтруктураВозврата = Новый Структура("ДокументОснование,Магазин,Номенклатура,УИДСтрокиНоменклатуры,Количество,ДатаОтправки,ТекстСообщения,ОтветственныеЛица");
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, ЭтаФорма);
	ОтправитьСообщениеНаСервере(СтруктураВозврата);
	СтруктураВозврата.Удалить("ДокументОснование");
	СтруктураВозврата.Удалить("Магазин");
	СтруктураВозврата.Удалить("ОтветственныеЛица");
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьСообщениеНаСервере(СтруктураДанные)
	
	ТемаСообщения = "Корректировки по " + ПредставлениеДокументаБезВремени(СтруктураДанные.ДокументОснование) + ": " + СтруктураДанные.Номенклатура;
	
	ДанныеМагазина = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктураДанные.Магазин, "Наименование,ТелефонныйНомер1,АдресЭлектроннойПочты");
	ТекстСообщения = "
	|Номенклатура: " + СтруктураДанные.Номенклатура + "
	|Количество: " + СтруктураДанные.Количество + "
	|
	|" + СтруктураДанные.ТекстСообщения + "
	|
	|С уважением, персонал магазина
	|" + ДанныеМагазина["Наименование"] + "
	|тел.: " + ДанныеМагазина["ТелефонныйНомер1"] + "
	|эл. почта: " + ДанныеМагазина["АдресЭлектроннойПочты"];
	
	МассивАдресовДляОтправки = Новый Массив;
	//+++АК SHEP 2018.12.06 ИП-00020580: если есть помощник, отправляем ему; иначе — технологу
	//МассивСтрокТЗн = СтруктураДанные["ОтветственныеЛица"].НайтиСтроки(Новый Структура("ТипРолиСтр", "Технолог по качеству"));
	СтруктураОтбора = Новый Структура("ТипРолиСтр,Помощник", "Технолог по качеству", Истина);
	МассивСтрокТЗн = СтруктураДанные["ОтветственныеЛица"].НайтиСтроки(СтруктураОтбора);
	Если МассивСтрокТЗн.Количество() = 0 Тогда
		СтруктураОтбора.Удалить("Помощник");
		МассивСтрокТЗн = СтруктураДанные["ОтветственныеЛица"].НайтиСтроки(СтруктураОтбора);
	КонецЕсли;
	//---АК SHEP 2018.12.06
	
	Для Каждого СтрокаТЗн Из МассивСтрокТЗн Цикл
		МассивАдресовДляОтправки.Добавить(СтрокаТЗн.АдресЭлПочты);
	КонецЦикла;
	
	Документы.ЗаданиеТехнологаМагазинам.ОтправитьСообщение(ТемаСообщения, ТекстСообщения, МассивАдресовДляОтправки, "Корректировки заданий",, ДанныеМагазина["АдресЭлектроннойПочты"]);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеДокументаБезВремени(ДокПредставление)
	
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(ДокПредставление), " ", Истина, Истина);
	МассивПодстрок.Удалить(МассивПодстрок.ВГраница()); // удаляем из массива последнее значение — время
	Возврат СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивПодстрок, " ");
	
КонецФункции
