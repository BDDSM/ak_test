
&НаКлиенте
Процедура ОтборУтверждающийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокПользователей = Новый СписокЗначений;
	ОтборУтверждающийНачалоВыбораСервер(СписокПользователей);
	Отбор = Новый Структура("Ссылка", СписокПользователей);
	ПараметрыОткрытия = Новый Структура("Отбор", Отбор);
	
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыОткрытия, Элемент);

КонецПроцедуры

&НаСервере
Процедура ОтборУтверждающийНачалоВыбораСервер(СписокПользователей)
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПраваПользователейНаРедактированиеГрафиков.Пользователь
	|ИЗ
	|	РегистрСведений.ПраваПользователейНаРедактированиеГрафиков КАК ПраваПользователейНаРедактированиеГрафиков");
	
	МассивПользователей = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
	//СписокПользователей = Новый СписокЗначений;
	СписокПользователей.ЗагрузитьЗначения(МассивПользователей);
	
	
КонецПроцедуры	
&НаКлиенте
Процедура ОтборУтверждающийПриИзменении(Элемент)
	
	УстановитьОтборы();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//+++АК ILIK 2018.07.06 ИП-00019079
	Если УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РазрешитьРедактированиеПремийТолькоПоСебе, Ложь) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
		Элементы.ОтборОтветственный.ТолькоПросмотр = Истина;
	//Если ЭтоРуководительПодразделения() Тогда
	ИначеЕсли ЭтоРуководительПодразделения() Тогда
	//---АК ILIK
		Утверждающий=ПараметрыСеанса.ТекущийПользователь;
		Элементы.ОтборУтверждающий.ТолькоПросмотр=Истина;
	Иначе
		Ответственный=ПараметрыСеанса.ТекущийПользователь;
		Элементы.ОтборОтветственный.ТолькоПросмотр=Истина;
	КонецЕсли;
	
	Если РольДоступна("ПолныеПрава") Тогда
		Элементы.ОтборУтверждающий.ТолькоПросмотр=Ложь;
		//+++АК ILIK 2018.10.01 ИП-00019987 
		//Элементы.ОтборОтветственный.ТолькоПросмотр=Ложь;
		//---АК ILIK
	КонецЕсли;
	
	Если УправлениеДопПравамиПользователей.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РазрешитьРедактированиеПремийДругихОтветственных, Ложь) тогда
		Элементы.ОтборУтверждающий.ТолькоПросмотр=Ложь;
		Элементы.ОтборОтветственный.ТолькоПросмотр=Ложь;
	КонецЕсли;
	
	УстановитьОтборы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборы()
		
	Список.Параметры.УстановитьЗначениеПараметра("Утверждающий", Утверждающий);
	Список.Параметры.УстановитьЗначениеПараметра("ЕстьОтборПоУтверждающему", ЗначениеЗаполнено(Утверждающий));
	
	Список.Параметры.УстановитьЗначениеПараметра("Ответственный", Ответственный);
	Список.Параметры.УстановитьЗначениеПараметра("ЕстьОтборПоОтветственному", ЗначениеЗаполнено(Ответственный));
	
	Список.Параметры.УстановитьЗначениеПараметра("НачалоМесяца", НачалоМесяца(Месяц));
	Список.Параметры.УстановитьЗначениеПараметра("КонецМесяца", КонецМесяца(Месяц));
	Список.Параметры.УстановитьЗначениеПараметра("ЕстьОтборДате", ЗначениеЗаполнено(Месяц));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцОчистка(Элемент, СтандартнаяОбработка)
	
	Месяц = Дата(1,1,1);
	УстановитьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачальноеЗначение = ?(ЗначениеЗаполнено(Месяц), Месяц, НачалоМесяца(ТекущаяДата()));
	
	СписокВыбора = Новый СписокЗначений;
	НачалоТекущегоГода = НачалоГода(НачальноеЗначение);
	НачалоПрошлогоГода = НачалоГода(НачалоТекущегоГода - 1);
	СписокВыбора.Добавить(НачалоПрошлогоГода, (Формат(НачалоПрошлогоГода, "ДФ='yyyy'") + "..."));
	НачалоМесяцаЗаполнения = НачалоТекущегоГода;
	ЭлементПоУмолчанию = Неопределено;
	Для а = 1 По 12 Цикл
		ДобавленныйЭлемент = СписокВыбора.Добавить(НачалоМесяцаЗаполнения,  Формат(НачалоМесяцаЗаполнения, "ДФ='ММММ гггг'"));
		Если НачальноеЗначение = НачалоМесяцаЗаполнения Тогда
			ЭлементПоУмолчанию = ДобавленныйЭлемент;
		КонецЕсли; 
		НачалоМесяцаЗаполнения = ДобавитьМесяц(НачалоМесяцаЗаполнения, 1);
	КонецЦикла;
	НачалоСледующегоГода = КонецГода(НачалоТекущегоГода) + 1;
	СписокВыбора.Добавить(НачалоСледующегоГода, (Формат(НачалоСледующегоГода, "ДФ='yyyy'") + "..."));
	
	ВыбранныйЭлемент = ЭтаФорма.ВыбратьИзСписка(СписокВыбора, Элемент, ЭлементПоУмолчанию);
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Месяц = ВыбранныйЭлемент.Значение;
	МесяцСтрокой  = Формат(Месяц, "ДФ='ММММ гггг'");
	
	УстановитьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМесяцОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ЭтоРуководительПодразделения()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПраваПользователейНаРедактированиеГрафиков.Подразделение
	               |ИЗ
	               |	РегистрСведений.ПраваПользователейНаРедактированиеГрафиков КАК ПраваПользователейНаРедактированиеГрафиков
	               |ГДЕ
	               |	ПраваПользователейНаРедактированиеГрафиков.Пользователь = &Пользователь";
				   
	Результат = Запрос.Выполнить();
	Возврат (НЕ Результат.Пустой());
	
КонецФункции

&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	УстановитьОтборы();
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоПремиям(Команда)
	ОткрытьФорму("Отчет.Премии.Форма.ФормаОтчета");
КонецПроцедуры
