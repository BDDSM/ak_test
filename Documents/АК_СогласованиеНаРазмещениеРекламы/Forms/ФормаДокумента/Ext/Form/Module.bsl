
&НаСервереБезКонтекста
Функция СохранитьФотоНаСервере(Адрес, ИмяФайла, КаталогИзображений)
	
	ДвДанные = ПолучитьИзВременногоХранилища(Адрес);
	
	ИмяФайла = "" + Новый УникальныйИдентификатор() + "_" + ИмяФайла;
	
	ДвДанные.Записать(КаталогИзображений + ИмяФайла);
	
	Возврат ИмяФайла;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//+++АК sils 07.06.2018 ИП-00018876
	ОперацияАпдекс = APDEX_ОценкаПроизводительностиКлиентСервер.ПолучитьОперацию("Открытие документа Согласование на размещение рекламы");
	APDEX_ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(ОперацияАпдекс);
	//---АК
		
	КонстантаКаталог = Константы.КаталогХраненияФайловКартинок.Получить();
	Если Прав(КонстантаКаталог, 1) <> "\" Тогда
		КонстантаКаталог = КонстантаКаталог + "\";
	КонецЕсли;
	
	//каталог хранения изображений
	КаталогИзображений = КонстантаКаталог + "РекламныеМатериалы\";
	
	ОбновитьИзображенияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИзображенияНаСервере()
	
	Для Каждого Строка Из Объект.ДизайнПроект Цикл
		Строка.Картинка = ПолучитьКартинкуСервер(КаталогИзображений + Строка.ИмяФайла);
		Строка.ОтображаемоеИмяФайла = Прав(Строка.ИмяФайла, СтрДлина(Строка.ИмяФайла) - Найти(Строка.ИмяФайла, "_"));
	КонецЦикла;
	
	Для Каждого Строка Из Объект.ГотовыйРезультат Цикл
		Строка.Картинка = ПолучитьКартинкуСервер(КаталогИзображений + Строка.ИмяФайла);
		Строка.ОтображаемоеИмяФайла = Прав(Строка.ИмяФайла, СтрДлина(Строка.ИмяФайла) - Найти(Строка.ИмяФайла, "_"));
	КонецЦикла;
	
	Для Каждого Строка Из Объект.СканыДокументаОснования Цикл
		Строка.Картинка = ПолучитьКартинкуСервер(КаталогИзображений + Строка.ИмяФайла);
		Строка.ОтображаемоеИмяФайла = Прав(Строка.ИмяФайла, СтрДлина(Строка.ИмяФайла) - Найти(Строка.ИмяФайла, "_"));
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКартинкуСервер(ПолноеИмяФайла)
	
	Файл = Новый Файл(ПолноеИмяФайла);
	РасширениеФайла = ПолучитьРасширениеФайла(ПолноеИмяФайла);
	Если Файл.Существует()
		И (РасширениеФайла = "bmp" ИЛИ РасширениеФайла = "jpg" ИЛИ РасширениеФайла = "gif" ИЛИ РасширениеФайла = "png") Тогда
		Возврат Новый Картинка(ПолноеИмяФайла);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьРасширениеФайла(ИмяФайла)

	ПозицияПоследнейТочки = 0;
	РасширениеФайла = ИмяФайла;

	Пока 1 = 1 Цикл	
		ПозицияПоследнейТочки = Найти(РасширениеФайла, ".");

		Если ПозицияПоследнейТочки = 0 Тогда
			Прервать;
		Иначе
			РасширениеФайла = Сред(РасширениеФайла, ПозицияПоследнейТочки + 1)
		КонецЕсли;
	КонецЦикла;

	Возврат ?(РасширениеФайла = ИмяФайла, "", РасширениеФайла);

КонецФункции 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступностьРеквизитов();
	
	//+++АК sils 07.06.2018 ИП-00018876
	APDEX_ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(ОперацияАпдекс, ?(Параметры.Ключ.Пустая(), "Новый документ", "" + Объект.Ссылка));
	//---АК
КонецПроцедуры
	
&НаКлиенте
Процедура БессрочноеПриИзменении(Элемент)
	
	ОбновитьДоступностьРеквизитов();
	
	Если Объект.Бессрочное Тогда
		Объект.СрокНачало = Дата('00010101');
		Объект.СрокКонец = Дата('00010101');
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьРеквизитов()
	
	Элементы.ГруппаСрокДействия.Доступность = НЕ Объект.Бессрочное;
	
КонецПроцедуры

&НаКлиенте
Процедура ДизайнПроектПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ПолноеИмяФайла = "";
	АдресХранилища = "";
	Если ПоместитьФайл(АдресХранилища,, ПолноеИмяФайла) Тогда
	
		мас = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ПолноеИмяФайла, "\");
	
		ИмяФайла = СохранитьФотоНаСервере(АдресХранилища, мас[мас.Количество()-1], КаталогИзображений);
	
			
		НовСтрока = Объект[Элемент.Имя].Добавить();
		НовСтрока.ИмяФайла = ИмяФайла;
		
		ОбновитьИзображенияНаСервере();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДизайнПроектПередУдалением(Элемент, Отказ)
	
	Ответ = Вопрос("Удалить файл?", РежимДиалогаВопрос.ДаНет);
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Попытка
			УдалитьФайлы(КаталогИзображений, Элемент.ТекущиеДанные.ИмяФайла);
		Исключение
			Отказ = Истина;
		КонецПопытки;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбновитьИзображенияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДизайнПроектВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	пИмяФайла = КаталогИзображений + Элемент.ТекущиеДанные.ИмяФайла;
	Попытка
		ЗапуститьПриложение(пИмяФайла);
	Исключение
		Картинка = Новый Картинка(пИмяФайла);
		АдресКартинки = ПоместитьВоВременноеХранилище(Картинка, УникальныйИдентификатор);
		ПараметрыФормы = Новый Структура("КартинкаСсылка", АдресКартинки); 
		ОткрытьФорму("ОбщаяФорма.Фото", ПараметрыФормы);	
	КонецПопытки;
	
КонецПроцедуры


