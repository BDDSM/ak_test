
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не РольДоступна("ПолныеПрава") Тогда
		УстановитьОтборСвоихОбещаний();
	КонецЕсли;
	УстановитьОтборСнятыхОбещаний();
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСвоихОбещаний()
	Если Не РольДоступна("ПолныеПрава") Тогда
		Кол=Список.Отбор.Элементы.Количество();
		Для Сч=0 По Кол-1 Цикл
			Если Список.Отбор.Элементы[Кол-1-Сч].РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
				Список.Отбор.Элементы.Удалить(Кол-1-Сч);	
			КонецЕсли; 
		КонецЦикла; 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбещаниеДок.Ссылка
		|ИЗ
		|	Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРоли
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.ТипыРолей КАК РолиПользователейТипыРолей
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Обещание КАК ОбещаниеДок
		|			ПО (ВЫБОР
		|					КОГДА ОбещаниеДок.Заказчик ССЫЛКА ПланВидовХарактеристик.ТипыРолейПользователя
		|							ИЛИ ОбещаниеДок.Исполнитель ССЫЛКА ПланВидовХарактеристик.ТипыРолейПользователя
		|						ТОГДА ОбещаниеДок.Заказчик ССЫЛКА ПланВидовХарактеристик.ТипыРолейПользователя
		|									И ОбещаниеДок.Заказчик = РолиПользователейТипыРолей.ТипРоли
		|								ИЛИ ОбещаниеДок.Исполнитель ССЫЛКА ПланВидовХарактеристик.ТипыРолейПользователя
		|									И ОбещаниеДок.Исполнитель = РолиПользователейТипыРолей.ТипРоли
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ)
		|		ПО РолиПользователейСоставРоли.Ссылка = РолиПользователейТипыРолей.Ссылка
		|ГДЕ
		|	РолиПользователейСоставРоли.Сотрудник = &Сотрудник
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ОбещаниеДок.Ссылка
		|ИЗ
		|	Документ.Обещание КАК ОбещаниеДок
		|ГДЕ
		|	(ОбещаниеДок.Заказчик = &Сотрудник
		|			ИЛИ ОбещаниеДок.Исполнитель = &Сотрудник)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ОбещаниеДок.Ссылка
		|ИЗ
		|	Документ.Обещание КАК ОбещаниеДок
		|ГДЕ
		|	(ОбещаниеДок.Заказчик = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ТипыРолейПользователя.ВсеСотрудники)
		|			ИЛИ ОбещаниеДок.Исполнитель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ТипыРолейПользователя.ВсеСотрудники)
		|			ИЛИ &ЭтоПродавец
		|				И (ОбещаниеДок.Заказчик = &РольПродавца
		|					ИЛИ ОбещаниеДок.Заказчик = &РольСтаршегоПродавца
		|					ИЛИ ОбещаниеДок.Исполнитель = &РольПродавца
		|					ИЛИ ОбещаниеДок.Исполнитель = &РольСтаршегоПродавца))";
		
		
		Запрос.УстановитьПараметр("Сотрудник", глЗначениеПеременной("глТекущийПользователь").Физлицо);
		Запрос.УстановитьПараметр("ЭтоПродавец", РольДоступна("Продавец"));
		Запрос.УстановитьПараметр("РольПродавца", ПланыВидовХарактеристик.ТипыРолейПользователя.НайтиПоНаименованию("Продавец-консультант"));
		Запрос.УстановитьПараметр("РольСтаршегоПродавца", ПланыВидовХарактеристик.ТипыРолейПользователя.НайтиПоНаименованию("Старший продавец-консультант"));
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		МасДок=Новый СписокЗначений;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МасДок.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбещаниеОбъектыПривязки.Характеристика,
		|	ОбещаниеОбъектыПривязки.Номенклатура,
		|	ОбещаниеОбъектыПривязки.СтруктурнаяЕдиница,
		|	ОбещаниеОбъектыПривязки.Ссылка,
		|	ЗначенияСвойствОбъектов.Значение КАК Производитель
		|ПОМЕСТИТЬ втОбещ
		|ИЗ
		|	Документ.Обещание.ОбъектыПривязки КАК ОбещаниеОбъектыПривязки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|		ПО (ЗначенияСвойствОбъектов.Объект = ОбещаниеОбъектыПривязки.Характеристика)
		|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовхарактеристик.СвойстваОбъектов.Производитель))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РолиПользователейСоставРоли.Сотрудник,
		|	РолиПользователейСоставРоли.Сотрудник.Наименование КАК СотрудникНаименование,
		|	РолиПользователейСоставРоли.Ссылка
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРоли
		|ГДЕ
		|	НЕ РолиПользователейСоставРоли.Ссылка.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоответствиеОбъектРольСрезПоследних1.Объект,
		|	СоответствиеОбъектРольСрезПоследних.ТипРоли
		|ПОМЕСТИТЬ втБрендМенеджеры
		|ИЗ
		|	РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&Дата, ) КАК СоответствиеОбъектРольСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&Дата, ) КАК СоответствиеОбъектРольСрезПоследних1
		|		ПО СоответствиеОбъектРольСрезПоследних.Объект = СоответствиеОбъектРольСрезПоследних1.РольПользователя
		|ГДЕ
		|	СоответствиеОбъектРольСрезПоследних.Объект ССЫЛКА Справочник.РолиПользователей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбещаниеОбъектыПривязки.Ссылка
		|ИЗ
		|	вт КАК РолиПользователейСоставРоли
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.ТипыРолей КАК РолиПользователейТипыРолей
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбещ КАК ОбещаниеОбъектыПривязки
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(&Дата, ) КАК СоответствиеОбъектРольСрезПоследних
		|				ПО (СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.Номенклатура
		|						ИЛИ СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.Характеристика
		|						ИЛИ СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.СтруктурнаяЕдиница
		|						ИЛИ СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.Производитель)
		|					И ОбещаниеОбъектыПривязки.Ссылка.Исполнитель = СоответствиеОбъектРольСрезПоследних.ТипРоли
		|			ПО (ОбещаниеОбъектыПривязки.Ссылка.Исполнитель = РолиПользователейТипыРолей.ТипРоли)
		|		ПО РолиПользователейСоставРоли.Ссылка = РолиПользователейТипыРолей.Ссылка
		|ГДЕ
		|	РолиПользователейСоставРоли.Сотрудник = &Сотрудник
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбещаниеОбъектыПривязки.Ссылка
		|ИЗ
		|	вт КАК РолиПользователейСоставРоли
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.ТипыРолей КАК РолиПользователейТипыРолей
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбещ КАК ОбещаниеОбъектыПривязки
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ втБрендМенеджеры КАК СоответствиеОбъектРольСрезПоследних
		|				ПО (СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.Номенклатура
		|						ИЛИ СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.Характеристика
		|						ИЛИ СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.СтруктурнаяЕдиница
		|						ИЛИ СоответствиеОбъектРольСрезПоследних.Объект = ОбещаниеОбъектыПривязки.Производитель)
		|					И ОбещаниеОбъектыПривязки.Ссылка.Исполнитель = СоответствиеОбъектРольСрезПоследних.ТипРоли
		|			ПО (ОбещаниеОбъектыПривязки.Ссылка.Исполнитель = РолиПользователейТипыРолей.ТипРоли)
		|		ПО РолиПользователейСоставРоли.Ссылка = РолиПользователейТипыРолей.Ссылка
		|ГДЕ
		|	РолиПользователейСоставРоли.Сотрудник = &Сотрудник";
		
		Запрос.УстановитьПараметр("Дата", ТекущаяДата());
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МасДок.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Ссылка");   
		ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование  = Не Чужие;
		ЭлементОтбора.ПравоеЗначение = МасДок;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли; 
КонецПроцедуры	

&НаСервере
Процедура УстановитьОтборСнятыхОбещаний()
	Кол=Список.Отбор.Элементы.Количество();
	Для Сч=0 По Кол-1 Цикл
		Если Строка(Список.Отбор.Элементы[Кол-1-Сч].ЛевоеЗначение) = "Статус" Тогда
			Список.Отбор.Элементы.Удалить(Кол-1-Сч);	
		КонецЕсли; 
	КонецЦикла; 
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Статус");   
    ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
    ЭлементОтбора.Использование  = Не Устаревшие;
    ЭлементОтбора.ПравоеЗначение = Перечисления.СтатусОбещания.СнятоеОбещание;
    ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто;

КонецПроцедуры
 

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия="АК_ОбновитьСписокОбещаний"   Тогда
		УстановитьОтборСвоихОбещаний();
	КонецЕсли; 
КонецПроцедуры


&НаКлиенте
Процедура УстаревшиеПриИзменении(Элемент)
	УстановитьОтборСнятыхОбещаний();
КонецПроцедуры


&НаКлиенте
Процедура ЧужиеПриИзменении(Элемент)
	УстановитьОтборСвоихОбещаний();
КонецПроцедуры
 
