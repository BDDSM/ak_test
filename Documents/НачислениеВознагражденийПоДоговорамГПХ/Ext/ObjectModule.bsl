//+++АК sole 2018.09.19 ИП-00019532
Процедура ПроверитьМесяцИГодНачисления(Отказ);
	
	Если ЭтотОбъект.МесяцИГодНачисления = ЭтотОбъект.Ссылка.МесяцИГодНачисления Тогда
		Возврат;
	КонецЕсли;
	
	ТекДата = НачалоДня(ТекущаяДата());
	
	Если День(ТекДата) > 6  Тогда
		Граница = НачалоМесяца(ТекДата);
	Иначе
		Граница = НачалоМесяца(ДобавитьМесяц(ТекДата, -1));
	КонецЕсли;
	
	Если  ЭтотОбъект.МесяцИГодНачисления < Граница  Тогда
		Отказ = Истина;
		Сообщить("Начисления уже проведены, создание документа на выбранную дату начисления невозможно. Выберите другой месяц начисления.");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ЭтотОбъект.СуммаДокумента = ЭтотОбъект.Состав.Итог("Сумма");
	ВыплаченнаяСумма = Состав.Итог("ВыплаченнаяСумма");
	
	//+++АК sole 2018.09.19 ИП-00019785
	ПроверитьМесяцИГодНачисления(Отказ);
	
	Для Каждого Стр Из ЭтотОбъект.Состав Цикл
		
		Если ЗначениеЗаполнено(Стр.УИН) Тогда
			Продолжить;
		КонецЕсли;		
		
		Стр.УИН = Новый УникальныйИдентификатор();
		
	КонецЦикла;
	//---АК sole 2018.09.19 ИП-00019785
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.Утверждено_Удалить = Ложь;
	//+++ АК Pans 2017.11.09  ИП-00017174
	Для каждого СтрокаТЧ1 Из Состав Цикл
		СтрокаТЧ1.ВыплаченнаяСумма = 0;
		СтрокаТЧ1.ДатаВыплаты = Неопределено;
	КонецЦикла;
	//--- АК Pans 2017.11.09  ИП-00017174
	
КонецПроцедуры

//+++АК sole 2018.10.02 ИП-00019785.02
Процедура ОтправитьНаСогласование() Экспорт
	
	БПР = Новый Структура();
	ПолучитьАдресСогласующего(БПР);
	СформироватьТекстПисьма(БПР);
	
	УчетнаяЗапись = ОбщиеПроцедуры.ПолучитьУчетнуюЗаписьДляРассылки();
	СписокКому = Новый СписокЗначений();
	
	Акцептант = ЭтотОбъект.Утверждающий;
	ID_MESSAGE = Новый УникальныйИдентификатор;
	TYPE_MESSAGE = 12;
	GUID_Заявки = Строка(ЭтотОбъект.Ссылка.УникальныйИдентификатор());
	Объект_Ссылка = ЭтотОбъект.Ссылка;
	
	УправлениеЭлектроннойПочтой.ОтправитьЗаявкуНаПодтверждение
		(
			БПР.Тема, 
			БПР.ТекстПисьма,
			ЭтотОбъект.Номер,
			СписокКому,
			GUID_Заявки,
			ID_MESSAGE,
			TYPE_MESSAGE,
			БПР.АдресЭлектроннойПочты,
			Акцептант,
			УчетнаяЗапись,
			Объект_Ссылка,
			Истина
		);
	
КонецПроцедуры

//+++АК sole 2018.10.02 ИП-00019785.02
Процедура ПолучитьАдресСогласующего(БПР)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
"ВЫБРАТЬ
|	КонтактнаяИнформация.Представление КАК АдресЭлектроннойПочты
|
|ИЗ Справочник.Пользователи КАК Пользователи
|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация ПО
|			КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)	
|		И	(
|						КонтактнаяИнформация.Объект = Пользователи.ФизЛицо
|					И	Пользователи.ФизЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
|				ИЛИ
|						КонтактнаяИнформация.Объект = Пользователи.Ссылка
|					И	Пользователи.ФизЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
|
|			)
|ГДЕ
|		Пользователи.Ссылка = &Ссылка
|";
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Утверждающий);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		АдресЭлектроннойПочты = Выборка.АдресЭлектроннойПочты;
	Иначе		
		АдресЭлектроннойПочты = "";	
	КонецЕсли;
	
	БПР.Вставить("АдресЭлектроннойПочты", АдресЭлектроннойПочты);
	
КонецПроцедуры

//+++АК sole 2018.10.02 ИП-00019785.02
Процедура СформироватьТекстПисьма(БПР)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
"ВЫБРАТЬ
|	НачислениеВознагражденийПоДоговорамГПХСостав.ФизЛицо.Наименование КАК ФИО,
|	НачислениеВознагражденийПоДоговорамГПХСостав.СтатьяДР.Наименование КАК СтатьяДР,
|	НачислениеВознагражденийПоДоговорамГПХСостав.Сумма,
|	НачислениеВознагражденийПоДоговорамГПХСостав.ПредметДоговора.Наименование КАК ПредметДоговора,
|	НачислениеВознагражденийПоДоговорамГПХСостав.Комментарий
|
|ИЗ Документ.НачислениеВознагражденийПоДоговорамГПХ.Состав КАК НачислениеВознагражденийПоДоговорамГПХСостав	
|ГДЕ
|		НачислениеВознагражденийПоДоговорамГПХСостав.Ссылка = &Ссылка
|";	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Таблица = "<tr><td>ФИО</td><td>Статья ДР</td><td>Сумма</td><td>Предмет договора</td><td>Комментарий</td></tr>";
		
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = "<tr><td>" +
		Выборка.ФИО + "</td><td>" +
		Выборка.СтатьяДР + "</td><td>" +
		Выборка.Сумма + "</td><td>" +
		Выборка.ПредметДоговора + "</td><td>" +
		Выборка.Комментарий + "</td></tr>";
		
		Таблица = Таблица + СтрокаТаблицы;
	КонецЦикла; 
		
	ТекстПисьма =
"<br>Добрый день!
|<br>Необходимо согласовать документ ""Начисление вознаграждений по договорам ГПХ"" № ""&НомерДокумента"" от ""&ДатаДокумента"".
|<p>
|<table border=""1"">
|&Таблица
|</table>
|</p>
|";
	ДатаДокумента = Формат(ЭтотОбъект.Дата,"ДФ=dd.MM.yyyy");
	НомерДокумента = СокрЛП(ЭтотОбъект.Номер);
	
	ТекстПисьма = СтрЗаменить(ТекстПисьма,"&НомерДокумента", НомерДокумента);
	ТекстПисьма = СтрЗаменить(ТекстПисьма,"&ДатаДокумента", ДатаДокумента);
	ТекстПисьма = СтрЗаменить(ТекстПисьма,"&Таблица", Таблица);
	
	Тема = "Начисление вознаграждений по договорам ГПХ №" + НомерДокумента + " от " + ДатаДокумента;	
	
	БПР.Вставить("ТекстПисьма", ТекстПисьма);
	БПР.Вставить("Тема", Тема);
КонецПроцедуры
//
