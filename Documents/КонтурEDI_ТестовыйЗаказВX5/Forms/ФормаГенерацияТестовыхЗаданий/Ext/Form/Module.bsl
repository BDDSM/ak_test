
&НаКлиенте
Процедура СоздатьТестовыеЗаказы(Команда)
	Если Магазины.Количество() > 0 И Товары.Количество() > 0  Тогда
		СоздатьТестовыеЗаказыНаСервере();	
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура СоздатьТестовыеЗаказыНаСервере()
	ОсновнойМодуль = Обработки.КонтурEDI.Создать(); 
	ОсновнойМодуль.ИнициализироватьПодключаемыеМодули();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЗМагазины.Выбран КАК Выбран,
	               |	ТЗМагазины.Магазин КАК Магазин
	               |ПОМЕСТИТЬ ВТ_Магазины
	               |ИЗ
	               |	&ТЗМагазины КАК ТЗМагазины
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗТовары.Номенклатура КАК Номенклатура,
	               |	ТЗТовары.Характеристика КАК Характеристика
	               |ПОМЕСТИТЬ ВТ_Товары
	               |ИЗ
	               |	&ТЗТовары КАК ТЗТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Магазины.Магазин КАК ТочкаДоставки,
	               |	СтруктурныеЕдиницы.КонтрагентСтороннейРозницы КАК Контрагент,
	               |	&Организация,
	               |	МАКСИМУМ(ДоговорыКонтрагентов.Ссылка) КАК ДоговорКонтрагента,
	               |	ЛОЖЬ КАК СуммаВключаетНДС
	               |ИЗ
	               |	ВТ_Магазины КАК ВТ_Магазины
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |			ПО СтруктурныеЕдиницы.КонтрагентСтороннейРозницы = ДоговорыКонтрагентов.Владелец
	               |				И (ДоговорыКонтрагентов.Организация = &Организация)
	               |		ПО ВТ_Магазины.Магазин = СтруктурныеЕдиницы.Ссылка
	               |ГДЕ
	               |	ВТ_Магазины.Выбран = ИСТИНА
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Магазины.Магазин,
	               |	СтруктурныеЕдиницы.КонтрагентСтороннейРозницы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Товары.Номенклатура,
	               |	ВТ_Товары.Характеристика,
	               |	Номенклатура1.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	               |	1 КАК Коэффициент,
	               |	1 КАК Количество,
	               |	1 КАК Цена,
	               |	1 КАК Сумма,
	               |	&СтавкаНДС,
	               |	0 КАК СуммаНДС,
	               |	&ДатаПроизводства
	               |ИЗ
	               |	ВТ_Товары КАК ВТ_Товары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1
	               |		ПО ВТ_Товары.Номенклатура = Номенклатура1.Ссылка";
	Запрос.УстановитьПараметр("ТЗМагазины", Магазины.Выгрузить());
	Запрос.УстановитьПараметр("ТЗТовары", Товары.Выгрузить());
	Запрос.УстановитьПараметр("Организация", Справочники.Организации.НайтиПоНаименованию("Тилси"));
	Запрос.УстановитьПараметр("СтавкаНДС", ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0"));
	Запрос.УстановитьПараметр("ДатаПроизводства", НачалоДня(ТекущаяДата()-3600*24));
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаМагазины = МассивРезультатов[2].Выбрать();
	ВыборкаТовары = МассивРезультатов[3].Выбрать();
	
	Пока ВыборкаМагазины.Следующий() Цикл
		ВыборкаТовары.Сбросить();
		Док = Документы.КонтурEDI_ТестовыйЗаказВX5.СоздатьДокумент();
		Док.Дата = ТекущаяДата();
		ЗаполнитьЗначенияСвойств(Док, ВыборкаМагазины);
		Пока ВыборкаТовары.Следующий() Цикл
			НоваяСтрока = Док.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
		КонецЦикла;
		Док.Записать(РежимЗаписиДокумента.Проведение);
		
		Сообщение = ОсновнойМодуль.ПодготовитьИсходящееСообщение("PORDERS", Док.Ссылка);  
		Если НЕ (Сообщение.СодержитОшибки = Истина) Тогда
			ПараметрыОтправки = Новый Структура();
			ПараметрыОтправки.Вставить("ОтправитьСообщениеИзФормы",	Истина);
			ПараметрыОтправки.Вставить("Сообщение",	Сообщение);
			ОсновнойМодуль.ОтправитьСообщение("PORDERS",Док.Ссылка,ПараметрыОтправки);
			ОсновнойМодуль.Сообщить_КонтурEDI("Создано тестовое сообщение: PORDERS");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьТаблицуМагазинов();
	ОтборПоТипуМагазина = Неопределено;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуМагазинов()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЛОЖЬ КАК Выбран,
	               |	СтруктурныеЕдиницы.Ссылка КАК Магазин,
	               |	СтруктурныеЕдиницы.ТипРозничнойТочки КАК Тип,
	               |	СтруктурныеЕдиницы.СтатусТорговойТочки КАК Статус,
	               |	СтруктурныеЕдиницы.ФорматРозничнойТочки КАК Формат
	               |ИЗ
	               |	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	               |ГДЕ
	               |	СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы = &ТипСтруктурнойЕдиницы
	               |	И СтруктурныеЕдиницы.ТипРозничнойТочки <> &ТипРозничнойТочкиВкусомат
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Тип,
	               |	Магазин";
	Запрос.УстановитьПараметр("ТипСтруктурнойЕдиницы", ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.СторонняяРозница"));
	Запрос.УстановитьПараметр("ТипРозничнойТочкиВкусомат", ПредопределенноеЗначение("Перечисление.ТипыРозничныхТочек.Вкусомат"));
	ТЗ_Магазины = Запрос.Выполнить().Выгрузить();
	Магазины.Загрузить(ТЗ_Магазины);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсеМагазины(Команда)
	Для Каждого Стр Из Магазины Цикл	
		Если Стр.Тип = ОтборПоТипуМагазина ИЛИ НЕ ЗначениеЗаполнено(ОтборПоТипуМагазина) Тогда
			Стр.Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВыборМагазинов(Команда)
	Для Каждого Стр Из Магазины Цикл	
		Если Стр.Тип = ОтборПоТипуМагазина ИЛИ НЕ ЗначениеЗаполнено(ОтборПоТипуМагазина) Тогда
			Стр.Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоАссортименту(Команда)
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораТТ", ЭтаФорма);	
    ПоказатьВводЗначения(Оповещение,,"Введите значение",Тип("СправочникСсылка.СтруктурныеЕдиницы"));
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораТТ(Результат, Параметры) Экспорт	
    Если Не Результат = Неопределено Тогда
        ЗаполнитьТаблицуТоваровПоАссортиментуТТ(Результат);		
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТоваровПоАссортиментуТТ(ТорговаяТочка) 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТоварныйАссортиментТочекСрезПоследних.Период,
	               |	ТоварныйАссортиментТочекСрезПоследних.ТорговаяТочка,
	               |	ТоварныйАссортиментТочекСрезПоследних.Номенклатура,
	               |	ТоварныйАссортиментТочекСрезПоследних.Характеристика,
	               |	ТоварныйАссортиментТочекСрезПоследних.Выведена,
	               |	ТоварныйАссортиментТочекСрезПоследних.Запрещена,
	               |	ТоварныйАссортиментТочекСрезПоследних.id_TT,
	               |	ТоварныйАссортиментТочекСрезПоследних.ДатаСозданияЗаписи,
	               |	ТоварныйАссортиментТочекСрезПоследних.Автор,
	               |	ТоварныйАссортиментТочекСрезПоследних.Комментарий,
	               |	ТоварныйАссортиментТочекСрезПоследних.ИзПотерянных
	               |ИЗ
	               |	РегистрСведений.ТоварныйАссортиментТочек.СрезПоследних(
	               |			,
	               |			ТорговаяТочка = &ТорговаяТочка
	               |				И Выведена = ЛОЖЬ) КАК ТоварныйАссортиментТочекСрезПоследних";
	Запрос.УстановитьПараметр("ТорговаяТочка", ТорговаяТочка);
	Рез = Запрос.Выполнить();
	Если НЕ Рез.Пустой() Тогда
		ЭтаФорма.Товары.Загрузить(Рез.Выгрузить());	
	КонецЕсли;
КонецПроцедуры