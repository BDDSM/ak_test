
Функция ПечатьЗаданияНаФасовку(Ссылка) Экспорт
	
	ТабДокумент	= Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПараметрыПечати_ЗаданиеНаФасовку";
	
	Макет = Документы.ЗаданиеНаФасовку.ПолучитьМакет("ПФ_ЗаданиеНаФасовку");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ТекНомер = Ссылка.Номер;
	Пока Лев(ТекНомер, 1) = "0" Цикл
		ТекНомер = Сред(ТекНомер, 2);
	КонецЦикла;
    ОбластьМакета.Параметры.Номер 	= ТекНомер;
    ОбластьМакета.Параметры.Дата 	= Формат(Ссылка.Дата, "ДЛФ=Д");
    ОбластьМакета.Параметры.СкладОтправитель 	= ?(ЗначениеЗаполнено(Ссылка.СкладОтправитель)	, СокрЛП(Ссылка.СкладОтправитель.Наименование)	, "");
    ОбластьМакета.Параметры.СкладПолучатель 	= ?(ЗначениеЗаполнено(Ссылка.СкладПолучатель)	, СокрЛП(Ссылка.СкладПолучатель.Наименование)	, "");
	ТабДокумент.Вывести(ОбластьМакета);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТЧТовары.НомерСтроки,
	|	ТЧТовары.Номенклатура,
	|	ТЧТовары.Характеристика,
	|	ТЧТовары.ЕдиницаИзмерения,
	|	ТЧТовары.ДатаПроизводства,
	|	ТЧТовары.Количество
	|ИЗ
	|	Документ.ЗаданиеНаФасовку.Товары КАК ТЧТовары
	|ГДЕ
	|	ТЧТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТЧТовары.НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
 	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Пока Выборка.Следующий() Цикл

		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.НомерСтроки 		= Формат(Выборка.НомерСтроки		, "ЧГ=");
		ОбластьМакета.Параметры.ДатаПроизводства 	= Формат(Выборка.ДатаПроизводства	, "ДЛФ=Д");
    	ТабДокумент.Вывести(ОбластьМакета);

	КонецЦикла;
	
 	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(ОбластьМакета);

	ТабДокумент.АвтоМасштаб 		= Истина;
	ТабДокумент.ФиксацияСверху 		= 7;
	ТабДокумент.ОтображатьСетку 	= Ложь;
	ТабДокумент.Защита 				= Ложь;
	ТабДокумент.ТолькоПросмотр 		= Ложь;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДокумент;
	
КонецФункции