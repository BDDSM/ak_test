
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПредМаршрут = Неопределено;
	Для Каждого СтрокаТаб Из Параметры.ТаблицаКонфликтов Цикл
		Если ПредМаршрут <> СтрокаТаб.Маршрут Тогда
			СтрокаРодитель = ДеревоКонфликтов.ПолучитьЭлементы().Добавить();
			СтрокаРодитель.Представление = СтрокаТаб.МаршрутПредставление;
			СтрокаРодитель.Маршрут = СтрокаТаб.Маршрут;
		КонецЕсли;
		СтрокаДерево = СтрокаРодитель.ПолучитьЭлементы().Добавить();
		СтрокаДерево.Представление 		= СтрокаТаб.СтруктурнаяЕдиницаПредставление;
		СтрокаДерево.Маршрут 			= СтрокаТаб.Маршрут;
		СтрокаДерево.СтруктурнаяЕдиница = СтрокаТаб.СтруктурнаяЕдиница;
		ПредМаршрут = СтрокаТаб.Маршрут;
	КонецЦикла;
	   
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонфликтовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(ДеревоКонфликтов.НайтиПоИдентификатору(ВыбраннаяСтрока).Маршрут);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтруктурныеЕдиницыИзМаршрутовСервер()
	
	Для Каждого СтрокаМаршут Из ДеревоКонфликтов.ПолучитьЭлементы() Цикл
		МаршрутОбъект = СтрокаМаршут.Маршрут.ПолучитьОбъект();
		Для Каждого СтрокаСтрукЕдиница Из СтрокаМаршут.ПолучитьЭлементы() Цикл
			КолвоСтрок = МаршрутОбъект.ТорговыеТочки.Количество();
			Для н = 1 По КолвоСтрок Цикл
				Если МаршрутОбъект.ТорговыеТочки[КолвоСтрок - н].СтруктурнаяЕдиница = СтрокаСтрукЕдиница.СтруктурнаяЕдиница Тогда
					МаршрутОбъект.ТорговыеТочки.Удалить(КолвоСтрок - н);
				КонецЕсли;	
			КонецЦикла;	
		КонецЦикла;
		МаршрутОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтруктурныеЕдиницыИзМаршрутов(Команда)
	
	УдалитьСтруктурныеЕдиницыИзМаршрутовСервер();
	Закрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьРедактированиеТекущего(Команда)
	
	Закрыть(Истина);
	
КонецПроцедуры
