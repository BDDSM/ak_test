
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	
	Для Каждого Стр Из Параметры.ТЗПривязки Цикл
		НовСтр = ТаблицаПривязки.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		
		НовСтр.ОрганизацияНЕРавна = НовСтр.Организация <> Организация;		
		
	КонецЦикла;
	
	ТаблицаПривязки.Сортировать("СтруктурнаяЕдиница, Датчик");
	
	Сч = 0;
	
	Для Каждого Стр Из ТаблицаПривязки Цикл
		Сч = Сч + 1;
		Стр.НомерСтроки = Сч;
	КонецЦикла;
	
	Для Каждого Стр Из Параметры.ТаблицаДатчикиПоОсам Цикл
		НовСтр = ТаблицаДатчикиПоОсам.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Стр);
	КонецЦикла;  	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоменятьДатчикиМестами(Команда)
	
	ВыдСтроки = Элементы.ТаблицаПривязки.ВыделенныеСтроки;	
	
	Если ВыдСтроки.Количество() > 2 Тогда
		Предупреждение("Выбрано более 2-х строк таблицы. Необходимо выбрать 2 строки для переноса датчиков между ними!");
		Возврат;
	ИначеЕсли ВыдСтроки.Количество() =  1 Тогда
		Предупреждение("Выбрана только 1 строка таблицы. Необходимо выбрать 2 строки для переноса датчиков между ними!");
		Возврат;
	ИначеЕсли ВыдСтроки.Количество() = 0 Тогда
		Предупреждение("Не выбрано ни одной строки таблицы. Необходимо выбрать 2 строки для переноса датчиков между ними!");
		Возврат;
	КонецЕсли;
	
	
	Строка1 = ТаблицаПривязки.НайтиПоИдентификатору(ВыдСтроки[0]);
	Строка2 = ТаблицаПривязки.НайтиПоИдентификатору(ВыдСтроки[1]);
	
	Если Строка1.СтруктурнаяЕдиница <> Строка2.СтруктурнаяЕдиница Тогда
		Предупреждение("Менять датчики местами можно только в пределах одной структурной единицы!!!");
		Возврат;
	КонецЕсли;
	
	ДатчикПромежуточный = Строка1.Датчик;
	МестоПромежуточное = Строка1.МестоВыкладки;
	Строка1.Датчик = Строка2.Датчик;
	Строка1.МестоВыкладки = Строка2.МестоВыкладки;
	Строка2.Датчик = ДатчикПромежуточный;
	Строка2.МестоВыкладки = МестоПромежуточное;
	
	ОбновитьНадписьКоличествоНеПривязанныхДатчиков();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПривязкиДатчикПриИзменении(Элемент)
	
	ТекДанные = Элементы.ТаблицаПривязки.ТекущиеДанные;
	
	НайдСтроки = ТаблицаПривязки.НайтиСтроки(Новый Структура("Датчик", ТекДанные.Датчик));
	
	Если НайдСтроки.Количество() > 1 Тогда
		Для Каждого НСтр Из НайдСтроки Цикл
			Если НСтр.НомерСтроки <> ТекДанные.НомерСтроки Тогда
				Предупреждение("Выбранный датчик уже используется в строке: " +  Строка(НСтр.НомерСтроки) +"! В этой строке датчик будет очищен.");
				НСтр.Датчик = ПредопределенноеЗначение("Справочник.Датчики.ПустаяСсылка");
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтаФорма.Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ТаблицаВозврата = ПолучитьТаблицуВозвратаСервер();
	
	ЭтаФорма.Закрыть(ТаблицаВозврата);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуВозвратаСервер()
	
	МассивСтрДляУд = Новый Массив;
	
	ТаблицаВозврата = ТаблицаПривязки.Выгрузить();
	
	Для Каждого Стр Из ТаблицаВозврата Цикл
		Если Не ЗначениеЗаполнено(Стр.ОсновноеСредство) Тогда
			МассивСтрДляУд.Добавить(Стр);
		КонецЕсли;
	КонецЦикла;
	
	Для КАждого Стр Из МассивСтрДляУд Цикл
		ТаблицаВозврата.Удалить(Стр);
	КонецЦикла;	
	
	Возврат ТаблицаВозврата;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаПривязкиОсновноеСредствоПриИзменении(Элемент)
	
	ТекДанные = Элементы.ТаблицаПривязки.ТекущиеДанные;
	
	Если ТекДанные.Организация <> Организация Тогда
		Предупреждение("Нельзя привязывать датчики по организациям, отличным от организации документа!");
		ТекДанные.ОсновноеСредство = ПредопределенноеЗначение("Справочник.ОсновныеСредства.ПустаяСсылка");
		Возврат;
	КонецЕсли;
	
	НайдСтроки = ТаблицаДатчикиПоОсам.НайтиСтроки(Новый Структура("ОсновноеСредство", ТекДанные.ОсновноеСредство));
	
	Если НайдСтроки.Количество() Тогда
		НСтр = НайдСтроки[0];
		
		СтрокаПредупреждения = "Данный ОС уже привязан к датчику: " + Строка(НСтр.Датчик) + " - по месту выкладки: " + НСтр.МестоВыкладки + " (магазин: " + Строка(НСтр.Магазин) + ")!";		
		
		Если ТекДанные.СтруктурнаяЕдиница <> НСтр.Магазин Тогда
			СтрокаПредупреждения = СтрокаПредупреждения + " Разные магазины. ОС будет очищено!";
			
			ТекДанные.ОсновноеСредство = ПредопределенноеЗначение("Справочник.ОсновныеСредства.ПустаяСсылка");
		КонецЕсли;
		
		Предупреждение(СтрокаПредупреждения);
		
	КонецЕсли;
	
	ОбновитьНадписьКоличествоНеПривязанныхДатчиков();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьНадписьКоличествоНеПривязанныхДатчиков();
	Элементы.ТаблицаПривязкиУбратьДатичикПоДругимОрганизациям.Пометка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьКоличествоНеПривязанныхДатчиков()
	
	НайдСтроки = ТаблицаПривязки.НайтиСтроки(Новый Структура("ОсновноеСредство", ПредопределенноеЗначение("Справочник.ОсновныеСредства.ПустаяСсылка")));
	
	Элементы.тКоличествоНеПривязанных.Заголовок = "Количество не привязанных датчиков: " + Строка(НайдСтроки.Количество());

КонецПроцедуры

&НаКлиенте
Процедура УбратьДатичикПоДругимОрганизациям(Команда)
	
	Элементы.ТаблицаПривязкиУбратьДатичикПоДругимОрганизациям.Пометка = НЕ Элементы.ТаблицаПривязкиУбратьДатичикПоДругимОрганизациям.Пометка;
	
	Если Элементы.ТаблицаПривязкиУбратьДатичикПоДругимОрганизациям.Пометка Тогда
		Элементы.ТаблицаПривязки.ОтборСтрок = Новый ФиксированнаяСтруктура("Организация", Организация);
	Иначе
		Элементы.ТаблицаПривязки.ОтборСтрок = Неопределено;
	КонецЕсли;	
	
КонецПроцедуры
