//+++AK susk
Перем мРазрешитьПроведение Экспорт;
//---AK susk

Функция ОпределитьСчетСписанияЗатрат(СчЗатрат) 
	СубСчет = Сред(СчЗатрат.Код,Найти(СчЗатрат.Код,".")+1);
	
	//+++AK susk
	//СчетСписанияЗатрат = ПланыСчетов.Финансовый.НайтиПоКоду("90."+СубСчет);
	
	//44.5 должен распределяться на 90.3	
	Если СокрЛП(СчЗатрат.Код) = "44.5" Тогда
		СчетСписанияЗатрат = ПланыСчетов.Финансовый.НайтиПоКоду("90.3");
	Иначе
		СчетСписанияЗатрат = ПланыСчетов.Финансовый.НайтиПоКоду("90."+СубСчет);
	КонецЕсли;
	//---AK susk

	Возврат СчетСписанияЗатрат;
КонецФункции

Процедура ОбработкаПроведения(Отказ, Режим)
	//+++ ZICD 13.02.2017 ИП-00014919
	Если ЭтотОбъект.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	//---
	
	//+++AK susk
	Если НЕ мРазрешитьПроведение Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Ручное проведение документа - ЗАПРЕЩЕНО!!!", Отказ);		
		Возврат;
	КонецЕсли;
	//---AK susk
	//+++АК sils 12.03.2018 ИП-00017960
	Если СокрЛП(СчетЗатрат.Код) = "X44" Тогда
		СчётСписанияЗатрат = ПланыСчетов.Финансовый.НайтиПоКоду("X90");
		Если СчётСписанияЗатрат = ПланыСчетов.Финансовый.ПустаяСсылка() Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не найден счет X90!", Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//---АК

	Движения.Финансовый.Записывать = Истина;
	Движения.Финансовый.Очистить();
	//+++АК ZICD 11.01.2017 ИП-00014365
	НачалоУчётаМСФО = АК_УчетМСФОПривилегированный.ПолучитьДатуНачалаУчетаМСФО(); //Дата("20180101");
	Если НачалоУчётаМСФО > Дата("00010101") И Дата >= НачалоУчётаМСФО Тогда
		ВыполнитьРаспределениеЗатрат();
		Возврат;
	КонецЕсли;
	//---АК
	Для Каждого ТекСтрокаРаспределение Из Распределение Цикл
		Движение = Движения.Финансовый.Добавить();
		Движение.СчетКт								= СчетЗатрат;
		Движение.СубконтоКт.СтатьиДоходовРасходов 	= ТекСтрокаРаспределение.СтатьяДоходовРасходов;
		Движение.СубконтоКт.ЦФО 					= ТекСтрокаРаспределение.ЦФО;
		Если НЕ ТекСтрокаРаспределение.Авторазмещение Тогда
			Движение.СубконтоКт.ТорговыеТочки 		= ТекСтрокаРаспределение.ТорговаяТочка;
		Иначе
			Движение.СубконтоКт.ТорговыеТочки		= ТекСтрокаРаспределение.ПодразделениеРасхода;
		КонецЕсли;	
		Движение.СчетДт 							= ОпределитьСчетСписанияЗатрат(СчетЗатрат); 
		Движение.Период 							= Дата;
		Движение.Сумма 								= ТекСтрокаРаспределение.Сумма;
		Движение.СубконтоДт.ТорговыеТочки 			= ТекСтрокаРаспределение.ТорговаяТочка;
		Движение.СубконтоДт.СтатьиДоходовРасходов 	= ТекСтрокаРаспределение.СтатьяДоходовРасходов;
		Движение.СубконтоДт.ЦФО 				  = ?(ЗначениеЗаполнено(ТекСтрокаРаспределение.Управление), ТекСтрокаРаспределение.Управление, ТекСтрокаРаспределение.ЦФО);
		Движение.НомерЖурнала					  = "ТТ";
		Движение.Содержание						  = "Распределены затраты. "+ Комментарий;
	КонецЦикла;
КонецПроцедуры

//+++АК ZICD 11.01.2017 ИП-00014365
Процедура ВыполнитьРаспределениеЗатрат()
	
	// Это нужно для замеров производительности - управляем записью вручную
	Движения.Финансовый.Записывать = Ложь;
	
	Если Проведен Тогда
		Движения.Финансовый.Очистить();
		Движения.Финансовый.Записать();
	КонецЕсли;
	
	РазделУчёта = ?(МСФО, "МСФО", "Основной");
	
	НаборЗаписей = Движения.Финансовый.Выгрузить();
	Если СчетЗатрат = ПланыСчетов.Финансовый.ЗатратыДляРаспределения Тогда
		
		// 44.1
		АК_УчетМСФОПривилегированный.Распределить44Счёт(Дата, Ссылка,
			ПланыСчетов.Финансовый.ЗатратыТоварныеДляРаспределения, НаборЗаписей, РазделУчёта, Комментарий);
		// 44.2
		АК_УчетМСФОПривилегированный.Распределить44Счёт(Дата, Ссылка,
			ПланыСчетов.Финансовый.ЗатратыОбщиеДляРаспределения, НаборЗаписей, РазделУчёта, Комментарий);
		// 44.3
		АК_УчетМСФОПривилегированный.Распределить44Счёт(Дата, Ссылка,
			ПланыСчетов.Финансовый.ЗатратыТочекДляРаспределения, НаборЗаписей, РазделУчёта, Комментарий);
		
		//44.5
		Счет445 = ПланыСчетов.Финансовый.НайтиПоКоду("44.5");
		Если ЗначениеЗаполнено(Счет445) Тогда
			// 44.5
			АК_УчетМСФОПривилегированный.Распределить44Счёт(Дата, Ссылка,
				Счет445, НаборЗаписей, РазделУчёта, Комментарий);	
		КонецЕсли;	
		
	//+++АК sils 12.03.2018 ИП-00017960
	ИначеЕсли СокрЛП(СчетЗатрат.Код) = "X44" Тогда
		АК_УчетМСФОПривилегированный.Распределить44Счёт(Дата, Ссылка,
			ПланыСчетов.Финансовый.НайтиПоКоду("X44"), НаборЗаписей, РазделУчёта, Комментарий);
	//---АК
	Иначе
		АК_УчетМСФОПривилегированный.Распределить44Счёт(Дата, Ссылка, СчетЗатрат, НаборЗаписей, РазделУчёта, Комментарий);
	КонецЕсли;
	Движения.Финансовый.Загрузить(НаборЗаписей);
	
	// Это нужно для замеров производительности - управляем записью вручную
	Движения.Финансовый.Записать();
	
КонецПроцедуры
Процедура РаспределитьОставшиесяКопейки(МассивСтрок, СуммаРаспределения, СуммаРаспределено) Экспорт
	
	Если СуммаРаспределения = СуммаРаспределено Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если СуммаРаспределения = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КопейкиРаспределения = СуммаРаспределения - СуммаРаспределено;
	
	МассивСтрок[0].Сумма = МассивСтрок[0].Сумма + КопейкиРаспределения;   		

КонецПроцедуры
//---АК

//+++AK susk
мРазрешитьПроведение = Ложь;
//---AK susk
