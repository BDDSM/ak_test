
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МесяцОбработки = НачалоМесяца(ДобавитьМесяц(ТекущаяДата(), -1));
	Месяц = Формат(МесяцОбработки, "ДФ='MMMM yyyy'");
	СчетЗатрат = ПланыСчетов.Финансовый.ЗатратыОбщиеДляРаспределения;
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	МесяцОбработки = НачалоМесяца(ДобавитьМесяц(МесяцОбработки, Направление));
	Месяц = Формат(МесяцОбработки, "ДФ='MMMM yyyy'");
	
КонецПроцедуры

&НаСервере
Функция ОпределитьЦФОДляТочки(Дата, ТТ, ТабСрез, ТабДвижения)
	
	ЦФО = Неопределено;
	МаксДата = '00010101';
	СтрокаСрез = ТабСрез.Найти(ТТ, "СтруктурнаяЕдиница");
	Если СтрокаСрез <> Неопределено Тогда
		ЦФО = СтрокаСрез.ЦФО;
	КонецЕсли;	
	
	СтрокиТТ = ТабДвижения.НайтиСтроки(Новый Структура("СтруктурнаяЕдиница", ТТ));
	Для Каждого СтрокаТТ Из СтрокиТТ Цикл
		Если СтрокаТТ.Период <= Дата
			И МаксДата <= СтрокаТТ.Период Тогда
			ЦФО = СтрокаТТ.ЦФО;
			МаксДата = СтрокаТТ.Период;
		КонецЕсли;	
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ЦФО) Тогда
		Если СтрокиТТ.Количество() > 0 Тогда
			ЦФО = СтрокиТТ[0].ЦФО;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат ЦФО;
	
КонецФункции	

&НаСервере
Процедура ДораспределитьКопейки(ОстатокКРаспределению, МассивСтрок)
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(Число(Формат(ТекущаяДата(), "ДФ=yyyymmss")));
	
	Пока ОстатокКРаспределению <> 0 Цикл
		СлучЧисло = ГСЧ.СлучайноеЧисло(0, МассивСтрок.Количество() - 1);
		СуммаИтерации = 0.01 * ?(ОстатокКРаспределению < 0, -1, 1);
		МассивСтрок[СлучЧисло].Сумма = МассивСтрок[СлучЧисло].Сумма + СуммаИтерации;
		ОстатокКРаспределению = ОстатокКРаспределению - СуммаИтерации;
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура СоздатьДокументыЗатрат()
	
	СвойствоРаспределение = ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию("Счет распределения Вкусвилл (общий)");
	Если СвойствоРаспределение.Пустая() Тогда
		СвойствоРаспределениеОб = ПланыВидовХарактеристик.СвойстваОбъектов.СоздатьЭлемент();
		СвойствоРаспределениеОб.Наименование = "Счет распределения Вкусвилл (общий)";
		СвойствоРаспределениеОб.ТипЗначения = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50));
		СвойствоРаспределениеОб.Записать();
		СвойствоРаспределение = СвойствоРаспределениеОб.Ссылка;
	КонецЕсли;
	
	ЗапросПредДоков = Новый Запрос();
	ЗапросПредДоков.Текст = "ВЫБРАТЬ
	                        |	Операция.Ссылка
	                        |ИЗ
	                        |	Документ.Операция КАК Операция
	                        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	                        |		ПО Операция.Ссылка = ЗначенияСвойствОбъектов.Объект
	                        |ГДЕ
	                        |	Операция.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	                        |	И Операция.ПометкаУдаления = ЛОЖЬ
	                        |	И ЗначенияСвойствОбъектов.Значение = &Значение
	                        |	И ЗначенияСвойствОбъектов.Свойство = &Свойство";
							
	ЗапросПредДоков.УстановитьПараметр("Свойство", СвойствоРаспределение);
	ЗапросПредДоков.УстановитьПараметр("Значение", Строка(СчетЗатрат.УникальныйИдентификатор()));
	ЗапросПредДоков.УстановитьПараметр("ДатаНачала"		, НачалоМесяца(МесяцОбработки));
	ЗапросПредДоков.УстановитьПараметр("ДатаОкончания"	, КонецМесяца(МесяцОбработки));
	ТабДоков = ЗапросПредДоков.Выполнить().Выгрузить();
	Для Каждого СтрокаДок Из ТабДоков Цикл
		ДокОбъект = СтрокаДок.Ссылка.ПолучитьОбъект();
		ДокОбъект.Движения.Финансовый.Очистить();
		ДокОбъект.Записать();
	КонецЦикла;	
	
	СписокСчетов = Новый СписокЗначений;
	СписокСчетов.Добавить(ПланыСчетов.Финансовый.ЗатратыТорговыхТочек);
	СписокСчетов.Добавить(ПланыСчетов.Финансовый.ВыручкаТорговыхТочек);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала"		, НачалоМесяца(МесяцОбработки));
	Запрос.УстановитьПараметр("ДатаОкончания"	, КонецМесяца(МесяцОбработки));
	Запрос.УстановитьПараметр("СчетЗатрат"		, СчетЗатрат);
	Запрос.УстановитьПараметр("СписокСчетов"	, СписокСчетов);
	Запрос.УстановитьПараметр("Суб"	, Справочники.СтруктурныеЕдиницы.НайтиПоНаименованию("Вкусвилл (общий)", Истина));
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФинансовыйОбороты.Субконто1 КАК ТорговаяТочка,
	               |	СУММА(ФинансовыйОбороты.СуммаОборотКт - ФинансовыйОбороты.СуммаОборотДт) КАК Выручка
	               |ИЗ
	               |	РегистрБухгалтерии.Финансовый.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор, Счет В (&СписокСчетов), , Субконто2 = ЗНАЧЕНИЕ(Справочник.СтатьиДоходовРасходов.РозничнаяВыручка), , ) КАК ФинансовыйОбороты
	               |ГДЕ
	               |	ФинансовыйОбороты.Регистратор ССЫЛКА Документ.ЛистУчета
	               |	И ФинансовыйОбороты.Субконто1.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ФинансовыйОбороты.Субконто1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЛистУчета.ТорговаяТочка,
	               |	СУММА(1) КАК КолвоДней
	               |ИЗ
	               |	Документ.ЛистУчета КАК ЛистУчета
	               |ГДЕ
	               |	ЛистУчета.Проведен = ИСТИНА
	               |	И ЛистУчета.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	               |	И ЛистУчета.ТорговаяТочка.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЛистУчета.ТорговаяТочка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФинансовыйОбороты.Субконто1 КАК ТорговаяТочка,
	               |	ФинансовыйОбороты.Субконто2 КАК СДР,
	               |	ФинансовыйОбороты.Субконто2.СпособРаспределенияЗатрат КАК СпособРаспределенияЗатрат,
	               |	ФинансовыйОбороты.Субконто3 КАК ЦФО,
	               |	ФинансовыйОбороты.СуммаОборот КАК СуммаОборот,
	               |	ФинансовыйОбороты.Организация,
	               |	ФинансовыйОбороты.СтруктурнаяЕдиница,
	               |	ФинансовыйОбороты.Период КАК Период
	               |ИЗ
	               |	РегистрБухгалтерии.Финансовый.Обороты(&ДатаНачала, &ДатаОкончания, День, Счет = &СчетЗатрат, , Субконто1 = &Суб, , ) КАК ФинансовыйОбороты
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЦФОСтруктурныхЕдиницСрезПоследних.СтруктурнаяЕдиница,
	               |	ЦФОСтруктурныхЕдиницСрезПоследних.ЦФО
	               |ИЗ
	               |	РегистрСведений.ЦФОСтруктурныхЕдиниц.СрезПоследних(&ДатаНачала, СтруктурнаяЕдиница.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)) КАК ЦФОСтруктурныхЕдиницСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЦФОСтруктурныхЕдиниц.Период,
	               |	ЦФОСтруктурныхЕдиниц.СтруктурнаяЕдиница,
	               |	ЦФОСтруктурныхЕдиниц.ЦФО
	               |ИЗ
	               |	РегистрСведений.ЦФОСтруктурныхЕдиниц КАК ЦФОСтруктурныхЕдиниц
	               |ГДЕ
	               |	ЦФОСтруктурныхЕдиниц.СтруктурнаяЕдиница.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)
	               |	И ЦФОСтруктурныхЕдиниц.Период МЕЖДУ &ДатаНачала И &ДатаОкончания";
				   
	Результаты = Запрос.ВыполнитьПакет();
	
	ТабВыручка = Результаты[0].Выгрузить();
	ТабСмены = Результаты[1].Выгрузить();
	ТабСрез = Результаты[3].Выгрузить();
	ТабСрез.Индексы.Добавить("СтруктурнаяЕдиница");
	ТабДвижения = Результаты[4].Выгрузить();
	ТабДвижения.Индексы.Добавить("СтруктурнаяЕдиница");
	
	ТабВыручка.Колонки.Добавить("Процент");
	ТабСмены.Колонки.Добавить("Процент");
	
	ОбщаяСуммаВыручки = ТабВыручка.Итог("Выручка");
	ОбщаяСуммаДней = ТабСмены.Итог("КолвоДней");
	
	Для Каждого СтрокаТаб Из ТабВыручка Цикл
		Если СтрокаТаб.Выручка = 0 Тогда
			СтрокаТаб.Процент = 0;
		Иначе
			СтрокаТаб.Процент = СтрокаТаб.Выручка / ОбщаяСуммаВыручки;
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого СтрокаТаб Из ТабСмены Цикл
		Если СтрокаТаб.КолвоДней = 0 Тогда
			СтрокаТаб.Процент = 0;
		Иначе
			СтрокаТаб.Процент = СтрокаТаб.КолвоДней / ОбщаяСуммаДней;
		КонецЕсли;	
	КонецЦикла;
	
	Если ТабДоков.Количество() = 0 Тогда
		ДокОперация = Документы.Операция.СоздатьДокумент();
		ДокОперация.Дата = КонецМесяца(МесяцОбработки);
		ДокОперация.Комментарий = "Распределение затрат перед закрытием месяца по Вкусвилл (общий)";
	Иначе
		ДокОперация = ТабДоков[0].Ссылка.ПолучитьОбъект();
	КонецЕсли;	
	
	ТаблицаРаспределение = Новый ТаблицаЗначений();
	ТаблицаРаспределение.Колонки.Добавить("Организация");
	ТаблицаРаспределение.Колонки.Добавить("СтруктурнаяЕдиница");
	ТаблицаРаспределение.Колонки.Добавить("ТорговаяТочка");
	ТаблицаРаспределение.Колонки.Добавить("СДР");
	ТаблицаРаспределение.Колонки.Добавить("ЦФО");
	ТаблицаРаспределение.Колонки.Добавить("Субконто1");
	ТаблицаРаспределение.Колонки.Добавить("Субконто2");
	ТаблицаРаспределение.Колонки.Добавить("Субконто3");
	ТаблицаРаспределение.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Выборка = Результаты[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		СуммаКРаспределению = Выборка.СуммаОборот;
		Если СуммаКРаспределению = 0 Тогда
			Продолжить;
		КонецЕсли;
		МассивДобавленных = Новый Массив();
		СуммаРаспределено = 0;
		Если Выборка.СпособРаспределенияЗатрат = Перечисления.СпособыРаспределенияЗатрат.ПоВыручке
			ИЛИ НЕ ЗначениеЗаполнено(Выборка.СпособРаспределенияЗатрат) Тогда
			Для Каждого СтрокаВыручка Из ТабВыручка Цикл
				ЦФО = ОпределитьЦФОДляТочки(Выборка.Период, СтрокаВыручка.ТорговаяТочка, ТабСрез, ТабДвижения);
				Если НЕ ЗначениеЗаполнено(ЦФО) Тогда
					Продолжить;
				КонецЕсли;	
				СтрокаДоб = ТаблицаРаспределение.Добавить();
				СтрокаДоб.Организация = Выборка.Организация;
				СтрокаДоб.СтруктурнаяЕдиница = Выборка.СтруктурнаяЕдиница;
				СтрокаДоб.ТорговаяТочка = Выборка.ТорговаяТочка;
				СтрокаДоб.СДР = Выборка.СДР;
				СтрокаДоб.ЦФО = Выборка.ЦФО;
				СтрокаДоб.Субконто1 = СтрокаВыручка.ТорговаяТочка;
				СтрокаДоб.Субконто2 = Выборка.СДР;
				СтрокаДоб.Субконто3 = ЦФО;
				СтрокаДоб.Сумма = СуммаКРаспределению * СтрокаВыручка.Процент;
				СуммаРаспределено = СуммаРаспределено + СтрокаДоб.Сумма;
				МассивДобавленных.Добавить(СтрокаДоб);
			КонецЦикла;	
			Если СуммаРаспределено <> СуммаКРаспределению Тогда
				ДораспределитьКопейки(СуммаКРаспределению - СуммаРаспределено, МассивДобавленных);
			КонецЕсли;
		ИначеЕсли Выборка.СпособРаспределенияЗатрат = Перечисления.СпособыРаспределенияЗатрат.ПоКоличествуСмен Тогда
			Для Каждого СтрокаВыручка Из ТабСмены Цикл
				ЦФО = ОпределитьЦФОДляТочки(Выборка.Период, СтрокаВыручка.ТорговаяТочка, ТабСрез, ТабДвижения);
				Если НЕ ЗначениеЗаполнено(ЦФО) Тогда
					Продолжить;
				КонецЕсли;
				СтрокаДоб = ТаблицаРаспределение.Добавить();
				СтрокаДоб.Организация = Выборка.Организация;
				СтрокаДоб.СтруктурнаяЕдиница = Выборка.СтруктурнаяЕдиница;
				СтрокаДоб.ТорговаяТочка = Выборка.ТорговаяТочка;
				СтрокаДоб.СДР = Выборка.СДР;
				СтрокаДоб.ЦФО = Выборка.ЦФО;
				СтрокаДоб.Субконто1 = СтрокаВыручка.ТорговаяТочка;
				СтрокаДоб.Субконто2 = Выборка.СДР;
				СтрокаДоб.Субконто3 = ЦФО;
				СтрокаДоб.Сумма = СуммаКРаспределению * СтрокаВыручка.Процент;
				СуммаРаспределено = СуммаРаспределено + СтрокаДоб.Сумма;
				МассивДобавленных.Добавить(СтрокаДоб);
			КонецЦикла;	
			Если СуммаРаспределено <> СуммаКРаспределению Тогда
				ДораспределитьКопейки(СуммаКРаспределению - СуммаРаспределено, МассивДобавленных);
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
	
	ТаблицаРаспределение.Свернуть("Организация, СтруктурнаяЕдиница, ТорговаяТочка, СДР, ЦФО, Субконто1, Субконто2, Субконто3", "Сумма");
	
	СуммаОперации = 0;
	Для Каждого СтрокаРаспределение Из ТаблицаРаспределение Цикл
		Проводка = ДокОперация.Движения.Финансовый.Добавить();
		Проводка.Период										= КонецМесяца(МесяцОбработки);
		Проводка.Организация								= СтрокаРаспределение.Организация;
		Проводка.СтруктурнаяЕдиница							= СтрокаРаспределение.ТорговаяТочка;
		
		Проводка.СчетДт 									= СчетЗатрат;
		Проводка.СубконтоДт.ТорговыеТочки 					= СтрокаРаспределение.Субконто1;
		Проводка.СубконтоДт.СтатьиДоходовРасходов			= СтрокаРаспределение.Субконто2;
		Проводка.СубконтоДт.ЦФО								= СтрокаРаспределение.Субконто3;
		
		Проводка.СчетКт 									= СчетЗатрат;
		Проводка.СубконтоКт.ТорговыеТочки 					= СтрокаРаспределение.ТорговаяТочка;
		Проводка.СубконтоКт.СтатьиДоходовРасходов			= СтрокаРаспределение.СДР;
		//Проводка.СубконтоКт.ЦФО								= СтрокаРаспределение.ЦФО;
		Проводка.СубконтоКт.ЦФО								= СтрокаРаспределение.Субконто3;
		Проводка.Сумма 										= СтрокаРаспределение.Сумма;
		СуммаОперации = СуммаОперации + Проводка.Сумма;
	КонецЦикла;	
	
	ДокОперация.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	ДокОперация.СуммаОперации = СуммаОперации;
	ДокОперация.Записать();
	
	ЗаписьСвойств = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьМенеджерЗаписи();
	ЗаписьСвойств.Объект = ДокОперация.Ссылка;
	ЗаписьСвойств.Свойство = СвойствоРаспределение;
	ЗаписьСвойств.Значение = Строка(СчетЗатрат.УникальныйИдентификатор());
	ЗаписьСвойств.Записать();
	
КонецПроцедуры	

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	Отказ = Ложь;
	Если НЕ ЗначениеЗаполнено(МесяцОбработки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан месяц обработки",,,, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СчетЗатрат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан счет затрат",,,, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	СоздатьДокументыЗатрат();
	
КонецПроцедуры
