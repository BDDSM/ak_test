
Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ЦеныНоменклатуры
	Если ЭтотОбъект.Отработан Тогда
		
		МенеджерЗаписи = РегистрыСведений.ЦеныПоставщиков.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Номенклатура 	= ЭтотОбъект.Номенклатура;
		МенеджерЗаписи.Характеристика 	= ЭтотОбъект.Характеристика;
		МенеджерЗаписи.Период 			= ЭтотОбъект.ДействуетС;
		МенеджерЗаписи.Цена 			= ЭтотОбъект.ЦенаНовая;
		МенеджерЗаписи.Поставщик 		= ЭтотОбъект.Поставщик;
		МенеджерЗаписи.Автор 			= ЭтотОбъект.Ответственный;
		МенеджерЗаписи.Записать();
		
		//
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Период"			, ЭтотОбъект.Дата);
		Запрос.УстановитьПараметр("Номенклатура"	, ЭтотОбъект.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика"	, ЭтотОбъект.Характеристика);
		Запрос.УстановитьПараметр("Ссылка"			, ЭтотОбъект.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СпецификацииПоставщиковСрезПоследних.Поставщик,
		|	СпецификацииПоставщиковСрезПоследних.Номенклатура,
		|	СпецификацииПоставщиковСрезПоследних.Характеристика,
		|	ВложенныйЗапрос.Сырье КАК НоменклатураСырье,
		|	СпецификацииПоставщиковСрезПоследних.Период
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПереходНаСвоюУпаковкуРасходникиКЗамене.Номенклатура КАК Сырье
		|	ИЗ
		|		Документ.ПереходНаСвоюУпаковку.РасходникиКЗамене КАК ПереходНаСвоюУпаковкуРасходникиКЗамене
		|	ГДЕ
		|		ПереходНаСвоюУпаковкуРасходникиКЗамене.Ссылка = &Ссылка) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпецификацииПоставщиков.СрезПоследних(
		|				&Период,
		|				Номенклатура = &Номенклатура
		|					И Характеристика = &Характеристика) КАК СпецификацииПоставщиковСрезПоследних
		|		ПО ВложенныйЗапрос.Сырье = СпецификацииПоставщиковСрезПоследних.НоменклатураСырье
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ЦеныПоставщиковСрезПоследних.Номенклатура КАК Номенклатура,
		|			ЦеныПоставщиковСрезПоследних.Характеристика КАК Характеристика
		|		ИЗ
		|			РегистрСведений.ЦеныПоставщиков.СрезПоследних(
		|					&Период,
		|					Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика) КАК ЦеныПоставщиковСрезПоследних) КАК ЦеныПоставщиков
		|		ПО (СпецификацииПоставщиковСрезПоследних.Номенклатура = ЦеныПоставщиков.Номенклатура)
		|			И (СпецификацииПоставщиковСрезПоследних.Характеристика = ЦеныПоставщиков.Характеристика)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МАКСИМУМ(СпецификацииПоставщиковСрезПоследних.Период) КАК Период,
		|			СпецификацииПоставщиковСрезПоследних.Номенклатура КАК Номенклатура,
		|			СпецификацииПоставщиковСрезПоследних.НоменклатураСырье КАК НоменклатураСырье,
		|			СпецификацииПоставщиковСрезПоследних.Характеристика КАК Характеристика
		|		ИЗ
		|			РегистрСведений.СпецификацииПоставщиков.СрезПоследних(
		|					&Период,
		|					Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика) КАК СпецификацииПоставщиковСрезПоследних
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|					ЦеныПоставщиковСрезПоследних.Номенклатура КАК Номенклатура,
		|					ЦеныПоставщиковСрезПоследних.Характеристика КАК Характеристика
		|				ИЗ
		|					РегистрСведений.ЦеныПоставщиков.СрезПоследних(
		|							&Период,
		|							Номенклатура = &Номенклатура
		|								И Характеристика = &Характеристика) КАК ЦеныПоставщиковСрезПоследних) КАК ЦеныПоставщиков
		|				ПО СпецификацииПоставщиковСрезПоследних.Номенклатура = ЦеныПоставщиков.Номенклатура
		|					И СпецификацииПоставщиковСрезПоследних.Характеристика = ЦеныПоставщиков.Характеристика
		|		
		|		СГРУППИРОВАТЬ ПО
		|			СпецификацииПоставщиковСрезПоследних.Номенклатура,
		|			СпецификацииПоставщиковСрезПоследних.НоменклатураСырье,
		|			СпецификацииПоставщиковСрезПоследних.Характеристика) КАК ВложенныйЗапрос1
		|		ПО (СпецификацииПоставщиковСрезПоследних.Номенклатура = ВложенныйЗапрос1.Номенклатура)
		|			И (СпецификацииПоставщиковСрезПоследних.НоменклатураСырье = ВложенныйЗапрос1.НоменклатураСырье)
		|			И (СпецификацииПоставщиковСрезПоследних.Характеристика = ВложенныйЗапрос1.Характеристика)
		|			И (СпецификацииПоставщиковСрезПоследних.Период = ВложенныйЗапрос1.Период)
		|ГДЕ
		|	СпецификацииПоставщиковСрезПоследних.НоменклатураСырье <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И (СпецификацииПоставщиковСрезПоследних.ДатаКонца = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СпецификацииПоставщиковСрезПоследних.ДатаКонца > &Период)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий()Цикл
			
			МенеджерЗаписи = РегистрыСведений.СпецификацииПоставщиков.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Прочитать();
			МенеджерЗаписи.ДатаКонца = НачалоДня(ЭтотОбъект.ДействуетС) - 1;
			МенеджерЗаписи.Записать();
			
		КонецЦикла;
		
    Иначе
		
		// регистр ЗапретИзмененияЦены
		Движения.ЗапретИзмененияЦены.Записывать = Истина;
		Движения.ЗапретИзмененияЦены.Очистить();
		Движение = Движения.ЗапретИзмененияЦены.Добавить();
		Движение.Период 		= ЭтотОбъект.Дата;
		Движение.Номенклатура 	= ЭтотОбъект.Номенклатура;
		Движение.Характеристика = ЭтотОбъект.Характеристика;

		// регистр КонтролируемыеОстаткиРасходников
		Движения.КонтролируемыеОстаткиРасходников.Записывать = Истина;
		Движения.КонтролируемыеОстаткиРасходников.Очистить();
		
		Для Каждого ТекСтрока Из РасходникиКЗамене Цикл
			Движение = Движения.КонтролируемыеОстаткиРасходников.Добавить();
			Движение.Номенклатура 	= ТекСтрока.Номенклатура;
			Движение.Документ 		= ЭтотОбъект.Ссылка;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ЭтотОбъект.Отработан Тогда
		
		МенеджерЗаписи = РегистрыСведений.ЦеныПоставщиков.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Номенклатура 	= ЭтотОбъект.Номенклатура;
		МенеджерЗаписи.Характеристика 	= ЭтотОбъект.Характеристика;
		МенеджерЗаписи.Период 			= ЭтотОбъект.ДействуетС;
		МенеджерЗаписи.Удалить();
		
	КонецЕсли;
	
КонецПроцедуры
