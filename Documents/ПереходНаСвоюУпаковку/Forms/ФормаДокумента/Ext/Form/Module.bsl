
&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.ДействуетС.ТолькоПросмотр = НЕ Объект.Отработан;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦену(ТекДата, ТекНоменклатура, ТекХарактеристика = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(ТекНоменклатура) Тогда
		Возврат 0
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Номенклатура", ТекНоменклатура);
	Если ЗначениеЗаполнено(ТекХарактеристика) Тогда
		СтруктураОтбора.Вставить("Характеристика", ТекХарактеристика);
	КонецЕсли;
	
	Результат = РегистрыСведений.ЦеныПоставщиков.ПолучитьПоследнее(ТекДата, СтруктураОтбора).Цена;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКоличество(ТекДата, ТекНоменклатура, Сырье)
	
	Если НЕ ЗначениеЗаполнено(ТекНоменклатура) Тогда
		Возврат 0
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Сырье) Тогда
		Возврат 0
	КонецЕсли;
	
	//
	СтруктураОтбора = Новый Структура("Номенклатура, НоменклатураСырье", ТекНоменклатура, Сырье);
	
	Результат = РегистрыСведений.СпецификацииПоставщиков.ПолучитьПоследнее(ТекДата, СтруктураОтбора).Количество;
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОстатокНаСкладах(ТекНоменклатура)
	
	Если НЕ ЗначениеЗаполнено(ТекНоменклатура) Тогда
		Возврат 0
	КонецЕсли;
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ТекНоменклатура);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	СУММА(ТоварыНаСкладахОстатки.КоличествоОстаток) КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Номенклатура = &Номенклатура) КАК ТоварыНаСкладахОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыНаСкладахОстатки.Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Количество;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОстатокПоЗаявкамНаПроизводство(ТекНоменклатура)
	
	Если НЕ ЗначениеЗаполнено(ТекНоменклатура) Тогда
		Возврат 0
	КонецЕсли;
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ТекНоменклатура);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаявкиОстатки.Номенклатура,
	|	СУММА(ЗаявкиОстатки.КоличествоКПроизводствуОстаток) КАК Количество
	|ИЗ
	|	РегистрНакопления.АК_ЗаявкиНаПроизводствоПерсональнойУпаковки.Остатки(, Номенклатура = &Номенклатура) КАК ЗаявкиОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкиОстатки.Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Количество;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

Процедура ПрочитатьЦеныИОстатки()
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата"		, Объект.Дата);
	Запрос.УстановитьПараметр("Таб"			, РеквизитФормыВЗначение("Объект").РасходникиКЗамене.Выгрузить());
	Запрос.УстановитьПараметр("Номенклатура", Объект.Номенклатура);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб.НомерСтроки,
	|	Таб.Номенклатура
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	&Таб КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацииПоставщиковСрезПоследних.НоменклатураСырье,
	|	СпецификацииПоставщиковСрезПоследних.Количество,
	|	СпецификацииПоставщиковСрезПоследних.Период
	|ПОМЕСТИТЬ Спецификации
	|ИЗ
	|	РегистрСведений.СпецификацииПоставщиков.СрезПоследних(
	|			&Дата,
	|			Номенклатура = &Номенклатура
	|				И НоменклатураСырье В
	|					(ВЫБРАТЬ
	|						Т.Номенклатура
	|					ИЗ
	|						СписокНоменклатуры КАК Т)) КАК СпецификацииПоставщиковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(ЦеныПоставщиковСрезПоследних.Цена, 0)) КАК ТекущаяЦена,
	|	СписокНоменклатуры.Номенклатура,
	|	ЕСТЬNULL(ДанныеКоличество.Количество, 0) КАК КоличествоПоСпецификации,
	|	ЕСТЬNULL(ЦеныПоставщиковСрезПоследних.Цена, 0) * ЕСТЬNULL(ДанныеКоличество.Количество, 0) КАК Сумма,
	|	ЕСТЬNULL(ВТОстаткиТоваров.Количество, 0) КАК ОстатокНаСкладах,
	|	ЕСТЬNULL(ВТОстаткиПоЗаявкам.Количество, 0) КАК ОстатокПоЗаявкамНаПроизводство
	|ИЗ
	|	СписокНоменклатуры КАК СписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЦеныПоставщиковСрезПоследних.Номенклатура КАК Номенклатура,
	|			МАКСИМУМ(ЦеныПоставщиковСрезПоследних.Период) КАК Период
	|		ИЗ
	|			РегистрСведений.ЦеныПоставщиков.СрезПоследних(
	|					&Дата,
	|					Номенклатура В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура
	|						ИЗ
	|							СписокНоменклатуры КАК Т)) КАК ЦеныПоставщиковСрезПоследних
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ЦеныПоставщиковСрезПоследних.Номенклатура) КАК ПоследниеЦены
	|		ПО СписокНоменклатуры.Номенклатура = ПоследниеЦены.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныПоставщиков.СрезПоследних(
	|				&Дата,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						Т.Номенклатура
	|					ИЗ
	|						СписокНоменклатуры КАК Т)) КАК ЦеныПоставщиковСрезПоследних
	|		ПО СписокНоменклатуры.Номенклатура = ЦеныПоставщиковСрезПоследних.Номенклатура
	|			И (ПоследниеЦены.Период = ЦеныПоставщиковСрезПоследних.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВложенныйЗапрос.НоменклатураСырье КАК НоменклатураСырье,
	|			МАКСИМУМ(Спецификации.Количество) КАК Количество
	|		ИЗ
	|			(ВЫБРАТЬ
	|				Спецификации.НоменклатураСырье КАК НоменклатураСырье,
	|				МАКСИМУМ(Спецификации.Период) КАК Период
	|			ИЗ
	|				Спецификации КАК Спецификации
	|			
	|			СГРУППИРОВАТЬ ПО
	|				Спецификации.НоменклатураСырье) КАК ВложенныйЗапрос
	|				ЛЕВОЕ СОЕДИНЕНИЕ Спецификации КАК Спецификации
	|				ПО ВложенныйЗапрос.НоменклатураСырье = Спецификации.НоменклатураСырье
	|					И ВложенныйЗапрос.Период = Спецификации.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВложенныйЗапрос.НоменклатураСырье) КАК ДанныеКоличество
	|		ПО СписокНоменклатуры.Номенклатура = ДанныеКоличество.НоменклатураСырье
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОстаткиТоваров.Номенклатура КАК Номенклатура,
	|			СУММА(ОстаткиТоваров.КоличествоОстаток) КАК Количество
	|		ИЗ
	|			РегистрНакопления.ТоварыНаСкладах.Остатки(
	|					,
	|					Номенклатура В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура
	|						ИЗ
	|							СписокНоменклатуры КАК Т)) КАК ОстаткиТоваров
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОстаткиТоваров.Номенклатура) КАК ВТОстаткиТоваров
	|		ПО (ВТОстаткиТоваров.Номенклатура = СписокНоменклатуры.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОстаткиПоЗаявкам.Номенклатура КАК Номенклатура,
	|			СУММА(ОстаткиПоЗаявкам.КоличествоКПроизводствуОстаток) КАК Количество
	|		ИЗ
	|			РегистрНакопления.АК_ЗаявкиНаПроизводствоПерсональнойУпаковки.Остатки(
	|					,
	|					Номенклатура В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура
	|						ИЗ
	|							СписокНоменклатуры КАК Т)) КАК ОстаткиПоЗаявкам
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОстаткиПоЗаявкам.Номенклатура) КАК ВТОстаткиПоЗаявкам
	|		ПО (ВТОстаткиПоЗаявкам.Номенклатура = СписокНоменклатуры.Номенклатура)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокНоменклатуры.НомерСтроки,
	|	СписокНоменклатуры.Номенклатура,
	|	ЕСТЬNULL(ДанныеКоличество.Количество, 0),
	|	ЕСТЬNULL(ЦеныПоставщиковСрезПоследних.Цена, 0) * ЕСТЬNULL(ДанныеКоличество.Количество, 0),
	|	ЕСТЬNULL(ВТОстаткиТоваров.Количество, 0),
	|	ЕСТЬNULL(ВТОстаткиПоЗаявкам.Количество, 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписокНоменклатуры.НомерСтроки";
	
	//
	Объект.РасходникиКЗамене.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ПодобратьПоставщикаНаСервере()
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата"			, Объект.Дата);
	Запрос.УстановитьПараметр("Номенклатура"	, Объект.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика"	, Объект.Характеристика);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЦеныПоставщиковСрезПоследних.Поставщик
	|ИЗ
	|	РегистрСведений.ЦеныПоставщиков.СрезПоследних(
	|			&Дата,
	|			Номенклатура = &Номенклатура
	|				И Характеристика = &Характеристика) КАК ЦеныПоставщиковСрезПоследних";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Объект.Поставщик = Выборка.Поставщик;
	КонецЕсли;
	//ПоследняяЦена = РегистрыСведений.ЦеныПоставщиков.ПолучитьПоследнее(Объект.Дата, Новый Структура("Номенклатура, Характеристика", Объект.Номенклатура, Объект.Характеристика));
	//Объект.Поставщик = ПоследняяЦена.Поставщик;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИтоги()
	
	ЭтаФорма.СтоимостьМатериалов 	= Объект.РасходникиКЗамене.Итог("Сумма");
	ЭтаФорма.ТекущаяСтоимость 		= ЭтаФорма.СтоимостьМатериалов + ЭтаФорма.ЦенаПродукта;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	ПоказатьИтоги();
	
	//+++АК sils 08.06.2018 ИП-00018876
	APDEX_ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(ОперацияАпдекс, ?(Параметры.Ключ.Пустая(), "Новый документ", "" + Объект.Ссылка));
	//---АК
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//+++АК sils 08.06.2018 ИП-00018876
	ОперацияАпдекс = APDEX_ОценкаПроизводительностиКлиентСервер.ПолучитьОперацию("Открытие документа Переход на свою упаковку");
	APDEX_ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(ОперацияАпдекс);
	//---АК
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		ЭтаФорма.ЦенаПродукта = ПолучитьЦену(Объект.Дата, Объект.Номенклатура, Объект.Характеристика);
		ПрочитатьЦеныИОстатки();
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Объект.СогласованоСТехнологом Тогда
		Возврат
	КонецЕсли;
	
	//
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Если Объект.Поставщик.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС
				ИЛИ Объект.Поставщик.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
			
			ТекСтавкаНДС = УчетНДС.ПолучитьСтавкуНДС(Объект.Номенклатура.СтавкаНДС);
			ПроцентПревышения = Окр(( ТекущийОбъект.ЦенаНовая * (1 + ТекСтавкаНДС / 100) /
								(ЭтаФорма.СтоимостьМатериалов + ЭтаФорма.ЦенаПродукта * (1 + ТекСтавкаНДС / 100)) - 1) * 100, 2);
			
		Иначе
			ПроцентПревышения = Окр((ТекущийОбъект.ЦенаНовая / ЭтаФорма.ТекущаяСтоимость - 1) * 100, 2);
		КонецЕсли;
		
		Если ПроцентПревышения > 5 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Новая цена превышает текущую стоимость на " + ПроцентПревышения + "%. Документ не проведен";
			Сообщение.Поле 	= "ЦенаНовая";
			Сообщение.УстановитьДанные(Объект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПоказатьИтоги();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПрочитатьЦеныИОстатки();
	
КонецПроцедуры


&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ЭтаФорма.ЦенаПродукта = ПолучитьЦену(Объект.Дата, Объект.Номенклатура, Объект.Характеристика);
	ПоказатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЦены(Команда)
	
	ПрочитатьЦеныИОстатки();
	НоменклатураПриИзменении(Неопределено);
	//ПоказатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ЭтаФорма.ЦенаПродукта = ПолучитьЦену(Объект.Дата, Объект.Номенклатура, Объект.Характеристика);
	ПоказатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	ЭтаФорма.ЦенаПродукта = ПолучитьЦену(Объект.Дата, Объект.Номенклатура, Объект.Характеристика);
	ПоказатьИтоги();
	
	ПодобратьПоставщикаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтработанПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры


Процедура ЗаполнитьРасходникиКЗаменеСервер()
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период"			, Объект.Дата);
	Запрос.УстановитьПараметр("Номенклатура"	, Объект.Номенклатура);
	Запрос.УстановитьПараметр("Поставщик"		, ?(Объект.Поставщик.Пустая(), Объект.Поставщик, Объект.Поставщик.ГоловнойКонтрагент));
	Запрос.УстановитьПараметр("Характеристика"	, Объект.Характеристика);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпецификацииПоставщиковСрезПоследних.Спецификация,
	|	МАКСИМУМ(СпецификацииПоставщиковСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ Спецификации
	|ИЗ
	|	РегистрСведений.СпецификацииПоставщиков.СрезПоследних(
	|			&Период,
	|			Поставщик.ГоловнойКонтрагент = &Поставщик
	|				И Характеристика = &Характеристика
	|				И Номенклатура = &Номенклатура
	|				И (ДатаКонца = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ ДатаКонца > &Период)) КАК СпецификацииПоставщиковСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацииПоставщиковСрезПоследних.Спецификация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацииПоставщиковСрезПоследних.НоменклатураСырье КАК Упаковка
	|ИЗ
	|	Спецификации КАК Спецификации,
	|	РегистрСведений.СпецификацииПоставщиков.СрезПоследних(
	|			&Период,
	|			Номенклатура = &Номенклатура
	|				И Поставщик.ГоловнойКонтрагент = &Поставщик
	|				И Характеристика = &Характеристика) КАК СпецификацииПоставщиковСрезПоследних
	|ГДЕ
	|	(СпецификацииПоставщиковСрезПоследних.ДатаКонца = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ СпецификацииПоставщиковСрезПоследних.ДатаКонца > &Период)";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Объект.РасходникиКЗамене.Очистить();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.РасходникиКЗамене.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Упаковка;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасходникиКЗамене(Команда)
	
	ЗаполнитьРасходникиКЗаменеСервер();
	
	ОбновитьЦены(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура РасходникиКЗаменеПриИзменении(Элемент)
	
	ОбновитьЦены(Неопределено)
	
КонецПроцедуры

&НаКлиенте
Процедура РасходникиКЗаменеНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасходникиКЗамене.ТекущиеДанные;
	ТекущиеДанные.ТекущаяЦена 				= ПолучитьЦену(Объект.Дата, ТекущиеДанные.Номенклатура);
	ТекущиеДанные.КоличествоПоСпецификации 	= ПолучитьКоличество(Объект.Дата, Объект.Номенклатура, ТекущиеДанные.Номенклатура);
	ТекущиеДанные.Сумма 					= ТекущиеДанные.ТекущаяЦена * ТекущиеДанные.КоличествоПоСпецификации;
	ТекущиеДанные.ОстатокНаСкладах			= ПолучитьОстатокНаСкладах(ТекущиеДанные.Номенклатура);
	ТекущиеДанные.ОстатокПоЗаявкамНаПроизводство = ПолучитьОстатокПоЗаявкамНаПроизводство(ТекущиеДанные.Номенклатура);
	                                                                    
	ПоказатьИтоги();
	
КонецПроцедуры
