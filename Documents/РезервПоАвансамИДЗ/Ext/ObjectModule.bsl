
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Финансовый.Записывать = Истина;
	Движения.Финансовый.Очистить();

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезервПоАвансамИДЗНачислениеСписаниеРезерва.Контрагент,
		|	РезервПоАвансамИДЗНачислениеСписаниеРезерва.Ссылка.Дата,
		|	РезервПоАвансамИДЗНачислениеСписаниеРезерва.Ссылка.Организация,
		|	СУММА(РезервПоАвансамИДЗНачислениеСписаниеРезерва.КНачислению) КАК КНачислению,
		|	СУММА(РезервПоАвансамИДЗНачислениеСписаниеРезерва.КСписанию) КАК КСписанию,
		|	ЗНАЧЕНИЕ(ПланСчетов.Финансовый.РезервНаСписаниеБезнадежнойДЗ) КАК СчетРезервы,
		|	НастройкаПроведения.Счет КАК СчетЗатраты,
		|	НастройкаПроведения.СтатьяДоходовРасходов,
		|	НастройкаПроведения.СтруктурнаяЕдиница КАК ТорговаяТочка,
		|	НастройкаПроведения.ЦФО
		|ПОМЕСТИТЬ втТаблицаРезервы
		|ИЗ
		|	Документ.РезервПоАвансамИДЗ.НачислениеСписаниеРезерва КАК РезервПоАвансамИДЗНачислениеСписаниеРезерва
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НастройкаОтраженияОперацийВУчетеСрезПоследних.ЦФО КАК ЦФО,
		|			НастройкаОтраженияОперацийВУчетеСрезПоследних.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
		|			НастройкаОтраженияОперацийВУчетеСрезПоследних.Счет КАК Счет,
		|			НастройкаОтраженияОперацийВУчетеСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
		|		ИЗ
		|			РегистрСведений.НастройкаОтраженияОперацийВУчете.СрезПоследних(&ДатаОкончания, ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.Резервы_БезнадежнаяДЗ)) КАК НастройкаОтраженияОперацийВУчетеСрезПоследних) КАК НастройкаПроведения
		|		ПО (ИСТИНА)
		|ГДЕ
		|	РезервПоАвансамИДЗНачислениеСписаниеРезерва.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РезервПоАвансамИДЗНачислениеСписаниеРезерва.Контрагент,
		|	РезервПоАвансамИДЗНачислениеСписаниеРезерва.Ссылка.Дата,
		|	РезервПоАвансамИДЗНачислениеСписаниеРезерва.Ссылка.Организация,
		|	НастройкаПроведения.Счет,
		|	НастройкаПроведения.СтатьяДоходовРасходов,
		|	НастройкаПроведения.СтруктурнаяЕдиница,
		|	НастройкаПроведения.ЦФО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТаблицаРезервы.Контрагент,
		|	втТаблицаРезервы.Дата КАК Период,
		|	втТаблицаРезервы.Организация,
		|	втТаблицаРезервы.КНачислению КАК Сумма,
		|	втТаблицаРезервы.СчетРезервы КАК СчетКт,
		|	втТаблицаРезервы.СчетЗатраты КАК СчетДт,
		|	втТаблицаРезервы.СтатьяДоходовРасходов,
		|	втТаблицаРезервы.ТорговаяТочка,
		|	втТаблицаРезервы.ЦФО
		|ПОМЕСТИТЬ втКНачислению
		|ИЗ
		|	втТаблицаРезервы КАК втТаблицаРезервы
		|ГДЕ
		|	втТаблицаРезервы.КНачислению > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТаблицаРезервы.Контрагент,
		|	втТаблицаРезервы.Дата КАК Период,
		|	втТаблицаРезервы.Организация,
		|	втТаблицаРезервы.КСписанию КАК Сумма,
		|	втТаблицаРезервы.СчетРезервы КАК СчетДт,
		|	втТаблицаРезервы.СчетЗатраты КАК СчетКт,
		|	втТаблицаРезервы.СтатьяДоходовРасходов,
		|	втТаблицаРезервы.ТорговаяТочка,
		|	втТаблицаРезервы.ЦФО
		|ПОМЕСТИТЬ втКСписанию
		|ИЗ
		|	втТаблицаРезервы КАК втТаблицаРезервы
		|ГДЕ
		|	втТаблицаРезервы.КСписанию > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втКНачислению.Контрагент,
		|	втКНачислению.Период,
		|	втКНачислению.Организация,
		|	втКНачислению.Сумма КАК СуммаМСФО,
		|	втКНачислению.СчетКт,
		|	втКНачислению.СчетДт,
		|	втКНачислению.СтатьяДоходовРасходов,
		|	втКНачислению.ТорговаяТочка,
		|	втКНачислению.ЦФО
		|ИЗ
		|	втКНачислению КАК втКНачислению
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втКСписанию.Контрагент,
		|	втКСписанию.Период,
		|	втКСписанию.Организация,
		|	втКСписанию.Сумма,
		|	втКСписанию.СчетКт,
		|	втКСписанию.СчетДт,
		|	втКСписанию.СтатьяДоходовРасходов,
		|	втКСписанию.ТорговаяТочка,
		|	втКСписанию.ЦФО
		|ИЗ
		|	втКСписанию КАК втКСписанию";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
    Запрос.УстановитьПараметр("ДатаОкончания", Дата);       
                    
	ВыборкаДвижения =Запрос.Выполнить().Выбрать();

	
	Пока ВыборкаДвижения.Следующий() Цикл
		
		НоваяПроводка = Движения.Финансовый.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка,ВыборкаДвижения);
		
		Если ВыборкаДвижения.СчетДт = ПланыСчетов.Финансовый.РезервНаСписаниеБезнадежнойДЗ Тогда
			СубконтоРезерв = НоваяПроводка.СубконтоДт;
			СубконтоРасходы = НоваяПроводка.СубконтоКт;			
			СчетРасходы = ВыборкаДвижения.СчетКт;				
		Иначе 
			СубконтоРезерв = НоваяПроводка.СубконтоКт;
			СубконтоРасходы = НоваяПроводка.СубконтоДт; 
			СчетРасходы = ВыборкаДвижения.СчетДт;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(СчетРасходы) Тогда
			Сообщить("Не установлена настройка проведения");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		СубконтоРезерв.Контрагенты = ВыборкаДвижения.Контрагент;
			

		Если ЗначениеЗаполнено(ВыборкаДвижения.ЦФО) Тогда
			СубконтоРасходы.ЦФО = ВыборкаДвижения.ЦФО
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаДвижения.ТорговаяТочка) Тогда
			СубконтоРасходы.ТорговыеТочки = ВыборкаДвижения.ТорговаяТочка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаДвижения.СтатьяДоходовРасходов) Тогда
			СубконтоРасходы.СтатьиДоходовРасходов = ВыборкаДвижения.СтатьяДоходовРасходов
		КонецЕсли;
		
	КонецЦикла;

	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезервПоАвансамИДЗСписаниеДолгов.Ссылка.Дата КАК Период,
		|	РезервПоАвансамИДЗСписаниеДолгов.Ссылка.Организация,
		|	РезервПоАвансамИДЗСписаниеДолгов.Контрагент,
		|	РезервПоАвансамИДЗСписаниеДолгов.СчетУчета КАК СчетКт,
		|	РезервПоАвансамИДЗСписаниеДолгов.Сумма КАК СуммаМСФО,
		|	ЗНАЧЕНИЕ(ПланСчетов.Финансовый.РезервНаСписаниеБезнадежнойДЗ) КАК СчетДт
		|ИЗ
		|	Документ.РезервПоАвансамИДЗ.СписаниеДолгов КАК РезервПоАвансамИДЗСписаниеДолгов
		|ГДЕ
		|	РезервПоАвансамИДЗСписаниеДолгов.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяПроводка = Движения.Финансовый.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка,ВыборкаДетальныеЗаписи);
		НоваяПроводка.СубконтоДт.Контрагенты = ВыборкаДетальныеЗаписи.Контрагент;
		НоваяПроводка.СубконтоКт.Контрагенты = ВыборкаДетальныеЗаписи.Контрагент;
		НоваяПроводка.СубконтоКт.Организации = ВыборкаДетальныеЗаписи.Организация;

	КонецЦикла;

	
КонецПроцедуры
