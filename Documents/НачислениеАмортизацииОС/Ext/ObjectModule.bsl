//+++AK susk
Перем мНеКорректироватьПервоначальнуюСтоимость Экспорт;
//---AK susk

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//+++AK susk
	// скорректируем первоначальную стоимость
	КорректировкаПервоначальнойСтоимости();
	//---Ak susk
	
	Запрос = Новый Запрос;
	//+++AK susk
	// поменял, чтобы списание еще учитывалось в месяце списания, а на след. месяц амортизация уже не считалась.
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПринятыеКУчетуОССрезПоследних.ОсновноеСредство,
	               |	ПринятыеКУчетуОССрезПоследних.НачальнаяСтоимость / ПринятыеКУчетуОССрезПоследних.СрокАмортизации КАК СуммаСписанияЗаМесяц,
	               |	ЕСТЬNULL(ПринятыеКУчетуОССрезПоследних.НачальнаяСтоимость, 0) КАК НачальнаяСтоимость,
	               |	ПринятыеКУчетуОССрезПоследних.СрокАмортизации,
	               |	ПринятыеКУчетуОССрезПоследних.ЭтоОприходование,
	               |	ПринятыеКУчетуОССрезПоследних.СтатьяДР
	               |ПОМЕСТИТЬ втПринятыеКУчету
	               |ИЗ
	               |	РегистрСведений.ПринятыеКУчетуОС.СрезПоследних(&ГраницаОкончания, Организация = &Организация) КАК ПринятыеКУчетуОССрезПоследних
				   |	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
				   |	ПО ПринятыеКУчетуОССрезПоследних.ОсновноеСредство = ОсновныеСредства.Ссылка
	               |ГДЕ
	               |	ПринятыеКУчетуОССрезПоследних.НачислятьАмортизацию
				   |	И ОсновныеСредства.ВидОсновногоСредства <> ЗНАЧЕНИЕ(Перечисление.ВидыОсновныхСредств.НеметериальныйАктив)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФинансовыйОстатки.Субконто1 КАК ОсновноеСредство,
	               |	ФинансовыйОстатки.СуммаОстаток КАК Сумма,
	               |	ФинансовыйОстатки.Счет
	               |ПОМЕСТИТЬ втСтоимостьОС
	               |ИЗ
	               |	РегистрБухгалтерии.Финансовый.Остатки(
	               |			&ГраницаНачала,
	               |			Счет = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.ОсновныеСредстваВОрганизации)
	               |				ИЛИ Счет = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.ОсновныеСредстваДо100000),
	               |			,
	               |			Субконто1 В
	               |				(ВЫБРАТЬ
	               |					втПринятыеКУчету.ОсновноеСредство
	               |				ИЗ
	               |					втПринятыеКУчету)) КАК ФинансовыйОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ				   
				   |	втПринятыеКУчету.ОсновноеСредство,
				   //+++AK susk
				   //|	СУММА(ВЫБОР
				   //|			КОГДА втСтоимостьОС.Сумма > 0
	               ////|				ТОГДА ВЫБОР
				   ////|						КОГДА втСтоимостьОС.Сумма - НачисленноАмортизации.Сумма < втСтоимостьОС.Сумма / втПринятыеКУчету.СрокАмортизации + 1
				   ////|							ТОГДА втПринятыеКУчету.НачальнаяСтоимость - НачисленноАмортизации.Сумма
				   ////|						ИНАЧЕ втСтоимостьОС.Сумма / втПринятыеКУчету.СрокАмортизации
				   ////|					КОНЕЦ
				   //|				ТОГДА втСтоимостьОС.Сумма / втПринятыеКУчету.СрокАмортизации		
				   ////|			ИНАЧЕ ВЫБОР
				   ////|					КОГДА втПринятыеКУчету.НачальнаяСтоимость - НачисленноАмортизации.Сумма < втПринятыеКУчету.СуммаСписанияЗаМесяц + 1
				   ////|						ТОГДА втПринятыеКУчету.НачальнаяСтоимость - НачисленноАмортизации.Сумма
				   ////|					ИНАЧЕ втПринятыеКУчету.СуммаСписанияЗаМесяц
				   ////|				КОНЕЦ
				   //|			ИНАЧЕ втПринятыеКУчету.СуммаСписанияЗаМесяц						   
				   //|		КОНЕЦ) КАК Сумма,				   
				   |	СУММА(ВЫБОР
				   |	КОГДА ВЫБОР
				   |			КОГДА втСтоимостьОС.Сумма > 0
				   |				ТОГДА втСтоимостьОС.Сумма / втПринятыеКУчету.СрокАмортизации
				   |			ИНАЧЕ втПринятыеКУчету.СуммаСписанияЗаМесяц
				   |		КОНЕЦ + НачисленноАмортизации.Сумма > втСтоимостьОС.Сумма
				   |		ТОГДА втСтоимостьОС.Сумма - НачисленноАмортизации.Сумма
				   |	ИНАЧЕ ВЫБОР
				   |			КОГДА втСтоимостьОС.Сумма > 0
				   |				ТОГДА втСтоимостьОС.Сумма / втПринятыеКУчету.СрокАмортизации
				   |			ИНАЧЕ втПринятыеКУчету.СуммаСписанияЗаМесяц
				   |		КОНЕЦ
				   |КОНЕЦ) КАК Сумма,
				   //---AK susk
	               |	втСтоимостьОС.Счет,
	               |	втПринятыеКУчету.ЭтоОприходование,
	               |	втПринятыеКУчету.СтатьяДР
	               |ПОМЕСТИТЬ втТребуетсяНачисление
	               |ИЗ
	               |	втПринятыеКУчету КАК втПринятыеКУчету
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ФинансовыйДвиженияССубконто.СубконтоКт1 КАК ОсновноеСредство,
	               |			СУММА(ЕСТЬNULL(ФинансовыйДвиженияССубконто.Сумма, 0)) КАК Сумма
	               |		ИЗ
	               |			РегистрБухгалтерии.Финансовый.ДвиженияССубконто(
	               |					,
	               |					&ДатаНачала,
	               |					СубконтоКт1 В
	               |							(ВЫБРАТЬ
	               |								втПринятыеКУчету.ОсновноеСредство
	               |							ИЗ
	               |								втПринятыеКУчету)
	               |						И (СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.АмортизацияОсновныхСредств)
	               |							ИЛИ СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.АмортизацияОсновныхСредствДо100000)),
	               |					,
	               |					) КАК ФинансовыйДвиженияССубконто
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ФинансовыйДвиженияССубконто.СубконтоКт1) КАК НачисленноАмортизации
	               |		ПО втПринятыеКУчету.ОсновноеСредство = НачисленноАмортизации.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьОС КАК втСтоимостьОС
	               |		ПО втПринятыеКУчету.ОсновноеСредство = втСтоимостьОС.ОсновноеСредство
	               |ГДЕ
	               |	ЕСТЬNULL(втПринятыеКУчету.НачальнаяСтоимость, 0) > ЕСТЬNULL(НачисленноАмортизации.Сумма, 0)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втПринятыеКУчету.ОсновноеСредство,
	               |	втСтоимостьОС.Счет,
	               |	втПринятыеКУчету.ЭтоОприходование,
	               |	втПринятыеКУчету.СтатьяДР
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА СостояниеОССрезПоследних.Местоположение ССЫЛКА Справочник.Склады
	               |			ТОГДА СостояниеОССрезПоследних.Местоположение.Владелец
	               |		ИНАЧЕ СостояниеОССрезПоследних.Местоположение
	               |	КОНЕЦ КАК ТорговаяТочка,
	               |	втТребуетсяНачисление.ОсновноеСредство,
	               |	втТребуетсяНачисление.Сумма,
	               |	втТребуетсяНачисление.Счет,
	               |	втТребуетсяНачисление.ЭтоОприходование,
	               |	втТребуетсяНачисление.СтатьяДР
	               |ПОМЕСТИТЬ втПоложениеОС
	               |ИЗ
	               |	втТребуетсяНачисление КАК втТребуетсяНачисление
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОС.СрезПоследних(&ДатаОкончания, ) КАК СостояниеОССрезПоследних
	               |		ПО втТребуетсяНачисление.ОсновноеСредство = СостояниеОССрезПоследних.ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втРезультат.ТорговаяТочка,
	               |	втРезультат.ОсновноеСредство,
	               |	втРезультат.Сумма,
	               |	ВЫБОР
	               |		КОГДА втРезультат.ЭтоОприходование
	               |			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизацииОприходованныхОС)
	               |		ИНАЧЕ ВЫБОР
	               |				КОГДА втРезультат.Счет = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.ОсновныеСредстваДо100000)
	               |					ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизацииДо100000)
	               |				ИНАЧЕ ВЫБОР
	               |						КОГДА втРезультат.ТорговаяТочка.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	               |							ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизацииХранениеНаСкладе)
	               |						КОГДА втРезультат.ТорговаяТочка.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	               |								ИЛИ втРезультат.ТорговаяТочка ЕСТЬ NULL 
	               |								ИЛИ втРезультат.ТорговаяТочка = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяССылка)
	               |							ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизацииВПодразделении)
	               |						ИНАЧЕ ВЫБОР
	               |								КОГДА РабочиеТорговыеТочки.РабочаяТочка = ИСТИНА
	               |									ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизации)
	               |								ИНАЧЕ ВЫБОР
	               |										КОГДА ОСЭксплуатируется.Эксплуатируется = ИСТИНА
	               |											ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизацииНаСкладе)
	               |										ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизацииХранениеНаСкладе)
	               |									КОНЕЦ
	               |							КОНЕЦ
	               |					КОНЕЦ
	               |			КОНЕЦ
	               |	КОНЕЦ КАК ВидАмортизация,
	               |	ВЫБОР
	               |		КОГДА втРезультат.ТорговаяТочка.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ВЫБОР
	               |				КОГДА РабочиеТорговыеТочки.РабочаяТочка = ИСТИНА
	               |					ТОГДА ЛОЖЬ
	               |				ИНАЧЕ ВЫБОР
	               |						КОГДА ОСЭксплуатируется.Эксплуатируется = ИСТИНА
	               |							ТОГДА ЛОЖЬ
	               |						ИНАЧЕ ЛОЖЬ
	               |					КОНЕЦ
	               |			КОНЕЦ
	               |	КОНЕЦ КАК ЭтоХранениеСклад,
	               |	втРезультат.Счет,
	               |	ВЫБОР
	               |		КОГДА втРезультат.ТорговаяТочка.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Подразделение)
	               |				ИЛИ втРезультат.ТорговаяТочка ЕСТЬ NULL 
	               |				ИЛИ втРезультат.ТорговаяТочка = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяССылка)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ЭтоПодразделение,
	               |	втРезультат.ЭтоОприходование,
	               |	втРезультат.СтатьяДР
	               |ПОМЕСТИТЬ втРезультат
	               |ИЗ
	               |	втПоложениеОС КАК втРезультат
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |			ЛистУчета.ТорговаяТочка КАК ТорговаяТочка,
	               |			ИСТИНА КАК РабочаяТочка
	               |		ИЗ
	               |			Документ.ЛистУчета КАК ЛистУчета
	               |		ГДЕ
	               |			ЛистУчета.Проведен = ИСТИНА
	               |			И ЛистУчета.Дата <= &ДатаОкончания
	               |			И ЛистУчета.Дата >= &ДатаНачала) КАК РабочиеТорговыеТочки
	               |		ПО (втРезультат.ТорговаяТочка = РабочиеТорговыеТочки.ТорговаяТочка
	               |				ИЛИ втРезультат.ТорговаяТочка.Владелец = РабочиеТорговыеТочки.ТорговаяТочка)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			СостояниеОС.ОсновноеСредство КАК ОсновноеСредство,
	               |			ИСТИНА КАК Эксплуатируется
	               |		ИЗ
	               |			РегистрСведений.СостояниеОС КАК СостояниеОС
	               |		ГДЕ
	               |			СостояниеОС.ОсновноеСредство В
	               |					(ВЫБРАТЬ
	               |						втПоложениеОС.ОсновноеСредство
	               |					ИЗ
	               |						втПоложениеОС)
	               |			И СостояниеОС.Эксплуатируется = ИСТИНА
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			СостояниеОС.ОсновноеСредство) КАК ОСЭксплуатируется
	               |		ПО втРезультат.ОсновноеСредство = ОСЭксплуатируется.ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втРезультат.ТорговаяТочка,
	               |	втРезультат.ОсновноеСредство,
	               |	втРезультат.Сумма,
	               |	СпособНачисленияАмортизации.ЦФО,
	               |	СпособНачисленияАмортизации.СтатьяДоходовРасходов,
	               |	СпособНачисленияАмортизации.СчетДт,
	               |	СпособНачисленияАмортизации.ТорговаяТочка КАК НастройкаТорговаяТочка,
	               |	втРезультат.ЭтоХранениеСклад КАК ЭтоСклад,
	               |	втРезультат.Счет,
	               |	втРезультат.ЭтоПодразделение,
	               |	&Организация КАК Организация,
	               |	втРезультат.СтатьяДР КАК СтатьяДРизПоступления,
	               |	втРезультат.ОсновноеСредство.ИнвентарныйНомер
	               |ИЗ
	               |	втРезультат КАК втРезультат
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			Срез.Счет КАК СчетДт,
	               |			Срез.СтруктурнаяЕдиница КАК ТорговаяТочка,
	               |			Срез.ЦФО КАК ЦФО,
	               |			Срез.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	               |			Срез.ВидОперации КАК ВидОперации
	               |		ИЗ
	               |			РегистрСведений.НастройкаОтраженияОперацийВУчете.СрезПоследних(&ДатаОкончания, ) КАК Срез) КАК СпособНачисленияАмортизации
	               |		ПО втРезультат.ВидАмортизация = СпособНачисленияАмортизации.ВидОперации
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОС.СрезПоследних(&ДатаНачала, ) КАК СостояниеОССрезПоследних
	               |		ПО втРезультат.ОсновноеСредство = СостояниеОССрезПоследних.ОсновноеСредство
	               |ГДЕ
	               |	НЕ ЕСТЬNULL(СостояниеОССрезПоследних.Списано, ЛОЖЬ)";
	//---AK susk
	
	//+++Суслин К.В.
	Если ЗначениеЗаполнено(ВременноТекстЗапросаВнешний) Тогда
		// небольшая хитрость, чтобы не ждать обновления.
		Запрос.Текст = ВременноТекстЗапросаВнешний;
	КонецЕсли;
	//---	
		
	Граница = Новый Граница(КонецМесяца(Дата),ВидГраницы.Включая);
	
	//+++AK susk
	ГраницаНачала = Новый Граница(Началомесяца(Дата),ВидГраницы.Включая);
	Запрос.УстановитьПараметр("ГраницаНачала", ГраницаНачала);
	//---AK susk
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("ГраницаОкончания",Граница);
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.Выполнить();

	ДвиженияФинансовый = РезультатЗапроса.Выгрузить();	
	НастройкаСписанияОприходования = ПолучитьНастройкуСписания(Перечисления.ВидыОперацийВУчете.ОсновныеСредства_НачислениеАмортизацииОприходованныхОС);
		
	ЭтотОбъект.Движения.Финансовый.Записывать = Истина;
	ЭтотОбъект.Движения.Финансовый.Очистить();
	
	Для каждого СтрокаФинансовый из ДвиженияФинансовый Цикл
		
		НоваяПроводка = ЭтотОбъект.Движения.Финансовый.Добавить();
		НоваяПроводка.Период = Дата;
		НоваяПроводка.Организация = СтрокаФинансовый.Организация;
		
		Если СтрокаФинансовый.Счет =  ПланыСчетов.Финансовый.ОсновныеСредстваДо100000 Тогда
			НоваяПроводка.СчетКт = ПланыСчетов.Финансовый.АмортизацияОсновныхСредствДо100000;
		Иначе 	
			НоваяПроводка.СчетКт = ПланыСчетов.Финансовый.АмортизацияОсновныхСредств;
		КонецЕсли;	
		НоваяПроводка.СубконтоКт.ОсновныеСредства = СтрокаФинансовый.ОсновноеСредство;
		
		Если СтрокаФинансовый.ЭтоСклад ИЛИ СтрокаФинансовый.ЭтоПодразделение Тогда
			//ВсеТТ
			Если ЗначениеЗаполнено(СтрокаФинансовый.НастройкаТорговаяТочка) Тогда
				НоваяПроводка.СубконтоДт.ТорговыеТочки = СтрокаФинансовый.НастройкаТорговаяТочка;
			КонецЕсли; 	
		Иначе 	
			Если ЗначениеЗаполнено(СтрокаФинансовый.ТорговаяТочка) Тогда
				НоваяПроводка.СубконтоДт.ТорговыеТочки = СтрокаФинансовый.ТорговаяТочка;
			КонецЕсли;
		КонецЕсли;	
			
		НоваяПроводка.СчетДт = СтрокаФинансовый.СчетДт;	
		
		Если ЗначениеЗаполнено(СтрокаФинансовый.СтатьяДРизПоступления) Тогда 
			НоваяПроводка.СубконтоДт.СтатьиДоходовРасходов = СтрокаФинансовый.СтатьяДРизПоступления;
		Иначе 	
			НоваяПроводка.СубконтоДт.СтатьиДоходовРасходов = СтрокаФинансовый.СтатьяДоходовРасходов;
		КонецЕсли;
		
		НоваяПроводка.СубконтоДт.ЦФО = СтрокаФинансовый.ЦФО;
		
		НоваяПроводка.Сумма = СтрокаФинансовый.Сумма; 
		
		НоваяПроводка.Содержание = "Отражение амортизации ОС за " + Формат(Дата,"ДФ='MMMM yyyy ""г.""'");
				
	КонецЦикла;	
		
КонецПроцедуры

Функция ПолучитьНастройкуСписания(ВидСписания)
	
	Возврат ОбщегоНазначенияСервер.ПолучитьНастройкиОтраженияОперацийВУчете(ВидСписания, Дата, Организация);
		
КонецФункции

Процедура РасчетАмортизации()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АК_УчетОССрезПоследних.ОсновноеСредство,
		|	АК_УчетОССрезПоследних.НачальнаяСтоимость,
		|	АК_УчетОССрезПоследних.СрокАмортизации,
		|	ВЫБОР
		|		КОГДА АК_УчетОССрезПоследних.СрокАмортизации > 0
		|			ТОГДА АК_УчетОССрезПоследних.НачальнаяСтоимость / АК_УчетОССрезПоследних.СрокАмортизации
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК АмортизацияЗаМесяц,
		|	АК_УчетОССрезПоследних.СчетУчетаАмортизация,
		|	АК_УчетОССрезПоследних.СчетУчета,
		|	АК_УчетОССрезПоследних.Организация,
		|	АК_УчетОССрезПоследних.ТорговаяТочка
		|ПОМЕСТИТЬ втОсновныеСредства
		|ИЗ
		|	РегистрСведений.АК_УчетОС.СрезПоследних(&КонецМесяца, ) КАК АК_УчетОССрезПоследних
		|ГДЕ
		|	АК_УчетОССрезПоследних.НачислятьАмортизацию = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОсновныеСредства.ОсновноеСредство,
		|	ЕСТЬNULL(АК_НачислениеАмортизацииОбороты.КоличествоОборот, 0) КАК Количество,
		|	ЕСТЬNULL(АК_НачислениеАмортизацииОбороты.СуммаОборот, 0) КАК Сумма,
		|	втОсновныеСредства.АмортизацияЗаМесяц,
		|	втОсновныеСредства.СчетУчетаАмортизация,
		|	втОсновныеСредства.СчетУчета,
		|	втОсновныеСредства.Организация,
		|	втОсновныеСредства.ТорговаяТочка
		|ПОМЕСТИТЬ втОсновныеСредстваДляНачисления
		|ИЗ
		|	втОсновныеСредства КАК втОсновныеСредства
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АК_НачислениеАмортизации.Обороты(, &НачалоМесяца, , ) КАК АК_НачислениеАмортизацииОбороты
		|		ПО втОсновныеСредства.ОсновноеСредство = АК_НачислениеАмортизацииОбороты.ОсновноеСредство
		|ГДЕ
		|	втОсновныеСредства.НачальнаяСтоимость > ЕСТЬNULL(АК_НачислениеАмортизацииОбороты.СуммаОборот, 0)
		|	И втОсновныеСредства.СрокАмортизации > ЕСТЬNULL(АК_НачислениеАмортизацииОбороты.КоличествоОборот, 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОсновныеСредстваДляНачисления.ОсновноеСредство,
		|	втОсновныеСредстваДляНачисления.АмортизацияЗаМесяц КАК Сумма,
		|	1 КАК Количество,
		|	&КонецМесяца КАК Период
		|ИЗ
		|	втОсновныеСредстваДляНачисления КАК втОсновныеСредстваДляНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОсновныеСредстваДляНачисления.ОсновноеСредство КАК ОсновноеСредство,
		|	втОсновныеСредстваДляНачисления.АмортизацияЗаМесяц КАК СуммаМСФО,
		|	втОсновныеСредстваДляНачисления.СчетУчета КАК СчетДт,
		|	втОсновныеСредстваДляНачисления.СчетУчетаАмортизация КАК СчетКт,
		|	&Содержание КАК Содержание,
		|	втОсновныеСредстваДляНачисления.Организация,
		|	втОсновныеСредстваДляНачисления.ТорговаяТочка,
		|	&КонецМесяца КАК Период
		|ИЗ
		|	втОсновныеСредстваДляНачисления КАК втОсновныеСредстваДляНачисления";

	Запрос.УстановитьПараметр("НачалоМесяца",НачалоМесяца(Дата)-1);
	Запрос.УстановитьПараметр("КонецМесяца",КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Содержание","Отражение амортизации ОС за " + Формат(Дата,"ДФ='MMMM yyyy ""г.""'"));
		
	РезультатЗапроса = Запрос.ВыполнитьПакет();

	ДвиженияНачислениеАмортизации = РезультатЗапроса[2].Выгрузить();
	ДвиженияФинансовыйУчет = РезультатЗапроса[3].Выгрузить();

	Движения.АК_НачислениеАмортизации.Загрузить(ДвиженияНачислениеАмортизации);
	Движения.АК_НачислениеАмортизации.Записать();
	
	мЦФО = Константы.ЦФОДляУчетаОсновныхСредств.Получить();	
	ПараметрыСписания01 					= ПолучитьПараметрыСписанияСоСчета01();
	
	Для каждого Проводка из ДвиженияФинансовыйУчет цикл
		
		НоваяПроводка = Движения.Финансовый.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка,Проводка);
		НоваяПроводка.СубконтоДт.ТорговыеТочки = Проводка.ТорговаяТочка;
		НоваяПроводка.СубконтоКт.ОсновныеСредства = Проводка.ОсновноеСредство;		
		
		НоваяПроводка.СубконтоДт.СтатьиДоходовРасходов = ПараметрыСписания01.СтатьяДоходовРасходов;
		НоваяПроводка.СубконтоДт.ЦФО					= мЦФО;

	КонецЦикла;	
	Движения.Финансовый.Записать();	
	
КонецПроцедуры

Функция ПолучитьПараметрыСписанияСоСчета01()
	
	//+++АК Susk (Суслин К.В.) 2018.12.06 ИП-00020497	 
	Возврат ОбщегоНазначенияСервер.ПолучитьНастройкиОтраженияОперацийВУчете(Перечисления.ВидыОперацийВУчете.СписаниеОССоСчета01ПриВводеВЭксплуатацию, Дата);	
	
КонецФункции

//+++AK susk

Функция ПолучитьДокументКорректировки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	АК_ИзменениеПараметровНачисленияАмортизацииОС.Ссылка
	               |ИЗ
	               |	Документ.АК_ИзменениеПараметровНачисленияАмортизацииОС КАК АК_ИзменениеПараметровНачисленияАмортизацииОС
	               |ГДЕ
	               |	АК_ИзменениеПараметровНачисленияАмортизацииОС.ДокументОснование = &ДокументОснование
	               |	И НЕ АК_ИзменениеПараметровНачисленияАмортизацииОС.ПометкаУдаления
	               |	И АК_ИзменениеПараметровНачисленияАмортизацииОС.Организация = &Организация";
				   
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка.ПолучитьОбъект();
	Иначе
		Док = Документы.АК_ИзменениеПараметровНачисленияАмортизацииОС.СоздатьДокумент();
		Док.ДокументОснование = Ссылка;
		Возврат Док;
	КонецЕсли;

	
КонецФункции

Процедура КорректировкаПервоначальнойСтоимости()
	
	Если мНеКорректироватьПервоначальнуюСтоимость Тогда
		Возврат;
	КонецЕсли;
	
	ДокКорректировки = ПолучитьДокументКорректировки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФинансовыйОбороты.Организация,
	               |	ФинансовыйОбороты.Субконто1 КАК ОсновноеСредство,
	               |	ФинансовыйОбороты.СуммаОстаток КАК СуммаОборотПо01,
	               |	ЕСТЬNULL(ПринятыеКУчетуОССрезПоследних.НачальнаяСтоимость, 0) КАК СтоимостьДляВычисленияАмортизации,
	               |	ПринятыеКУчетуОССрезПоследних.ДатаВводаВЭксплуатацию,
	               |	ПринятыеКУчетуОССрезПоследних.НачислятьАмортизацию,
	               |	ПринятыеКУчетуОССрезПоследних.СрокАмортизации,
	               |	ПринятыеКУчетуОССрезПоследних.СчетУчетаАмортизация,
	               |	ПринятыеКУчетуОССрезПоследних.СчетУчета,
	               |	ПринятыеКУчетуОССрезПоследних.АмортизационнаяГруппа,
	               |	ПринятыеКУчетуОССрезПоследних.ЭтоОприходование,
	               |	ПринятыеКУчетуОССрезПоследних.СтатьяДР
	               |ПОМЕСТИТЬ Сводная
	               |ИЗ
	               |	РегистрБухгалтерии.Финансовый.Остатки(&КонецПериода, Счет В (&Счета01), , Организация = &Организация) КАК ФинансовыйОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПринятыеКУчетуОС.СрезПоследних(&КонецПериода, ) КАК ПринятыеКУчетуОССрезПоследних
	               |		ПО ФинансовыйОбороты.Субконто1 = ПринятыеКУчетуОССрезПоследних.ОсновноеСредство
	               |			И ФинансовыйОбороты.Организация = ПринятыеКУчетуОССрезПоследних.Организация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Сводная.Организация КАК Организация,
	               |	Сводная.ОсновноеСредство,
	               |	Сводная.ОсновноеСредство.Код,
	               |	Сводная.СуммаОборотПо01,
	               |	Сводная.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	               |	Сводная.СуммаОборотПо01 - Сводная.СтоимостьДляВычисленияАмортизации КАК Разница,
	               |	Сводная.СуммаОборотПо01 КАК НачальнаяСтоимость,
	               |	Сводная.ДатаВводаВЭксплуатацию,
	               |	Сводная.НачислятьАмортизацию,
	               |	Сводная.СрокАмортизации,
	               |	Сводная.СчетУчетаАмортизация,
	               |	Сводная.СчетУчета,
	               |	Сводная.АмортизационнаяГруппа,
	               |	Сводная.ЭтоОприходование,
	               |	Сводная.СтатьяДР
	               |ИЗ
	               |	Сводная КАК Сводная
	               |ГДЕ
	               |	Сводная.СуммаОборотПо01 - Сводная.СтоимостьДляВычисленияАмортизации <> 0";
				   
	СписокСчетов = Новый СписокЗначений;
	СписокСчетов.Добавить(ПланыСчетов.Финансовый.ОсновныеСредстваВОрганизации);
	СписокСчетов.Добавить(ПланыСчетов.Финансовый.ОсновныеСредстваДо100000);
	
	Граница = Новый Граница(НачалоМесяца(Дата),ВидГраницы.Исключая);				   
	
	Запрос.УстановитьПараметр("Организация", Организация); 
	Запрос.УстановитьПараметр("КонецПериода", Граница); 
	Запрос.УстановитьПараметр("Счета01", СписокСчетов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДокКорректировки.Организация = Организация;
	ДокКорректировки.Дата = НачалоМесяца(Дата);
	ДокКорректировки.Комментарий = "Корректировка стоимости для начисления амортизации на " + Строка(НачалоМесяца(Дата));	
	ДокКорректировки.ДокументОснование = Ссылка;
	ДокКорректировки.ОС.Очистить();  	
	
	ЕстьИзменения = Ложь;
	
	Пока Выборка.Следующий() Цикл
		НовСтр = ДокКорректировки.ОС.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		ЕстьИзменения = Истина;
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
	
		Попытка
			ДокКорректировки.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке("Не удалось провести корректировку стоимости для начисления амортизации!");		
			ВызватьИсключение;		
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры


мНеКорректироватьПервоначальнуюСтоимость = Ложь;

//---AK susk
