
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ВводВЭксплуатациюОС ИЛИ ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ПустаяСсылка() Тогда
		ВыполнитьКонтрольПоСчетамУчетаОС(Отказ);
		АК_ОсновныеСредства.ВыполнитьКонтрольВводаВЭксплуатацию(ЭтотОбъект,Отказ);	
	КонецЕсли;
	
    ВыполнитьДвиженияПринятиеКУчетуОС(Отказ);
	ВыполнитьДвиженияФинансовый(Отказ);			
	АК_ОсновныеСредства.ПроисвоитьИнвентарныеНомераВДокументе(ЭтотОбъект);	
	
КонецПроцедуры 

Процедура ВыполнитьКонтрольПоСчетамУчетаОС(Отказ)

 		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВводВЭксплуатациюОборудование.ОсновноеСредство
		|ПОМЕСТИТЬ втОсновныеСредства
		|ИЗ
		|	Документ.ВводВЭксплуатацию.Оборудование КАК ВводВЭксплуатациюОборудование
		|ГДЕ
		|	ВводВЭксплуатациюОборудование.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФинансовыйОстатки.Счет КАК СчетДт,
		|	ФинансовыйОстатки.Субконто1 КАК СубконтоДт1
		|ПОМЕСТИТЬ втСчетаУчетаОС
		|ИЗ
		|	РегистрБухгалтерии.Финансовый.Остатки(
		|			&ДатаОкончания,
		|			Счет В (&СчетаУчетаОСМСФО),
		|			,
		|			Организация = &Организация
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						втОсновныеСредства.ОсновноеСредство
		|					ИЗ
		|						втОсновныеСредства)) КАК ФинансовыйОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ФинансовыйОстатки.Счет,
		|	ФинансовыйОстатки.Субконто1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСтоимостьОС.СубконтоДт1,
		|	СУММА(1) КАК КоличествоСчетов
		|ПОМЕСТИТЬ втКоличествоСчетовУчета
		|ИЗ
		|	втСчетаУчетаОС КАК втСтоимостьОС
		|
		|СГРУППИРОВАТЬ ПО
		|	втСтоимостьОС.СубконтоДт1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втКоличествоСчетовУчета.СубконтоДт1
		|ПОМЕСТИТЬ ОСсНесколькимиСчетамиУчета
		|ИЗ
		|	втКоличествоСчетовУчета КАК втКоличествоСчетовУчета
		|ГДЕ
		|	втКоличествоСчетовУчета.КоличествоСчетов > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСчетаУчетаОС.СубконтоДт1 КАК ОсновноеСредство,
		|	втСчетаУчетаОС.СчетДт
		|ИЗ
		|	втСчетаУчетаОС КАК втСчетаУчетаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОСсНесколькимиСчетамиУчета КАК ОСсНесколькимиСчетамиУчета
		|		ПО втСчетаУчетаОС.СубконтоДт1 = ОСсНесколькимиСчетамиУчета.СубконтоДт1
		|ИТОГИ ПО
		|	ОсновноеСредство";
		
	Запрос.УстановитьПараметр("ДатаОкончания",КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("СчетаУчетаОСМСФО",АК_УчетМСФО.СчетаУчетаОС());
		
	ВыборкаОС = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаОС.Следующий() Цикл
		
		СчетаУчета = "";
		ОсновноеСредство = ВыборкаОС.ОсновноеСредство;	
		ВыборкаСчета = ВыборкаОС.Выбрать();
		ЭтоПервый = Истина;
		
		Пока ВыборкаСчета.Следующий() Цикл
			Если НЕ ЭтоПервый Тогда
				СчетаУчета = СчетаУчета + ", ";
			Иначе 	
				ЭтоПервый = Ложь;
			КонецЕсли;	
			СчетаУчета = СчетаУчета + ВыборкаСчета.СчетДт;			
			
		КонецЦикла;			
		
		ОбщегоНазначения.СообщитьОбОшибке("Учет затрат по основному средству: " + ОсновноеСредство + " ведется на разных счетах: "  + СчетаУчета, Отказ);		
		
	КонецЦикла;	
	
КонецПроцедуры	

//+++АК Susk (Суслин К.В.) 2018.10.08 переписал
Процедура ВыполнитьДвиженияФинансовый(Отказ)
	
	Если ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ВводВЭксплуатациюОС ИЛИ ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ПустаяСсылка() Тогда
		ВыполнитьДвижения_ОС_Финансовый(Отказ);
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ВводВЭксплуатациюНМА Тогда
		ВыполнитьДвижения_НМА_Финансовый(Отказ);
	КонецЕсли;
	
КонецПроцедуры	

Функция ПолучитьСтатьюДР(СтатьяДДС)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатьиДоходовРасходов.Ссылка
		|ИЗ
		|	Справочник.СтатьиДоходовРасходов КАК СтатьиДоходовРасходов
		|ГДЕ
		|	СтатьиДоходовРасходов.ОсновнаяСтатьяДвиженияДенежныхСредств = &СтатьяДДС";

	Запрос.УстановитьПараметр("СтатьяДДС", СтатьяДДС);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат  ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;

КонецФункции

Функция ПолучитьНастройкуСписания(ВидСписания)
	
	//+++АК Susk (Суслин К.В.) 2018.12.06 ИП-00020497	 
	Возврат ОбщегоНазначенияСервер.ПолучитьНастройкиОтраженияОперацийВУчете(ВидСписания, Дата, Организация);
	//---АК Susk (Суслин К.В.) 
		
КонецФункции

//+++АК Susk (Суслин К.В.) 2018.10.08 переписал
Процедура ВыполнитьДвиженияПринятиеКУчетуОС(Отказ)
		
	Если ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ВводВЭксплуатациюОС ИЛИ ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ПустаяСсылка() Тогда
		ВыполнитьДвижения_ОС_ПринятиеКучету(Отказ);
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ВводВЭксплуатациюНМА Тогда
		ВыполнитьДвижения_НМА_ПринятиеКучету(Отказ);
	КонецЕсли;		
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Дата = КонецДня(Дата);
КонецПроцедуры

//+++АК Susk (Суслин К.В.) 2018.10.08 старые задачи
Процедура ЗаписатьВМодулеОБъекта() Экспорт
	
	ЭтотОбъект.Записать(РежимЗаписиДокумента.Проведение);	
	
КонецПроцедуры

//+++АК Susk (Суслин К.В.) 2018.07.12 оптимизация
Функция ПолучитьСтруктуруПроверяемыхРеквизитовСтроки()
	
	СтруктураПроверяемыхРеквизитовСтроки = Новый Структура;
	СтруктураПроверяемыхРеквизитовСтроки.Вставить("АмортизационнаяГруппа", "Амортизационная группа");
	СтруктураПроверяемыхРеквизитовСтроки.Вставить("ГруппаОС", "Группа ОС");
	СтруктураПроверяемыхРеквизитовСтроки.Вставить("КодПоОКОФ", "Код по ОКОФ");
	СтруктураПроверяемыхРеквизитовСтроки.Вставить("СрокПолезногоИспользования", "Срок полезного использования");
	
	Возврат СтруктураПроверяемыхРеквизитовСтроки;	
	
КонецФункции

//+++АК Susk (Суслин К.В.) 2018.07.12 оптимизация
Процедура ПроверитьСтрокуНаОшибки(СтруктураПроверяемыхРеквизитовСтроки, СтрокаТЧ, Отказ)
	
	Если не ЗначениеЗаполнено(СтрокаТЧ.СчетКт) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Затраты на приобретение: " + СтрокаТЧ.ОсновноеСредство + " с инв. номером: " + СтрокаТЧ.ИнвентарныйНомер + " не обнаружены в указанном периоде!", Отказ);					
	КонецЕсли;
	
	Если НЕ СтрокаТЧ.СписываетсяВМоментВвода Тогда
		Для Каждого Реквизит Из СтруктураПроверяемыхРеквизитовСтроки Цикл
			Если Не ЗначениеЗаполнено(СтрокаТЧ[Реквизит.Ключ]) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("В строке № " + СтрокаТЧ.НомерСтроки + " у ОС: " + СтрокаТЧ.ОсновноеСредство  + " с инв. номером: " + СтрокаТЧ.ИнвентарныйНомер + " не заполнен реквизит " + Реквизит.Значение, Отказ);				
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

//+++АК Susk (Суслин К.В.) 2018.09.11 ИП-00019623
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидОперации = Перечисления.ВидыОперацийВводВЭксплуатацию.ВводВЭксплуатациюНМА Тогда		
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Оборудование.НомерСтроки,
	               |	Оборудование.Местоположение,
	               |	Оборудование.СтатьяДР
	               |ПОМЕСТИТЬ ТЗ_Оборудование
	               |ИЗ
	               |	&Оборудование КАК Оборудование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗ_Оборудование.НомерСтроки,
	               |	ТЗ_Оборудование.Местоположение КАК ТорговаяТочка,
	               |	ТЗ_Оборудование.СтатьяДР
	               |ПОМЕСТИТЬ Оборудование_Сводная
	               |ИЗ
	               |	ТЗ_Оборудование КАК ТЗ_Оборудование
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	               |		ПО ТЗ_Оборудование.Местоположение = Склады.Ссылка
	               |ГДЕ
	               |	Склады.Ссылка ЕСТЬ NULL
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Оборудование_Сводная.НомерСтроки,
	               |	Оборудование_Сводная.ТорговаяТочка КАК ТорговаяТочка,
	               |	МИНИМУМ(ЕСТЬNULL(ЛистУчета.Дата, ДАТАВРЕМЯ(1, 1, 1))) КАК Дата,
	               |	Оборудование_Сводная.СтатьяДР
	               |ИЗ
	               |	Оборудование_Сводная КАК Оборудование_Сводная
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЛистУчета КАК ЛистУчета
	               |		ПО Оборудование_Сводная.ТорговаяТочка = ЛистУчета.ТорговаяТочка
	               |			И (ЛистУчета.Проведен)
	               |			И (ЛистУчета.Дата МЕЖДУ &НачалоПериода И &КонецПериода)
	               |ГДЕ
	               |	НЕ Оборудование_Сводная.СтатьяДР В ИЕРАРХИИ (&ГруппаСтатей6)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Оборудование_Сводная.НомерСтроки,
	               |	Оборудование_Сводная.ТорговаяТочка,
	               |	Оборудование_Сводная.СтатьяДР
	               |ИТОГИ
	               |	МИНИМУМ(Дата)
	               |ПО
	               |	ТорговаяТочка";
	
	Запрос.УстановитьПараметр("Оборудование", Оборудование.Выгрузить());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ДобавитьМесяц(Дата, -1)));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("ГруппаСтатей6", Справочники.СтатьиДоходовРасходов.ПолучитьСсылку(Новый УникальныйИдентификатор("fa2094f9-e81d-11e0-b470-001517297ae2")));
	
	ВыборкаТочки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаТочки.Следующий() Цикл
		
		Если ВыборкаТочки.Дата < НачалоМесяца(Дата) Тогда //есть листы учета за прошлый месяц, точка не является новой
			Продолжить;
		КонецЕсли;	
		
		ПродолжениеСообщенияОбОшибке = " основное средство вводится в эксплуатацию на новой торговой точке, необходимо выбрать инвестиционную статью";
		
		Выборка = ВыборкаТочки.Выбрать();
		
		НачалоСообщенияОбОшибке = "";
		
		Пока Выборка.Следующий() Цикл
			НачалоСообщенияОбОшибке = НачалоСообщенияОбОшибке + Строка(Выборка.НомерСтроки) + ",";
		КонецЦикла;
		
		НачалоСообщенияОбОшибке = Лев(НачалоСообщенияОбОшибке, СтрДлина(НачалоСообщенияОбОшибке)-1);
		
		ОбщегоНазначения.СообщитьОбОшибке("В строках № " + НачалоСообщенияОбОшибке + ПродолжениеСообщенияОбОшибке, Отказ);
	КонецЦикла;
	
КонецПроцедуры

//+++АК Susk (Суслин К.В.) 2018.10.08 оптимизация
Процедура ВыполнитьДвижения_ОС_Финансовый(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ВводВЭксплуатациюОборудование.ОсновноеСредство,
		               |	ВводВЭксплуатациюОборудование.Ссылка,
		               |	ВводВЭксплуатациюОборудование.НомерСтроки,
		               |	ВводВЭксплуатациюОборудование.ДатаВвода,
		               |	ВводВЭксплуатациюОборудование.СрокПолезногоИспользования,
		               |	ОсновныеСредства.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
		               |	ВводВЭксплуатациюОборудование.ИнвентарныйНомер,
		               |	ВводВЭксплуатациюОборудование.ПервоначальнаяСтоимость,
		               |	ВводВЭксплуатациюОборудование.СписываетсяВМоментВвода,
		               |	ВЫБОР
		               |		КОГДА ВводВЭксплуатациюОборудование.Местоположение ССЫЛКА Справочник.Склады
		               |			ТОГДА Склады.Владелец
		               |		ИНАЧЕ ВводВЭксплуатациюОборудование.Местоположение
		               |	КОНЕЦ КАК ТорговаяТочка,
		               |	ВводВЭксплуатацию.Дата КАК ДатаДокумента,
		               |	ВводВЭксплуатацию.Организация,
		               |	ОсновныеСредства.КодПоОКОФ КАК КодПоОКОФ,
		               |	ОсновныеСредства.ГруппаОС КАК ГруппаОС,
		               |	ВводВЭксплуатациюОборудование.СчетУчета,
		               |	ВводВЭксплуатациюОборудование.СтатьяДР,
		               |	ВводВЭксплуатациюОборудование.ЦФО,
		               |	СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы
		               |ПОМЕСТИТЬ втОсновныеСредства
		               |ИЗ
		               |	Документ.ВводВЭксплуатацию.Оборудование КАК ВводВЭксплуатациюОборудование
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводВЭксплуатацию КАК ВводВЭксплуатацию
		               |		ПО ВводВЭксплуатациюОборудование.Ссылка = ВводВЭксплуатацию.Ссылка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
		               |		ПО ВводВЭксплуатациюОборудование.ОсновноеСредство = ОсновныеСредства.Ссылка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		               |		ПО ВводВЭксплуатациюОборудование.Местоположение = Склады.Ссылка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		               |		ПО ВводВЭксплуатациюОборудование.Местоположение = СтруктурныеЕдиницы.Ссылка
		               |ГДЕ
		               |	ВводВЭксплуатациюОборудование.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ФинансовыйОстатки.Счет КАК СчетЗатрат,
		               |	ФинансовыйОстатки.Субконто1 КАК ОсновноеСредство,
		               |	ФинансовыйОстатки.СуммаОстаток КАК Сумма,
		               |	ФинансовыйОстатки.Организация
		               |ПОМЕСТИТЬ ВводимыеВЭксплуатацию
		               |ИЗ
		               |	РегистрБухгалтерии.Финансовый.Остатки(
		               |			&Момент,
		               |			Счет В (&СчетаУчетаОСМСФО),
		               |			,
		               |			Субконто1 В
		               |					(ВЫБРАТЬ
		               |						втОсновныеСредства.ОсновноеСредство
		               |					ИЗ
		               |						втОсновныеСредства)
		               |				И (Организация = &Организация
		               |					ИЛИ Организация ЕСТЬ NULL
		               |					ИЛИ Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))) КАК ФинансовыйОстатки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Таблица.ТорговаяТочка КАК ТорговаяТочка,
		               |	МИНИМУМ(Таблица.Дата) КАК Дата
		               |ПОМЕСТИТЬ втДатаПервогоЛиста
		               |ИЗ
		               |	Документ.ЛистУчета КАК Таблица
		               |ГДЕ
		               |	НАЧАЛОПЕРИОДА(Таблица.Дата, МЕСЯЦ) >= НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаОкончания, МЕСЯЦ, -1), МЕСЯЦ)
		               |	И Таблица.ТорговаяТочка В
		               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |				втОсновныеСредства.ТорговаяТочка
		               |			ИЗ
		               |				втОсновныеСредства)
		               |	И Таблица.Проведен
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Таблица.ТорговаяТочка
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	ТорговаяТочка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОприходованиеОСНоменклатура.ОсновноеСредство,
		               |	МАКСИМУМ(ИСТИНА) КАК ЭтоОприходование
		               |ПОМЕСТИТЬ втОприходованияОС
		               |ИЗ
		               |	Документ.ОприходованиеОС.Номенклатура КАК ОприходованиеОСНоменклатура
		               |ГДЕ
		               |	ОприходованиеОСНоменклатура.Ссылка.Проведен = ИСТИНА
		               |	И ОприходованиеОСНоменклатура.ОсновноеСредство В
		               |			(ВЫБРАТЬ
		               |				втОсновныеСредства.ОсновноеСредство
		               |			ИЗ
		               |				втОсновныеСредства)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ОприходованиеОСНоменклатура.ОсновноеСредство
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втОсновныеСредства.ОсновноеСредство,
		               |	втОсновныеСредства.Ссылка,
		               |	втОсновныеСредства.НомерСтроки,
		               |	втОсновныеСредства.ДатаВвода,
		               |	ЕСТЬNULL(втОсновныеСредства.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
		               |	втОсновныеСредства.ИнвентарныйНомер,
		               |	втОсновныеСредства.СписываетсяВМоментВвода,
		               |	втОсновныеСредства.ДатаДокумента КАК Период,
		               |	ВводимыеВЭксплуатацию.СчетЗатрат КАК СчетКт,
		               |	ВводимыеВЭксплуатацию.Сумма КАК Сумма,
		               |	ВЫБОР
		               |		КОГДА втОсновныеСредства.СписываетсяВМоментВвода
		               |			ТОГДА ""Списание на расходы при вводе в эксплуатацию""
		               |		ИНАЧЕ ""Ввод в эксплуатацию""
		               |	КОНЕЦ КАК Содержание,
		               |	1 КАК Количество,
		               |	втОсновныеСредства.ТорговаяТочка,
		               |	ВЫБОР
		               |		КОГДА втОсновныеСредства.ТорговаяТочка.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Склад)
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК ЭтоСклад,
		               |	втОсновныеСредства.АмортизационнаяГруппа,
		               |	втОсновныеСредства.КодПоОКОФ,
		               |	втОсновныеСредства.ГруппаОС,
		               |	втОсновныеСредства.СчетУчета КАК СчетДт,
		               |	ВводимыеВЭксплуатацию.Организация,
		               |	ВЫБОР
		               |		КОГДА КОНЕЦПЕРИОДА(втОсновныеСредства.ДатаДокумента, МЕСЯЦ) <= ЕСТЬNULL(втДатаПервогоЛиста.Дата, ДАТАВРЕМЯ(3999, 1, 1))
		               |				И втОсновныеСредства.ТорговаяТочка.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСТруктурныхЕдиниц.Розница)
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК ЭтоНоваяТочка,
		               |	втОсновныеСредства.СтатьяДР,
		               |	втОсновныеСредства.ЦФО,
		               |	ЕСТЬNULL(втОприходованияОС.ЭтоОприходование, ЛОЖЬ) КАК ЭтоОприходование,
		               |	втОсновныеСредства.ТипСтруктурнойЕдиницы
		               |ИЗ
		               |	втОсновныеСредства КАК втОсновныеСредства
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВводимыеВЭксплуатацию КАК ВводимыеВЭксплуатацию
		               |		ПО втОсновныеСредства.ОсновноеСредство = ВводимыеВЭксплуатацию.ОсновноеСредство
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втДатаПервогоЛиста КАК втДатаПервогоЛиста
		               |		ПО втОсновныеСредства.ТорговаяТочка = втДатаПервогоЛиста.ТорговаяТочка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втОприходованияОС КАК втОприходованияОС
		               |		ПО втОсновныеСредства.ОсновноеСредство = втОприходованияОС.ОсновноеСредство
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втОсновныеСредства.ОсновноеСредство,
		               |	втОсновныеСредства.ИнвентарныйНомер,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеТоваровУслугОборудование.Ссылка) КАК Ссылка
		               |ИЗ
		               |	втОсновныеСредства КАК втОсновныеСредства
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
		               |		ПО втОсновныеСредства.ОсновноеСредство = ПоступлениеТоваровУслугОборудование.ОсновноеСредство
		               |			И (ПоступлениеТоваровУслугОборудование.Ссылка.Проведен)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втОсновныеСредства.ОсновноеСредство,
		               |	втОсновныеСредства.ИнвентарныйНомер
		               |
		               |ИМЕЮЩИЕ
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеТоваровУслугОборудование.Ссылка) > 1";		
			
	Запрос.УстановитьПараметр("ДатаОкончания",КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Организация",Организация);
	
	//Нет заведенной задачи. Разовая продажа была через 08.Т. Надо поставить на учет.
	СписокСчетов = АК_УчетМСФО.СчетаУчетаОС();
	СписокСчетов.Добавить(ПланыСчетов.Финансовый.НайтиПоКоду("08.Т"));
	
	Запрос.УстановитьПараметр("СчетаУчетаОСМСФО",СписокСчетов);
		
	Гр = Новый Граница(МоментВремени(), ВидГраницы.Исключая);	
	Запрос.УстановитьПараметр("Момент", Гр);
			
	РезультатЗапроса = Запрос.ВыполнитьПакет();
		
	ВыборкаОС          = РезультатЗапроса[4].Выбрать();		
	КонтрольКоличество = РезультатЗапроса[5].Выгрузить();
	
	Для Каждого Строка из КонтрольКоличество Цикл
		
		ОбщегоНазначения.СообщитьОбОшибке("Введено более одного документа поступления для основного средства: " + Строка.ОсновноеСредство + " с инв. номером: " + Строка.ИнвентарныйНомер, Отказ);		
		
	КонецЦикла;	
	
	ЭтотОбъект.Движения.Финансовый.Очистить();
	ЭтотОбъект.Движения.Финансовый.Записывать = Истина;
	
	НастрокаСписания = ПолучитьНастройкуСписания(Перечисления.ВидыОперацийВУчете.ОсновныеСредства_СписаниеПриВводеВЭксплуатацию);	
	НастрокаСписанияНовыеТочки = ПолучитьНастройкуСписания(Перечисления.ВидыОперацийВУчете.ОсновныеСредства_СписаниеПриВводеВЭксплуатациюНовыеТочки);	
	
	Если НЕ ЗначениеЗаполнено(НастрокаСписания.Счет) Тогда		
		ОбщегоНазначения.СообщитьОбОшибке("Не задан счет списания в настройке списания (см. регистр сведений ""Настройка отражения операций в учете"")", Отказ);		
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	МассивСтатей442 = АК_ОсновныеСредства.МассивСтатейДРДляОтнесенияНаРасходыОфис();
	
	СтруктураПроверяемыхРеквизитовСтроки = ПолучитьСтруктуруПроверяемыхРеквизитовСтроки();
	
	Пока ВыборкаОС.Следующий() Цикл
		ПроверитьСтрокуНаОшибки(СтруктураПроверяемыхРеквизитовСтроки, ВыборкаОС, Отказ);		
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаОС.Сбросить();
	
	Пока ВыборкаОС.Следующий() Цикл
		
		НоваяПроводка = Движения.Финансовый.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка,ВыборкаОС);		
		НоваяПроводка.СубконтоКт.ОсновныеСредства = ВыборкаОС.ОсновноеСредство;		
				
		Если ВыборкаОС.СписываетсяВМоментВвода Тогда
			
			НастрокаСписанияПроводки = НастрокаСписания;						
			НоваяПроводка.СчетДт =  НастрокаСписанияПроводки.Счет;
			
			СтатьяДР = ВыборкаОС.СтатьяДР;
			
			Если НЕ ЗначениеЗаполнено(СтатьяДР) Тогда
				СтатьяДР = НастрокаСписанияПроводки.СтатьяДоходовРасходов;
			КонецЕсли;	
			
			ЭтоСтатья442 = МассивСтатей442.Найти(СтатьяДР) <> Неопределено;
			
			Если ЭтоСтатья442 Тогда
				НоваяПроводка.СчетДт = ПланыСчетов.Финансовый.ЗатратыОбщиеДляРаспределения;
			Иначе 	
				НоваяПроводка.СчетДт =  НастрокаСписанияПроводки.Счет;
			КонецЕсли;
			
			НоваяПроводка.СубконтоДт.СтатьиДоходовРасходов = СтатьяДР;
			
			Если ВыборкаОС.ЭтоСклад Тогда
				//ВсеТТ
				Если ЗначениеЗаполнено(НастрокаСписанияПроводки.ТорговаяТочка) Тогда
					НоваяПроводка.СубконтоДт.ТорговыеТочки = НастрокаСписанияПроводки.ТорговаяТочка;
				КонецЕсли;
				
			Иначе	
				Если ЗначениеЗаполнено(ВыборкаОС.ТорговаяТочка) Тогда
					НоваяПроводка.СубконтоДт.ТорговыеТочки = ВыборкаОС.ТорговаяТочка;
				КонецЕсли;	
			КонецЕсли;
			
			Если НоваяПроводка.СчетДт = ПланыСчетов.Финансовый.ЗатратыОбщиеДляРаспределения И ТипЗнч(ВыборкаОС.ТорговаяТочка) = Тип("СправочникСсылка.СтруктурныеЕдиницы") И ВыборкаОС.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Розница Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Строка №: " + Строка(ВыборкаОС.НомерСтроки) + ". Основное средство: " +  ВыборкаОС.ОсновноеСредство + " с инв. номером: " + ВыборкаОС.ИнвентарныйНомер + " - идёт на счет 44.2!", , , СтатусСообщения.Информация);
			КонецЕсли;
						
			НоваяПроводка.СубконтоДт.ЦФО = НастрокаСписанияПроводки.ЦФО;
			
		Иначе 			
			НоваяПроводка.СубконтоДт.ОсновныеСредства = ВыборкаОС.ОсновноеСредство;
		КонецЕсли;
		
		//нет задачи.
		//чтобы не было искажений данных, надо сверить суммы
	    НайдСтр = Оборудование.Найти(ВыборкаОС.ОсновноеСредство, "ОсновноеСредство");
		
		Если НайдСтр <> Неопределено И НайдСтр.ПервоначальнаяСтоимость <> ВыборкаОС.Сумма Тогда
			НайдСтр.ПервоначальнаяСтоимость = ВыборкаОС.Сумма;
			
			ОбщегоНазначения.СообщитьОбОшибке("Скорректирована первоначальная стоимость в строке № " + Строка(НайдСтр.НомерСтроки));
			
			//+++АК Susk (Суслин К.В.) 2018.02.08 ИП-00017835			 
			СуммаПервоначальнойСтоимости0110 = АК_УчетМСФО.ВернутьСуммуДляОприходованияОСНа0110(Дата);
			
			Если НайдСтр.ПервоначальнаяСтоимость > СуммаПервоначальнойСтоимости0110 И 
				НайдСтр.СчетУчета = ПланыСчетов.Финансовый.ОсновныеСредстваДо100000 Тогда
					ОбщегоНазначения.СообщитьОбОшибке("В строке № " + Строка(НайдСтр.НомерСтроки) + " - первоначальная стоимость изменена задним числом и превышает " + Строка(СуммаПервоначальнойСтоимости0110) + " рублей, а счет учета остался 01.10!!!");
			КонецЕсли;
			//---АК Susk (Суслин К.В.) 
				
		КонецЕсли;
		//---susk
		
	КонецЦикла;	
	
КонецПроцедуры

//+++АК Susk (Суслин К.В.) 2018.10.08 ИП-00020055
Процедура ВыполнитьДвижения_НМА_Финансовый(Отказ)
	
	Движения.Финансовый.Записывать = Истина;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВводВЭксплуатациюОборудование.ОсновноеСредство,
	               |	ВводВЭксплуатацию.Дата КАК Период,
	               |	ВводВЭксплуатациюОборудование.СчетУчета КАК СчетДт,
	               |	ВводВЭксплуатациюОборудование.ПервоначальнаяСтоимость КАК Сумма
	               |ИЗ
	               |	Документ.ВводВЭксплуатацию.Оборудование КАК ВводВЭксплуатациюОборудование
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводВЭксплуатацию КАК ВводВЭксплуатацию
	               |		ПО ВводВЭксплуатациюОборудование.Ссылка = ВводВЭксплуатацию.Ссылка
	               |ГДЕ
	               |	ВводВЭксплуатациюОборудование.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пвх = ПланыВидовХарактеристик.ВидыСубконто.ПолучитьСсылку(Новый УникальныйИдентификатор("ab296307-42e5-11e8-adf1-005056a714c6")); //НМА
	
	Пока Выборка.Следующий() Цикл
		НовДвиж = Движения.Финансовый.Добавить();
		ЗаполнитьЗначенияСвойств(НовДвиж, Выборка);
		НовДвиж.СубконтоДт.ОсновныеСредства = Выборка.ОсновноеСредство;
		НовДвиж.СчетКт = ОбщегоНазначенияПовтИсп.ВернутьСчетПоУсловию("08.5", "Финансовый");
		НовДвиж.СубконтоКт[Пвх] = Выборка.ОсновноеСредство;
		НовДвиж.Организация = Организация;
	КонецЦикла;
	
КонецПроцедуры

//+++АК Susk (Суслин К.В.) 2018.10.08 оптимизация
Процедура ВыполнитьДвижения_ОС_ПринятиеКУчету(Отказ)
	
	Запрос = Новый Запрос;	
	//+++АК Susk (Суслин К.В.) 2018.07.11 ИП-00019167		
	Запрос.Текст = "ВЫБРАТЬ
	               |	Таблица.Ссылка,
	               |	Таблица.НомерСтроки,
	               |	Таблица.ОсновноеСредство,
	               |	Таблица.ДатаВвода КАК Период,
	               |	Таблица.ДатаПринятияКУчету КАК ДатаВводаВЭксплуатацию,
	               |	Таблица.СрокПолезногоИспользования КАК СрокАмортизации,
	               |	Таблица.АмортизационнаяГруппа,
	               |	Таблица.ИнвентарныйНомер,
	               |	Таблица.ПервоначальнаяСтоимость КАК НачальнаяСтоимость,
	               |	ВЫБОР
	               |		КОГДА Таблица.СписываетсяВМоментВвода = ИСТИНА
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК НачислятьАмортизацию,
	               |	Таблица.Местоположение,
	               |	ВЫБОР
	               |		КОГДА Таблица.СписываетсяВМоментВвода = ИСТИНА
	               |			ТОГДА НастройкаСписаниеПриВводе.Счет
	               |		ИНАЧЕ Таблица.СчетУчета
	               |	КОНЕЦ КАК СчетУчета,
	               |	ВЫБОР
	               |		КОГДА Таблица.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.ОсновныеСредстваДо100000)
	               |			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Финансовый.АмортизацияОсновныхСредствДо100000)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Финансовый.АмортизацияОсновныхСредств)
	               |	КОНЕЦ КАК СчетУчетаАмортизация,
	               |	ДокВвод.Организация,
	               |	ВЫБОР
	               |		КОГДА Таблица.Местоположение ССЫЛКА Справочник.Склады
	               |			ТОГДА Склады.Владелец
	               |		ИНАЧЕ Таблица.Местоположение
	               |	КОНЕЦ КАК ТорговаяТочка,
	               |	ДокВвод.Дата КАК Дата,
	               |	Таблица.СтатьяДР
	               |ПОМЕСТИТЬ втРезультат
	               |ИЗ
	               |	Документ.ВводВЭксплуатацию.Оборудование КАК Таблица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	               |		ПО Таблица.Местоположение = Склады.Ссылка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводВЭксплуатацию КАК ДокВвод
	               |		ПО Таблица.Ссылка = ДокВвод.Ссылка,
	               |	(ВЫБРАТЬ
	               |		Таблица.Счет КАК Счет,
	               |		Таблица.СтруктурнаяЕдиница КАК ТорговаяТочка,
	               |		Таблица.ЦФО КАК ЦФО,
	               |		Таблица.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов
	               |	ИЗ
	               |		РегистрСведений.НастройкаОтраженияОперацийВУчете.СрезПоследних(&ДатаОкончания, ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ОсновныеСредства_СписаниеПриВводеВЭксплуатацию)) КАК Таблица) КАК НастройкаСписаниеПриВводе
	               |ГДЕ
	               |	Таблица.Ссылка = &Ссылка
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ТорговаяТочка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОприходованиеОСНоменклатура.ОсновноеСредство,
	               |	ИСТИНА КАК ЭтоОприходование
	               |ПОМЕСТИТЬ втОприходованиеОС
	               |ИЗ
	               |	Документ.ОприходованиеОС.Номенклатура КАК ОприходованиеОСНоменклатура
	               |ГДЕ
	               |	ОприходованиеОСНоменклатура.Ссылка.Проведен
	               |	И ОприходованиеОСНоменклатура.ОсновноеСредство В
	               |			(ВЫБРАТЬ
	               |				втРезультат.ОсновноеСредство
	               |			ИЗ
	               |				втРезультат)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Таблица.Ссылка,
	               |	Таблица.НомерСтроки,
	               |	Таблица.ОсновноеСредство,
	               |	Таблица.Период,
	               |	Таблица.ДатаВводаВЭксплуатацию,
	               |	Таблица.СрокАмортизации,
	               |	Таблица.АмортизационнаяГруппа,
	               |	Таблица.ИнвентарныйНомер,
	               |	Таблица.НачальнаяСтоимость,
	               |	Таблица.НачислятьАмортизацию,
	               |	Таблица.Местоположение,
	               |	Таблица.СчетУчета,
	               |	Таблица.СчетУчетаАмортизация,
	               |	Таблица.Организация,
	               |	Таблица.ТорговаяТочка,
	               |	ЕСТЬNULL(втОприходованиеОС.ЭтоОприходование, ЛОЖЬ) КАК ЭтоОприходование,
	               |	Таблица.СтатьяДР
	               |ИЗ
	               |	втРезультат КАК Таблица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втОприходованиеОС КАК втОприходованиеОС
	               |		ПО Таблица.ОсновноеСредство = втОприходованиеОС.ОсновноеСредство";	
	
	//---АК Susk (Суслин К.В.)
	
	НачалоВеденияУчета = АК_УчетМСФОПривилегированный.ПолучитьПараметрыУчетаОС().НачалоВеденияУчета;	
		
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("ДатаОкончания",КонецМесяца(ЭтотОбъект.Дата));
	Запрос.УстановитьПараметр("ДатаНачалаВеденияОС",НачалоВеденияУчета); 
	
	Результат = Запрос.Выполнить().Выгрузить();

	ЭтотОбъект.Движения.ПринятыеКУчетуОС.Записывать = Истина;
	ЭтотОбъект.Движения.ПринятыеКУчетуОС.Загрузить(Результат);
	
КонецПроцедуры

//+++АК Susk (Суслин К.В.) 2018.10.08 ИП-00020055 
Процедура ВыполнитьДвижения_НМА_ПринятиеКУчету(Отказ)
	
	Запрос = Новый Запрос;		
	Запрос.Текст = "ВЫБРАТЬ
	               |	Таблица.Ссылка,
	               |	Таблица.НомерСтроки,
	               |	Таблица.ОсновноеСредство,
	               |	Таблица.ДатаВвода КАК Период,
	               |	Таблица.ДатаПринятияКУчету КАК ДатаВводаВЭксплуатацию,
	               |	Таблица.СрокПолезногоИспользования КАК СрокАмортизации,
	               |	Таблица.АмортизационнаяГруппа,
	               |	Таблица.ИнвентарныйНомер,
	               |	Таблица.ПервоначальнаяСтоимость КАК НачальнаяСтоимость,
	               |	ИСТИНА КАК НачислятьАмортизацию,
	               |	Таблица.Местоположение,
	               |	Таблица.СчетУчета КАК СчетУчета,
	               |	Таблица.СчетУчетаАмортизации КАК СчетУчетаАмортизация,
	               |	ДокВвод.Организация,
	               |	ДокВвод.Дата КАК Дата	               
	               |ИЗ
	               |	Документ.ВводВЭксплуатацию.Оборудование КАК Таблица
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводВЭксплуатацию КАК ДокВвод
	               |		ПО Таблица.Ссылка = ДокВвод.Ссылка
	               |ГДЕ
	               |	Таблица.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Результат = Запрос.Выполнить().Выгрузить();

	ЭтотОбъект.Движения.ПринятыеКУчетуОС.Записывать = Истина;
	ЭтотОбъект.Движения.ПринятыеКУчетуОС.Загрузить(Результат);
	
КонецПроцедуры


 
 
 