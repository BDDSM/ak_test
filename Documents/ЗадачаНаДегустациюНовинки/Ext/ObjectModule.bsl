
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда Возврат; КонецЕсли;
	
	Если ОценкаПоказателей.Количество() = 0 Тогда
		
		СтруктураПоказателей = Новый Структура;
		// название комментария и обязательно для заполнения / нет
		
		// Вкус
		КомментарийСоответствие = Новый Соответствие;
		КомментарийСоответствие.Вставить("Нравится", Ложь);
		КомментарийСоответствие.Вставить("Не нравится", Истина);
		КомментарийСоответствие.Вставить("Понравился бы, если", Истина);
		СтруктураПоказателей.Вставить("Вкус", КомментарийСоответствие);
		
		// Консистенция
		КомментарийСоответствие = Новый Соответствие;
		КомментарийСоответствие.Вставить("Нравится", Ложь);
		КомментарийСоответствие.Вставить("Не нравится", Истина);
		КомментарийСоответствие.Вставить("Понравился бы, если", Истина);
		СтруктураПоказателей.Вставить("Консистенция", КомментарийСоответствие);
		
		// Упаковка
		КомментарийСоответствие = Новый Соответствие;
		КомментарийСоответствие.Вставить("Удобна", Ложь);
		КомментарийСоответствие.Вставить("Не удобна", Ложь);
		СтруктураПоказателей.Вставить("Упаковка", КомментарийСоответствие);
		
		// Цена
		КомментарийСоответствие = Новый Соответствие;
		СтруктураПоказателей.Вставить("Цена", КомментарийСоответствие);
		
		Для Каждого КлючИЗначение Из СтруктураПоказателей Цикл
			
			НоваяСтрокаТЧ = ОценкаПоказателей.Добавить();
			НоваяСтрокаТЧ.Показатель = КлючИЗначение.Ключ;
			НоваяСтрокаТЧ.УИДСтрокиПоказателя = Строка(Новый УникальныйИдентификатор);
			
			Для Каждого КлючКомментария Из КлючИЗначение.Значение Цикл
				НоваяСтрокаКомментария = КомментарииПоказателей.Добавить();
				НоваяСтрокаКомментария.ТипКомментария = КлючКомментария.Ключ;
				НоваяСтрокаКомментария.Обязательный = КлючКомментария.Значение;
				НоваяСтрокаКомментария.УИДСтрокиПоказателя = НоваяСтрокаТЧ.УИДСтрокиПоказателя;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда ЭтотОбъект.Закрыта = Истина; КонецЕсли;
	
	// заполняем таблицу значений для проведения операций на sql
	Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	1 КАК НомерСтроки,
			|	ЗадачаНаДегустациюНовинки.Номенклатура,
			|	ЗадачаНаДегустациюНовинки.ХарактеристикаНоменклатуры КАК Характеристика,
			|	СпрНоменклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
			|	ДокЗаданиеНаДегустациюНовинки.Цена,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПроизводства,
			|	ЗадачаНаДегустациюНовинки.Количество КАК Количество
			|ПОМЕСТИТЬ ТаблицаТоваровБыло
			|ИЗ
			|	Документ.ЗадачаНаДегустациюНовинки КАК ЗадачаНаДегустациюНовинки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаДегустациюНовинки КАК ДокЗаданиеНаДегустациюНовинки
			|		ПО ЗадачаНаДегустациюНовинки.ЗаданиеНаДегустациюНовинки = ДокЗаданиеНаДегустациюНовинки.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
			|		ПО ЗадачаНаДегустациюНовинки.Номенклатура = СпрНоменклатура.Ссылка
			|ГДЕ
			|	ЗадачаНаДегустациюНовинки.Ссылка = &Ссылка
			|	И ЗадачаНаДегустациюНовинки.Проведен
			|	И НЕ ЗадачаНаДегустациюНовинки.Количество = 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	1 КАК НомерСтроки,
			|	ЗадачаНаДегустациюНовинки.Номенклатура,
			|	ЗадачаНаДегустациюНовинки.ХарактеристикаНоменклатуры КАК Характеристика,
			|	СпрНоменклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
			|	ДокЗаданиеНаДегустациюНовинки.Цена,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПроизводства,
			|	&Количество КАК Количество
			|ПОМЕСТИТЬ ТаблицаТоваровСтало
			|ИЗ
			|	Документ.ЗадачаНаДегустациюНовинки КАК ЗадачаНаДегустациюНовинки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаДегустациюНовинки КАК ДокЗаданиеНаДегустациюНовинки
			|		ПО ЗадачаНаДегустациюНовинки.ЗаданиеНаДегустациюНовинки = ДокЗаданиеНаДегустациюНовинки.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
			|		ПО ЗадачаНаДегустациюНовинки.Номенклатура = СпрНоменклатура.Ссылка
			|ГДЕ
			|	ЗадачаНаДегустациюНовинки.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИСТИНА КАК Сторно,
			|	ТаблицаТоваровБыло.НомерСтроки,
			|	ТаблицаТоваровБыло.Номенклатура,
			|	ТаблицаТоваровБыло.Характеристика,
			|	ТаблицаТоваровБыло.ЕдиницаИзмерения,
			|	ТаблицаТоваровБыло.Количество - ЕСТЬNULL(ТаблицаТоваровСтало.Количество, 0) КАК Количество,
			|	ТаблицаТоваровБыло.ДатаПроизводства,
			|	ТаблицаТоваровБыло.Цена,
			|	ТаблицаТоваровБыло.Цена * (ТаблицаТоваровБыло.Количество - ЕСТЬNULL(ТаблицаТоваровСтало.Количество, 0)) КАК Сумма
			|ИЗ
			|	ТаблицаТоваровБыло КАК ТаблицаТоваровБыло
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровСтало КАК ТаблицаТоваровСтало
			|		ПО ТаблицаТоваровБыло.ДатаПроизводства = ТаблицаТоваровСтало.ДатаПроизводства
			|ГДЕ
			|	ТаблицаТоваровБыло.Количество > ЕСТЬNULL(ТаблицаТоваровСтало.Количество, 0)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЛОЖЬ,
			|	ТаблицаТоваровСтало.НомерСтроки,
			|	ТаблицаТоваровСтало.Номенклатура,
			|	ТаблицаТоваровСтало.Характеристика,
			|	ТаблицаТоваровСтало.ЕдиницаИзмерения,
			|	ТаблицаТоваровСтало.Количество - ЕСТЬNULL(ТаблицаТоваровБыло.Количество, 0),
			|	ТаблицаТоваровСтало.ДатаПроизводства,
			|	ТаблицаТоваровСтало.Цена,
			|	ТаблицаТоваровСтало.Цена * (ТаблицаТоваровСтало.Количество - ЕСТЬNULL(ТаблицаТоваровБыло.Количество, 0))
			|ИЗ
			|	ТаблицаТоваровСтало КАК ТаблицаТоваровСтало
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровБыло КАК ТаблицаТоваровБыло
			|		ПО ТаблицаТоваровСтало.ДатаПроизводства = ТаблицаТоваровБыло.ДатаПроизводства
			|ГДЕ
			|	ТаблицаТоваровСтало.Количество > ЕСТЬNULL(ТаблицаТоваровБыло.Количество, 0)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сторно УБЫВ");
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Количество", Количество);
		
		ТаблицаДвижений_ТоварныеОперации = Запрос.Выполнить().Выгрузить();
		Если ТаблицаДвижений_ТоварныеОперации.Количество() > 0 И ТаблицаДвижений_ТоварныеОперации.НайтиСтроки(Новый Структура("Сторно", Ложь)).Количество() > 0 Тогда ID_Doc_SQL = Строка(Новый УникальныйИдентификатор()); КонецЕсли;
		ДополнительныеСвойства.Вставить("ТаблицаДвижений_ТоварныеОперации", ТаблицаДвижений_ТоварныеОперации);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
Перем ТаблицаДвижений_ТоварныеОперации;
	
	Если ОбменДанными.Загрузка Тогда Возврат; КонецЕсли;
	
	// изменений не было, ничего не делаем
	Если НЕ (ДополнительныеСвойства.Свойство("ТаблицаДвижений_ТоварныеОперации", ТаблицаДвижений_ТоварныеОперации) И ТаблицаДвижений_ТоварныеОперации.Количество() > 0) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипСкладскогоОрдера = "ПриходныйОрдерСклад";
	
	ОрдерНаСкладОбъект = Документы[ТипСкладскогоОрдера].СоздатьДокумент();
	ОрдерНаСкладОбъект.Дата = ТекущаяДата();
	ОрдерНаСкладОбъект.Заполнить(Ссылка);
	//+++АК KIRN 2018.04.24 ИП-00018501     
	ОрдерНаСкладОбъект.Организация = ОрдерНаСкладОбъект.Склад.Организация;
	//---АК KIRN 
	
	// если предыдущий складской ордер не пустой, сторнируем
	Если ЗначениеЗаполнено(Документы.ЗадачаНаДегустациюНовинки.ПолучитьОрдерНаСклад(Ссылка)) Тогда
		ОрдерНаСкладОбъект.Товары.Очистить();
		
		Для Каждого СтрокаТД Из ТаблицаДвижений_ТоварныеОперации Цикл
			СтрокаТЧ = ОрдерНаСкладОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаТД, "Номенклатура,Характеристика,ЕдиницаИзмерения,ДатаПроизводства");
			Если ТипСкладскогоОрдера = "ПеремещениеСклад" Тогда
				СтрокаТЧ.КоличествоОтправитель = ?(СтрокаТД.Сторно, -СтрокаТД.Количество, СтрокаТД.Количество);
				СтрокаТЧ.КоличествоПолучатель  = СтрокаТЧ.КоличествоОтправитель;
			Иначе
				СтрокаТЧ.Количество = ?(СтрокаТД.Сторно, -СтрокаТД.Количество, СтрокаТД.Количество);
			КонецЕсли;
		КонецЦикла;
		
		ОрдерНаСкладОбъект.Товары.Свернуть("Номенклатура,Характеристика,ЕдиницаИзмерения,ДатаПроизводства",
			?(ТипСкладскогоОрдера = "ПеремещениеСклад", "КоличествоОтправитель,КоличествоПолучатель", "Количество,КоличествоКоробок,КоличествоПаллет"));
	КонецЕсли;
	
	ОрдерНаСкладОбъект.Записать();
	ДополнительныеСвойства.Вставить("ОрдерНаСклад", ОрдерНаСкладОбъект.Ссылка);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
Перем ИДДок, ТочкаПолучатель, КодОперации, КодОбратнойОперации, ТаблицаДвижений_ТоварныеОперации;
	
	//заблокируем таблицу
	//Блокировка = Новый БлокировкаДанных();
	//ЭлементБлокировка = Блокировка.Добавить();
	//ЭлементБлокировка.Область = "РегистрСведений.ОценкиЗадачНаДегустациюНовинок";
	//ЭлементБлокировка.Режим = РежимБлокировкиДанных.Исключительный;
	//ЭлементБлокировка.УстановитьЗначение("ИмяНумератора", ИмяНумератора);
	//ЭлементБлокировка.УстановитьЗначение("Организация", Организация);
	//ЭлементБлокировка.УстановитьЗначение("Период", Период);
	//Блокировка.Заблокировать();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗадачаНаДегустациюНовинки.ЗаданиеНаДегустациюНовинки,
		|	ЗадачаНаДегустациюНовинки.Номенклатура,
		|	ЗадачаНаДегустациюНовинки.ХарактеристикаНоменклатуры,
		|	ЗадачаНаДегустациюНовинки.Магазин,
		|	ЗадачаНаДегустациюНовинкиОценкаПоказателей.Показатель,
		|	ЗадачаНаДегустациюНовинкиОценкаПоказателей.Оценка,
		|	ЗадачаНаДегустациюНовинкиОценкаПоказателей.Комментарий
		|ИЗ
		|	Документ.ЗадачаНаДегустациюНовинки.ОценкаПоказателей КАК ЗадачаНаДегустациюНовинкиОценкаПоказателей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗадачаНаДегустациюНовинки КАК ЗадачаНаДегустациюНовинки
		|		ПО ЗадачаНаДегустациюНовинкиОценкаПоказателей.Ссылка = ЗадачаНаДегустациюНовинки.Ссылка
		|ГДЕ
		|	ЗадачаНаДегустациюНовинкиОценкаПоказателей.Ссылка = &Ссылка
		|	И НЕ ЗадачаНаДегустациюНовинкиОценкаПоказателей.Оценка = 0");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ДвиженияОценки = ЭтотОбъект.Движения.ОценкиЗадачНаДегустациюНовинок;
	ДвиженияОценки.Записывать = Истина;
	ДвиженияОценки.Загрузить(Запрос.Выполнить().Выгрузить());
	//ДвиженияОценки.Записать();
	
	// изменений не было, ничего не делаем
	Если НЕ (ДополнительныеСвойства.Свойство("ТаблицаДвижений_ТоварныеОперации", ТаблицаДвижений_ТоварныеОперации) И ТаблицаДвижений_ТоварныеОперации.Количество() > 0) Тогда
		Возврат;
	КонецЕсли;
	
	// проверяем остатки (если есть сторнирующие операции, проверяем, чтобы новое кол-во не превышало остаток)
	ПроверятьНаличиеОстатков = (Магазин.ТипРозничнойТочки = Перечисления.ТипыРозничныхТочек.Магазин И Магазин.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Розница);
	Если ПроверятьНаличиеОстатков И ПараметрыСеанса.НомерТочкиПоАйпи <> 999 Тогда
		Попытка
			ТорговаяТочкаПоАйпи = ПараметрыСеанса.ТорговаяТочкаПоАйпи;
		Исключение
			ТорговаяТочкаПоАйпи = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
		КонецПопытки;
		ПолныеПрава.УстановитьТекущийМагазин(Магазин);
		
		Попытка
			
			ТЗнДляОстатков = ТаблицаДвижений_ТоварныеОперации.Скопировать(Новый Структура("Сторно", Ложь), "Номенклатура,Количество");
			Если ТЗнДляОстатков.Количество() > 0 Тогда
				ТЗнДляОстатков.Свернуть("Номенклатура", "Количество");
				ТЗнОстатков = ВнешниеДанные.ПолучитьТаблицуОстатковПоМассивуНоменклатурыПоТДОст(ТЗнДляОстатков.ВыгрузитьКолонку("Номенклатура"));
				
				Для Каждого СтрокаТД Из ТЗнДляОстатков Цикл
					
					ТекНоменклатура = СтрокаТД.Номенклатура;
					МассивСтрокТЧ = ТЗнОстатков.НайтиСтроки(Новый Структура("Номенклатура", ТекНоменклатура));
					Если МассивСтрокТЧ.Количество() = 1 Тогда
						ТЗнОстатков_Остаток = МассивСтрокТЧ[0].Остаток;
					Иначе
						ТЗнОстатков_Остаток = 0;
					КонецЕсли;
					
					// уменьшаем проверяемое кол-во на сторнируемое кол-во
					ТекКоличество = СтрокаТД.Количество;
					МассивСтрокТЧ = ТаблицаДвижений_ТоварныеОперации.НайтиСтроки(Новый Структура("Номенклатура,Сторно", ТекНоменклатура, Истина));
					Для Каждого СтрокаТЧ Из МассивСтрокТЧ Цикл
						ТекКоличество = ТекКоличество - СтрокаТЧ.Количество;
					КонецЦикла;
					
					// проверяем наличие остатков, только если количество увеличилось
					Если ТекКоличество > 0 И ТекКоличество > ТЗнОстатков_Остаток Тогда
						
				        Сообщение = Новый СообщениеПользователю();
				        Сообщение.Текст = "По номенклатуре '" + Строка(ТекНоменклатура) + "' количество (" + СтрокаТД.Количество +
							") превышает возможный остаток (" + (ТЗнОстатков_Остаток + СтрокаТД.Количество - ТекКоличество) + ")";
						
						Сообщение.Поле 	= "Оценки[0].Количество";
						
				        Сообщение.УстановитьДанные(ЭтотОбъект);
				        Сообщение.Сообщить();
				        Отказ = Истина;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
			
		Исключение
			
			ПолныеПрава.УстановитьТекущийМагазин(ТорговаяТочкаПоАйпи);
			ВызватьИсключение ОписаниеОшибки();
			
		КонецПопытки;
		
		ПолныеПрава.УстановитьТекущийМагазин(ТорговаяТочкаПоАйпи);
		Если Отказ Тогда Возврат; КонецЕсли;
	КонецЕсли;
	
	// проводим перемещение на новый виртуальный склад "Задание на дегустацию"
	ОрдерНаСклад = "";
	ДополнительныеСвойства.Свойство("ОрдерНаСклад", ОрдерНаСклад);
	Если ЗначениеЗаполнено(ОрдерНаСклад) Тогда
		ОрдерНаСкладОбъект = ОрдерНаСклад.ПолучитьОбъект();
		ОрдерНаСкладОбъект.ПометкаУдаления = Ложь;
		ОрдерНаСкладОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
	ПроводитьОперацииМагазина = (Магазин.ТипРозничнойТочки = Перечисления.ТипыРозничныхТочек.Магазин И Магазин.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Розница);
	Если ПроводитьОперацииМагазина И НЕ Отказ Тогда
		НомерТочкиПоАйпи = Магазин.НомерТочки;
		//ИДДок = Строка(Ссылка.УникальныйИдентификатор());
		ИдСсылкиСтр = Строка(Ссылка.УникальныйИдентификатор());
		ИДДок = Строка(Новый УникальныйИдентификатор());
		
		ИДНоменклатуры = Документы.МП_ЗадачаТехнолога.ВернутьИДНоменклатуры(ЭтотОбъект.Номенклатура);
		ИДХарактеристики = Документы.МП_ЗадачаТехнолога.ВернутьИДХарактеристики(ЭтотОбъект.ХарактеристикаНоменклатуры);
		ТекРозничнаяЦена = Документы.МП_ЗадачаТехнолога.РозничнаяЦена(ЭтотОбъект.Номенклатура);
		
		ОбработкаОбъект = Документы.МП_ЗадачаТехнолога.ПолучитьИПодключитьВнешнююОбработкуТоварныеОперации();
		//ОбработкаОбъект.УдалитьВсеСтрокиДокумента(ИДДок, НомерТочкиПоАйпи);
		
		ПредТипЗадания = Неопределено; ПредСторно = Неопределено; ЕстьДвиженияТД = Ложь;
		Для Каждого СтрокаТД Из ТаблицаДвижений_ТоварныеОперации Цикл
			
			ТекТипЗадания = "ЗадачаНаДегустациюНовинки";
			//Если ТекТипЗадания = Перечисления.ТипыЗаданийТехнологаМагазинам.УбратьСПолок ИЛИ ТекТипЗадания = Перечисления.ТипыЗаданийТехнологаМагазинам.ВернутьНаПолки Тогда
			Если ТекТипЗадания = Перечисления.ТипыЗаданийТехнологаМагазинам.ВернутьНаПолки Тогда
				Продолжить;
			КонецЕсли;
			
			ТекСторно = СтрокаТД.Сторно;
			Если ТекСторно <> ПредСторно И ЕстьДвиженияТД Тогда
				//Отказ = НЕ ОбработкаОбъект.ПровестиДок(ИДДок, КодОперации, ТекущаяДата(), НомерТочкиПоАйпи);
				ТекстОшибки = Документы.МП_ЗадачаТехнолога.ТоварныеОперации_ПровестиДок(ИдСсылкиСтр, ИДДок, ?(ПредСторно, КодОбратнойОперации, КодОперации), ТекущаяДата(), НомерТочкиПоАйпи, Истина); //Обязательно = ПредСторно
				//Если НЕ ПустаяСтрока(ТекстОшибки) Тогда Сообщить("Документ не проведён по причине: " + ТекстОшибки); Отказ = Истина; Возврат; КонецЕсли;
				Если НЕ ПустаяСтрока(ТекстОшибки) Тогда ВызватьИсключение "Документ не проведён по причине: " + ТекстОшибки; Возврат; КонецЕсли;
				
				ЕстьДвиженияТД = Ложь;
				ИДДок = ?(ТекСторно, Строка(Новый УникальныйИдентификатор()), ID_Doc_SQL);
			КонецЕсли;
			ПредСторно = ТекСторно;
			
			Если ТекТипЗадания <> ПредТипЗадания Тогда
				РегистрыСведений.СоответствиеТиповЗаданийТехнологаМагазинамИСкладов.ПолучитьТочкуПолучателяИКодыОпераций(ТекТипЗадания, ТочкаПолучатель, КодОперации, КодОбратнойОперации);
				ПредТипЗадания = ТекТипЗадания;
				ИДДок = ?(ТекСторно, Строка(Новый УникальныйИдентификатор()), ID_Doc_SQL);
			КонецЕсли;
			
			//+++АК SHEP 20161025: отключил проверку
			// если операция не принята сервером, то сторнировать её не будем
			//Если ТекСторно И НЕ ТоварныеОперации_ОперацияПринятаСервером(ИДДок, СтрокаТД.КлючСтрокиСтр, НомерТочкиПоАйпи) Тогда
			//	Продолжить;
			//КонецЕсли;
			//---АК SHEP 
			
			РозничнаяСумма = СтрокаТД.Количество * ТекРозничнаяЦена;
			Отказ = НЕ ОбработкаОбъект.ДобавитьНовуюСтрокуТовародвижения(
				ИДНоменклатуры,
				СтрокаТД.Количество, СтрокаТД.НомерСтроки, ТекущаяДата(), ИДДок, ?(ТекСторно, КодОбратнойОперации, КодОперации),
				ТочкаПолучатель, РозничнаяСумма, 0, Строка(Новый УникальныйИдентификатор),
				ИДХарактеристики, СтрокаТД.ДатаПроизводства,,,,, НомерТочкиПоАйпи);
				
			Если Отказ Тогда Возврат; КонецЕсли;
			
			ЕстьДвиженияТД = Истина;
			
		КонецЦикла;
		
		Если ЕстьДвиженияТД Тогда
			
			ТекстОшибки = Документы.МП_ЗадачаТехнолога.ТоварныеОперации_ПровестиДок(ИдСсылкиСтр, ИДДок, ?(ТекСторно, КодОбратнойОперации, КодОперации), ТекущаяДата(), НомерТочкиПоАйпи, Истина); //Обязательно = ТекСторно
			//Если НЕ ПустаяСтрока(ТекстОшибки) Тогда Сообщить("Документ не проведён по причине: " + ТекстОшибки); Отказ = Истина; Возврат; КонецЕсли;
			Если НЕ ПустаяСтрока(ТекстОшибки) Тогда ВызватьИсключение "Документ не проведён по причине: " + ТекстОшибки; Возврат; КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
