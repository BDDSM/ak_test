
Функция ПечатьСличительнойВедомости(Ссылка) Экспорт 
	
	ТабДокумент	= Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПараметрыПечати_ОприходованиеОС_СличительнаяВедомость";
	
	Макет = Документы.ОприходованиеОС.ПолучитьМакет("СличительнаяВедомость");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ТекНомер = Ссылка.Номер;
	Пока Лев(ТекНомер, 1) = "0" Цикл
		ТекНомер = Сред(ТекНомер, 2);
	КонецЦикла;
    ОбластьМакета.Параметры.Номер 	= ТекНомер;
    ОбластьМакета.Параметры.Дата 	= Формат(Ссылка.Дата, "ДЛФ=Д");
    ОбластьМакета.Параметры.Склад 	= ?(ЗначениеЗаполнено(Ссылка.Склад), СокрЛП(Ссылка.Склад.Наименование), "");
    ОбластьМакета.Параметры.ЦФО 	= СокрЛП(Ссылка.ЦФО.Наименование);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Запрос = Новый Запрос;
	ТекВидГраницы = ?(Ссылка.Проведен, ВидГраницы.Включая, ВидГраницы.Исключая);
	Запрос.Параметры.Вставить("ДатаСреза"	, Новый Граница(Ссылка.МоментВремени(), ТекВидГраницы));
	Запрос.Параметры.Вставить("Ссылка"		, Ссылка);
	Запрос.Параметры.Вставить("Склад"		, Ссылка.Склад);
	Запрос.Параметры.Вставить("ЦФО"			, Ссылка.ЦФО);
	Запрос.УстановитьПараметр("СчетУчета"	, Ссылка.СчетУчетаБУ);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТЧОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТЧОС.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	1 КАК Количество,
	|	0 КАК Сумма,
	|	ЕСТЬNULL(ФинансовыйОстатки.КоличествоОстаток, 0) КАК КоличествоУчет,
	|	ЕСТЬNULL(ФинансовыйОстатки.СуммаОстаток, 0) КАК СуммаУчет
	|ПОМЕСТИТЬ ВТОсновная
	|ИЗ
	|	Документ.ОприходованиеОС.Номенклатура КАК ТЧОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Финансовый.Остатки(
	|				&ДатаСреза,
	|				Счет = &СчетУчета,
	|				,
	|				Субконто1 = &ЦФО
	|					И Субконто2 = &Склад) КАК ФинансовыйОстатки
	|		ПО (ФинансовыйОстатки.Субконто3 = ТЧОС.ОсновноеСредство)
	|ГДЕ
	|	ТЧОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИнвентарныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОсновная.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТОсновная.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	СУММА(ВТОсновная.Количество) КАК КоличествоФакт,
	|	СУММА(ВТОсновная.КоличествоУчет) КАК КоличествоУчет,
	|	СУММА(ВТОсновная.Количество) - СУММА(ВТОсновная.КоличествоУчет) КАК КоличествоРасхождение,
	|	СУММА(ВТОсновная.Сумма) КАК СуммаФакт,
	|	СУММА(ВТОсновная.СуммаУчет) КАК СуммаУчет,
	|	СУММА(ВТОсновная.Сумма) - СУММА(ВТОсновная.СуммаУчет) КАК СуммаРасхождение
	|ИЗ
	|	ВТОсновная КАК ВТОсновная
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОсновная.ОсновноеСредство,
	|	ВТОсновная.ИнвентарныйНомер
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТОсновная.ОсновноеСредство.Наименование,
	|	ВТОсновная.ИнвентарныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОсновная";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НомерСтроки = 0;
	
	ИтСуммаУчет = 0;
	ИтСуммаРасх = 0;
	
 	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Пока Выборка.Следующий() Цикл

		НомерСтроки = НомерСтроки + 1;
		
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.НомерСтроки = Формат(НомерСтроки, "ЧГ=");
    	ТабДокумент.Вывести(ОбластьМакета);

		ИтСуммаУчет = ИтСуммаУчет + Выборка.СуммаУчет;
		ИтСуммаРасх = ИтСуммаРасх + Выборка.СуммаРасхождение;
		
	КонецЦикла;
	
 	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.СуммаУчет 			= ИтСуммаУчет;
	ОбластьМакета.Параметры.СуммаРасхождение 	= ИтСуммаРасх;
	ТабДокумент.Вывести(ОбластьМакета);

	ТабДокумент.АвтоМасштаб 		= Истина;
	ТабДокумент.ФиксацияСверху 		= 8;
	ТабДокумент.ОтображатьСетку 	= Ложь;
	ТабДокумент.Защита 				= Ложь;
	ТабДокумент.ТолькоПросмотр 		= Ложь;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДокумент;
	
КонецФункции
