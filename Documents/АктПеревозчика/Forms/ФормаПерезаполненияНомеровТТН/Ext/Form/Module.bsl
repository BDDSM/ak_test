
&НаСервереБезКонтекста
Процедура ПерезаполнитьНомераТТНСервер(мДатаНачала, мДатаОкончания, мОрганизация, мВидОперации, НачальныйНомерТТН)
	
	ЕстьДатаОкончания = ЗначениеЗаполнено(мДатаОкончания);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала"			, мДатаНачала);
	Если ЕстьДатаОкончания Тогда
		Запрос.УстановитьПараметр("ДатаОкончания"	, КонецДня(мДатаОкончания));
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидОперации"			, мВидОперации);
	Запрос.УстановитьПараметр("Организация"			, мОрганизация);
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АктПеревозчика.Ссылка
	|ИЗ
	|	Документ.АктПеревозчика КАК АктПеревозчика
	|ГДЕ
	|	АктПеревозчика.ВидОперации = &ВидОперации
	|	И АктПеревозчика.Организация = &Организация
	|	И НЕ АктПеревозчика.ДатаОкончания < &ДатаНачала
	|	И НЕ АктПеревозчика.ПометкаУдаления
	|	И &УсловиеПоДатеОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	АктПеревозчика.МоментВремени";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "
	|	И &УсловиеПоДатеОкончания",
		?(ЕстьДатаОкончания, "
	|	И НЕ АктПеревозчика.ДатаОкончания > &ДатаОкончания", ""));
	
	Запрос.Текст = ТекстЗапроса;
	
	
	//
	ЭтоОтгрузкаВТТ 	= (мВидОперации = Перечисления.ВидыОперацийАктПеревозчика.ОтгрузкаВТорговуюТочку);
	ИмяТЧ 			= ?(ЭтоОтгрузкаВТТ, "РасходныеОрдера", "ПриходныеОрдера");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
		
		ВремТаблица = ОбъектДокумента[ИмяТЧ].Выгрузить();
		ВремТаблица.Колонки.Добавить("ДатаДень");
		ВремТаблица.Колонки.Добавить("Дата");
		Для Каждого СтрокаТаблицы Из ВремТаблица Цикл			
			СтрокаТаблицы.Дата 		= ?(ЭтоОтгрузкаВТТ, СтрокаТаблицы.РасходныйОрдер.Дата, СтрокаТаблицы.ПриходныйОрдер.ДатаВремяЗаездаМашины);
			СтрокаТаблицы.ДатаДень 	= НачалоДня(СтрокаТаблицы.Дата);
		КонецЦикла;
		ВремТаблица.Сортировать("Дата, Автомобиль");
		
		ТекДатаДень		= Неопределено;
		ТекАвтомобиль 	= Неопределено;
		ТекНомерТТН 	= НачальныйНомерТТН - 1;
		Для Каждого СтрокаТаблицы Из ВремТаблица Цикл
			
			Если (НЕ СтрокаТаблицы.ДатаДень = ТекДатаДень)
					ИЛИ НЕ СтрокаТаблицы.Автомобиль = ТекАвтомобиль ТОгда
				ТекНомерТТН = ТекНомерТТН + 1;
			КонецЕсли;
			
			СтрокаТЧ = ОбъектДокумента[ИмяТЧ][СтрокаТаблицы.НомерСтроки - 1];
			СтрокаТЧ.НомерТТН = ТекНомерТТН;
			
			ТекДатаДень 	= СтрокаТаблицы.ДатаДень;
			ТекАвтомобиль 	= СтрокаТаблицы.Автомобиль;
		КонецЦикла;
		
		Попытка
			ОбъектДокумента.Записать();
			НачальныйНомерТТН = ТекНомерТТН + 1;
			Сообщить(ОбъектДокумента);
		Исключение
			Сообщить(ИнформацияОбОшибке().Описание);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьНомераТТН(Команда)
	
	НачальныйНомерТТН = ДопМодульСервер.ПолучитьНачальныйНомерТТН(ЭтаФорма.Организация, ЭтаФорма.ВидОперации, ЭтаФорма.ДатаНачала, Год(ЭтаФорма.ДатаНачала));
	Если НачальныйНомерТТН = 0 Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Не определен начальный номер ТТН!";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПерезаполнитьНомераТТНСервер(ЭтаФорма.ДатаНачала, ЭтаФорма.ДатаОкончания, ЭтаФорма.Организация, ЭтаФорма.ВидОперации, НачальныйНомерТТН);
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтаФорма.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийАктПеревозчика.ОтгрузкаВТорговуюТочку");
	
КонецПроцедуры
