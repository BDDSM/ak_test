
Функция СформироватьТаблицуДоли() 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(РасходыНаМобильнуюСвязь.ДатаНачала, МЕСЯЦ) КАК Дата,
		|	РасходыНаМобильнуюСвязь.ДатаОкончания
		|ПОМЕСТИТЬ втДаты
		|ИЗ
		|	Документ.РасходыНаМобильнуюСвязь КАК РасходыНаМобильнуюСвязь
		|ГДЕ
		|	РасходыНаМобильнуюСвязь.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(РасходыНаМобильнуюСвязь.ДатаОкончания, МЕСЯЦ),
		|	РасходыНаМобильнуюСвязь.ДатаОкончания
		|ИЗ
		|	Документ.РасходыНаМобильнуюСвязь КАК РасходыНаМобильнуюСвязь
		|ГДЕ
		|	РасходыНаМобильнуюСвязь.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втДаты.Дата КАК Месяц,
		|	1 КАК Доля,
		|	втДаты.ДатаОкончания КАК Период
		|ИЗ
		|	втДаты КАК втДаты";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ТаблицаДоли = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаДоли.Количество() = 1 Тогда
		Возврат ТаблицаДоли;
	КонецЕсли;	
	
	ТаблицаДоли.Очистить();
	
	ДатаПеребор = ДатаНачала;
	
	Пока ДатаПеребор <= ДатаОкончания Цикл
		
		НоваяСтрока = ТаблицаДоли.Добавить();
		НоваяСтрока.Месяц = НачалоМесяца(ДатаПеребор);
		НоваяСтрока.Доля  = 1;
		ДатаПеребор = ДатаПеребор+24*60*60;
		
	КонецЦикла;		
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(Массив);

	
	ТаблицаДоли.Свернуть("Месяц","Доля");
	Всего = ТаблицаДоли.Итог("Доля");
	ТаблицаДоли.Колонки.Добавить("Период",ОписаниеТиповДата);
	
	Для каждого СтрокаМесяц из ТаблицаДоли Цикл
		СтрокаМесяц.Доля = СтрокаМесяц.Доля/Всего;
		Если СтрокаМесяц.Месяц = НачалоМесяца(ЭтотОбъект.ДатаОкончания) Тогда
			СтрокаМесяц.Период = ЭтотОбъект.ДатаОкончания;
		Иначе 
			СтрокаМесяц.Период = КонецМесяца(СтрокаМесяц.Месяц);
		КонецЕсли;	
	КонецЦикла;	
	
	ВсегоПослеДеления = ТаблицаДоли.Итог("Доля");
	
	Корректировка = 1-ВсегоПослеДеления;
	ПолследняяСтрока = ТаблицаДоли.Количество()-1;
	ТаблицаДоли[ПолследняяСтрока].Доля=ТаблицаДоли[ПолследняяСтрока].Доля+Корректировка;
	
	Возврат ТаблицаДоли;
	
КонецФункции	

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	ДоляМесяцев = СформироватьТаблицуДоли();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоляМесяцев.Месяц,
		|	ДоляМесяцев.Доля,
		|	ДоляМесяцев.Период
		|ПОМЕСТИТЬ втДниМесяцев
		|ИЗ
		|	&ДоляМесяцев КАК ДоляМесяцев
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходыНаМобильнуюСвязь.СимКарта,
		|	РасходыНаМобильнуюСвязь.АбонентскаяПлата,
		|	РасходыНаМобильнуюСвязь.МобильнаяСвязь,
		|	РасходыНаМобильнуюСвязь.SMSMMS,
		|	РасходыНаМобильнуюСвязь.Интернет,
		|	РасходыНаМобильнуюСвязь.Роуминг,
		|	РасходыНаМобильнуюСвязь.ДополнительныеУслуги,
		|	РасходыНаМобильнуюСвязь.Скидка,
		|	РасходыНаМобильнуюСвязь.Ссылка.Оператор
		|ПОМЕСТИТЬ втРасходыДокумента
		|ИЗ
		|	Документ.РасходыНаМобильнуюСвязь.Расходы КАК РасходыНаМобильнуюСвязь
		|ГДЕ
		|	РасходыНаМобильнуюСвязь.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРасходыДокумента.СимКарта,
		|	втДниМесяцев.Период КАК Период,
		|	втРасходыДокумента.АбонентскаяПлата * втДниМесяцев.Доля * &СтавкаНДС КАК АбонентскаяПлата,
		|	втРасходыДокумента.МобильнаяСвязь * втДниМесяцев.Доля * &СтавкаНДС КАК МобильнаяСвязь,
		|	втРасходыДокумента.SMSMMS * втДниМесяцев.Доля * &СтавкаНДС КАК SMSMMS,
		|	втРасходыДокумента.Интернет * втДниМесяцев.Доля * &СтавкаНДС КАК Интернет,
		|	втРасходыДокумента.Роуминг * втДниМесяцев.Доля * &СтавкаНДС КАК Роуминг,
		|	втРасходыДокумента.ДополнительныеУслуги * втДниМесяцев.Доля * &СтавкаНДС КАК ДополнительныеУслуги,
		|	втРасходыДокумента.Скидка * втДниМесяцев.Доля * &СтавкаНДС КАК Скидка,
		|	втРасходыДокумента.Оператор
		|ИЗ
		|	втРасходыДокумента КАК втРасходыДокумента,
		|	втДниМесяцев КАК втДниМесяцев";

	Если ЭтотОбъект.ВариантРасчетаНДС = Перечисления.ВариантыРасчетаНДС.НДСсверху Тогда
		СтавкаНДС = 1.18;
	Иначе 
		СтавкаНДС = 1;
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("СтавкаНДС",СтавкаНДС);
	Запрос.УстановитьПараметр("ДоляМесяцев",ДоляМесяцев);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Результат = Запрос.Выполнить().Выгрузить();

	Движения.РасходыНаМобильнуюСвязь.Записывать = Истина;
	Движения.РасходыНаМобильнуюСвязь.Загрузить(Результат);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ТаблицаРасходы = ЭтотОбъект.Расходы.Выгрузить();
	ИтогоАбонентскаяПлата = ТаблицаРасходы.Итог("АбонентскаяПлата");
	ИтогоМобильнаяСвязь = ТаблицаРасходы.Итог("МобильнаяСвязь");
	ИтогоSMSMMS = ТаблицаРасходы.Итог("SMSMMS");
	ИтогоИнтернет = ТаблицаРасходы.Итог("Интернет");
	ИтогоРоуминг = ТаблицаРасходы.Итог("Роуминг");
	ИтогоДополнительныеУслуги = ТаблицаРасходы.Итог("ДополнительныеУслуги");
	ИтогоСкидка = ТаблицаРасходы.Итог("Скидка");
	СуммаДокумента = ИтогоАбонентскаяПлата+
					ИтогоМобильнаяСвязь +
					ИтогоSMSMMS +
					ИтогоИнтернет +
					ИтогоРоуминг +
					ИтогоДополнительныеУслуги -
					ИтогоСкидка;
 
КонецПроцедуры
