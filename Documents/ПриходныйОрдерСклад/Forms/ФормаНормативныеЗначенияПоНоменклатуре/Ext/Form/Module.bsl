
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ТекущаяДата() - 86400));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ТекущаяДата() + 86400));
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаказПоставщику.Поставщик,
	               |	ЗаказПоставщику.Склад
	               |ИЗ
	               |	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	               |ГДЕ
	               |	ЗаказПоставщику.ДатаПоступления МЕЖДУ &ДатаНач И &ДатаКон
	               |	И ЗаказПоставщику.Проведен = ИСТИНА
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЗаказПоставщику.Склад.Наименование,
	               |	ЗаказПоставщику.Поставщик.Наименование";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаб = ТабПоставщики.Добавить();
		СтрокаТаб.Склад = Выборка.Склад;
		СтрокаТаб.Поставщик = Выборка.Поставщик;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДанныеПоПоставщикуСервер(Склад, Поставщик)
	
	ТабДок.Очистить();
	
	Макет = Документы.ПриходныйОрдерСклад.ПолучитьМакет("НормативныеЗначения");
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Склад = Склад;
	Область.Параметры.Поставщик = Поставщик;
	ТабДок.Вывести(Область);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ТекущаяДата() - 86400));
	Запрос.УстановитьПараметр("ДатаТек", ТекущаяДата());
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ТекущаяДата() + 86400));
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Поставщик", Поставщик);
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаказПоставщикуПредзаказы.Документ
	               |ПОМЕСТИТЬ ВТ_Предзаказы
	               |ИЗ
	               |	Документ.ЗаказПоставщику.Предзаказы КАК ЗаказПоставщикуПредзаказы
	               |ГДЕ
	               |	ЗаказПоставщикуПредзаказы.Ссылка.ДатаПоступления МЕЖДУ &ДатаНач И &ДатаКон
	               |	И ЗаказПоставщикуПредзаказы.Ссылка.Проведен = ИСТИНА
	               |	И ЗаказПоставщикуПредзаказы.Ссылка.Поставщик = &Поставщик
	               |	И ЗаказПоставщикуПредзаказы.Ссылка.Склад = &Склад
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПредзаказТовары.Номенклатура,
	               |	ПредзаказТовары.Характеристика КАК Характеристика,
	               |	СУММА(ПредзаказТовары.Количество) КАК Количество,
	               |	ПредзаказТовары.Характеристика.ПредельноеКоличествоДнейСрокаГодности КАК ПредельноеКоличествоДнейСрокаГодности,
	               |	ЕСТЬNULL(НормативныйКвантУпаковкиСрезПоследних.Квант, 0) КАК Квант
	               |ИЗ
	               |	Документ.Предзаказ.Товары КАК ПредзаказТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйКвантУпаковки.СрезПоследних(&ДатаТек, Склад = &Склад) КАК НормативныйКвантУпаковкиСрезПоследних
	               |		ПО ПредзаказТовары.Характеристика = НормативныйКвантУпаковкиСрезПоследних.Характеристика
	               |ГДЕ
	               |	ПредзаказТовары.Ссылка В
	               |			(ВЫБРАТЬ
	               |				ВТ_Предзаказы.Документ
	               |			ИЗ
	               |				ВТ_Предзаказы КАК ВТ_Предзаказы)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПредзаказТовары.Номенклатура,
	               |	ПредзаказТовары.Характеристика,
	               |	ЕСТЬNULL(НормативныйКвантУпаковкиСрезПоследних.Квант, 0),
	               |	ПредзаказТовары.Характеристика.ПредельноеКоличествоДнейСрокаГодности
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПредзаказТовары.Номенклатура.Наименование,
	               |	Характеристика";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Строка");
		Область.Параметры.Номенклатура = Выборка.Номенклатура;
		Область.Параметры.Характеристика = Выборка.Характеристика;
		Область.Параметры.Количество = Выборка.Количество;
		Область.Параметры.Квант = Выборка.Квант;
		Если НЕ ЗначениеЗаполнено(Выборка.ПредельноеКоличествоДнейСрокаГодности) Тогда
			Область.Параметры.ДатаПоставки = "";
		Иначе	
			Область.Параметры.ДатаПоставки = Формат(ТекущаяДата() - 86400 * Выборка.ПредельноеКоличествоДнейСрокаГодности, "ДФ=dd.MM.yyyy");
		КонецЕсли;	
		ТабДок.Вывести(Область);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДанныеПоПоставщику()
	
	СформироватьДанныеПоПоставщикуСервер(Элементы.ТабПоставщики.ТекущиеДанные.Склад, Элементы.ТабПоставщики.ТекущиеДанные.Поставщик);
	
КонецПроцедуры

&НаКлиенте
Процедура ТабПоставщикиПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("СформироватьДанныеПоПоставщику", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ТабДок.Напечатать(РежимИспользованияДиалогаПечати.Использовать);
	
КонецПроцедуры
