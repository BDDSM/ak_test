
#Область ОбработчикиСобытий

//+++АК LATV 2018.10.03 ИП-00020009
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	УникальныйИдентификатор = ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор;
	ПечатьВФоне(ПараметрКоманды, УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.10.03 ИП-00020009
&НаКлиенте
Процедура ПечатьВФоне(МассивСсылок, УникальныйИдентификатор)

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения	= Истина;
	ПараметрыОжидания.ВыводитьСообщения				= Истина;
	ПараметрыОжидания.ТекстСообщения				= НСтр("ru = 'Печать документов ""Задание на разборку""'");
	ПараметрыОжидания.ОповещениеПользователя.Показать	= Истина;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение	= НСтр("ru = 'Печать документов ""Задание на разборку"" завершена'");
	
	Оповещение = Новый ОписаниеОповещения("ПечатьВФонеЗавершение", ЭтотОбъект);
	
	ПараметрыДополнительнойОбработки = Новый Структура;
	ПараметрыДополнительнойОбработки.Вставить("УстановитьПризнакНапечатан");
	ПараметрыДополнительнойОбработки.Вставить("СформироватьРасходныеОрдера");
	
	ДлительнаяОперация = ПечатьНаСервере(МассивСсылок, ПараметрыДополнительнойОбработки, УникальныйИдентификатор);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

//+++АК LATV 2018.10.03 ИП-00020009
&НаСервере
Функция ПечатьНаСервере(МассивСсылок, ПараметрыДополнительнойОбработки, УникальныйИдентификатор)

	// Выполняемый метод
	ИмяМетода = "Документы.ЗаданиеНаРазборку.ПечатьСДополнительнойОбработкой";
	
	ПараметрыМетода = Новый Структура();
	ПараметрыМетода.Вставить("ИмяМакета",							"ЗаданиеНаРазборку");
	ПараметрыМетода.Вставить("МассивСсылок",						МассивСсылок);
	ПараметрыМетода.Вставить("ПараметрыДополнительнойОбработки",	ПараметрыДополнительнойОбработки);
	
	// Запуск фонового задания
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыМетода, ПараметрыВыполнения);

КонецФункции

//+++АК LATV 2018.10.03 ИП-00020009
&НаКлиенте
Процедура ПечатьВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда // Задание отменено
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		Если Результат.Сообщения <> Неопределено Тогда
			Для Каждого Сообщение Из Результат.Сообщения Цикл
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		ОбработатьРезультат(Результат.АдресРезультата);
		Возврат;
	КонецЕсли;

КонецПроцедуры

//+++АК LATV 2018.10.03 ИП-00020009
&НаКлиенте
Процедура ОбработатьРезультат(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	ТабДок = Результат.ТабличныйДокумент;
	ТабДок.ОтображатьСетку		= Ложь;
	ТабДок.Защита				= Истина;
	ТабДок.ТолькоПросмотр		= Истина;
	ТабДок.ОтображатьЗаголовки	= Ложь;
	ТабДок.Показать();
	
	// Обновление открытых документов
	Оповестить("Напечатали", Результат.МассивСсылок);

КонецПроцедуры

#КонецОбласти
