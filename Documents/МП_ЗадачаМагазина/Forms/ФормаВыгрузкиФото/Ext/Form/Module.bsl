
&НаКлиенте
Процедура РедактироватьПериод(Команда)
	
	СтандартныйПериод = Новый СтандартныйПериод(ДатаНачала, ДатаОкончания);
	ДиалогРедактирования = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогРедактирования.Период = СтандартныйПериод;
	Если ДиалогРедактирования.Редактировать() Тогда
		СтандартныйПериод = ДиалогРедактирования.Период;
		ДатаНачала = СтандартныйПериод.ДатаНачала;
		ДатаОкончания = СтандартныйПериод.ДатаОкончания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогДляСохраненияФотоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогСохраненияФайла.Заголовок   		= "Выберите каталог для сохранения фото";
	
	Если ДиалогСохраненияФайла.Выбрать() Тогда
		
		КаталогДляСохраненияФото = ДиалогСохраненияФайла.Каталог;
		
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаНачала = ТекущаяДата() - 86400*7;
	ДатаОкончания = ТекущаяДата();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтруктуруДляВыгрузки()
	
	КаталогФотографий = СокрЛП(Константы.МП_КаталогХраненияФайловЗадачМП.Получить());
	Если ПустаяСтрока(КаталогФотографий) Тогда
		Сообщить("Не заполнена константа КаталогХраненияФайловЗадачМП!");
		Возврат;
	КонецЕсли;
	
	Если Прав(КаталогФотографий, 1) <> "\" Тогда
		КаталогФотографий = КаталогФотографий + "\";
	КонецЕсли;
	
	ТаблицаСФайлами.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	МП_ЗадачаМагазина.Магазин,
	               |	МП_ЗадачаМагазинаРакурсы.Ракурс,
	               |	МП_ФотографииКЗадачам.РасширениеФайла,
	               |	МП_ФотографииКЗадачам.ОтносительноеИмяФайла,
	               |	МП_ЗадачаМагазина.Дата,
	               |	МП_ФотографииКЗадачам.СтатусФото КАК Статус
	               |ИЗ
	               |	Документ.МП_ЗадачаМагазина КАК МП_ЗадачаМагазина
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МП_ЗадачаМагазина.Ракурсы КАК МП_ЗадачаМагазинаРакурсы
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МП_ФотографииКЗадачам КАК МП_ФотографииКЗадачам
	               |			ПО МП_ЗадачаМагазинаРакурсы.Ссылка = МП_ФотографииКЗадачам.Задача
	               |				И МП_ЗадачаМагазинаРакурсы.Ракурс = МП_ФотографииКЗадачам.Ракурс
	               |		ПО МП_ЗадачаМагазина.Ссылка = МП_ЗадачаМагазинаРакурсы.Ссылка
	               |ГДЕ
	               |	МП_ЗадачаМагазина.Дата МЕЖДУ &ДатаНач И &ДатаКон
	               |	И НЕ МП_ФотографииКЗадачам.Задача ЕСТЬ NULL 
	               |	И (МП_ЗадачаМагазина.Магазин В (&Магазины)
	               |			ИЛИ &ЕстьОтборПоМагазинам = ЛОЖЬ)
	               |	И (МП_ФотографииКЗадачам.Ракурс В (&Ракурсы)
	               |			ИЛИ &ЕстьОтборПоРакурсам = ЛОЖЬ)
	               |	И (МП_ФотографииКЗадачам.СтатусФото В (&СтатусыФото)
	               |			ИЛИ &ЕстьОтборПоСтатусуФото = ЛОЖЬ)
	               |	И ПОДСТРОКА(МП_ФотографииКЗадачам.ОтносительноеИмяФайла, 1, 1) <> "" ""
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	МП_ЗадачаМагазина.Магазин,
	               |	МП_ЗадачаМагазина.Дата,
	               |	МП_ЗадачаМагазинаРакурсы.Ракурс";
				   
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Магазины", Магазины.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ЕстьОтборПоМагазинам", Магазины.Количество() > 0);
	Запрос.УстановитьПараметр("Ракурсы", Ракурсы.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ЕстьОтборПоРакурсам", Ракурсы.Количество() > 0);
	Запрос.УстановитьПараметр("СтатусыФото", СтатусыФото.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ЕстьОтборПоСтатусуФото", СтатусыФото.Количество() > 0);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ФайлКартинка = Новый Файл(КаталогФотографий + Выборка.ОтносительноеИмяФайла);
		Если ФайлКартинка.Существует() Тогда
			СтрокаДоб = ТаблицаСФайлами.Добавить();
			СтрокаДоб.Дата = Выборка.Дата;
			СтрокаДоб.Магазин = Выборка.Магазин;
			СтрокаДоб.Ракурс = Выборка.Ракурс;
			СтрокаДоб.Статус = Выборка.Статус;
			СтрокаДоб.Расширение = Выборка.РасширениеФайла;
			СтрокаДоб.ПолныйПуть = ФайлКартинка.ПолноеИмя;
			//СтрокаДоб.Картинка = Новый Картинка(ФайлКартинка.ПолноеИмя);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура ПоместитьКартинкиДляВыгрузки()
	
	Счетчик = 0;
	Пока Счетчик <> 10 И Счетчик <= ТаблицаСФайлами.Количество() - 1 Цикл
		ТаблицаСФайлами[Счетчик].Заполнено = Истина;
		ТаблицаСФайлами[Счетчик].Картинка = Новый Картинка(ТаблицаСФайлами[Счетчик].ПолныйПуть);
		Счетчик = Счетчик + 1;
	КонецЦикла;	
	
КонецПроцедуры	



&НаКлиенте
Процедура ВыполнитьВыгрузку(Команда)
	
	Если НЕ ЗначениеЗаполнено(КаталогДляСохраненияФото) Тогда
		Сообщить("Не заполнен каталог для выгрузки фото");
		Возврат;
	КонецЕсли;
	
	Если Прав(КаталогДляСохраненияФото, 1) <> "\" Тогда
		КаталогДляСохраненияФото = КаталогДляСохраненияФото + "\";
	КонецЕсли;
	
	ПолучитьСтруктуруДляВыгрузки();
	
	Счетчик = 0;
	Пока ТаблицаСФайлами.Количество() <> 0 Цикл
		ПоместитьКартинкиДляВыгрузки();
		КолвоСтрок = ТаблицаСФайлами.Количество();
		Для н = 1 По КолвоСтрок Цикл
			Если ТаблицаСФайлами[КолвоСтрок - н].Заполнено Тогда
				ИмяФайла = Формат(Счетчик, "ЧГ=0") + "_" + Формат(ТаблицаСФайлами[КолвоСтрок - н].Дата, "ДФ=yyyyMMdd") + "_" + ТаблицаСФайлами[КолвоСтрок - н].Магазин + "_"
								+ ТаблицаСФайлами[КолвоСтрок - н].Ракурс + "_" + ТаблицаСФайлами[КолвоСтрок - н].Статус;
				
				ИмяФайла = СокрЛП(ИмяФайла);
				ИмяФайла = СтрЗаменить(ИмяФайла, ".", "_");
				ИмяФайла = СтрЗаменить(ИмяФайла, "/", "_");
				ИмяФайла = СтрЗаменить(ИмяФайла, "\", "_");
				ИмяФайла = СтрЗаменить(ИмяФайла, """", "");
				ТаблицаСФайлами[КолвоСтрок - н].Картинка.Записать(КаталогДляСохраненияФото + ИмяФайла + "." + ТаблицаСФайлами[КолвоСтрок - н].Расширение);
				ТаблицаСФайлами.Удалить(КолвоСтрок - н);
				Счетчик = Счетчик + 1;
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры
