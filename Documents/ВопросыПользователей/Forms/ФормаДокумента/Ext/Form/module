
////////////////////////////////////////////////////////////////////////////////
// ГЛОБАЛЬНЫЕ ПЕРЕМЕННЫЕ
////////////////////////////////////////////////////////////////////////////////
Перем мЭтоНовый;

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////

Процедура ПриОткрытии()
	Если ЭтоНовый() Тогда
		Дата 		= ТекущаяДата();
		Автор 		= ПараметрыСеанса.ТекущийПользователь;
		мЭтоНовый 	= Истина;
	Иначе
		мЭтоНовый 	= Ложь;
		Вопрос		= ФорматироватьСтрокуДляКомментария(Вопрос);
	КонецЕсли;
	ТолькоПросмотр = НЕ СвойВопрос() И НЕ Админ;
	ЭлементыФормы.Автор.ТолькоПросмотр = НЕ Админ;
	ЭлементыФормы.ПоказыватьВНовостях.Видимость = Админ;
	ЭлементыФормы.ОбязателенДляОзнакомления.Видимость = Админ;
КонецПроцедуры // ПриОткрытии()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Отказ = МодульФорум.НезаполненыОбязательныеПоля(ЭтаФорма);
	Для каждого СтрокаТЧ Из ТЧСсылки Цикл
		Если СтрокаТЧ.СсылкаНаОбъект = Неопределено Тогда
			ЭлементыФормы.ТЧСсылки.ТекущаяСтрока  	= СтрокаТЧ;
			ЭлементыФормы.ТЧСсылки.ТекущаяКолонка 	= ЭлементыФормы.ТЧСсылки.Колонки.СсылкаНаОбъект;
			ЭлементыФормы.ПанельТЧ.ТекущаяСтраница 	= ЭлементыФормы.ПанельТЧ.Страницы.СтраницаСсылки;
			Сообщить("В строке №" + СтрокаТЧ.НомерСтроки + " табличной части ""Ссылки на объект"" не выбран объект!
			|Выберите объект или удалите пустую строку", СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
	Вопрос = ФорматироватьСтрокуДляHTML(Вопрос);
КонецПроцедуры // ПередЗаписью()

Процедура ПослеЗаписи()
	Если мЭтоНовый Тогда
		МодульФорум.ОповеститьАдминистраторовОНовомВопросе(Ссылка);
		Оповестить("ЗаписанНовыйВопрос","",Ссылка);
		мЭтоНовый = Ложь;
	КонецЕсли;
КонецПроцедуры // ПослеЗаписи()



////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////

Процедура ТЧФайлыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	Если Колонка.Имя <> "Комментарий" Тогда
		ПоказатьПрикрепленныйФайл(ВыбраннаяСтрока.ИмяФайла);
	КонецЕсли;
КонецПроцедуры

Процедура ТЧФайлыПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = Истина;
КонецПроцедуры




////////////////////////////////////////////////////////////////////////////////
// НАЖАТИЕ КНОПОК
////////////////////////////////////////////////////////////////////////////////

Процедура ДобавитьФайл(Кнопка)
	ЗначениеВозврата = ВыбратьФайл();
	Если ЗначениеВозврата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураВозврата = ПолучитьИнформациюОФайле(ЗначениеВозврата);
	МаксРазмер = Справочники.НастройкиФорума.ОграничениеНаРазмерФайлов_МБ.Наименование;
	Попытка
		МаксРазмер = Число(МаксРазмер);
	Исключение
		МаксРазмер = 0;
	КонецПопытки;
	Если МаксРазмер <> 0 И СтруктураВозврата.Размер > МаксРазмер Тогда
		Предупреждение("Ограничение на размер файлов: " + МаксРазмер + " МБ", 10);
		Возврат;
	КонецЕсли;
	Если ТЧФайлы.Найти(СтруктураВозврата.ИмяФайла, "ИмяФайла") = Неопределено Тогда
		НовСтр				= ТЧФайлы.Добавить();
		НовСтр.Файл 		= Новый ХранилищеЗначения(Новый ДвоичныеДанные(СтруктураВозврата.ПолноеИмяФайла));
		НовСтр.ИмяФайла 	= СтруктураВозврата.ИмяФайла;
		//НовСтр.Комментарий 	= СтруктураВозврата.Комментарий;
	Иначе
		Предупреждение("Имена файлов должны отличаться", 30);
	КонецЕсли;
КонецПроцедуры // ДобавитьФайл()

Процедура ПоказатьФайл(Кнопка)
	ТекСтрока = ЭлементыФормы.ТЧФайлы.ТекущаяСтрока;
	Если ТекСтрока = Неопределено Тогда
		Предупреждение("Не выбрана строка", 20); 
		Возврат;
	КонецЕсли;
	ПоказатьПрикрепленныйФайл(ТекСтрока.ИмяФайла);
КонецПроцедуры // ПоказатьФайл()


////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

Функция ВыбратьФайл() Экспорт
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФайла.Фильтр = "Все файлы(*.*)|*.*";
	Если ДиалогФайла.Выбрать() Тогда
		Возврат ДиалогФайла.ПолноеИмяФайла;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции //ВыбратьФайл()

Функция ПолучитьИнформациюОФайле(ПутьКФайлу)
	Файл = Новый Файл(ПутьКФайлу);
	КомментарийКФайлу = "Размер: " + Файл.Размер() + " байт; изменен: " + Файл.ПолучитьВремяИзменения() + "; сохранен в ИБ: " + ТекущаяДата();
	СтруктураВозврата = Новый Структура("Комментарий, ПолноеИмяФайла, ИмяФайла, Размер", КомментарийКФайлу, Файл.ПолноеИмя, Файл.Имя, Файл.Размер()/1024/1024);
	Возврат СтруктураВозврата;
КонецФункции // ПолучитьИнформациюОФайле()

Процедура КатегорияПриИзменении(Элемент)
	Если Элемент.Значение.Предопределенный И НЕ МодульФорум.ПолныеПраваНаФоруме() Тогда
		Элемент.Значение = Справочники.КатегорииВопросов.ПустаяСсылка();
		Предупреждение("Предопределенные категории могут использоваться только администраторами форума!", 30);
	КонецЕсли;
КонецПроцедуры

Функция ФорматироватьСтрокуДляКомментария(Знач стр) Экспорт
	RegExp = Новый COMОбъект("VBScript.RegExp");
	RegExp.MultiLine    = Истина;
	RegExp.Global 		= Истина;
	RegExp.IgnoreCase   = Истина;
	RegExp.Pattern = "<br>";
	Возврат СокрЛП(RegExp.Replace(стр, Символы.ПС));
КонецФункции // ФорматироватьСтрокуДляКомментария()






////////////////////////////////////////////////////////////////////////////////
// ОСНОВНАЯ ЧАСТЬ
////////////////////////////////////////////////////////////////////////////////
