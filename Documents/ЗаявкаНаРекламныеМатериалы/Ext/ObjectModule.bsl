
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//+++АК MIND 2017.11.16 
	Если Дата >= '20171120'
		И НЕ Утверждена Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Строка(Ссылка) + " не может быть проведена, так как не утверждена",,,, Отказ);
	КонецЕсли;	
	//---АК MIND 
	
	//АК БЕЛН 14.03.17+
	Если ТипЗаявки=ПредопределенноеЗначение("Перечисление.ТипЗаявкиНаРекламныеМатериалы.МассоваяРассылка") И
		ТорговыеТочки.Количество()=0 Тогда
		Отказ=Истина;
		Сообщить("Заполните список магазинов");
	ИначеЕсли Не ТипЗаявки=ПредопределенноеЗначение("Перечисление.ТипЗаявкиНаРекламныеМатериалы.МассоваяРассылка") И
		Не ЗначениеЗаполнено(ТорговаяТочка) Тогда
		Отказ=Истина;
		Сообщить("Выберите магазин");
	КонецЕсли;
	Если Не Отказ Тогда
		Если ТипЗаявки=ПредопределенноеЗначение("Перечисление.ТипЗаявкиНаРекламныеМатериалы.МассоваяРассылка") Тогда

			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	СтруктурныеЕдиницы.Ссылка,
				|	СтруктурныеЕдиницы.ТипРозничнойТочки
				|ИЗ
				|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы";

			Результат = Запрос.Выполнить();

			ТЗТТ = Результат.Выгрузить();
			
			Для каждого Стр Из ТорговыеТочки Цикл
				Мен=РегистрыСведений.ДвижениеКонвертовРеклама.СоздатьМенеджерЗаписи();
				Мен.Период=Дата;
				Мен.Заявка=Ссылка;
				Мен.ТТ=Стр.Магазин;
				Мен.ЗаявкаНаНовыйМагазин=ЗаявкаНаНовыйМагазин;
				Мен.ТипЗаявки=ТипЗаявки;
				Мен.ТипРозничнойТочки=ТЗТТ.НайтиСтроки(Новый Структура("Ссылка",Стр.Магазин))[0].ТипРозничнойТочки;
				Мен.Записать();
			КонецЦикла; 	
		Иначе
			Мен=РегистрыСведений.ДвижениеКонвертовРеклама.СоздатьМенеджерЗаписи();
			Мен.Период=Дата;
			Мен.Заявка=Ссылка;
			Мен.ТТ=ТорговаяТочка;
			Мен.ЗаявкаНаНовыйМагазин=ЗаявкаНаНовыйМагазин;
			Мен.ТипЗаявки=ТипЗаявки;
			Мен.ТипРозничнойТочки=ТорговаяТочка.ТипРозничнойТочки;
			Мен.Записать();
		КонецЕсли; 
	КонецЕсли; 
	//АК БЕЛН 14.03.17-
	Движения.ЗаявкиНаРекламныеМатериалы.Записывать = Истина;
	Движения.ЗаявкиНаРекламныеМатериалы.Очистить();
	
	Для Каждого СтрокаТовар Из Товары Цикл
		Движение = Движения.ЗаявкиНаРекламныеМатериалы.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Заявка = Ссылка;
		Движение.Номенклатура = СтрокаТовар.Номенклатура;
		Движение.Характеристика = СтрокаТовар.Характеристика;
		Движение.Количество = СтрокаТовар.Количество;
		//+++АК sils 27.04.2018 ИП-00018026
		Если ЗначениеЗаполнено(СтрокаТовар.Номенклатура) и
			СтрокаТовар.Номенклатура.ВидРекламногоМатериала = Перечисления.ПодразделРекламныхМатериалов.ФормаДляДействующихВВ Тогда
			Движение.Сотрудник = СтрокаТовар.Сотрудник;
		Иначе
			Движение.Сотрудник = Справочники.СотрудникиОрганизаций.ПустаяСсылка();
		КонецЕсли;	
		//---АК
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Утверждена = Ложь;
	КтоУтвердил = Неопределено;
	ДатаУтверждения = '00010101';
	                    
КонецПроцедуры

//+++АК sils 28.06.2018 ИП-00019117
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Для Каждого СтрокаТовар Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Номенклатура) и
				СтрокаТовар.Номенклатура.ВидРекламногоМатериала = Перечисления.ПодразделРекламныхМатериалов.ФормаДляДействующихВВ Тогда
				Если не ЗначениеЗаполнено(СтрокаТовар.Сотрудник) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В строке " + (Товары.Индекс(СтрокаТовар) + 1) + " не указан сотрудник!",,,, Отказ);
				КонецЕсли;
				Если не ЗначениеЗаполнено(СтрокаТовар.Пол) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В строке " + (Товары.Индекс(СтрокаТовар) + 1) + " не указан пол!",,,, Отказ);
				КонецЕсли;
			КонецЕсли;
			Если Дата > '20180627' и СтрокаТовар.Количество <= 0 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В строке " + (Товары.Индекс(СтрокаТовар) + 1) + " не верное количество!",,,, Отказ);
			КонецЕсли;
		КонецЦикла;	
		//+++АК sils 12.07.2018 ИП-00019219
	КонецЕсли;
	Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения и Дата >= Дата("20180712000000") Тогда
		ТЗ = Товары.Выгрузить();
		ТЗ.Свернуть("Номенклатура, Характеристика, Сотрудник", "Количество");
		Для каждого стр из ТЗ Цикл
			Если ЗначениеЗаполнено(стр.Номенклатура) и
				стр.Номенклатура.ВидРекламногоМатериала = Перечисления.ПодразделРекламныхМатериалов.ФормаДляДействующихВВ Тогда
				Если стр.Количество > 1 Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для сотрудника " + СокрЛП(стр.Сотрудник) + 
						" заказано " + СокрЛП(стр.Номенклатура) + " больше одного комплекта, а допустимо заказывать только один комплект!",,,, Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		//---АК
	КонецЕсли;
	
	//+++АК sils 28.07.2018 ИП-00019380
	// Если дата выдачи спецодежды (конкретной номенклатуры указанной в заявке) прошло менее чем полгода, 
	// то в заявке необходимо указать причину раннего заказа спецодежды для сотрудника.
	Если ЭтотОбъект.Дата > '20180727' И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения и не Отказ Тогда
		флСпецодежда = Ложь;
		Для Каждого СтрокаТовар Из ЭтотОбъект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовар.Номенклатура) и
				СтрокаТовар.Номенклатура.ВидРекламногоМатериала = Перечисления.ПодразделРекламныхМатериалов.ФормаДляДействующихВВ Тогда
				флСпецодежда = Истина;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		Если флСпецодежда Тогда
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Дата", ЭтотОбъект.Дата);
			Запрос.УстановитьПараметр("Таб", ЭтотОбъект.Товары.Выгрузить());
			Запрос.Текст = "ВЫБРАТЬ
			               |	Таб.Номенклатура,
			               |	Таб.Количество,
			               |	Таб.Сотрудник,
			               |	Таб.ПричинаПреждевременнойЗамены
			               |ПОМЕСТИТЬ ВТ_Товары
			               |ИЗ
			               |	&Таб КАК Таб
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	ВТ_Товары.Номенклатура,
			               |	ВТ_Товары.Количество,
			               |	ВТ_Товары.Сотрудник,
			               |	ВТ_Товары.ПричинаПреждевременнойЗамены
			               |ПОМЕСТИТЬ ВТ_ТоварыБезПричины
			               |ИЗ
			               |	ВТ_Товары КАК ВТ_Товары
			               |ГДЕ
			               |	ВТ_Товары.Сотрудник <> ЗНАЧЕНИЕ(Справочник.СотрудникиОрганизаций.ПустаяСсылка)
			               |	И ВТ_Товары.ПричинаПреждевременнойЗамены = ЗНАЧЕНИЕ(Перечисление.ПричиныПреждевременнойЗамены.ПустаяСсылка)
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	ВТ_ТоварыБезПричины.Номенклатура,
			               |	ВТ_ТоварыБезПричины.Количество,
			               |	ВТ_ТоварыБезПричины.Сотрудник,
			               |	ВложенныйЗапрос.Период,
			               |	ВТ_ТоварыБезПричины.ПричинаПреждевременнойЗамены
			               |ИЗ
			               |	ВТ_ТоварыБезПричины КАК ВТ_ТоварыБезПричины
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			               |			ЗаявкиНаРекламныеМатериалыОбороты.Номенклатура КАК Номенклатура,
			               |			ЗаявкиНаРекламныеМатериалыОбороты.Сотрудник КАК Сотрудник,
			               |			МАКСИМУМ(ЗаявкиНаРекламныеМатериалыОбороты.Период) КАК Период
			               |		ИЗ
			               |			РегистрНакопления.ЗаявкиНаРекламныеМатериалы.Обороты(, &Дата, Регистратор, ) КАК ЗаявкиНаРекламныеМатериалыОбороты
			               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТоварыБезПричины КАК ВТ_ТоварыБезПричины
			               |				ПО ЗаявкиНаРекламныеМатериалыОбороты.Сотрудник = ВТ_ТоварыБезПричины.Сотрудник
			               |					И ЗаявкиНаРекламныеМатериалыОбороты.Номенклатура = ВТ_ТоварыБезПричины.Номенклатура
			               |		ГДЕ
			               |			ЗаявкиНаРекламныеМатериалыОбороты.КоличествоПриход > 0
			               |		
			               |		СГРУППИРОВАТЬ ПО
			               |			ЗаявкиНаРекламныеМатериалыОбороты.Номенклатура,
			               |			ЗаявкиНаРекламныеМатериалыОбороты.Сотрудник) КАК ВложенныйЗапрос
			               |		ПО ВТ_ТоварыБезПричины.Номенклатура = ВложенныйЗапрос.Номенклатура
			               |			И ВТ_ТоварыБезПричины.Сотрудник = ВложенныйЗапрос.Сотрудник";
						   
			ТЗ = Запрос.Выполнить().Выгрузить();
			Для каждого стр из ТЗ Цикл
				Если стр.Период > ДобавитьМесяц(ЭтотОбъект.Дата, -6) и не ЗначениеЗаполнено(стр.ПричинаПреждевременнойЗамены) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заявка на спецодежду " + СокрЛП(стр.Номенклатура) + " для сотрудника " + СокрЛП(стр.Сотрудник) + 
						" была оформлена " + Символы.ПС + Символы.Таб + "меньше пол года назад (" + Формат(стр.Период, "ДЛФ=DD") + "). Необходимо указать причину преждевременной замены.",,,, Отказ);
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;	
	КонецЕсли;
	//---АК
КонецПроцедуры

//+++АК SHEP 2018.08.01 ИП-00019216
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.МП_ЗадачаМагазина") Тогда
		ТипЗаявки = ПредопределенноеЗначение("Перечисление.ТипЗаявкиНаРекламныеМатериалы.ЗаявкаМагазина");
		ТорговаяТочка = ДанныеЗаполнения.Магазин;
		ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
КонецПроцедуры
