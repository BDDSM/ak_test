
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Дата > ТекущаяДата() Тогда
		РежимПроведения = РежимПроведенияДокумента.Неоперативный;
	КонецЕсли;
	
	Движения.Финансовый.Очистить();
	Движения.Финансовый.Записывать = Истина;
	
	Для Каждого Строка Из ЗачетДепозита Цикл
		
		Проводка = Движения.Финансовый.Добавить();
		Проводка.Период = Дата;
		Проводка.СчетДт = ПланыСчетов.Финансовый.РасчетыПоАренде;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Организации", Организация);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Контрагенты", Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ТорговыеТочки", ТорговаяТочка);
		Проводка.СчетКт = ПланыСчетов.Финансовый.ОбеспечительныйВзнос;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Организации", Организация);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Контрагенты", Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ТорговыеТочки", ТорговаяТочка);
		Проводка.Содержание = "Зачет оплаты аренда в счет депозита за период с " + Формат(НачалоМесяца(Строка.ПериодРегистрации), "ДФ=dd.MM.yyyy") + " по " + Формат(КонецМесяца(Строка.ПериодРегистрации), "ДФ=dd.MM.yyyy");
		Проводка.Сумма = Строка.Сумма;

	КонецЦикла;

	АК_УчетМСФО.ЗаполнитьОрганизацию(Движения.Финансовый,Организация);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если (НЕ ЭтотОбъект.ДоговорКонтрагента = ЭтотОбъект.Ссылка.ДоговорКонтрагента)
			И ДопМодульСервер.НекорректныйДоговор(ЭтотОбъект.ДоговорКонтрагента) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Указан некорректный договор (помечен на удаление либо не используется)");
		Отказ = Истина;
	КонецЕсли;
		
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ТаблицаДепозитов = ЭтотОбъект.ЗачетДепозита.Выгрузить();
		ТаблицаДепозитов.Свернуть("ТипАренднойПлаты, ПериодРегистрации", "Сумма");
		
		Для Каждого СтрокаТаблицы Из ТаблицаДепозитов Цикл
			ПроцедурыНачисления.ПроверитьЛимитыПоАрендеЗаявкиИОплаты(ЭтотОбъект.Ссылка, , СтрокаТаблицы.Сумма, ЭтотОбъект.ТорговаяТочка,
																		СтрокаТаблицы.ТипАренднойПлаты, СтрокаТаблицы.ПериодРегистрации, Отказ);
		КонецЦикла;  
	КонецЕсли; 
	
КонецПроцедуры
