
Функция ПечатьЗаданияНаПеремещение(Ссылка) Экспорт

	
	ТабДок	= Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ЗаданиеНаПеремещение";
	
	Макет = Документы.ЗаданиеНаПеремещение.ПолучитьМакет("ПФ_ЗаданиеНаПеремещение");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Характеристика КАК Ссылка,
	|	ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, 0) КАК КоличествоВУпаковке
	|ИЗ
	|	Документ.ЗаданиеНаПеремещение.Товары КАК ХарактеристикиНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО ХарактеристикиНоменклатуры.Характеристика = ЗначенияСвойствОбъектов.Объект
	|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.КоличествоВУпаковке))
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Ссылка в (&Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗХар = РезультатЗапроса.Выгрузить();

	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаданиеНаПеремещение.Ссылка КАК Ссылка,
	|	ЗаданиеНаПеремещение.ВерсияДанных,
	|	ЗаданиеНаПеремещение.ПометкаУдаления,
	|	ЗаданиеНаПеремещение.Номер,
	|	ЗаданиеНаПеремещение.Дата КАК Дата,
	|	ЗаданиеНаПеремещение.Проведен,
	|	ЗаданиеНаПеремещение.СкладОтправитель,
	|	ЗаданиеНаПеремещение.СкладПолучатель,
	|	ЗаданиеНаПеремещение.Автор как Автор,
	|	ЗаданиеНаПеремещение.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Характеристика,
	|		ДатаПроизводства,
	|		ЕдиницаИзмерения,
	|		Количество
	|	)
	|ИЗ
	|	Документ.ЗаданиеНаПеремещение КАК ЗаданиеНаПеремещение
	|ГДЕ
	|	ЗаданиеНаПеремещение.Ссылка В(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка" ;
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	ВыборкаДок = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВставлятьРазделительСтраниц = Ложь;
	ТабДок.Очистить();
	Пока ВыборкаДок.Следующий() Цикл
		Выборка = ВыборкаДок.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			
			
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
			ТекНомер = ВыборкаДок.Номер;
			Пока Лев(ТекНомер, 1) = "0" Цикл
				ТекНомер = Сред(ТекНомер, 2);
			КонецЦикла;
			ОбластьМакета.Параметры.Номер 	= ТекНомер;
			ОбластьМакета.Параметры.Дата 	= Формат(ВыборкаДок.Дата, "ДЛФ=Д");
			ОбщегоНазначенияКлиентСервер.ДобавитьШтрихкодВОбластьДокумента(ОбластьМакета,ВыборкаДок.Ссылка);
			ОбластьМакета.Параметры.СкладОтправитель 	= ?(ЗначениеЗаполнено(ВыборкаДок.СкладОтправитель)	, СокрЛП(ВыборкаДок.СкладОтправитель.Наименование)	, "");
			ОбластьМакета.Параметры.СкладПолучатель 	= ?(ЗначениеЗаполнено(ВыборкаДок.СкладПолучатель)	, СокрЛП(ВыборкаДок.СкладПолучатель.Наименование)	, "");
			ТабДок.Вывести(ОбластьМакета);
			
			
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			ВыборкаНоменклатура = Выборка.Товары.Выбрать();
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(ВыборкаНоменклатура);
				ОбластьМакета.Параметры.НомерСтроки 		= Формат(ВыборкаНоменклатура.НомерСтроки		, "ЧГ=");
				ОбластьМакета.Параметры.ДатаПроизводства 	= Формат(ВыборкаНоменклатура.ДатаПроизводства	, "ДЛФ=Д");
				СтрХар=ТЗХар.Найти(ВыборкаНоменклатура.Характеристика);
				Если СтрХар<>Неопределено Тогда
					КолвоВКоробке=СтрХар.КоличествоВУпаковке;
					ОбластьМакета.Параметры.КоличествоКоробок 	= ?(КолвоВКоробке = 0, 0, Цел(ВыборкаНоменклатура.Количество / КолвоВКоробке));
				КонецЕсли; 
				ТабДок.Вывести(ОбластьМакета);
				
			КонецЦикла;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
			ОбластьВремяПечати 	= Макет.ПолучитьОбласть("ВремяПечати");
			ТабДок.Вывести(ОбластьМакета);
			
			ОбластьВремяПечати.Параметры.ВремяПечати = "["+ТекущаяДата()+"] "+"Автор: "+Выборка.Автор;
			
			ТабДок.Вывести(ОбластьВремяПечати);
			ВставлятьРазделительСтраниц = Истина;
		КонецЦикла;
	КонецЦикла;
	ТабДок.АвтоМасштаб 		= Истина;
	//ТабДок.ФиксацияСверху 		= 7;
	ТабДок.ОтображатьСетку 	= Ложь;
	ТабДок.Защита 				= Ложь;
	ТабДок.ТолькоПросмотр 		= Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	Возврат ТабДок;
	
КонецФункции


Процедура ОбъединитьЗадания(МассивСсылок) Экспорт
	ИсходныйДок=МассивСсылок[0];
	НовДок=Документы.ЗаданиеНаПеремещение.СоздатьДокумент();
	НовДок.Дата=ТекущаяДата();
	НовДок.СкладОтправитель=ИсходныйДок.СкладОтправитель;
	НовДок.СкладПолучатель=ИсходныйДок.СкладПолучатель;
	НовДок.Перемещение=ИсходныйДок.Перемещение;
	НовДок.Автор=ИсходныйДок.Автор;
	НовДок.СтруктурнаяЕдиница=ИсходныйДок.СтруктурнаяЕдиница;
	
	Для каждого Док Из МассивСсылок Цикл
		Для каждого Стр Из Док.Товары Цикл
			НовСтр=НовДок.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр,Стр);		
		КонецЦикла; 
	КонецЦикла; 
	НовДок.Записать(РежимЗаписиДокумента.Проведение);
	Сообщить("Создано новое задание на перемещение - "+НовДок.Ссылка);
	Для каждого Док Из МассивСсылок Цикл
		ОбъектДок=Док.ПолучитьОбъект();
		Если ОбъектДок.Проведен Тогда
			ОбъектДок.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЕсли; 
		ОбъектДок.ПометкаУдаления=Истина;
		ОбъектДок.Записать(РежимЗаписиДокумента.Запись);
	КонецЦикла; 
КонецПроцедуры
 