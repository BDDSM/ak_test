Процедура ДействияФормыПроводкиДтКт(Кнопка)

	Если ЭлементыФормы.ДокументСписок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	БухгалтерскийУчет.ОткрытьЖурналПроводок(ЭлементыФормы.ДокументСписок.ТекущиеДанные.Ссылка);

КонецПроцедуры

Процедура ДокументСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	//
	МассивРеализаций = Новый Массив;
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		МассивРеализаций.Добавить(ОформлениеСтроки.ДанныеСтроки.Ссылка);
	КонецЦикла;
	
	//
	ТаблицаОтличий = ПолучитьТаблицуОтличий(МассивРеализаций);
	
	//
	Если ТаблицаОтличий.Количество() > 0 Тогда
		
		//
		Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
			
			НайденнаяСтрока = ТаблицаОтличий.Найти(ОформлениеСтроки.ДанныеСтроки.Ссылка, "Поступление");
			Если НайденнаяСтрока <> Неопределено Тогда
				ОформлениеСтроки.ЦветФона = WebЦвета.ЛососьСветлый;				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;	
	
	//+++АК LAGP 2018.05.04 ИП-00018465 Учет расчетов с ТТПерекресток.
	ТаблицаНезаполненныхДоговоров = Документы.РеализацияТоваровУслуг.ПолучитьТаблицуНезаполненныхДоговоров(МассивРеализаций);	
	
	Если ТаблицаНезаполненныхДоговоров.Количество() > 0 Тогда
		Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл			
			НайденнаяСтрока = ТаблицаНезаполненныхДоговоров.Найти(ОформлениеСтроки.ДанныеСтроки.Ссылка, "РеализацияТоваровИУслуг");
			Если НайденнаяСтрока <> Неопределено Тогда
				ОформлениеСтроки.ЦветФона = WebЦвета.БледноЗолотистый;				
			КонецЕсли;			
		КонецЦикла;		
	КонецЕсли;
	//---АК LAGP
	
	//+++АК sils 16.10.2018 ИП-00019421.01
	ДанныеРеализации = ПолучитьДанныеРеализации(МассивРеализаций);
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл			
		НайденнаяСтрока = ДанныеРеализации.Найти(ОформлениеСтроки.ДанныеСтроки.Ссылка, "Документ");
		Если НайденнаяСтрока <> Неопределено Тогда
			ОформлениеСтроки.Ячейки.СуммаНДС.УстановитьТекст(Формат(НайденнаяСтрока.СуммаНДС, "ЧДЦ=2; ЧРД=,; ЧРГ=' '"));				
			ОформлениеСтроки.Ячейки.ФайлПрикреплен.УстановитьФлажок(?(СокрЛП(НайденнаяСтрока.ИмяФайлаСРасширением) = "", Ложь, Истина));				
		КонецЕсли;
	КонецЦикла;		
	//---АК
	
	//+++АК luza 05.11.2018 ЗА-00015569
	ТаблицаСтатусовЭДО = ПолучитьТаблицуСтатусовЭДО(МассивРеализаций);
	мСтатусУтвержден = Перечисления.ЭДО_СтатусыДокументов.Утвержден;
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл			
		НайденнаяСтрока = ТаблицаСтатусовЭДО.Найти(ОформлениеСтроки.ДанныеСтроки.Ссылка, "Документ");
		Если НайденнаяСтрока <> Неопределено И НайденнаяСтрока.Статус =  мСтатусУтвержден Тогда
			ОформлениеСтроки.Ячейки.Картинка.ЦветФона = WebЦвета.СветлоЗеленый;
			ОформлениеСтроки.Ячейки.ПометкаУдаления.ЦветФона = WebЦвета.СветлоЗеленый;
		ИначеЕсли НайденнаяСтрока <> Неопределено Тогда	
			ОформлениеСтроки.Ячейки.Картинка.ЦветФона = WebЦвета.Красный;
			ОформлениеСтроки.Ячейки.ПометкаУдаления.ЦветФона = WebЦвета.Красный;
		КонецЕсли;
	КонецЦикла;		
	//---АК
	
КонецПроцедуры

//+++АК sils 17.10.2018 ИП-00019421.01
Функция ПолучитьДанныеРеализации(МассивРеализаций)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.Документ,
	|	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС,
	|	ВложенныйЗапрос.Документ.ИмяФайлаСРасширением КАК ИмяФайлаСРасширением
	|ИЗ
	|	(ВЫБРАТЬ
	|		РеализацияТоваровУслугТовары.Ссылка КАК Документ,
	|		РеализацияТоваровУслугТовары.СуммаНДС КАК СуммаНДС
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|	ГДЕ
	|		РеализацияТоваровУслугТовары.Ссылка В(&МассивРеализаций)
	|		И РеализацияТоваровУслугТовары.Ссылка.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РеализацияТоваровУслугУслуги.Ссылка,
	|		РеализацияТоваровУслугУслуги.СуммаНДС
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|	ГДЕ
	|		РеализацияТоваровУслугУслуги.Ссылка В(&МассивРеализаций)
	|		И РеализацияТоваровУслугУслуги.Ссылка.Проведен) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Документ");
	
	//
	Запрос.УстановитьПараметр("МассивРеализаций", МассивРеализаций);
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаДанных;
	
КонецФункции	

Функция ПолучитьТаблицуОтличий(МассивРеализаций)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугРасходныеОрдера.Ссылка КАК Поступление,
	|	РеализацияТоваровУслугРасходныеОрдера.РасходныйОрдер
	|ПОМЕСТИТЬ РасходныеОрдера
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.РасходныеОрдера КАК РеализацияТоваровУслугРасходныеОрдера
	|ГДЕ
	|	РеализацияТоваровУслугРасходныеОрдера.Ссылка В(&МассивРеализаций)
	|	И РеализацияТоваровУслугРасходныеОрдера.Ссылка.Проведен
	|	И РеализацияТоваровУслугРасходныеОрдера.РасходныйОрдер.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходныеОрдера.Поступление,
	|	РасходныйОрдерСкладТовары.Номенклатура,
	|	СУММА(РасходныйОрдерСкладТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТЗ_КоличествоВДокументах
	|ИЗ
	|	РасходныеОрдера КАК РасходныеОрдера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерСклад.Товары КАК РасходныйОрдерСкладТовары
	|		ПО РасходныеОрдера.РасходныйОрдер = РасходныйОрдерСкладТовары.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходныеОрдера.Поступление,
	|	РасходныйОрдерСкладТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	-СУММА(РеализацияТоваровУслугТовары.ПоРасходнымОрдерам)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка В(&МассивРеализаций)
	|	И РеализацияТоваровУслугТовары.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Ссылка,
	|	РеализацияТоваровУслугТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЗ_КоличествоВДокументах.Поступление КАК Поступление
	|ИЗ
	|	ТЗ_КоличествоВДокументах КАК ТЗ_КоличествоВДокументах
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЗ_КоличествоВДокументах.Поступление,
	|	ТЗ_КоличествоВДокументах.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТЗ_КоличествоВДокументах.Количество) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Поступление");
	
	//
	Запрос.УстановитьПараметр("МассивРеализаций", МассивРеализаций);
	
	ТаблицаОтличий = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаОтличий;
	
КонецФункции	
	
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	ТекСтрока = ЭлементыФормы.ДокументСписок.ТекущаяСтрока;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(ТекСтрока);
	
КонецПроцедуры

Функция ПолучитьТаблицуСтатусовЭДО(МассивРеализаций)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭДО_СопоставлениеДокументов.Документ КАК Документ,
	|	ЭДО_СопоставлениеДокументов.ДокументЭДО.Статус.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ЭДО_СопоставлениеДокументов КАК ЭДО_СопоставлениеДокументов
	|ГДЕ
	|	ЭДО_СопоставлениеДокументов.Документ В(&МассивРеализаций)");
	
	//
	Запрос.УстановитьПараметр("МассивРеализаций", МассивРеализаций);
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаДанных;
	
КонецФункции