
Процедура ПриОткрытии()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИСТИНА КАК Использование,
	|	РасходныйОрдерСклад.Ссылка КАК РасходныйОрдер
	|ПОМЕСТИТЬ ВремТаб
	|ИЗ
	|	Документ.РасходныйОрдерСклад КАК РасходныйОрдерСклад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.РасходныеОрдера КАК РеализацияТУРасходныеОрдера
	|		ПО РасходныйОрдерСклад.Ссылка = РеализацияТУРасходныеОрдера.РасходныйОрдер
	|ГДЕ
	|	РасходныйОрдерСклад.Проведен
	|	И РасходныйОрдерСклад.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСкладскойУчет.Реализация)
	|	И РасходныйОрдерСклад.Получатель = &Контрагент
	|	И РеализацияТУРасходныеОрдера.РасходныйОрдер ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	РеализацияТУРасходныеОрдера.РасходныйОрдер
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.РасходныеОрдера КАК РеализацияТУРасходныеОрдера
	|ГДЕ
	|	РеализацияТУРасходныеОрдера.Ссылка = &РеализацияТУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремТаб.Использование,
	|	ВремТаб.РасходныйОрдер
	|ИЗ
	|	ВремТаб КАК ВремТаб
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремТаб.Использование,
	|	ВремТаб.РасходныйОрдер
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВремТаб.РасходныйОрдер.Дата"
	);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("РеализацияТУ", РеализацияТУ);
	
	ТаблицаРасходныхОрдеров = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Список.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаРасходныхОрдеров Цикл
		Если СтрокаТаблицы.Использование Тогда
			Список.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(КодВозвратаДиалога.ОК);	
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОтмена(Кнопка)
	
	Закрыть(КодВозвратаДиалога.Отмена);	
	
КонецПроцедуры

Процедура КоманднаяПанель1ВыделитьВсе(Кнопка)
	
	УстановитьЗначениеИспользования(Истина);	
	
КонецПроцедуры

Процедура КоманднаяПанель1СнятьВсе(Кнопка)
	
	УстановитьЗначениеИспользования(Ложь);
	
КонецПроцедуры

Процедура УстановитьЗначениеИспользования(ЗначениеИспользования)

	Для Каждого Строка Из ТаблицаРасходныхОрдеров Цикл
		Строка.Использование = ЗначениеИспользования;
	КонецЦикла;
	
КонецПроцедуры