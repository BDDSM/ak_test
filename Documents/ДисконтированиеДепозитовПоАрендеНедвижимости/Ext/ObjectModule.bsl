
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	НастройкаОтраженияОперацийВУчетеСрезПоследних.ЦФО КАК ЦФО,
	|	НастройкаОтраженияОперацийВУчетеСрезПоследних.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	НастройкаОтраженияОперацийВУчетеСрезПоследних.Счет КАК Счет
	|ИЗ
	|	РегистрСведений.НастройкаОтраженияОперацийВУчете.СрезПоследних(&Дата, ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ДисконтированиеДепозитов_НачислениеДисконта)) КАК НастройкаОтраженияОперацийВУчетеСрезПоследних");
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	ВыборкаНачисление = Запрос.Выполнить().Выбрать();
	
	Если НЕ ВыборкаНачисление.Следующий() ИЛИ НЕ ЗначениеЗаполнено(ВыборкаНачисление.Счет) ИЛИ НЕ ЗначениеЗаполнено(ВыборкаНачисление.ЦФО) ИЛИ НЕ ЗначениеЗаполнено(ВыборкаНачисление.СтатьяДоходовРасходов)  Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнены настройки отражения в учете для операции начисления дисконта по депозитам!", Отказ);
		Возврат;
	КонецЕсли;   
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	НастройкаОтраженияОперацийВУчетеСрезПоследних.ЦФО КАК ЦФО,
	                      |	НастройкаОтраженияОперацийВУчетеСрезПоследних.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	                      |	НастройкаОтраженияОперацийВУчетеСрезПоследних.Счет КАК Счет
	                      |ИЗ
	                      |	РегистрСведений.НастройкаОтраженияОперацийВУчете.СрезПоследних(&Дата, ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВУчете.ДисконтированиеДепозитов_НачислениеПроцентов)) КАК НастройкаОтраженияОперацийВУчетеСрезПоследних");
						  
	Запрос.УстановитьПараметр("Дата", Дата);
	
	ВыборкаПроценты = Запрос.Выполнить().Выбрать();
	
	Если НЕ ВыборкаПроценты.Следующий() ИЛИ НЕ ЗначениеЗаполнено(ВыборкаПроценты.Счет) ИЛИ НЕ ЗначениеЗаполнено(ВыборкаПроценты.ЦФО) ИЛИ НЕ ЗначениеЗаполнено(ВыборкаПроценты.СтатьяДоходовРасходов)  Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнены настройки отражения в учете для операции начисления процентов по дисконтированным депозитам!", Отказ);
		Возврат;
	КонецЕсли;   
	
	//
	Движения.ДисконтПоДепозитамАрендыНедвижимости.Записывать = Истина;
	Движения.ДисконтПоДепозитамАрендыНедвижимости.Очистить();
	
	Движения.Финансовый.Записывать = Истина;
	Движения.Финансовый.Очистить();
	
	Для Каждого Строка Из НачислениеДисконта Цикл
		Движение = Движения.ДисконтПоДепозитамАрендыНедвижимости.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Контрагент = Строка.Контрагент;
		Движение.ДоговорКонтрагента = Строка.ДоговорКонтрагента;
		Движение.Документ = Строка.Документ;
		Движение.СтруктурнаяЕдиница = Строка.СтруктурнаяЕдиница;		
		Движение.СтавкаДисконта = Строка.СтавкаДисконта;
		Движение.ДатаОкончанияДоговора = Строка.ДатаОкончанияДоговора;
		Движение.СуммаДисконта = Строка.СуммаДисконта;
		Движение.СуммаДепозита = Строка.СуммаДепозита;
		
		Проводка = Движения.Финансовый.Добавить();
		Проводка.Организация = Организация;
		Проводка.Период = Дата;
		Проводка.СчетДт = ВыборкаНачисление.Счет;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Строка.СтруктурнаяЕдиница);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, ВыборкаНачисление.СтатьяДоходовРасходов);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, ВыборкаНачисление.ЦФО);
		Проводка.СчетКт = ПланыСчетов.Финансовый.ОбеспечительныйВзнос;                                                                                    
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Организация);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, Строка.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, Строка.СтруктурнаяЕдиница);
		Проводка.СуммаМСФО = Строка.СуммаДисконта;
		Проводка.Содержание = "Начисление дисконта по депозитам аренды";		
	КонецЦикла;
	
	Для Каждого Строка Из СписаниеДисконта Цикл
		Движение = Движения.ДисконтПоДепозитамАрендыНедвижимости.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Контрагент = Строка.Контрагент;
		Движение.ДоговорКонтрагента = Строка.ДоговорКонтрагента;
		Движение.Документ = Строка.Документ;
		Движение.СтруктурнаяЕдиница = Строка.СтруктурнаяЕдиница;				
		Движение.СтавкаДисконта = Строка.СтавкаДисконта;
		Движение.ДатаОкончанияДоговора = Строка.ДатаОкончанияДоговора;		
		Движение.ТипСписания = Строка.ТипСписания;
		Движение.СуммаДисконта = Строка.СуммаДисконта;
		Движение.СуммаДепозита = Строка.СуммаДепозита;
		
		Проводка = Движения.Финансовый.Добавить();
		Проводка.Организация = Организация;
		Проводка.Период = Дата;
		Проводка.СчетДт = ПланыСчетов.Финансовый.ОбеспечительныйВзнос;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Организация);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, Строка.Контрагент);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, Строка.СтруктурнаяЕдиница);
		Проводка.СчетКт = ВыборкаПроценты.Счет;                                                                                    
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Строка.СтруктурнаяЕдиница);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, ВыборкаПроценты.СтатьяДоходовРасходов);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3, ВыборкаПроценты.ЦФО);
		Проводка.СуммаМСФО = Строка.СуммаДисконта;
		Проводка.Содержание = "Начисление процентов по дисконтированным депозитам аренды";		
		
	КонецЦикла;
	
	
КонецПроцедуры
