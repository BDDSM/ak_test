
Функция ПечатьОтчетаПоСтавкам(Ссылка) Экспорт
	
	// служ. переменные
	КолвоДней = День(КонецМесяца(Ссылка.Период));
	МассивКоличеств = Новый Массив;
	
	// вывод в таб.документ
	ТабДокумент	= Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПараметрыПечати_ДоставкаНаТТ_ОтчетПоСтавкам";
	
	Макет = Документы.ДоставкаНаТТ.ПолучитьМакет("ОтчетПоСтавкам");
	
	// шапка
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Начало");
	ТекНомер = Ссылка.Номер;
	Пока Лев(ТекНомер, 1) = "0" Цикл
		ТекНомер = Сред(ТекНомер, 2);
	КонецЦикла;
	ОбластьМакета.Параметры.НомерДок 	= ТекНомер;
	ОбластьМакета.Параметры.ДатаДок 	= Формат(Ссылка.Дата, "ДЛФ=Д");
	ОбластьМакета.Параметры.Комментарий = СокрЛП(Ссылка.Комментарий);
	ТабДокумент.Вывести(ОбластьМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка|День");
	Для н = 1 По КолвоДней Цикл
		ОбластьМакета.Параметры.ДеньМесяца = н;
		ТабДокумент.Присоединить(ОбластьМакета);
		МассивКоличеств.Добавить(0);
	КонецЦикла;
	
	//
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоставкаНаТТСостав.Ставка,
	|	ДЕНЬ(ДоставкаНаТТСостав.ДатаДоставки) КАК День,
	|	СУММА(ДоставкаНаТТСостав.Количество) КАК Количество
	|ИЗ
	|	Документ.ДоставкаНаТТ.Состав КАК ДоставкаНаТТСостав
	|ГДЕ
	|	ДоставкаНаТТСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоставкаНаТТСостав.Ставка,
	|	ДЕНЬ(ДоставкаНаТТСостав.ДатаДоставки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоставкаНаТТСостав.Ставка КАК Ставка,
	|	СУММА(ДоставкаНаТТСостав.Количество) КАК Количество,
	|	СУММА(ДоставкаНаТТСостав.Ставка * ДоставкаНаТТСостав.Количество) КАК Сумма
	|ИЗ
	|	Документ.ДоставкаНаТТ.Состав КАК ДоставкаНаТТСостав
	|ГДЕ
	|	ДоставкаНаТТСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоставкаНаТТСостав.Ставка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ставка
	|ИТОГИ ПО
	|	ОБЩИЕ";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаОсновная = РезультатыЗапроса[0].Выгрузить();
	СтруктураОтбора = Новый Структура("Ставка, День");
	
	ОбластьМакета1 = Макет.ПолучитьОбласть("Строка|Начало");
	ОбластьМакета2 = Макет.ПолучитьОбласть("Строка|День");
	
	ВыборкаОбщие = РезультатыЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбщие.Следующий() Цикл
		
		ВыборкаПоСтавкам = ВыборкаОбщие.Выбрать();
		Пока ВыборкаПоСтавкам.Следующий() Цикл
			ОбластьМакета1.Параметры.Ставка 	= Формат(ВыборкаПоСтавкам.Ставка	, "ЧГ=");
			ОбластьМакета1.Параметры.Количество = Формат(ВыборкаПоСтавкам.Количество, "ЧГ=");
			ОбластьМакета1.Параметры.Сумма 		= Формат(ВыборкаПоСтавкам.Сумма		, "ЧГ=");
			ТабДокумент.Вывести(ОбластьМакета1);
			
			Для н = 1 По КолвоДней Цикл
				СтруктураОтбора.Ставка 	= ВыборкаПоСтавкам.Ставка;
				СтруктураОтбора.День 	= н;
				СтрокиТаблицы = ТаблицаОсновная.НайтиСтроки(СтруктураОтбора);
				
				Если СтрокиТаблицы.Количество() > 0 Тогда
					ОбластьМакета2.Параметры.КолДень = Формат(СтрокиТаблицы[0].Количество, "ЧГ=");
					МассивКоличеств[н - 1] = МассивКоличеств[н - 1] + СтрокиТаблицы[0].Количество;
				Иначе
					ОбластьМакета2.Параметры.КолДень = "";
				КонецЕсли;
				ТабДокумент.Присоединить(ОбластьМакета2);
			КонецЦикла;
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Итого|Начало");
		ОбластьМакета.Параметры.Количество 	= Формат(ВыборкаОбщие.Количество, "ЧГ=");
		ОбластьМакета.Параметры.Сумма 		= Формат(ВыборкаОбщие.Сумма		, "ЧГ=");
		ТабДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("Итого|День");
		Для н = 1 По КолвоДней Цикл
			ОбластьМакета.Параметры.КолДень = Формат(МассивКоличеств[н - 1], "ЧГ=");
			ТабДокумент.Присоединить(ОбластьМакета);
		КонецЦикла;
			
	КонецЦикла;
	
	//
	ТабДокумент.ПолеСнизу 			= 5;
	ТабДокумент.ПолеСверху 			= 5;
	ТабДокумент.ФиксацияСверху		= 6;
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабДокумент.АвтоМасштаб 		= Истина;
	ТабДокумент.ОтображатьСетку 	= Ложь;
	ТабДокумент.Защита 				= Ложь;
	ТабДокумент.ТолькоПросмотр 		= Ложь;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДокумент;
	
КонецФункции

//+++АК SHEP 2018.04.19 ИП-00018423: перенёс из модуля объекта
Процедура ДвиженияФинансовыйЗаполнить(ЭтотОбъект, мПроводки, Отказ, СНДС = Истина) Экспорт
	
	СтруктураНастроек =
		ОбщегоНазначенияСервер.ПолучитьНастройкиОтраженияОперацийВУчете(Перечисления.ВидыОперацийВУчете.ТранспортныеРасходыПоДоставкеНаТочку, ЭтотОбъект.Дата);
	
	Если ЗначениеЗаполнено(СтруктураНастроек.Счет) Тогда
		
		//
		Счет604 	= ПланыСчетов.Финансовый.ПрочаяЗадолженность;
		Счет6802 	= ПланыСчетов.Финансовый.НалогиУН;
		ВидНалогаНДС = Справочники.ВидыНалогов.НайтиПоКоду("000000001");
		ТекСтавкаНДС = УчетНДС.ПолучитьСтавкуНДС(ЭтотОбъект.СтавкаНДС);
		
		//
		тзТорговыеТочки = ЭтотОбъект.Состав.Выгрузить();
		тзТорговыеТочки.Свернуть("ТТ, Ставка", "Количество");
		
		
		//
		Для Каждого СтрокаТЧ Из тзТорговыеТочки Цикл
			
			//
			ТекСумма = СтрокаТЧ.Ставка * СтрокаТЧ.Количество; 
			Если ТекСумма = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ТекСуммаНДС = Окр(ТекСумма * ТекСтавкаНДС / (100 + ТекСтавкаНДС), 2);
			
			//
			ЦФО = ОбщиеПроцедуры.ПолучитьЦФОПоСтруктурнойЕдинице(СтрокаТЧ.ТТ, ЭтотОбъект.Дата);
			
			//
			мПроводка = мПроводки.Добавить();
		
			мПроводка.Период 		= ЭтотОбъект.Дата;
			//мПроводка.Организация 	= ЭтотОбъект.Организация;
			мПроводка.Сумма 		= ТекСумма - ТекСуммаНДС;
			мПроводка.Содержание	= "Транспортные расходы по доставке на точку";
			
			// Дт
			мПроводка.СчетДт = СтруктураНастроек.Счет;
			Попытка
				мПроводка.СубконтоДт.ТорговыеТочки 			= СтрокаТЧ.ТТ;
				мПроводка.СубконтоДт.СтатьиДоходовРасходов 	= СтруктураНастроек.СтатьяДоходовРасходов;
				//мПроводка.СубконтоДт.ЦФО 					= СтруктураНастроек.ЦФО;
				мПроводка.СубконтоДт.ЦФО 					= ?(ЗначениеЗаполнено(СтруктураНастроек.ЦФО), СтруктураНастроек.ЦФО, ЦФО);
			Исключение
			КонецПопытки;
			
			// Кт
			мПроводка.СчетКт = Счет604;
			мПроводка.СубконтоКт.Организации 	= ЭтотОбъект.Организация;
			мПроводка.СубконтоКт.Контрагенты 	= ЭтотОбъект.Контрагент;
			
			Если СНДС = Ложь Тогда ТекСуммаНДС = 0; КонецЕсли; //+++АК SHEP 2018.04.19 ИП-00018423
			
			// НДС
			//
			Если ТекСуммаНДС <> 0 Тогда
				мПроводка = мПроводки.Добавить();
			
				мПроводка.Период 		= ЭтотОбъект.Дата;
				//мПроводка.Организация 	= ЭтотОбъект.Организация;
				мПроводка.Сумма 		= ТекСуммаНДС;
				мПроводка.Содержание	= "НДС по доставке на точку";
				
				// Дт
				мПроводка.СчетДт = Счет6802;
				мПроводка.СубконтоДт.Организации	= ЭтотОбъект.Организация;
				мПроводка.СубконтоДт.ВидыНалогов 	= ВидНалогаНДС;
				мПроводка.СубконтоДт.СтавкиНДС		= ЭтотОбъект.СтавкаНДС;
				
				// Кт
				мПроводка.СчетКт = Счет604;
				мПроводка.СубконтоКт.Организации 	= ЭтотОбъект.Организация;
				мПроводка.СубконтоКт.Контрагенты 	= ЭтотОбъект.Контрагент;
			КонецЕсли;
			
		КонецЦикла;
		
    КонецЕсли;
	
	//+++АК SHEP 2018.04.26 ИП-00018321.02: с 01.04.2018 г. доп. тарифы
	СтруктураНастроекДопТариф =
		ОбщегоНазначенияСервер.ПолучитьНастройкиОтраженияОперацийВУчете(Перечисления.ВидыОперацийВУчете.ТранспортныеРасходыПоДоставкеНаТочкуДопТариф, ЭтотОбъект.Дата);
	
	Если ЗначениеЗаполнено(СтруктураНастроекДопТариф.Счет) Тогда
		
		// на случай проводок по каждой ТТ
		//ТекСумма = 0;
		//тзТорговыеТочки = ЭтотОбъект.Состав.Выгрузить();
		//тзТорговыеТочки.Свернуть("ТТ, СтавкаДопТарифа", "Количество");
		//Для Каждого СтрокаТЧ Из тзТорговыеТочки Цикл
		//	ТекСумма = ТекСумма + СтрокаТЧ.Ставка * СтрокаТЧ.Количество;
		//КонецЦикла;
		
		ТекСумма = ЭтотОбъект.СуммаДопТарифа;
		Если ТекСумма <> 0 Тогда
			
			ТекСуммаНДС = Окр(ТекСумма * ТекСтавкаНДС / (100 + ТекСтавкаНДС), 2);
			
			//ЦФО = ОбщиеПроцедуры.ПолучитьЦФОПоСтруктурнойЕдинице(СтрокаТЧ.ТТ, ЭтотОбъект.Дата);
			
			мПроводка = мПроводки.Добавить();
			мПроводка.Период 		= ЭтотОбъект.Дата;
			//мПроводка.Организация 	= ЭтотОбъект.Организация;
			мПроводка.Сумма 		= ТекСумма - ТекСуммаНДС;
			мПроводка.Содержание	= "Транспортные расходы по доставке на точку (доп. тариф)";
			
			// Дт
			мПроводка.СчетДт = СтруктураНастроекДопТариф.Счет;
			мПроводка.СубконтоДт.ТорговыеТочки 			= СтруктураНастроекДопТариф.СтруктурнаяЕдиница; //СтрокаТЧ.ТТ;
			мПроводка.СубконтоДт.СтатьиДоходовРасходов 	= СтруктураНастроекДопТариф.СтатьяДоходовРасходов;
			мПроводка.СубконтоДт.ЦФО 					= СтруктураНастроекДопТариф.ЦФО; //?(ЗначениеЗаполнено(СтруктураНастроекДопТариф.ЦФО), СтруктураНастроекДопТариф.ЦФО, ЦФО);
			
			// Кт
			мПроводка.СчетКт = Счет604;
			мПроводка.СубконтоКт.Организации 	= ЭтотОбъект.Организация;
			мПроводка.СубконтоКт.Контрагенты 	= ЭтотОбъект.Контрагент;
			
			Если СНДС = Ложь Тогда ТекСуммаНДС = 0; КонецЕсли; //+++АК SHEP 2018.04.19 ИП-00018423
			
			// НДС
			Если ТекСуммаНДС <> 0 Тогда
				мПроводка = мПроводки.Добавить();
			
				мПроводка.Период 		= ЭтотОбъект.Дата;
				//мПроводка.Организация 	= ЭтотОбъект.Организация;
				мПроводка.Сумма 		= ТекСуммаНДС;
				мПроводка.Содержание	= "НДС по доставке на точку (доп. тариф)";
				
				// Дт
				мПроводка.СчетДт = Счет6802;
				мПроводка.СубконтоДт.Организации	= ЭтотОбъект.Организация;
				мПроводка.СубконтоДт.ВидыНалогов 	= ВидНалогаНДС;
				мПроводка.СубконтоДт.СтавкиНДС		= ЭтотОбъект.СтавкаНДС;
				
				// Кт
				мПроводка.СчетКт = Счет604;
				мПроводка.СубконтоКт.Организации 	= ЭтотОбъект.Организация;
				мПроводка.СубконтоКт.Контрагенты 	= ЭтотОбъект.Контрагент;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	//---АК SHEP 2018.04.26
	
	АК_УчетМСФО.ЗаполнитьОрганизацию(мПроводки, ЭтотОбъект.Организация);	
	
КонецПроцедуры
