//+++АК MOSD +Модуль 2018.11.22 ЗА-00019570

Функция Токен() Экспорт
	
	Возврат Константы.ИнтеграцияТилсиТокен.Получить();
	
КонецФункции

Функция ПолучитьСкладДляОрдера()
	
	Возврат Константы.ИнтеграцияТилсиСклад.Получить();
	
КонецФункции

Функция ПолучитьОрганизациюДляОрдера()
	
	Возврат Константы.ИнтеграцияТилсиОрганизация.Получить();
	
КонецФункции

Процедура ДополнитьТекстОшибки(ТекстОшибки, Дополнение) Экспорт
	ТекстОшибки = ТекстОшибки + ?(ЗначениеЗаполнено(ТекстОшибки), Символы.ПС, "") + Дополнение;
КонецПроцедуры

Функция ВосстановитьДатуИзСтроки(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	//Формат даты: 2018-11-21 16:28:57-05
	Год     = Число(Сред(Значение,1,4));
	Месяц   = Число(Сред(Значение,6,2));
	День    = Число(Сред(Значение,9,2));
	
	Час     = Число(Сред(Значение,12, 2));
	Минута  = Число(Сред(Значение,15,2));
	Секунда = Число(Сред(Значение,18,2));
	
	Смещение= Число(Сред(Значение,20));
	
	Возврат Дата(Год, Месяц, День, Час, Минута, Секунда) + Смещение * 3600;
	
КонецФункции

Функция ПолучитьДанныеЗаполненияВебЗаказаПокупателя(Данные)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаСтрок.ШтрихКод,
		|	ТаблицаСтрок.Количество
		|ПОМЕСТИТЬ втТаблицаСтрок
		|ИЗ
		|	&ТаблицаСтрок КАК ТаблицаСтрок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШтриховыеКоды.Номенклатура,
		|	ШтриховыеКоды.ЕдиницаИзмерения,
		|	ШтриховыеКоды.Характеристика,
		|	втТаблицаСтрок.ШтрихКод,
		|	втТаблицаСтрок.Количество
		|ПОМЕСТИТЬ втНоменклатура
		|ИЗ
		|	втТаблицаСтрок КАК втТаблицаСтрок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтриховыеКоды КАК ШтриховыеКоды
		|		ПО втТаблицаСтрок.ШтрихКод = ШтриховыеКоды.ШтрихКод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Покупатель
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ПокупательИНН
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНоменклатура.Номенклатура,
		|	втНоменклатура.Характеристика,
		|	СУММА(втНоменклатура.Количество) КАК Количество
		|ИЗ
		|	втНоменклатура КАК втНоменклатура
		|ГДЕ
		|	НЕ втНоменклатура.Номенклатура ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	втНоменклатура.Номенклатура,
		|	втНоменклатура.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНоменклатура.ШтрихКод
		|ИЗ
		|	втНоменклатура КАК втНоменклатура
		|ГДЕ
		|	втНоменклатура.Номенклатура ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ПокупательИНН", Данные.ПокупательИНН);
	Запрос.УстановитьПараметр("ТаблицаСтрок",  Данные.ТаблицаСтрок);
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Покупатель", Неопределено);
	
	Выборка = МассивРезультатовЗапроса[2].Выбрать();
	Если Выборка.Следующий() Тогда
		ДанныеЗаполнения.Вставить("Покупатель", Выборка.Покупатель);
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("ТаблицаТоварыНайденные",   МассивРезультатовЗапроса[3].Выгрузить());
	ДанныеЗаполнения.Вставить("ТаблицаТоварыНеНайденные", МассивРезультатовЗапроса[4].Выгрузить());
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Процедура СоздатьВебЗаказПокупателя(Данные, ТекстОшибки) Экспорт
	
	ДанныеЗаполнения = ПолучитьДанныеЗаполненияВебЗаказаПокупателя(Данные);
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения.Покупатель) Тогда
		ДополнитьТекстОшибки(ТекстОшибки, "Не найден контрагент по ИНН " + Данные.ПокупательИНН);
	КонецЕсли;
	
	Если ДанныеЗаполнения.ТаблицаТоварыНеНайденные.Количество() > 0 Тогда
		Для Каждого ТекСтр Из ДанныеЗаполнения.ТаблицаТоварыНеНайденные Цикл
			ДополнитьТекстОшибки(ТекстОшибки, "Не найден товар по штрихкоду " + ТекСтр.ШтрихКод);
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Возврат
	КонецЕсли;
	
	ДокументОбъект = Документы.ВебЗаказПокупателя.СоздатьДокумент();
	ДокументОбъект.Дата = Данные.ОжидаемаяДатаДоставки;
	ДокументОбъект.Склад = ПолучитьСкладДляОрдера();
	ДокументОбъект.Организация = ПолучитьОрганизациюДляОрдера();
	ДокументОбъект.Заказчик = ДанныеЗаполнения.Покупатель;
	ДокументОбъект.Товары.Загрузить(ДанныеЗаполнения.ТаблицаТоварыНайденные);
	
	Попытка
		ДокументОбъект.Записать();
	Исключение
		ДополнитьТекстОшибки(ТекстОшибки, "Ошибка при записи документа.");
	КонецПопытки;                                                      	
	
КонецПроцедуры

Процедура СохранитьОшибкуВЖурналеРегистрации(ТекстОшибки) Экспорт
	ЗаписьЖурналаРегистрации("ИнтеграцияТилси", УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
КонецПроцедуры
