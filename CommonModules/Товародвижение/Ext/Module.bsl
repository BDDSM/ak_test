//+++АК Susk (Суслин К.В.) 2018.03.07 ИП-00017896 
Функция ТовародвижениеПоНовойСхеме(НаДату) Экспорт
	
	ДатаТДвНоваяСхема = ТовародвижениеПривелигированный.ДатаТовародвиженияПоНовойСхеме();
	
	//константа заполнена и переданная дата уже превышает дату в константе, используем новую схему.
	Возврат ЗначениеЗаполнено(ДатаТДвНоваяСхема) И ДатаТДвНоваяСхема <= НаДату;
	
КонецФункции

#Область Разобрать_Комплекты_В_Движениях_По_41_Счёту

//+++АК LATV 2018.06.23 ИП-00018971
Функция АК_РазобратьКомплектыВДвиженияхПо41Счёту(ДвиженияФинансовый, Ссылка, ТабСебестоимость = Неопределено) Экспорт  //+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 добавил параметр ТабСебестоимость
	
	Перем БПР; //Блок параметров
	Перем ТекстОшибки;
	
	ТекстОшибки = Неопределено;
	БПР	= Неопределено;
	
	Попытка
		БПР = АК_ЗаполнитьБлокПараметровРК(ДвиженияФинансовый, Ссылка);
		
		АК_ЗаполнитьТаблицуНоменклатуры(БПР);
		АК_СформироватьВтНоменклатура(БПР);
		АК_СформироватьТаблицуНомеровКорректируемыхДвижений(БПР);
		
		//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 
		АК_СформироватьСоставКомплектаНаКаждуюДату(БПР);
		//---АК Susk (Суслин К.В.) 
		
		Если БПР.тзНомераКорректируемыхДвижений.Количество() = 0 Тогда
			Возврат Новый ТаблицаЗначений();
		КонецЕсли;
		
		АК_СформироватьТаблицуДляКорректировкиДвижений(БПР);
		АК_СформироватьТаблицуНеразделямыхКомплектов(БПР);
		
		//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 	 
		//АК_ВыполнитьКорректировкуДвижений(БПР);
		АК_ВыполнитьКорректировкуДвижений(БПР, ТабСебестоимость);
		//---АК Susk (Суслин К.В.)
		
	Исключение
		ТекстОшибки = ОписаниеОшибки();	
	КонецПопытки;
	
	Если ЗначениеЗаполнено(БПР) Тогда
		БПР.Запрос.МенеджерВременныхТаблиц.Закрыть();	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ВызватьИсключение(ТекстОшибки);
	КонецЕсли;
	
	Возврат БПР.тзНеразделямыеКомплекты;
КонецФункции

//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 
Процедура АК_СформироватьСоставКомплектаНаКаждуюДату(БПР)
	
	Перем Запрос;
	
	Запрос = БПР.Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	0 КАК Цифра
	               |ПОМЕСТИТЬ ТЗ_Цифры
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	1
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	2
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	3
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	4
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	5
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	6
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	7
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	8
	               |
	               |ОБЪЕДИНИТЬ
	               |
	               |ВЫБРАТЬ
	               |	9
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	10 * ТЗ_Десятки.Цифра + ТЗ_Единицы.Цифра КАК Число
	               |ПОМЕСТИТЬ ТЗ_Числа
	               |ИЗ
	               |	ТЗ_Цифры КАК ТЗ_Десятки,
	               |	ТЗ_Цифры КАК ТЗ_Единицы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ), ДЕНЬ, ТЗ_Числа.Число) КАК ПериодДень
	               |ПОМЕСТИТЬ ТЗ_Даты
	               |ИЗ
	               |	ТЗ_Числа КАК ТЗ_Числа
	               |ГДЕ
	               |	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ), ДЕНЬ, ТЗ_Числа.Число) МЕЖДУ &НачалоПериода И &КонецПериода
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТЗ_Даты.ПериодДень,
	               |	СоставКомплекта.Период,
	               |	СоставКомплекта.Характеристика,
	               |	СоставКомплекта.Составляющая,
	               |	СоставКомплекта.Количество,
	               |	СоставКомплекта.Комплект,
	               |	СоставКомплекта.ТоварКомплекта
	               |ПОМЕСТИТЬ КомплектыПоДням
	               |ИЗ
	               |	ТЗ_Даты КАК ТЗ_Даты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставКомплекта КАК СоставКомплекта
	               |		ПО ТЗ_Даты.ПериодДень >= СоставКомплекта.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(КомплектыПоДням.Период) КАК Период,
	               |	КомплектыПоДням.Характеристика,
	               |	КомплектыПоДням.Составляющая,
	               |	КомплектыПоДням.ПериодДень
	               |ПОМЕСТИТЬ МаксимумДаты
	               |ИЗ
	               |	КомплектыПоДням КАК КомплектыПоДням
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	КомплектыПоДням.Характеристика,
	               |	КомплектыПоДням.Составляющая,
	               |	КомплектыПоДням.ПериодДень
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КомплектыПоДням.Комплект КАК Комплект,
	               |	КомплектыПоДням.Характеристика КАК Характеристика,
	               |	КомплектыПоДням.ПериодДень КАК ПериодДень,
	               |	КомплектыПоДням.Период,
	               |	КомплектыПоДням.Составляющая,
	               |	КомплектыПоДням.Количество,
	               |	КомплектыПоДням.ТоварКомплекта
				   |ПОМЕСТИТЬ СоставКомплектаНаДату
				   |ИЗ
	               |	МаксимумДаты КАК МаксимумДаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КомплектыПоДням КАК КомплектыПоДням
	               |		ПО МаксимумДаты.ПериодДень = КомплектыПоДням.ПериодДень
	               |			И МаксимумДаты.Характеристика = КомплектыПоДням.Характеристика
	               |			И МаксимумДаты.Составляющая = КомплектыПоДням.Составляющая
	               |			И МаксимумДаты.Период = КомплектыПоДням.Период
	               |ГДЕ
				   |	НЕ КомплектыПоДням.ПериодДень ЕСТЬ NULL
				   |";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(БПР.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Бпр.Дата));
	
	Запрос.Выполнить();	
	
КонецПроцедуры

//+++АК LATV 2018.06.23 ИП-00018971
Функция АК_ЗаполнитьБлокПараметровРК(ДвиженияФинансовый, Ссылка)

	Перем БПР;
	
	БПР = Новый Структура();
	
	тзДвижения = ДвиженияФинансовый.Выгрузить();
	
	БПР.Вставить("тзДвижения", тзДвижения);
	БПР.Вставить("ДвиженияФинансовый", ДвиженияФинансовый);
	БПР.Вставить("Запрос", Новый Запрос());
	БПР.Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.РасходныйОрдерСклад") Тогда
		//+++АК Susk (Суслин К.В.) 2018.08.14 ИП-00019544 - не всегда в РО эта дата заполнена
		СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Дата, ДатаОтраженияВФинУчете");		
		ДатаОтрВФУ = СтруктураРеквизитов["ДатаОтраженияВФинУчете"];
		ДатаДок = СтруктураРеквизитов["Дата"];
		//БПР.Вставить("Дата", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ДатаОтраженияВФинУчете"));
		БПР.Вставить("Дата", ?(ЗначениеЗаполнено(ДатаОтрВФУ), ДатаОтрВФУ, ДатаДок));
		//---АК Susk (Суслин К.В.) 
	Иначе
		БПР.Вставить("Дата", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Дата"));
	КонецЕсли;
	
	Возврат БПР;

КонецФункции

//+++АК sole 2018.04.12 ИП-00018345
Функция АК_СформироватьКолонкиТаблицыНоменклатуры()
	тзРезультат = Новый ТаблицаЗначений();
	
	КЧ = Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный);
	тзРезультат.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число", , ,КЧ));
	тзРезультат.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	тзРезультат.Колонки.Добавить("ФлагОбработки", Новый ОписаниеТипов("Булево"));
	тзРезультат.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	тзРезультат.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	тзРезультат.Колонки.Добавить("кКоличествоИмя", Новый ОписаниеТипов("Строка"));
	//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 
	тзРезультат.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	//---АК Susk (Суслин К.В.) 
	Возврат тзРезультат;
КонецФункции

//+++АК sole 2018.04.23 ИП-00018345
Процедура АК_ЗаполнитьТаблицуНоменклатуры(БПР)
	
	СчетТовары = ПланыСчетов.Финансовый.Товары;
	
	тзНоменклатуры = АК_СформироватьКолонкиТаблицыНоменклатуры();
	
	КолонкиСубконто = Новый Массив();
	
	Для Каждого Колонка Из БПР.тзДвижения.Колонки Цикл
		
		Если Найти(Колонка.Имя, "Субконто") <> 1 Тогда
			Продолжить;
		КонецЕсли;
		
		КолонкиСубконто.Добавить(Колонка.Имя);
	КонецЦикла;

	
	Для Каждого Стр Из БПР.тзДвижения Цикл
		
		кНоменклатураИмя = Неопределено;
		Для Каждого Имя Из КолонкиСубконто Цикл
			Если ТипЗнч(Стр[Имя]) = Тип("СправочникСсылка.Номенклатура") Тогда
				кНоменклатураИмя = Имя;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если кНоменклатураИмя = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НомСтр = тзНоменклатуры.Добавить();
		
		Если Стр["СчетДт"] = СчетТовары ИЛИ Стр["СчетКт"] = СчетТовары Тогда
			ФлагОбработки = Истина;
		Иначе
			ФлагОбработки = Ложь;
		КонецЕсли;
		
		НомСтр["НомерСтроки"] = Стр["НомерСтроки"];
		НомСтр["Номенклатура"] = Стр[кНоменклатураИмя];		   
		
		Если Найти(кНоменклатураИмя, "Дт") > 0 Тогда
			НомСтр["Количество"] = Стр["КоличествоДт"];
			НомСтр["кКоличествоИмя"] = "КоличествоДт";
		Иначе
			НомСтр["Количество"] = Стр["КоличествоКт"];
			НомСтр["кКоличествоИмя"] = "КоличествоКт";
		КонецЕсли;
		
		НомСтр["ФлагОбработки"] = ФлагОбработки;
		НомСтр["Сумма"] = Стр["Сумма"];
		
		//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 	 
		НомСтр["Период"] = НачалоДня(Стр["Период"]);
		//---АК Susk (Суслин К.В.)

	КонецЦикла;
	
	БПР.Вставить("тзНоменклатуры", тзНоменклатуры);	
КонецПроцедуры

//+++АК sole 2018.04.12 ИП-00018345
Процедура АК_СформироватьВтНоменклатура(БПР)
	
	Перем Запрос;
	
	Запрос = БПР.Запрос;
	Запрос.Текст =
"ВЫБРАТЬ
|	т.НомерСтроки,
//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 
|	т.Период,
//---АК Susk (Суслин К.В.) 
|   т.Номенклатура КАК НоменклатураСсылка,
|   т.ФлагОбработки,
|   т.Сумма,
|   т.Количество,
|	т.кКоличествоИмя
|
|		ПОМЕСТИТЬ вт
|
|ИЗ &Таблица т
|;
|//////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	вт.НомерСтроки,
//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 
|	вт.Период,
//---АК Susk (Суслин К.В.) 
|   вт.НоменклатураСсылка,
|   вт.ФлагОбработки,
|   вт.Сумма,
|   вт.Количество,
|	вт.кКоличествоИмя,
|	ЕСТЬNULL(ном.ЭтоКомплект, Ложь) КАК ЭтоКомплект
|
|   	ПОМЕСТИТЬ втНоменклатура
|
|ИЗ вт
|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ном ПО 
|			ном.Ссылка = вт.НоменклатураСсылка
|;
|//////////////////////////////////////////////////////////
|УНИЧТОЖИТЬ вт
|";
	Запрос.Параметры.Вставить("Таблица", БПР.тзНоменклатуры);
	Запрос.Выполнить();
	Запрос.Параметры.Очистить();
	
КонецПроцедуры

//+++АК sole 2018.04.12 ИП-00018345
Процедура АК_СформироватьТаблицуНомеровКорректируемыхДвижений(БПР)
	
	Перем Запрос;
	
	Запрос = БПР.Запрос;
	
	Запрос.Текст = 
"ВЫБРАТЬ
|	втНоменклатура.НомерСтроки
|	
|ИЗ втНоменклатура
|ГДЕ
|		втНоменклатура.ФлагОбработки = Истина
|	И   втНоменклатура.ЭтоКомплект = Истина
|";
	
	тзНомераКорректируемыхДвижений = Запрос.Выполнить().Выгрузить();
	
	БПР.Вставить("тзНомераКорректируемыхДвижений", тзНомераКорректируемыхДвижений);

КонецПроцедуры

//+++АК sole 2018.04.13 ИП-00018345
Процедура АК_СформироватьТаблицуДляКорректировкиДвижений(БПР)
	
	Перем Запрос;
	
	Запрос = БПР.Запрос;
	
	//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 
	//меняю запрос к составу комплекта. Получаю состав комплекта на каждую дату периода комплекта, т.к. в регистре состава
	//встречаются последние записи с нулевым количеством, например с 20 июня, а у нас все движения до 20 июня. А до 20 июня
	//количество есть. Обработка же подбирает последнюю запись на конец месяца, где нулевое количество, и начинает выдавать ошибку.
	
//	Запрос.Текст = 
//"ВЫБРАТЬ РАЗРЕШЕННЫЕ
//|	втНоменклатура.НомерСтроки,
//|	СоставКомплектаСрезПоследних.Составляющая.Владелец КАК Составляющая,
//|   втНоменклатура.Количество * СоставКомплектаСрезПоследних.Количество КАК Количество,
//|   втНоменклатура.Сумма КАК ОбщаяСумма,
//|	втНоменклатура.кКоличествоИмя
//|
//|   	ПОМЕСТИТЬ вт1
//|
//|ИЗ втНоменклатура
//|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставКомплекта.СрезПоследних(&ДатаСреза) КАК СоставКомплектаСрезПоследних ПО
//|   	 	СоставКомплектаСрезПоследних.Характеристика.Владелец = втНоменклатура.НоменклатураСсылка
//|		И	СоставКомплектаСрезПоследних.Количество > 0
//|
//|ГДЕ
//|		втНоменклатура.ФлагОбработки = Истина
//|	И   втНоменклатура.ЭтоКомплект = Истина
//|	И	НЕ СоставКомплектаСрезПоследних.Количество ЕСТЬ NULL
//|;                                   

Запрос.Текст = 
"ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	втНоменклатура.НомерСтроки,
|	СоставКомплектаНаДату.Составляющая.Владелец КАК Составляющая,
|   втНоменклатура.Количество * СоставКомплектаНаДату.Количество КАК Количество,
|   втНоменклатура.Сумма КАК ОбщаяСумма,
|	втНоменклатура.кКоличествоИмя
|
|		ПОМЕСТИТЬ вт1
|
|ИЗ втНоменклатура
|	ЛЕВОЕ СОЕДИНЕНИЕ СоставКомплектаНаДату КАК СоставКомплектаНаДату ПО
|   	 	СоставКомплектаНаДату.Характеристика.Владелец = втНоменклатура.НоменклатураСсылка
|			И втНоменклатура.Период = СоставКомплектаНаДату.ПериодДень
|			И СоставКомплектаНаДату.Количество > 0
|
|ГДЕ
|		втНоменклатура.ФлагОбработки = Истина
|	И   втНоменклатура.ЭтоКомплект = Истина
|	И	НЕ СоставКомплектаНаДату.Количество ЕСТЬ NULL
|;                                   
//---АК Susk (Суслин К.В.) 
|//////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	вт1.НомерСтроки,
|   СУММА(вт1.Количество) КАК ОбщееКоличество,
|   вт1.ОбщаяСумма
|
|		ПОМЕСТИТЬ вт2
|
|ИЗ	вт1
|СГРУППИРОВАТЬ ПО
|	вт1.НомерСтроки,
|   вт1.ОбщаяСумма
|;
|//////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	вт1.НомерСтроки,
|   вт1.Составляющая,
|   вт1.Количество,
|	вт1.ОбщаяСумма,
|	вт2.ОбщееКоличество,
|	вт1.кКоличествоИмя
|
|ИЗ	вт1
|	ЛЕВОЕ СОЕДИНЕНИЕ вт2 ПО вт2.НомерСтроки = вт1.НомерСтроки
|";
	Запрос.Параметры.Вставить("ДатаСреза", БПР.Дата);
	тзКорректировкиДвижений = Запрос.Выполнить().Выгрузить();
	
	БПР.Вставить("тзКорректировкиДвижений", тзКорректировкиДвижений);
КонецПроцедуры

//+++АК sole 2018.04.17 ИП-00018345
Процедура АК_СформироватьТаблицуНеразделямыхКомплектов(БПР)
	Перем Запрос;
	
	Запрос = БПР.Запрос;
	
	//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.11 
	//меняю запрос к составу комплекта. Получаю состав комплекта на каждую дату периода комплекта, т.к. в регистре состава
	//встречаются последние записи с нулевым количеством, например с 20 июня, а у нас все движения до 20 июня. А до 20 июня
	//количество есть. Обработка же подбирает последнюю запись на конец месяца, где нулевое количество, и начинает выдавать ошибку.
	
//	Запрос.Текст = 
//"ВЫБРАТЬ РАЗРЕШЕННЫЕ
//|	втНоменклатура.НоменклатураСсылка.Наименование КАК Наименование
//|
//|ИЗ втНоменклатура
//|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставКомплекта.СрезПоследних(&ДатаСреза) КАК СоставКомплектаСрезПоследних ПО
//|   	 	СоставКомплектаСрезПоследних.Характеристика.Владелец = втНоменклатура.НоменклатураСсылка
//|
//|ГДЕ
//|		втНоменклатура.ФлагОбработки = Истина
//|	И   втНоменклатура.ЭтоКомплект = Истина
//|
//|СГРУППИРОВАТЬ ПО втНоменклатура.НоменклатураСсылка
//|
//|ИМЕЮЩИЕ СУММА(СоставКомплектаСрезПоследних.Количество) = 0
//|";	

	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				   |	втНоменклатура.НоменклатураСсылка.Наименование КАК Наименование
				   |
				   |ИЗ втНоменклатура
				   |	ЛЕВОЕ СОЕДИНЕНИЕ СоставКомплектаНаДату КАК СоставКомплектаНаДату ПО
				   |   	 	СоставКомплектаНаДату.Характеристика.Владелец = втНоменклатура.НоменклатураСсылка
				   |			И втНоменклатура.Период = СоставКомплектаНаДату.ПериодДень
				   |
				   |ГДЕ
				   |		втНоменклатура.ФлагОбработки = Истина
				   |	И   втНоменклатура.ЭтоКомплект = Истина
				   |
				   |СГРУППИРОВАТЬ ПО втНоменклатура.НоменклатураСсылка
				   |
				   |ИМЕЮЩИЕ СУММА(СоставКомплектаНаДату.Количество) = 0
				   |";
	
	//---АК Susk (Суслин К.В.) 

	тзНеразделямыеКомплекты = Запрос.Выполнить().Выгрузить();
	
	БПР.Вставить("тзНеразделямыеКомплекты", тзНеразделямыеКомплекты);
КонецПроцедуры

//+++АК sole 2018.04.13 ИП-00018345
Процедура АК_ВыполнитьКорректировкуДвижений(БПР, ТабСебестоимость)
	
	Перем НомерСтроки;
	Перем ПараметрыОтбора;
	Перем тзКорректировкиДвижений;
	Перем тзДвиженияМод;
	
	тзКорректировкиДвижений = БПР.тзКорректировкиДвижений;
	
	Если тзКорректировкиДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	БПР.ДвиженияФинансовый.Очистить();
	
	ПараметрыОтбора = Новый Структура();
	тзДвиженияМод = БПР.тзДвижения.СкопироватьКолонки();
	
	КолонкиСубконто = Новый Массив();
	
	Для Каждого Колонка Из тзДвиженияМод.Колонки Цикл
		
		Если Найти(Колонка.Имя, "Субконто") <> 1 Тогда
			Продолжить;
		КонецЕсли;
		
		КолонкиСубконто.Добавить(Колонка.Имя);
	КонецЦикла;
	
	НомерСтрокиМод = 1;
	
	//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.10 
	ДатаБольше1Мая2018 = БПР.Дата >= Дата("20180501");
	//---АК Susk (Суслин К.В.) 	 
	
	Для Каждого Движ Из БПР.тзДвижения Цикл
		НомерСтроки = Движ["НомерСтроки"];
		
		ПараметрыОтбора.Очистить();
		ПараметрыОтбора.Вставить("НомерСтроки", НомерСтроки);
		
		тз = тзКорректировкиДвижений.НайтиСтроки(ПараметрыОтбора);
		
		Если тз.Количество() = 0 Тогда
			// Оставляем движение без измененений
			
			НСтр = тзДвиженияМод.Добавить();
			ЗаполнитьЗначенияСвойств(НСтр, Движ);
			НСтр["НомерСтроки"] = НомерСтрокиМод;
			НомерСтрокиМод = НомерСтрокиМод + 1;
			Продолжить;
		КонецЕсли;
		
		// Формируем новые движения для составляющих комплекта
		
		кНоменклатураИмя = Неопределено;
		Для Каждого Имя Из КолонкиСубконто Цикл
			Если ТипЗнч(Движ[Имя]) = Тип("СправочникСсылка.Номенклатура") Тогда
				кНоменклатураИмя = Имя;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Остаток = Движ["Сумма"]; 
		Инд = 0;
		кИнд = тз.Количество() - 1;
		
		Пока Инд <= кИнд Цикл
			Стр = тз[Инд];
			кКоличествоИмя = Стр["кКоличествоИмя"];
			
			НСтр = тзДвиженияМод.Добавить();
			ЗаполнитьЗначенияСвойств(НСтр, Движ);
			НСтр["НомерСтроки"] = НомерСтрокиМод; 
			НСтр[кНоменклатураИмя] = Стр["Составляющая"];
			НСтр[кКоличествоИмя] = Стр["Количество"];
			
			//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.10 			 
			Себестоимость = 0;
			Если ДатаБольше1Мая2018 Тогда
				Себестоимость = ПолучитьСебестоимость(Стр["Составляющая"], ТабСебестоимость);
				Если Себестоимость <> Неопределено Тогда
					НСтр["Сумма"] = Стр["Количество"] * Себестоимость;
					Остаток = Остаток - НСтр["Сумма"];
					
					Если Остаток < 0 Тогда
						Остаток = 0;
					КонецЕсли;
				Иначе
					Себестоимость = 0;
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ ДатаБольше1Мая2018 ИЛИ Себестоимость = 0 Тогда
			//---АК Susk (Суслин К.В.) 	
				Если Инд < кИнд Тогда
					Сумма = Стр["ОбщаяСумма"] * Стр["Количество"] / Стр["ОбщееКоличество"];
					НСтр["Сумма"] = Сумма;
					Остаток = Остаток - Сумма;
				Иначе
					НСтр["Сумма"] = Остаток;
				КонецЕсли;
			КонецЕсли;
			
			НомерСтрокиМод = НомерСтрокиМод + 1;
			Инд = Инд + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	БПР.ДвиженияФинансовый.Загрузить(тзДвиженияМод);
	
	// Не записываем, так как запись в финансовый регистр 
	// осуществляется через механизм отложенного проведения
	//БПР.ДвиженияФинансовый.Записать();

КонецПроцедуры

#КонецОбласти

//+++АК Susk (Суслин К.В.) ИП-00018553^06 2018.07.10 
Функция ПолучитьСебестоимость(Номенклатура, ТабСебестоимость)
	
	Если ТабСебестоимость = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	ЗначениеВозврата = 0;
	СтрокаСебестоимость = ТабСебестоимость.Найти(Номенклатура, "Номенклатура");
	
	Если СтрокаСебестоимость <> Неопределено Тогда
		ЗначениеВозврата = СтрокаСебестоимость.Себестоимость;		
	КонецЕсли;
	
	Возврат ЗначениеВозврата;
	
КонецФункции 
