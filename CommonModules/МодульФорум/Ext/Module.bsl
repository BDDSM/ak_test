
//////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ "МОДУЛЬФОРУМ"
//////////////////////////////////////////////////////////////////////////////////

// Функция возвращает ссылку на элемент справочника "Пользователи", 
// соответствующий текущему пользователю информационной базы.
// Используется для инициализации глобальной переменной глТекущийПользователь
// При начале работы системы
//
Функция ОпределитьТекущегоПользователя() Экспорт
	
	Если ПустаяСтрока(ИмяПользователя()) Тогда
		ИмяПользователя           = "НеАвторизован";
		ПолноеИмяПользователя     = "Не авторизован";
	Иначе
		ИмяПользователя           = ИмяПользователя();
		Если ПустаяСтрока(ПолноеИмяПользователя()) Тогда
			ПолноеИмяПользователя = ИмяПользователя;
		Иначе
			ПолноеИмяПользователя = ПолноеИмяПользователя();
		КонецЕсли;
	КонецЕсли;
	
	ДлинаКодаПользователя = Метаданные.Справочники.Пользователи.ДлинаКода;
	Если СтрДлина(ИмяПользователя) > ДлинаКодаПользователя Тогда
		ИмяПользователя = Лев(ИмяПользователя, ДлинаКодаПользователя);
	КонецЕсли; 
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	(НЕ Пользователи.ЭтоГруппа)
	|	И Пользователи.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", ИмяПользователя);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОбъектПользователь = Справочники.Пользователи.СоздатьЭлемент();
		ОбъектПользователь.Код          = ИмяПользователя;
		ОбъектПользователь.Наименование = ПолноеИмяПользователя;
		Попытка
			ОбъектПользователь.Записать();
        Исключение
            #Если Клиент Тогда
			Предупреждение("Пользователь : " + ИмяПользователя + " не был найден в справочнике пользователей. Возникла ошибка при добавлении пользователя в справочник.
				|" + ОписаниеОшибки());
			ЗавершитьРаботуСистемы(Ложь);
            #КонецЕсли
			Возврат Справочники.Пользователи.ПустаяСсылка();
		КонецПопытки;
		ТекущийПользователь = ОбъектПользователь.Ссылка;
    Иначе
        Выборка = Результат.Выбрать();
		Выборка.Следующий();
        ТекущийПользователь = Выборка.Ссылка;
	КонецЕсли; 
	
	Возврат ТекущийПользователь;
	
КонецФункции

Функция ПолучитьЗначениеПеременнойОкружения(Имя) Экспорт
	
	Попытка
		Ctrl = Новый COMОбъект("MSScriptControl.ScriptControl");
		Ctrl.Language = "vbscript";
		Ctrl.AddCode("
		|Function SpecialFolders(Name)
		|Set Shell = CreateObject(""Wscript.Shell"")
		|SpecialFolders = Shell.ExpandEnvironmentStrings(Name)
		|End Function");
		ИмяПапки = Ctrl.Run("SpecialFolders", "%" + СокрЛП(Имя) + "%");
	Исключение
		ИмяПапки = Неопределено;
	КонецПопытки;
	
	Возврат ИмяПапки;
	
КонецФункции

Функция НезаполненыОбязательныеПоля(Форма) Экспорт
	
	Для каждого ЭлементФормы Из Форма.ЭлементыФормы Цикл
		Если ТипЗнч(ЭлементФормы) = Тип("Полеввода") Тогда
			Если ЭлементФормы.АвтоОтметкаНезаполненного Тогда
				Если НЕ ЗначениеЗаполнено(Форма.ЭлементыФормы[ЭлементФормы.Имя].Значение) Тогда
					Сообщить("Не заполнено обязательное поле """ + ЭлементФормы.Имя + """");
					Форма.ТекущийЭлемент = ЭлементФормы;
					Возврат Истина;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолныеПраваНаФоруме() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПользователейФорума.АдминистраторФорума
	|ИЗ
	|	РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|ГДЕ
	|	НастройкиПользователейФорума.Пользователь = &ТекПользователь
	|	И НастройкиПользователейФорума.АдминистраторФорума
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиПользователейФорума.АдминистраторФорума";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0 ИЛИ РольДоступна("ПолныеПрава");
	
КонецФункции // ПолныеПраваНаФоруме()

Функция РазложитьСтрокуВМассив(Знач Стр, Разделитель = ",") Экспорт 
	
	МассивСтрок = Новый Массив; 
	ДлинаРазделителя = СтрДлина(Разделитель); 
	Поз = Найти(Стр, Разделитель); 
	Пока Поз <> 0 Цикл 
	  	МассивСтрок.Добавить(Лев(Стр, Поз - 1)); 
	  	Стр = Сред(Стр, Поз + ДлинаРазделителя); 
	  	Поз = Найти(Стр, Разделитель) 
	КонецЦикла; 
	
	МассивСтрок.Добавить(Стр);
	
	Возврат МассивСтрок
	
КонецФункции


// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОТПРАВКИ ПОЧТЫ

Функция КорректныйЕмайл(СтрокаАдреса) Экспорт
	
	Шаблон = ".+@.+\..+"; 
	
	RegExp = Новый COMОбъект("VBScript.RegExp"); 
  	RegExp.MultiLine 	= Ложь; 
  	RegExp.Global 		= Истина; 
  	RegExp.IgnoreCase 	= Истина; 
  	RegExp.Pattern 		= Шаблон;
	Если RegExp.Test(СтрокаАдреса) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // КорректныйЕмайл()

Функция ПолучитьПочтовыйПрофиль() 
	
	// Сформировать почтовый профиль.
	ИПП = Новый ИнтернетПочтовыйПрофиль;
	ИПП.АдресСервераSMTP 	= СокрЛП(Справочники.НастройкиФорума.АдресСервераSMTP);
	ИПП.ПортSMTP 			= СокрЛП(Справочники.НастройкиФорума.ПортSMTP);
	ИПП.АутентификацияSMTP 	= СпособSMTPАутентификации.Login;
	ИПП.ПользовательSMTP 	= СокрЛП(Справочники.НастройкиФорума.ПользовательSMTP);
	ИПП.ПарольSMTP 			= СокрЛП(Справочники.НастройкиФорума.ПарольSMTP);
	
	Возврат ИПП;
	
КонецФункции

Функция ПолучитьМассивАдресов(НаВопрос)
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаВопрос"		, НаВопрос);
	Запрос.УстановитьПараметр("ТекПользователь"	, ПараметрыСеанса.ТекущийПользователь);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Емайл КАК Емайл
	|ИЗ
	|	(ВЫБРАТЬ
	|		НастройкиПользователейФорума.Емайл КАК Емайл
	|	ИЗ
	|		Документ.ВопросыПользователей КАК ВопросыПользователей
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|			ПО ВопросыПользователей.Автор = НастройкиПользователейФорума.Пользователь
	|	ГДЕ
	|		ВопросыПользователей.Ссылка = &НаВопрос
	|		И НастройкиПользователейФорума.ОповещатьПоЕмайл
	|		И НастройкиПользователейФорума.Пользователь <> &ТекПользователь
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НастройкиПользователейФорума.Емайл
	|	ИЗ
	|		РегистрСведений.ОтветыНаВопрос КАК ОтветыНаВопрос
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|			ПО ОтветыНаВопрос.Пользователь = НастройкиПользователейФорума.Пользователь
	|	ГДЕ
	|		ОтветыНаВопрос.Вопрос = &НаВопрос
	|		И ОтветыНаВопрос.Пользователь <> &ТекПользователь
	|		И НастройкиПользователейФорума.ОповещатьПоЕмайл) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Емайл";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Адрес = СокрЛП(Выборка.Емайл);
		//Если Массив.Найти(Адрес) = Неопределено Тогда
			Массив.Добавить(Адрес);
		//КонецЕсли;
	КонецЦикла;
	Возврат Массив;
КонецФункции // ПолучитьМассивАдресов()

Функция ПолучитьМассивАдресовАдминистраторов() 
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПользователейФорума.Емайл
	|ИЗ
	|	РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|ГДЕ
	|	НастройкиПользователейФорума.АдминистраторФорума";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Адрес = СокрЛП(Выборка.Емайл);
		Если Массив.Найти(Адрес) = Неопределено Тогда
			Массив.Добавить(Адрес);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции

Функция РассылкаПисьма(МассивАдресов, ТемаПисьма, ТекстПисьма, СписокВложений = Неопределено) Экспорт
	
	ТекстВКонце = "
	|
	|Это письмо сгенерировано автоматически, отвечать на него не нужно!
	|Для ответа перейдите в обсуждение этого вопроса на форуме в 1С.";
	
	// Создать сообщение
	Сообщение = Новый ИнтернетПочтовоеСообщение;
	Для каждого ТекАдрес Из МассивАдресов Цикл
		Если ТекАдрес <> "" Тогда
			Сообщение.Получатели.Добавить(ТекАдрес);
		КонецЕсли;
	КонецЦикла;
	Если Сообщение.Получатели.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	Сообщение.Отправитель.Адрес = СокрЛП(Справочники.НастройкиФорума.ПользовательSMTP);
	Сообщение.ИмяОтправителя 	= СокрЛП(Справочники.НастройкиФорума.ИмяОтправителяПисем);
	Сообщение.Тема 				= ТемаПисьма;
	Сообщение.Тексты.Добавить(ТекстПисьма + ТекстВКонце);
	
	// Вложения
	Если СписокВложений <> Неопределено Тогда
		Для каждого ТекЭлемент Из СписокВложений Цикл
			Сообщение.Вложения.Добавить(Новый ДвоичныеДанные(ТекЭлемент.Значение), ТекЭлемент.Представление);
		КонецЦикла;
	КонецЕсли;
	
	// Подключиться и отправить
	Почта = Новый ИнтернетПочта;
	Попытка
		Почта.Подключиться(ПолучитьПочтовыйПрофиль());
		Почта.Послать(Сообщение);
		Почта.Отключиться();
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции


// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОТПРАВКИ ПОЧТЫ

// Массив кодов пользователей для отправки сообщений в терминальный сервер
Функция ПолучитьРезультатЗапросаПоЛогинам(НаВопрос) 
	
	Массив = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаВопрос", НаВопрос);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ТерминальныйСервер КАК Сервер,
	|	ВложенныйЗапрос.Логин КАК Логин
	|ИЗ
	|	(ВЫБРАТЬ
	|		НастройкиПользователейФорума.ТерминальныйСервер КАК ТерминальныйСервер,
	|		ОтветыНаВопрос.Пользователь.Код КАК Логин
	|	ИЗ
	|		РегистрСведений.ОтветыНаВопрос КАК ОтветыНаВопрос
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|			ПО ОтветыНаВопрос.Пользователь = НастройкиПользователейФорума.Пользователь
	|	ГДЕ
	|		ОтветыНаВопрос.Вопрос = &НаВопрос
	|		И НастройкиПользователейФорума.ОповещатьНаСервер
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НастройкиПользователейФорума.ТерминальныйСервер,
	|		ВопросыПользователей.Автор.Код
	|	ИЗ
	|		Документ.ВопросыПользователей КАК ВопросыПользователей
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|			ПО ВопросыПользователей.Автор = НастройкиПользователейФорума.Пользователь
	|	ГДЕ
	|		ВопросыПользователей.Ссылка = &НаВопрос
	|		И НастройкиПользователейФорума.ОповещатьНаСервер
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НастройкиПользователейФорума.ТерминальныйСервер,
	|		НастройкиПользователейФорума.Пользователь.Код
	|	ИЗ
	|		РегистрСведений.НастройкиПользователейФорума КАК НастройкиПользователейФорума
	|	ГДЕ
	|		НастройкиПользователейФорума.АдминистраторФорума) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ТерминальныйСервер,
	|	ВложенныйЗапрос.Логин
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.ТерминальныйСервер";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ОтправитьОповещениеНаТерминальныйСервер(ДокВопрос, ЭтоКомментарий = Истина)
	
	//КаталогВиндовз 		= ПолучитьЗначениеПеременнойОкружения("windir") + "\System32\";
	ФайлРассылки 		= ПолучитьИмяВременногоФайла();
	ЗапТекст 			= Новый ЗаписьТекста(ФайлРассылки, КодировкаТекста.ANSI);
	РезультатЗапроса 	= ПолучитьРезультатЗапросаПоЛогинам(ДокВопрос);
	ВыборкаЛогинов 		= РезультатЗапроса.Выбрать();
	
	// Формируем временный файл
	Пока ВыборкаЛогинов.Следующий() Цикл
		ЗапТекст.ЗаписатьСтроку(СокрЛП(ВыборкаЛогинов.Логин));
	КонецЦикла;
	ЗапТекст.Закрыть();
	
	// Формирование бат файла
	врФайл = ПолучитьИмяВременногоФайла("bat");
	ЗапБат = Новый ЗаписьТекста(врФайл, КодировкаТекста.OEM);
	МассивТермСерверов = РазложитьСтрокуВМассив(Справочники.НастройкиФорума.ТерминальныеСервераЧерезЗапятую.Наименование, ",");
	Для каждого ТермСерв Из МассивТермСерверов Цикл
		ЗапБат.ЗаписатьСтроку("MSG.EXE @""" + ФайлРассылки + """ /SERVER:" + ТермСерв + " На форум в 1С (" + Метаданные.Синоним + ") добавлен новый " + ?(ЭтоКомментарий, "комментарий!","вопрос!"));
	КонецЦикла;
	ЗапБат.Закрыть();
	
	// Отправляем сообщение на все сервера
	ЗапуститьПриложение(врФайл, , Ложь);
	
КонецПроцедуры

Процедура ОповеститьАдминистраторовОНовомВопросе(ДокВопрос) Экспорт
	
	МассивАдресов = ПолучитьМассивАдресовАдминистраторов();
	РассылкаПисьма(МассивАдресов, "Новый вопрос (" + Метаданные.Имя + ") от """ + ДокВопрос.Автор + """ в категории " + ДокВопрос.Категория, ДокВопрос.Вопрос);
	
КонецПроцедуры

// Рассылка оповещений
Процедура ОповеститьУчастниковОбсужденияВопроса(ДокВопрос, ТекстКомментария) Экспорт
	
	// Оповещение на почту
	МассивАдресов = ПолучитьМассивАдресов(ДокВопрос);
	Если МассивАдресов.Количество() > 0 Тогда
		ТекстПисьма = СокрЛП("Комментарий к вопросу: 
		| " + Лев(ДокВопрос.Вопрос, 100) + "...") + "
		|
		|Комментирует " + СокрЛП(ПараметрыСеанса.ТекущийПользователь) + ": 
		| " + ТекстКомментария;	
		РассылкаПисьма(МассивАдресов, "" + Метаданные.Имя + ": комментарий к вопросу №" + ДокВопрос.Номер + " от """ + СокрЛП(ПараметрыСеанса.ТекущийПользователь.Код) + """", ТекстПисьма);
	КонецЕсли;
	// Оповещение в терминальный сервер (Win7 и выше)
	// ОтправитьОповещениеНаТерминальныйСервер(ДокВопрос, Истина);
КонецПроцедуры
