//+++АК KOPA +Модуль 2018.02.016 ИП-00017767.01
//Модуль предназначен для получения данных для http сервиса TelegramBot
//Сервис будет обмениваться информацией с базой бота управления развития.

Функция ПолучитьОтвет(СтрокаJSON) Экспорт 
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка	
		Данные = ТелеграмТехБот.ПрочитатьJSONИзСтроки(СтрокаJSON);	
//Для отладки можно использовать обработку из внешних обработок		
		ИмяФайла = "";
		обк = ТелеграмТехБот.ПолучитьТестОбработку("uprrazv", ИмяФайла);
		
		Если обк = Неопределено Тогда
		    Если Не Данные.Свойство("Действие")Тогда
				ВызватьИсключение "Неправильный запрос";	
			КонецЕсли;
			
			Результат = ПолучитьОтветТочкаВхода(Данные);
		Иначе 	
		    Результат = обк.ПолучитьОтветТочкаВхода(Данные);
			
			УдалитьФайлы(ИмяФайла);
		КонецЕсли;			
	Исключение
		Ошибка = ОписаниеОшибки();
		
		Результат = Новый Структура;
		Результат.Вставить("Ошибка", Ошибка);
	КонецПопытки;
	
	Результат = ТелеграмТехБот.ЗаписатьJSONВСтроку(Результат);
	
	Возврат Результат;
КонецФункции

Функция ПолучитьОтветТочкаВхода(Данные) Экспорт 
	Действие = ВРег(Данные.Действие);
	
	Если Действие = "ДАННЫЕМАГАЗИНОВ"  Тогда		
		Результат = ПолучитьДанныеПоМагазинам(Данные);		
	Иначе		
		ВызватьИсключение "Неизвестное действие: " + Данные.Действие;			
	КонецЕсли;
	
	Возврат Результат;
КонецФункции






//ИП-00017767.01
Функция ПолучитьДанныеПоМагазинам(Данные) Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствиеОбъектРольСрезПоследних.ТипРолиID,
		|	СоответствиеОбъектРольСрезПоследних.РольПользователя,
		|	РолиПользователейСоставРоли.Сотрудник,
		|	СоответствиеОбъектРольСрезПоследних.Период,
		|	СоответствиеОбъектРольСрезПоследних.ТипРоли,
		|	СоответствиеОбъектРольСрезПоследних.Объект,
		|	СоответствиеОбъектРольСрезПоследних.Автор
		|ПОМЕСТИТЬ тРоль
		|ИЗ
		|	РегистрСведений.СоответствиеОбъектРоль.СрезПоследних(, ) КАК СоответствиеОбъектРольСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиПользователей.СоставРоли КАК РолиПользователейСоставРоли
		|		ПО СоответствиеОбъектРольСрезПоследних.РольПользователя = РолиПользователейСоставРоли.Ссылка
		|ГДЕ
		|	РолиПользователейСоставРоли.НомерСтроки = 1
		|	И СоответствиеОбъектРольСрезПоследних.ТипРолиID В(&ТипРолиID)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЛимитыАренднойПлатыСрезПоследних.ТорговаяТочка,
		|	МАКСИМУМ(ЛимитыАренднойПлатыСрезПоследних.МесяцАренды) КАК МесяцАренды
		|ПОМЕСТИТЬ тМаксМесяцПериодАренды
		|ИЗ
		|	РегистрСведений.ЛимитыАренднойПлаты.СрезПоследних(&Период, ) КАК ЛимитыАренднойПлатыСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	ЛимитыАренднойПлатыСрезПоследних.ТорговаяТочка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тМаксМесяцПериодАренды.ТорговаяТочка,
		|	тМаксМесяцПериодАренды.МесяцАренды,
		|	ЛимитыАренднойПлатыСрезПоследних.СуммаАрендаПостоянная,
		|	ЛимитыАренднойПлатыСрезПоследних.СуммаАрендаПеременная
		|ПОМЕСТИТЬ тАренда
		|ИЗ
		|	тМаксМесяцПериодАренды КАК тМаксМесяцПериодАренды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЛимитыАренднойПлаты.СрезПоследних(&Период, ) КАК ЛимитыАренднойПлатыСрезПоследних
		|		ПО тМаксМесяцПериодАренды.ТорговаяТочка = ЛимитыАренднойПлатыСрезПоследних.ТорговаяТочка
		|			И тМаксМесяцПериодАренды.МесяцАренды = ЛимитыАренднойПлатыСрезПоследних.МесяцАренды
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 100000
		|	ВЫБОР
		|		КОГДА СтруктурныеЕдиницы.СтатусТорговойТочки = ЗНАЧЕНИЕ(Перечисление.СтатусыТорговыхТочек.пустаяссылка)
		|			ТОГДА -1
		|		ИНАЧЕ СтруктурныеЕдиницы.СтатусТорговойТочки.Порядок
		|	КОНЕЦ КАК СтатутТорговойТочки1,
		|	ВЫБОР
		|		КОГДА СтруктурныеЕдиницы.Город = ЗНАЧЕНИЕ(Справочник.Города.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ СтруктурныеЕдиницы.Город.Наименование
		|	КОНЕЦ КАК Город2,
		|	СтруктурныеЕдиницы.Адрес КАК Адрес2,
		|	СтруктурныеЕдиницы.ОбщаяПлощадь КАК ОбщаяПлощадь3,
		|	СтруктурныеЕдиницы.ТорговаяПлощадь КАК ТорговаяПлощадь4,
		|	СтруктурныеЕдиницы.КоординатыШирота КАК КоординатыШирота5,
		|	СтруктурныеЕдиницы.КоординатыДолгота КАК КоординатыДолгота5,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА тРоль.ТипРолиID = ""SpecialistPoOtkrytiyu""
		|				ТОГДА ВЫБОР
		|						КОГДА тРоль.Сотрудник ЕСТЬ NULL
		|							ТОГДА """"
		|						ИНАЧЕ тРоль.Сотрудник.Наименование
		|					КОНЕЦ
		|			ИНАЧЕ """"
		|		КОНЕЦ) КАК СпециалистПоОткрытию6,
		|	СтруктурныеЕдиницы.НаименованиеАрендодателя КАК НаименованиеАрендодателя7,
		|	СтруктурныеЕдиницы.ТелефонАрендодателя КАК ТелефонАрендодателя8,
		|	СтруктурныеЕдиницы.АдресАрендодателя КАК АдресАрендодателя9,
		|	ЕСТЬNULL(тАренда.СуммаАрендаПостоянная, 0) КАК СуммаАрендаПостоянная10,
		|	ЕСТЬNULL(тАренда.СуммаАрендаПеременная, 0) КАК СуммаАрендаПеременная10,
		|	ЕСТЬNULL(ЗаявкиНаОткрытиеМагазина.ЭлектроэнергииПоДоговору, 0) КАК ЭлектроэнергииПоДоговору11,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА тРоль.ТипРолиID = ""PomoshnikPoRaskrutke""
		|				ТОГДА ВЫБОР
		|						КОГДА тРоль.Сотрудник ЕСТЬ NULL
		|							ТОГДА """"
		|						ИНАЧЕ тРоль.Сотрудник.Наименование
		|					КОНЕЦ
		|			ИНАЧЕ """"
		|		КОНЕЦ) КАК ПомощникПоРаскрутке12
		|ИЗ
		|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		|		ЛЕВОЕ СОЕДИНЕНИЕ тАренда КАК тАренда
		|		ПО (тАренда.ТорговаяТочка = СтруктурныеЕдиницы.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаявкиНаОткрытиеМагазина КАК ЗаявкиНаОткрытиеМагазина
		|		ПО (ЗаявкиНаОткрытиеМагазина.Магазин = СтруктурныеЕдиницы.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ тРоль КАК тРоль
		|		ПО СтруктурныеЕдиницы.Ссылка = тРоль.Объект
		|ГДЕ
		|	НЕ СтруктурныеЕдиницы.ПометкаУдаления
		|	И НЕ СтруктурныеЕдиницы.НомерТочки = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	СтруктурныеЕдиницы.Адрес,
		|	СтруктурныеЕдиницы.ОбщаяПлощадь,
		|	СтруктурныеЕдиницы.ТорговаяПлощадь,
		|	СтруктурныеЕдиницы.КоординатыШирота,
		|	СтруктурныеЕдиницы.КоординатыДолгота,
		|	СтруктурныеЕдиницы.НаименованиеАрендодателя,
		|	СтруктурныеЕдиницы.ТелефонАрендодателя,
		|	СтруктурныеЕдиницы.АдресАрендодателя,
		|	ЕСТЬNULL(тАренда.СуммаАрендаПостоянная, 0),
		|	ЕСТЬNULL(тАренда.СуммаАрендаПеременная, 0),
		|	ЕСТЬNULL(ЗаявкиНаОткрытиеМагазина.ЭлектроэнергииПоДоговору, 0),
		|	ВЫБОР
		|		КОГДА СтруктурныеЕдиницы.Город = ЗНАЧЕНИЕ(Справочник.Города.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ СтруктурныеЕдиницы.Город.Наименование
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СтруктурныеЕдиницы.СтатусТорговойТочки = ЗНАЧЕНИЕ(Перечисление.СтатусыТорговыхТочек.пустаяссылка)
		|			ТОГДА -1
		|		ИНАЧЕ СтруктурныеЕдиницы.СтатусТорговойТочки.Порядок
		|	КОНЕЦ";
	
	Если Данные.Свойство("Первые") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "100000", Формат(Данные.Первые, "ЧГ=0"));
	КонецЕсли;
	
	Если Данные.Свойство("НомерТочки") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ СтруктурныеЕдиницы.НомерТочки = 0", "СтруктурныеЕдиницы.НомерТочки = &НомерТочки");
		Запрос.УстановитьПараметр("НомерТочки", Данные.НомерТочки);
	КонецЕсли;
	
	Период = ТекущаяДата();
	
	Если Данные.Свойство("Период") Тогда
		Период = Данные.Период;	
	КонецЕсли;
	
	ТипРолиID = Новый Массив;
	ТипРолиID.Добавить("SpecialistPoOtkrytiyu");
	ТипРолиID.Добавить("PomoshnikPoRaskrutke");
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ТипРолиID", ТипРолиID);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Таблица = РезультатЗапроса.Выгрузить();	
	
	МассивКолонки = Новый Массив;

	Для каждого Колонка Из Таблица.Колонки Цикл
		МассивКолонки.Добавить(Колонка.Имя);	
	КонецЦикла;
	
	СтрокаКлючи = ТелеграмТехБот.МассивВСтроку(МассивКолонки, ",");
	
	МассивМагазины = Новый Массив;
	
	Для каждого Строка Из Таблица Цикл	
		Магазин = Новый Структура(СтрокаКлючи);
		ЗаполнитьЗначенияСвойств(Магазин, Строка);	
		
		МассивМагазины.Добавить(Магазин);
	КонецЦикла;
	
	Результат = Новый Структура("Магазины", МассивМагазины);
	
	Возврат Результат;
КонецФункции 

