
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//
	ТЗ_ИсходнаяТаблица = Параметры.ТЗ_ИсходнаяТаблица;
	ТЗ_ТаблицаДокумента = Параметры.ТаблицаРезультататаВсеДокументы;
	
	//
	НомераДокументов = Новый Соответствие;
	Для каждого СтрокаТЗ Из ТЗ_ИсходнаяТаблица Цикл
		НомераДокументов.Вставить(СтрокаТЗ.НомерДокумента);
	КонецЦикла;
	
	//
	НомераДокументовДляВыбора = Новый Массив;
	
	//
	Для каждого ЭлементСоответствия Из НомераДокументов Цикл
		
		//
		СП = Новый Структура("НомерДокумента, МаркерОбласти", ЭлементСоответствия.Ключ, "СТ");
		
		//
		НайденныеСтроки = ТЗ_ИсходнаяТаблица.НайтиСтроки(СП);
		Если НайденныеСтроки.Количество() > 0  Тогда
			НомераДокументовДляВыбора.Добавить(ЭлементСоответствия.Ключ);
		КонецЕсли; 
	
	КонецЦикла; 
	
	//
	Для каждого НомерДокумента Из НомераДокументовДляВыбора Цикл
		
		//
		СП = Новый Структура("НомерДокумента, МаркерОбласти", НомерДокумента, "ШД");
		
		//
		Представление = "";
		
		//
		НайденныеСтроки = ТЗ_ИсходнаяТаблица.НайтиСтроки(СП); 
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			Представление = Представление + НайденнаяСтрока.F1 + Символы.ПС;
		КонецЦикла; 
		
		//
		НоваяСтрока = ТЗ_Документы.Добавить();
		НоваяСтрока.НомерДокумента = НомерДокумента;
		НоваяСтрока.Представление = Представление;
		
		//
		КоличествоСтрок = 0;
		Количество = 0;
		СуммаБезНДС = 0;
		СуммаНДС = 0;
		СуммаСНДС = 0;
		
		//
		СП = Новый Структура("НомерДокумента", НомерДокумента);
		
		//
		НайденныеСтрокиТаблицы = ТЗ_ТаблицаДокумента.НайтиСтроки(СП);
		Для каждого НайденнаяСтрокаТаблицы Из НайденныеСтрокиТаблицы Цикл
			
			//
			КоличествоСтрок = КоличествоСтрок + 1;
			Количество = Количество + НайденнаяСтрокаТаблицы.Количество;
			СуммаБезНДС = СуммаБезНДС + НайденнаяСтрокаТаблицы.СтоимостьБезНДС;
			СуммаНДС = СуммаНДС + НайденнаяСтрокаТаблицы.СуммаНДС;
			СуммаСНДС = СуммаСНДС + НайденнаяСтрокаТаблицы.СтоимостьСНДС;
		
		КонецЦикла; 
		
		//
		НоваяСтрока.КоличествоСтрок = КоличествоСтрок;
		НоваяСтрока.Количество = Количество;
		НоваяСтрока.СуммаБезНДС = СуммаБезНДС;
		НоваяСтрока.СуммаНДС = СуммаНДС;
		НоваяСтрока.СуммаСНДС = СуммаСНДС;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	//
	ТекущаяСтрока = ТЗ_Документы.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	//
	МассивВыбранныхСтрок = Новый Массив;
	МассивВыбранныхСтрок.Добавить(ТекущаяСтрока.НомерДокумента);
	Закрыть(МассивВыбранныхСтрок);
	
КонецПроцедуры

//+++АК SHEP 20160819: добавил выбор нескольких строк
&НаСервере
Процедура ОбновитьИтогиТЗ()
	
	ТЗн = РеквизитФормыВЗначение("ТЗ_Документы");
	СтрокаИтоги = "КоличествоСтрок,Количество,СуммаБезНДС,СуммаНДС,СуммаСНДС";
	ТЗн.Свернуть("Пометка", СтрокаИтоги);
	СтрокаТЧ = ТЗн.Найти(Истина, "Пометка");
	
	СтруктураИтоги = Новый Структура(СтрокаИтоги);
	Для Каждого КлючИЗначение Из СтруктураИтоги Цикл
		ИмяКолонки = КлючИЗначение.Ключ;
		Элементы["ТЗ_Документы" + ИмяКолонки].ТекстПодвала = ?(СтрокаТЧ = Неопределено, "", Формат(СтрокаТЧ[ИмяКолонки], ?(Найти(ИмяКолонки, "Сумма") = 1, "ЧДЦ=2", "ЧДЦ=0")));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого СтрокаТЧ Из ТЗ_Документы Цикл
		СтрокаТЧ.Пометка = Истина;
	КонецЦикла;
	ОбновитьИтогиТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого СтрокаТЧ Из ТЗ_Документы Цикл
		СтрокаТЧ.Пометка = Ложь;
	КонецЦикла;
	ОбновитьИтогиТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПомеченныеСтроки(Команда)
	
	МассивВыбранныхСтрок = Новый Массив;
	Для Каждого СтрокаТЧ Из ТЗ_Документы Цикл
		Если СтрокаТЧ.Пометка Тогда
			МассивВыбранныхСтрок.Добавить(СтрокаТЧ.НомерДокумента);
		КонецЕсли;
	КонецЦикла;
	Если МассивВыбранныхСтрок.Количество() = 0 Тогда
		Сообщить("Не выбрано ни одной строки!");
		Возврат;
	КонецЕсли;
	Закрыть(МассивВыбранныхСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ДокументыПометкаПриИзменении(Элемент)
	ОбновитьИтогиТЗ();
КонецПроцедуры
//---АК SHEP 20160819