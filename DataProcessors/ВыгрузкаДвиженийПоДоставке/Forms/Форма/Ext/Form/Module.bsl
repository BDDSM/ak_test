
&НаКлиенте
Процедура РедактироватьПериод(Команда)
	
	СтандартныйПериод = Новый СтандартныйПериод(Объект.ДатаНачала, Объект.ДатаОкончания);
	ДиалогРедактирования = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогРедактирования.Период = СтандартныйПериод;
	Если ДиалогРедактирования.Редактировать() Тогда
		СтандартныйПериод = ДиалогРедактирования.Период;
		Объект.ДатаНачала = СтандартныйПериод.ДатаНачала;
		Объект.ДатаОкончания = СтандартныйПериод.ДатаОкончания;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Объект.ДатаНачала = НачалоМесяца(ТекущаяДата());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Объект.ДатаОкончания = КонецМесяца(ТекущаяДата());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеСервер()
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Объект.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Объект.ДатаОкончания));
	Запрос.УстановитьПараметр("СчетДт", ПланыСчетов.Финансовый.НайтиПоКоду("71.1"));
	Запрос.УстановитьПараметр("СчетДтПост", ПланыСчетов.Финансовый.НайтиПоКоду("50"));
	Запрос.УстановитьПараметр("СчетКт", ПланыСчетов.Финансовый.НайтиПоКоду("50"));
	Запрос.УстановитьПараметр("СчетКтПост", ПланыСчетов.Финансовый.НайтиПоКоду("71.1"));
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ЛистУчета).ТорговаяТочка КАК ТорговаяТочка,
	               |	НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ЛистУчета).Дата, ДЕНЬ) КАК Дата,
	               |	ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ЛистУчета).Организация КАК Организация,
	               |	СУММА(Финансовый.Сумма) КАК Сумма
	               |ИЗ
	               |	РегистрБухгалтерии.Финансовый КАК Финансовый
	               |ГДЕ
	               |	Финансовый.Период МЕЖДУ &ДатаНач И &ДатаКон
	               |	И Финансовый.Активность = ИСТИНА
	               |	И Финансовый.СчетДт = &СчетДт
	               |	И Финансовый.СчетКт = &СчетКт
	               |	И Финансовый.Регистратор ССЫЛКА Документ.ЛистУчета
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ЛистУчета).ТорговаяТочка,
	               |	НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ЛистУчета).Дата, ДЕНЬ),
	               |	ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ЛистУчета).Организация
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата,
	               |	ТорговаяТочка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ПоступлениеВКассу).Дата, ДЕНЬ) КАК Дата,
	               |	ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ПоступлениеВКассу).Организация КАК Организация,
	               |	СУММА(Финансовый.Сумма) КАК Сумма
	               |ИЗ
	               |	РегистрБухгалтерии.Финансовый КАК Финансовый
	               |ГДЕ
	               |	Финансовый.Период МЕЖДУ &ДатаНач И &ДатаКон
	               |	И Финансовый.Активность = ИСТИНА
	               |	И Финансовый.СчетДт = &СчетДтПост
	               |	И Финансовый.СчетКт = &СчетКтПост
	               |	И Финансовый.Регистратор ССЫЛКА Документ.ПоступлениеВКассу
	               |	И ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ПоступлениеВКассу).СубконтоКт2 В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				СотрудникиУправленияДоставки.СотрудникУправленияДоставки
	               |			ИЗ
	               |				РегистрСведений.СотрудникиУправленияДоставки КАК СотрудникиУправленияДоставки)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ПоступлениеВКассу).Дата, ДЕНЬ),
	               |	ВЫРАЗИТЬ(Финансовый.Регистратор КАК Документ.ПоступлениеВКассу).Организация
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата";
				   
	Результаты = Запрос.ВыполнитьПакет();
	Объект.ВыданоНаДоставку.Загрузить(Результаты[0].Выгрузить());
	Объект.СданоПоДоставке.Загрузить(Результаты[1].Выгрузить());
	
КонецПроцедуры	

&НаКлиенте
Процедура ПрочитатьДанные(Команда)
	
	ПрочитатьДанныеСервер();
	
КонецПроцедуры

Процедура ВыполнитьВыгрузкуВБухгалтерию()
	
	Обработки.ВыгрузкаДвиженийПоДоставке.ВыгрузитьВБухгалтерию(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВыданоНаДоставку.Выгрузить(), Объект.СданоПоДоставке.Выгрузить(), Объект.ПередаватьНаФизЛицо);
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыгрузитьВБухгалтерию(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ПередаватьНаФизЛицо) Тогда
		Сообщить("Не указано физ. лицо на кого будут передаваться ДС в подотчет");
		Возврат;
	КонецЕсли;	
	
	Ответ = Вопрос("Будет выполнена выгрузка данных в бухгалтерию. Продолжить?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
    ВыполнитьВыгрузкуВБухгалтерию();
	
КонецПроцедуры

