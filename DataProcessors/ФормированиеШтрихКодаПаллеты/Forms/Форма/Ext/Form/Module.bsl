
&НаСервере
Процедура ПечатьНаСервере()
	
	ТабДокШК.Очистить();
	
	Попытка
		УстановитьПривилегированныйРежим(Истина);
		ВнешняяКомпонента = Справочники.Номенклатура.ПодключитьВнешнююКомпонентуПечатиШтрихкода();
		МассивПаллеты = Новый Массив();
		МассивРасходники = Новый Массив();
		Для Каждого СтрокаТаб Из Объект.Расходники Цикл
			//Для н = 1 По СтрокаТаб.КоличествоПаллет Цикл
			Для н = 1 По 1 Цикл
				Паллета=Справочники.СоставПаллеты.НайтиПоРеквизиту("РасходныйОрдер",СтрокаТаб.РасходныйОрдер);
				Если ЗначениеЗаполнено(Паллета) Тогда
					СпрОбъект=Паллета.ПолучитьОбъект();
				Иначе	
					СпрОбъект = Справочники.СоставПаллеты.СоздатьЭлемент();
					СпрОбъект.РасходныйОрдер = СтрокаТаб.РасходныйОрдер;
					СпрОбъект.Записать();
				КонецЕсли; 
				МассивПаллеты.Добавить(СпрОбъект.Ссылка);
			КонецЦикла;
			МассивРасходники.Добавить(СтрокаТаб.РасходныйОрдер);
		КонецЦикла;
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	СоставПаллеты.Ссылка,
		               |	ПРЕДСТАВЛЕНИЕ(СоставПаллеты.РасходныйОрдер) КАК РасходныйОрдер,
		               |	ПРЕДСТАВЛЕНИЕ(СоставПаллеты.РасходныйОрдер.Получатель) КАК Получатель,
		               |	СоставПаллеты.ИД,
		               |	ПРЕДСТАВЛЕНИЕ(ВЗ_Маршруты.Маршрут) КАК Маршрут,
		               |	ВЗ_Маршруты.Документ.Склад КАК Склад,
		               |	ВЗ_Маршруты.Маршрут.ПолноеНаименование КАК ПолноеНаименование,
		               |	ВЗ_Маршруты.Документ.Склад.ДляШтучногоТовара КАК ДляШтучногоТовара
		               |ИЗ
		               |	Справочник.СоставПаллеты КАК СоставПаллеты
		               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			МаршрутныйЛистРасходныеОрдера.Документ КАК Документ,
		               |			МАКСИМУМ(МаршрутныйЛистРасходныеОрдера.Ссылка.Маршрут) КАК Маршрут
		               |		ИЗ
		               |			Документ.МаршрутныйЛист.РасходныеОрдера КАК МаршрутныйЛистРасходныеОрдера
		               |		ГДЕ
		               |			МаршрутныйЛистРасходныеОрдера.Ссылка.ПометкаУдаления = ЛОЖЬ
		               |			И МаршрутныйЛистРасходныеОрдера.Документ В(&Документы)
		               |		
		               |		СГРУППИРОВАТЬ ПО
		               |			МаршрутныйЛистРасходныеОрдера.Документ) КАК ВЗ_Маршруты
		               |		ПО СоставПаллеты.РасходныйОрдер = ВЗ_Маршруты.Документ
		               |ГДЕ
		               |	СоставПаллеты.Ссылка В(&Паллеты)";
					   
		Запрос.УстановитьПараметр("Паллеты", МассивПаллеты);
		Запрос.УстановитьПараметр("Документы", МассивРасходники);
		Выборка = Запрос.Выполнить().Выбрать();
		ТабДок = Новый ТабличныйДокумент();
		Макет = Обработки.ФормированиеШтрихКодаПаллеты.ПолучитьМакет("Этикетка");
		Пока Выборка.Следующий() Цикл
			Если ТабДок.ВысотаТаблицы > 0 Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			Область = Макет.ПолучитьОбласть("Этикетка");
			Область.Параметры.ТорговаяТочка = Выборка.Получатель;
			Область.Параметры.Документ = Строка(Выборка.РасходныйОрдер);
			Область.Параметры.Маршрут =   "Маршрут: " + Выборка.Маршрут;
			Область.Параметры.МаршрутДоп =  Выборка.ПолноеНаименование;
			Область.Параметры.Коробка = ?(Выборка.ДляШтучногоТовара,"Коробка склада штучного товара "+Строка(Выборка.Склад),""); 
			 
			ШтрихКод = "850" + Прав("000000000000" + Формат(Выборка.ИД, "ЧГ=0"), 12);
			Рисунок 					= Область.Рисунки["ШтрихКод"];
		
			ПараметрыШК = Новый Структура();
			ПараметрыШК.Вставить("Ширина", Рисунок.Ширина);
			ПараметрыШК.Вставить("Высота", Рисунок.Высота);
			ПараметрыШК.Вставить("ТипКода", 4);
			ПараметрыШК.Вставить("ОтображатьТекст", Ложь);
			ПараметрыШК.Вставить("РазмерШрифта", 10);
			ПараметрыШК.Вставить("Штрихкод", Штрихкод);
			Рисунок.Картинка = ОбщегоНазначенияКлиентСервер.ПолучитьКартинкуШтрихкода(ВнешняяКомпонента, ПараметрыШК);
			Область.Параметры.ШК = ШтрихКод;
			ТабДок.Вывести(Область);
		КонецЦикла;	
		
		ТабДок.ПолеСверху = 0;
		ТабДок.ПолеСнизу = 0;
		ТабДок.ПолеСправа = 0;
		ТабДок.ПолеСлева = 0;
		ТабДок.ВысотаСтраницы = 80;
		ТабДок.ШиринаСтраницы = 100;
		ТабДок.РазмерСтраницы = "Custom";
		
		ТабДокШК.Вывести(ТабДок);
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;	
	//Возврат ТабДок;
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ТабДокШК.Напечатать(РежимИспользованияДиалогаПечати.Использовать);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.МассивРасходники) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из Параметры.МассивРасходники Цикл
			СтрокаДоб = Объект.Расходники.Добавить();
			СтрокаДоб.РасходныйОрдер = ЭлементМассива;
			СтрокаДоб.КоличествоПаллет = 1;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	//ТабДокШК.Очистить();
	//ТабДокШК.Вывести(ПечатьНаСервере());
	ПечатьНаСервере();
	ТабДокШК.ПолеСверху = 0;
	ТабДокШК.ПолеСнизу = 0;
	ТабДокШК.ПолеСправа = 0;
	ТабДокШК.ПолеСлева = 0;
	Элементы.Группа1.ТекущаяСтраница = Элементы.ГруппаЭтикетки;
	
КонецПроцедуры
