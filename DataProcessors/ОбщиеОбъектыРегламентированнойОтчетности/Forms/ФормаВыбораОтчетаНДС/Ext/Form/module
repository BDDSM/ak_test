
// Процедура - обработчик события ПередОткрытием формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ОтборДинамическогоСписка = ЖурналОтчетов.Отбор;
	
	ИсточникОтчета      = ОтборДинамическогоСписка.ИсточникОтчета;
	Периодичность       = ОтборДинамическогоСписка.Периодичность;
	Организация         = ОтборДинамическогоСписка.Организация;
	ДатаНачала          = ОтборДинамическогоСписка.ДатаНачала;
	ДатаОкончания       = ОтборДинамическогоСписка.ДатаОкончания;
	
	ИсточникОтчета.Использование = Истина;
	ИсточникОтчета.ВидСравнения = ВидСравнения.Равно;
	ИсточникОтчета.Значение = "РегламентированныйОтчетНДС";
	
	Периодичность.Использование = Истина;
	Периодичность.ВидСравнения = ВидСравнения.Равно;
	Периодичность.Значение = ВладелецФормы.мПериодичность;
	
	Организация.Использование = Истина;
	Организации = Новый СписокЗначений;
	Организации.Добавить(ВладелецФормы.Организация);
	Организация.ВидСравнения = ВидСравнения.ВСписке;
	Организация.Значение = Организации;
	
	ДатаНачала.Использование = Истина;
	ДатаНачала.ВидСравнения = ВидСравнения.БольшеИлиРавно;
	ДатаНачала.Значение = ВладелецФормы.мДатаНачалаПериодаОтчета;
	
	ДатаОкончания.Использование = Истина;
	ДатаОкончания.ВидСравнения = ВидСравнения.Равно;
	ДатаОкончания.Значение = ВладелецФормы.мДатаКонцаПериодаОтчета;
	
КонецПроцедуры

// Процедура - действие кнопки ОК командной панели ОсновныеДействияФормы.
//
Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если ЭлементыФормы.ЖурналОтчетов.ТекущиеДанные = Неопределено Тогда
		Предупреждение("Выберите отчет для открытия!");
		Возврат;
	КонецЕсли;
	
	Закрыть(ЭлементыФормы.ЖурналОтчетов.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

// Процедура - обработчик события Выбор табличного поля ЖурналОтчетов.
//
Процедура ЖурналОтчетовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Закрыть(ВыбраннаяСтрока);
	
КонецПроцедуры

// Процедура - обработчик события ПриПолученииДанных табличного поля ЖурналОтчетов.
//
Процедура ЖурналОтчетовПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.Период.УстановитьТекст(ПредставлениеПериода(ОформлениеСтроки.ДанныеСтроки.ДатаНачала, КонецДня(ОформлениеСтроки.ДанныеСтроки.ДатаОкончания), "ФП=Истина"));
		ОформлениеСтроки.Ячейки.Вид.УстановитьТекст(РегламентированнаяОтчетность.ПредставлениеВидаДокумента(ОформлениеСтроки.ДанныеСтроки.Вид));
	КонецЦикла;
	
КонецПроцедуры

ЖурналОтчетов.Колонки.Добавить("ДатаНачала");
ЖурналОтчетов.Колонки.Добавить("ДатаОкончания");
