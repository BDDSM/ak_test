Перем ТекущийФорматВыгрузки;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ЭлементыФормы.Организация.Доступность = ОрганизацияДоступна;
	
	ПереключательФормата2ПриИзменении("");
	
	ОрганизацияПриИзменении("");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Если РегламентированнаяОтчетность.ФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация) = Неопределено Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Если РегламентированнаяОтчетность.ФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация) = Неопределено Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Сообщить("Не указана организация, настройки формата выгрузки статистических отчетов не сохранены!", СтатусСообщения.Внимание);
			Возврат;
		КонецЕсли;
		
		ТекущийФорматВыгрузки = ?(Переключатель, 2, 1);
		
		Ответ = Вопрос("Настройки были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			// В настройках сохраняем выбранный пользователем формат.
			Если ТекущийФорматВыгрузки = 2 Тогда
				Сообщить("Для организации """ + СокрЛП(Организация.Наименование) + """ сохранены настройки выгрузки стат. отчетности в формате ПО ""Заполнение форм статистической отчетности"", версии 2.4.4!", СтатусСообщения.Информация);
			ИначеЕсли ТекущийФорматВыгрузки = 1 Тогда
				Сообщить("Для организации """ + СокрЛП(Организация.Наименование) + """ сохранены настройки выгрузки стат. отчетности в формате ПО ""Формы статотчетности (предприятие)"", версии 1.21!", СтатусСообщения.Информация);
			КонецЕсли;
			
			СохранитьНастройки();
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			
			Если РегламентированнаяОтчетность.ФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация) = Неопределено Тогда
				
				// В настройках принудительно сохраняем наиболее распространенный - "старый" формат.
				Сообщить("Для организации """ + СокрЛП(Организация.Наименование) + """ сохранены настройки выгрузки стат. отчетности в формате ПО ""Формы статотчетности (предприятие)"", версии 1.21!", СтатусСообщения.Информация);
				
				ТекущийФорматВыгрузки = 1;
				
				СохранитьНастройки();
				
			КонецЕсли;
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
		// Если форма вызвана из отчета, напоминаем пользователю, где он сможет изменить формат.
		Если ЭлементыФормы.Организация.Доступность = Ложь Тогда
			Сообщить("Для изменения формата выгрузки перейдите в форму настроек журнала ""Регламентированная и финансовая отчетность"".");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ЭЛЕМЕНТОВ ФОРМЫ

Процедура СохранитьНастройки()
	
	СтруктураФорматовВыгрузки = ВосстановитьЗначение("ФорматыВыгрузкиСтатистическойОтчетностиВЭлектронномВиде");
	
	Если ТипЗнч(СтруктураФорматовВыгрузки) <> Тип("Структура") Тогда
		СтруктураФорматовВыгрузки = Новый Структура;
		СтруктураФорматовВыгрузки.Вставить("Формат1", Новый Массив);
		СтруктураФорматовВыгрузки.Вставить("Формат2", Новый Массив);
	КонецЕсли;
	
	МассивОрганизацийСФорматомВыгрузки1 = СтруктураФорматовВыгрузки["Формат1"];
	МассивОрганизацийСФорматомВыгрузки2 = СтруктураФорматовВыгрузки["Формат2"];
	
	Если ТекущийФорматВыгрузки = 1 Тогда
		
		Если МассивОрганизацийСФорматомВыгрузки1.Найти(Организация) = Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки1.Добавить(Организация);
		КонецЕсли;
		
		Если МассивОрганизацийСФорматомВыгрузки2.Найти(Организация) <> Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки2.Удалить(МассивОрганизацийСФорматомВыгрузки2.Найти(Организация));
		КонецЕсли;
		
	ИначеЕсли ТекущийФорматВыгрузки = 2 Тогда
		
		Если МассивОрганизацийСФорматомВыгрузки2.Найти(Организация) = Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки2.Добавить(Организация);
		КонецЕсли;
		
		Если МассивОрганизацийСФорматомВыгрузки1.Найти(Организация) <> Неопределено Тогда
			МассивОрганизацийСФорматомВыгрузки1.Удалить(МассивОрганизацийСФорматомВыгрузки1.Найти(Организация));
		КонецЕсли;
		
	КонецЕсли;
	
	СохранитьЗначение("ФорматыВыгрузкиСтатистическойОтчетностиВЭлектронномВиде", СтруктураФорматовВыгрузки);
	
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекущийФорматВыгрузки = РегламентированнаяОтчетность.ФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация);
		Если ТекущийФорматВыгрузки = 2 Тогда
			Переключатель = Истина;
		Иначе
			Переключатель = Ложь;
		КонецЕсли;
	Иначе
		Переключатель = Ложь;
	КонецЕсли;
	
	Если РегламентированнаяОтчетность.ФорматВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация) = Неопределено Тогда
		Модифицированность = Истина;
	Иначе
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереключательФормата2ПриИзменении(Элемент)
	
	ТекущийФорматВыгрузки = ?(Переключатель, 2, 1);
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Организация = Справочники.Организации.ПустаяСсылка();
	Переключатель = Ложь;
	ТекущийФорматВыгрузки = 1;
	Модифицированность = Ложь;
	
КонецПроцедуры