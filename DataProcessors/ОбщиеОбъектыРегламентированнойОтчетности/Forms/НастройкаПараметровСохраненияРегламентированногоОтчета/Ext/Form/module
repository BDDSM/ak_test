////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если СокрЛП(ПутьДляВыгрузки)="" Тогда
		ПутьДляВыгрузки = ВосстановитьЗначение("ПутьДляВыгрузкиРегламентированныхОтчетов");
		Если СокрЛП(ПутьДляВыгрузки) = "" Тогда
			ПутьДляВыгрузки = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнойКаталогФайлов");
		КонецЕсли;
	КонецЕсли;
	
	Если (ВРег(ПутьДляВыгрузки) = "A:\") ИЛИ (СокрЛП(ПутьДляВыгрузки) = "") Тогда
		ЭлементыФормы.СохрНаДискету.Значение = 1;
		ЭлементыФормы.Дискета.Значение = ЭлементыФормы.Дискета.СписокВыбора.Получить(0).Значение;
	ИначеЕсли ВРЕГ(ПутьДляВыгрузки) = "B:\" Тогда
		ЭлементыФормы.СохрНаДискету.Значение = 1;
		ЭлементыФормы.Дискета.Значение = ЭлементыФормы.Дискета.СписокВыбора.Получить(1).Значение;
	Иначе
		ЭлементыФормы.СохрНаДискету.Значение = 2;
	КонецЕсли;
	
	УстановитьДоступностьПолей();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

Процедура СохрНаДискетуПриИзменении(Элемент)
	
	УстановитьДоступностьПолей();
	
КонецПроцедуры

Процедура ПутьДляВыгрузкиНачалоВыбора()
	
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораКаталога.Заголовок = "Укажите каталог";
	
	Если ДиалогВыбораКаталога.Выбрать() Тогда
		РазделительПути = РегламентированнаяОтчетность.ПолучитьРазделительПути82();
		
		ПутьДляВыгрузки = ДиалогВыбораКаталога.Каталог
		                + ?(Прав(ДиалогВыбораКаталога.Каталог, 1) <> РазделительПути, РазделительПути, "");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

Процедура Сохранить(Элемент)
	
	Если ЭлементыФормы.СохрНаДискету.Значение = 1 Тогда
		ПутьДляВыгрузки = ЭлементыФормы.Дискета.Значение;
	КонецЕсли;
	
	Если Лев(ПутьДляВыгрузки, 2) <> "\\" Тогда
		
		РазделительПути = РегламентированнаяОтчетность.ПолучитьРазделительПути82();
		Если Прав(ПутьДляВыгрузки, 1) <> РазделительПути Тогда
			ПутьДляВыгрузки = ПутьДляВыгрузки + РазделительПути;
		КонецЕсли;
		
		Кат = Новый Файл(ПутьДляВыгрузки + "NUL");
		
		Если НЕ Кат.Существует() Тогда
			Текст = "Нет доступа к каталогу " + ПутьДляВыгрузки + ".";
			
			Если ЭлементыФормы.СохрНаДискету.Значение = 1 Тогда
				Текст = Текст + Символы.ПС + "Вставьте дискету в дисковод!";
			Иначе
				Текст = Текст + Символы.ПС + "Проверьте корректность имени каталога выгрузки!";
			КонецЕсли;
			
			Предупреждение(Текст);
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Кат = Новый Файл(ПутьДляВыгрузки);
	
	Если Кат.Существует() И Кат.ЭтоКаталог() Тогда
		СохранитьЗначение("ПутьДляВыгрузкиРегламентированныхОтчетов", ?(ЭлементыФормы.СохрНаДискету.Значение = 1, ЭлементыФормы.Дискета.Значение, ПутьДляВыгрузки));
		ЭтаФорма.Закрыть(Истина);
	Иначе
		Предупреждение("Имя каталога задано неверно! Проверьте правильность указания имени каталога!");
	КонецЕсли;
	
КонецПроцедуры

Процедура Отменить(Элемент)
	
	ЭтаФорма.Закрыть(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьДоступностьПолей()
	
	Если ЭлементыФормы.СохрНаДискету.Значение = 2 Тогда
		ЭлементыФормы.Дискета.Доступность = Ложь;
		ЭлементыФормы.ПутьДляВыгрузки.Доступность = Истина;
	Иначе
		ЭлементыФормы.Дискета.Доступность = Истина;
		ЭлементыФормы.ПутьДляВыгрузки.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры
