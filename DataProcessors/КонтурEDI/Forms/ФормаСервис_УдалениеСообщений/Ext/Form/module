
Процедура ПриОткрытии()
	
	ПриОткрытииФормы(ЭтаФорма);
	
	УстановитьВидимостьДоступностьЭлементов(); 	
	УстановитьТекстКоличествоСообщенийДляУдаления();
	
КонецПроцедуры

Процедура КнопкаПосчитатьНажатие(Кнопка)
	
	Состояние("Расчет количества сообщений для удаления");
	КоличествоСообщенийДляУдаления = ПосчитатьКоличествоСообщенийДляУдаления(НачалоПериода, КонецПериода);
	КоличествоСообщенийПосчитано = Истина;
	УстановитьТекстКоличествоСообщенийДляУдаления();
	Состояние("");
	
КонецПроцедуры

Процедура УстановитьТекстКоличествоСообщенийДляУдаления()
	
	Если КоличествоСообщенийДляУдаления = 0 
		И НЕ КоличествоСообщенийПосчитано Тогда
		ЭлементыФормы.КоличествоСообщенийДляУдаления.Формат = "ЧН='не посчитано'";
	ИначеЕсли КоличествоСообщенийДляУдаления = 0
		И КоличествоСообщенийПосчитано Тогда
		ЭлементыФормы.КоличествоСообщенийДляУдаления.Формат = "ЧН='0'";
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаУдалитьНажатие(Кнопка)
	
	Если КоличествоСообщенийДляУдаления = 0
		И НЕ КоличествоСообщенийПосчитано Тогда
		КнопкаПосчитатьНажатие(Кнопка);
	КонецЕсли;

	Если КоличествоСообщенийДляУдаления = 0 Тогда
		ВывестиПредупреждение_КонтурEDI("За выбранный период не найдено сообщений для удаления.");
	Иначе
		ТекстВопроса = "За выбранный период найдено сообщений для удаления: " + КоличествоСообщенийДляУдаления
						+ Символы.ПС + "Вы уверены, что хотите продолжить удаление?";
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить("Да, удалить сообщения");
		КнопкиВопроса.Добавить("Нет");
		
		РезультатВопроса = Вопрос(ТекстВопроса, КнопкиВопроса,,,"EDI.Контур");
		ОбработчикСогласияУдаленияСообщений(РезультатВопроса);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработчикСогласияУдаленияСообщений(РезультатВопроса)
	
	Если РезультатВопроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВопроса = "Да, удалить сообщения" Тогда
		ТекПароль = "";
		ТекстПросьбы = "Введите пароль от учетной записи Контур.EDI";
		
		Если ВвестиСтроку(ТекПароль, ТекстПросьбы) Тогда
			ОбработчикВводаПароля(ТекПароль);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработчикВводаПароля(ТекПароль, ДопПараметрОбработчика = Неопределено) Экспорт
	
	Если ТекПароль = Неопределено Тогда
		// отказ от ввода
		Возврат;
	КонецЕсли;
	
	ВерныйПароль = Ложь;
	ТекущийПарольПользователя = ПараметрыПользователяEDI.Пароль;
	
	Если СокрЛП(ТекПароль) = СокрЛП(ТекущийПарольПользователя) 
		ИЛИ СокрЛП(ТекПароль) = "******" Тогда
		ВерныйПароль = Истина;
	КонецЕсли;
	
	Если НЕ ВерныйПароль Тогда 
		ВывестиПредупреждение_КонтурEDI("Указан неверный пароль");
	Иначе
		ОбработчикПродолженияУдаленияСообщений(НачалоПериода, КонецПериода);
	КонецЕсли;
	
КонецПроцедуры // ОбработчикВводаПароля()

Процедура ОбработчикПродолженияУдаленияСообщений(НачалоПериода, КонецПериода)

	КоличествоСообщенийУдалено = УдалитьСообщения_КонтурEDI(НачалоПериода, КонецПериода);
	Состояние("");
	КоличествоСообщенийДляУдаления = КоличествоСообщенийДляУдаления - КоличествоСообщенийУдалено;
	УстановитьТекстКоличествоСообщенийДляУдаления();

	ВывестиПредупреждение_КонтурEDI("Завершено удаление сообщений: "  + КоличествоСообщенийУдалено);
	
КонецПроцедуры

Процедура НачалоПериодаПриИзменении(Элемент)
	
	Если НачалоПериода > КонецПериода
		И ЗначениеЗаполнено(КонецПериода) Тогда 
		НачалоПериода = КонецПериода;
	КонецЕсли;
	
	СброситьКоличествоСообщенийДляУдаления();
	УстановитьТекстКоличествоСообщенийДляУдаления();
	
КонецПроцедуры

Процедура КонецПериодаПриИзменении(Элемент)
	
	Если НачалоПериода > КонецПериода Тогда 
		КонецПериода = НачалоПериода;
	КонецЕсли;
	
	СброситьКоличествоСообщенийДляУдаления();
	УстановитьТекстКоличествоСообщенийДляУдаления();
	
КонецПроцедуры

Процедура УстановитьВидимостьДоступностьЭлементов()

	Если НЕ ПараметрыПользователяEDI.РольПользователяEDI = "ПолныеПрава" Тогда
		
		ЭлементыФормы.НадписьПредупреждениеПраваПользователяEDI.Видимость = Истина;
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаУдалить.Доступность = Ложь;
		
	КонецЕсли;

КонецПроцедуры 

Процедура СброситьКоличествоСообщенийДляУдаления()
	
	КоличествоСообщенийДляУдаления = 0;
	КоличествоСообщенийПосчитано = Ложь;
	
КонецПроцедуры
