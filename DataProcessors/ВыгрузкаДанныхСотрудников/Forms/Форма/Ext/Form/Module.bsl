

#Область ОбработчикиСобытийФормы

//+++АК LATV 2018.07.24 0-000018902
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Объект.ДатаУвольнения = Дата(2017, 11, 01);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ПоказатьВыборФайла();

КонецПроцедуры

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура ИмяФайлаОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяФайла", Объект.ИмяФайла);
	ОткрытьКаталог(ДополнительныеПараметры);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура Выгрузить(Команда)

	ТекстСообщения = НСтр("ru = 'Выгрузка данных сотрудников'");
	ПоказатьОповещениеПользователя(ТекстСообщения);

	ПолучаемыеФайлы = ВыгрузитьНаСервере();
	Если ПолучаемыеФайлы <> Неопределено Тогда
	
		Оповещение = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтаФорма);
		НачатьПолучениеФайлов(Оповещение, ПолучаемыеФайлы,, Ложь);
		Возврат;
		
	КонецЕсли;
	
	ВыгрузитьЗавершение(Неопределено);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.07.24 0-000018902
&НаСервере
Функция ВыгрузитьНаСервере()

	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("csv");
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ИмяФайла = ИмяВременногоФайла;
	ОбработкаОбъект.Выгрузить();
	
	Файл = Новый Файл(ИмяВременногоФайла);
	Если Не Файл.Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФайлыВыгрузки = Новый Массив;
	ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
	ПередаваемыйФайл.Имя		= Объект.ИмяФайла;
	ПередаваемыйФайл.Хранение	= ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяВременногоФайла), УникальныйИдентификатор);
	
	ФайлыВыгрузки.Добавить(ПередаваемыйФайл);
	УдалитьФайлы(ИмяВременногоФайла);
	
	Возврат ФайлыВыгрузки;

КонецФункции

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура ВыгрузитьЗавершение(ПолученныеФайлы, ДополнительныеПараметры = Неопределено) Экспорт

	Если ПолученныеФайлы = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Выгрузка завершилась с ошибкой'");
		ПоказатьОповещениеПользователя(ТекстСообщения);
	Иначе
		ТекстСообщения = НСтр("ru = 'Выгрузка завершена'");
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяФайла", ПолученныеФайлы[0].Имя);
		Оповещение = Новый ОписаниеОповещения("ОткрытьКаталог", ЭтаФорма, ДополнительныеПараметры);
		
		ПоказатьОповещениеПользователя(ТекстСообщения, Оповещение, НСтр("ru = 'Открыть каталог с файлом'"));
	КонецЕсли;

КонецПроцедуры

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура ОткрытьКаталог(ДополнительныеПараметры = Неопределено) Экспорт

	Файл = Новый Файл(ДополнительныеПараметры.ИмяФайла);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьКаталогЗавершение", ЭтаФорма);
	
	НачатьЗапускПриложения(Оповещение, Файл.Путь);

КонецПроцедуры

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура ОткрытьКаталогЗавершение(КодВозврата, ДополнительныеПараметры = Неопределено) Экспорт
	// Действия не требуется
КонецПроцедуры

#КонецОбласти

#Область ВыборФайла

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура ПоказатьВыборФайла()

	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.ПолноеИмяФайла	= Объект.ИмяФайла;
	ДиалогВыбораФайла.Фильтр			= "Формат выгрузки (*.csv)|*.csv";
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВыборФайлаЗавершение", ЭтаФорма);
	ДиалогВыбораФайла.Показать(ОписаниеОповещения)

КонецПроцедуры

//+++АК LATV 2018.07.24 0-000018902
&НаКлиенте
Процедура ПоказатьВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ИмяФайла = ВыбранныеФайлы[0];

КонецПроцедуры

#КонецОбласти