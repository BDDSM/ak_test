
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ДокКорректировки) Тогда
		КоррДок = ЭтотОбъект.ДокКорректировки.ПолучитьОбъект();
		КоррДок.Движения.РасчетыПоСделкамСПоставщиками.Очистить();
		КоррДок.Записать();
	Иначе
		КоррДок = Документы.КорректировкаЗаписейРегистровНакопления.СоздатьДокумент();
	КонецЕсли;	
	
	КоррДок.Дата = ЭтотОбъект.НаДату;
	КоррДок.Комментарий = "Приведение остатков по сделке "+ЭтотОбъект.Сделка;
	КоррДок.ТаблицаРегистровНакопления.Очистить();
	
	НС = КоррДок.ТаблицаРегистровНакопления.Добавить();
	НС.Имя = "РасчетыПоСделкамСПоставщиками";
	НС.Представление = "Расчеты по сделкам с поставщиками";
	
	ТекстЗапроса="ВЫБРАТЬ
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Сделка,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.УИН_Этапа,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Комплектация,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.УИН_СтрокиКомплектации,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.ДатаПлатежа КАК ДатаПлатежа,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.СуммаОстаток КАК Сумма
	             |ИЗ
	             |	РегистрНакопления.РасчетыПоСделкамСПоставщиками.Остатки(
	             |			&НаДату,
	             |			Сделка = &Сделка
	             |				И ДатаПлатежа <> ДАТАВРЕМЯ(1, 1, 1)) КАК РасчетыПоСделкамСПоставщикамиОстатки
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ДатаПлатежа УБЫВ
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	РасчетыПоСделкамСПоставщиками.Сделка,
	             |	РасчетыПоСделкамСПоставщиками.УИН_Этапа,
	             |	РасчетыПоСделкамСПоставщиками.Комплектация,
	             |	РасчетыПоСделкамСПоставщиками.УИН_СтрокиКомплектации,
	             |	РасчетыПоСделкамСПоставщиками.ДатаПлатежа КАК ДатаПлатежа,
	             |	РасчетыПоСделкамСПоставщиками.Сумма
	             |ИЗ
	             |	РегистрНакопления.РасчетыПоСделкамСПоставщиками КАК РасчетыПоСделкамСПоставщиками
	             |ГДЕ
	             |	РасчетыПоСделкамСПоставщиками.Сделка = &Сделка
	             |	И РасчетыПоСделкамСПоставщиками.ДатаПлатежа <> ДАТАВРЕМЯ(1, 1, 1)
	             |	И РасчетыПоСделкамСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	             |	И РасчетыПоСделкамСПоставщиками.Период <= &НаДату
				 //|	И (РасчетыПоСделкамСПоставщиками.Регистратор ССЫЛКА Документ.ПредпоступлениеПоКомплектации
				 //|			ИЛИ РасчетыПоСделкамСПоставщиками.Регистратор ССЫЛКА Документ.СделкаСПоставщиком)
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ДатаПлатежа УБЫВ
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	РасчетыСКонтрагентамиОстатки.СуммаОстаток,
	             |	РасчетыСКонтрагентамиОстатки.АвансПоСделкеОстаток
	             |ИЗ
	             |	РегистрНакопления.РасчетыСКонтрагентами.Остатки(&НаДату, Сделка = &Сделка) КАК РасчетыСКонтрагентамиОстатки";
	Запрос = Новый Запрос(ТекстЗапроса);			 
	Запрос.УстановитьПараметр("НаДату",ЭтотОбъект.НаДату);
	Запрос.УстановитьПараметр("Сделка",ЭтотОбъект.Сделка);
	Результаты = Запрос.ВыполнитьПакет();
	ТабОстатковПоСделкеВСделках=Результаты[0].Выгрузить();
	//ТабПриходовПоСделкеВСделках=Результаты[1].Выгрузить();
	Выборка = Результаты[2].Выбрать();
	СуммаОстаткаВРасчетах = 0;
	Если Выборка.Следующий() Тогда
		СуммаОстаткаВРасчетах = -Выборка.СуммаОстаток;
	КонецЕсли;	
	Сообщить("Сумма остатка по сделке в расчетах с контрагентами "+СуммаОстаткаВРасчетах);
	Если ТабОстатковПоСделкеВСделках.Итог("Сумма") = СуммаОстаткаВРасчетах Тогда
		Сообщить("Приведение не требуется, остатки совпадают");
		Возврат;
	КонецЕсли;	
	Выборка = Результаты[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		НД = КоррДок.Движения.РасчетыПоСделкамСПоставщиками.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(НД,Выборка);
		НД.Период = КоррДок.Дата;
	КонецЦикла;	
	
	Выборка = Результаты[1].Выбрать();
	Осталось = СуммаОстаткаВРасчетах;
	Пока Выборка.Следующий() Цикл
		Если Осталось = 0 Тогда
			Прервать;
		КонецЕсли;
		
		НД = КоррДок.Движения.РасчетыПоСделкамСПоставщиками.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(НД,Выборка);
		НД.Период = КоррДок.Дата;
		Осталось = Осталось-НД.Сумма;
		Если Осталось<0 Тогда
			НД.Сумма = НД.Сумма+Осталось;
			Осталось=0;
		КонецЕсли;	
			
	КонецЦикла;	
	
	КоррДок.ОбменДанными.Загрузка = Истина;
	Если КоррДок.Движения.РасчетыПоСделкамСПоставщиками.Количество()>0 Тогда
		КоррДок.Записать();
		ЭтотОбъект.ДокКорректировки = КоррДок.Ссылка;
	Иначе
		Сообщить("Суммы в расчетах по сделкам и расчетах с контрагентами совпадают, приведение не требуется");
	КонецЕсли;	
КонецПроцедуры
ЭтотОбъект.НаДату = НачалоДня(ТекущаяДата());
