
Функция СформироватьПрайсыДляТочек()
	
	Макет = Обработки.ФормированиеПрайсЛистаТТ.ПолучитьМакет("Прайс");
	МассивТабДок = Новый Массив();
	ЗапросТТ = Новый Запрос();
	ЗапросТТ.УстановитьПараметр("ТТ", Объект.ТорговыеТочки.Выгрузить().ВыгрузитьКолонку("ТТ"));
	ЗапросТТ.УстановитьПараметр("ЕстьОтборПоТТ", Объект.ТорговыеТочки.Количество() > 0);
	ЗапросТТ.Текст = "ВЫБРАТЬ
	                 |	СтруктурныеЕдиницы.Ссылка
	                 |ИЗ
	                 |	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	                 |ГДЕ
	                 |	(СтруктурныеЕдиницы.Ссылка В (&ТТ)
	                 |			ИЛИ &ЕстьОтборПоТТ = ЛОЖЬ)
	                 |	И СтруктурныеЕдиницы.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Избенка)
	                 |	И (СтруктурныеЕдиницы.Активное = ИСТИНА
	                 |			ИЛИ &ЕстьОтборПоТТ = ИСТИНА)
	                 |
	                 |УПОРЯДОЧИТЬ ПО
	                 |	СтруктурныеЕдиницы.НомерТочки";
					 
	ВыборкаТТ = ЗапросТТ.Выполнить().Выбрать();
	Пока ВыборкаТТ.Следующий() Цикл
		ТабДокумент = Новый ТабличныйДокумент();
		
		ЗапросТовары = Новый Запрос();
		ЗапросТовары.УстановитьПараметр("ДатаПрайса", ТекущаяДата() + 3*86400);
		ЗапросТовары.УстановитьПараметр("ТТ", ВыборкаТТ.Ссылка);
		ЗапросТовары.УстановитьПараметр("Группы", Объект.ГруппыТоваров.Выгрузить().ВыгрузитьКолонку("Группа"));
		ЗапросТовары.УстановитьПараметр("ЕстьОтборПоГруппам", Объект.ГруппыТоваров.Количество() > 0);
		ЗапросТовары.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		                     |	ДвиженияТоваровПоЛистамУчетаОбороты.Номенклатура
		                     |ПОМЕСТИТЬ ВТ_Движения
		                     |ИЗ
		                     |	РегистрНакопления.ДвиженияТоваровПоЛистамУчета.Обороты(
		                     |			ДОБАВИТЬКДАТЕ(&ДатаПрайса, МЕСЯЦ, -3),
		                     |			&ДатаПрайса,
		                     |			,
		                     |			СтруктурнаяЕдиница = &ТТ
		                     |				И ВидДвиженияТовара В (ЗНАЧЕНИЕ(Перечисление.ВидДвиженияТовараПоЛистуУчета.Продажа), ЗНАЧЕНИЕ(Перечисление.ВидДвиженияТовараПоЛистуУчета.Поступление))) КАК ДвиженияТоваровПоЛистамУчетаОбороты
		                     |ГДЕ
		                     |	(ДвиженияТоваровПоЛистамУчетаОбороты.Номенклатура В ИЕРАРХИИ (&Группы)
		                     |			ИЛИ &ЕстьОтборПоГруппам = ЛОЖЬ)
		                     |;
		                     |
		                     |////////////////////////////////////////////////////////////////////////////////
		                     |ВЫБРАТЬ РАЗЛИЧНЫЕ
		                     |	ТоварныйАссортиментТочекСрезПоследних.Номенклатура
		                     |ПОМЕСТИТЬ ВТ_Ассортимент
		                     |ИЗ
		                     |	РегистрСведений.ТоварныйАссортиментТочек.СрезПоследних(
		                     |			&ДатаПрайса,
		                     |			ТорговаяТочка = &ТТ
		                     |				И (Номенклатура В ИЕРАРХИИ (&Группы)
		                     |					ИЛИ &ЕстьОтборПоГруппам = ЛОЖЬ)) КАК ТоварныйАссортиментТочекСрезПоследних
		                     |ГДЕ
		                     |	ТоварныйАссортиментТочекСрезПоследних.Выведена = ЛОЖЬ
		                     |;
		                     |
		                     |////////////////////////////////////////////////////////////////////////////////
		                     |ВЫБРАТЬ
		                     |	ТоварныйАссортиментТочекСрезПоследних.Номенклатура,
		                     |	ТоварныйАссортиментТочекСрезПоследних.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдИзм,
		                     |	ТоварныйАссортиментТочекСрезПоследних.Номенклатура.ЕдиницаХраненияОстатков.Вес КАК Вес,
		                     |	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена,
		                     |	ВЫБОР
		                     |		КОГДА ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Родитель.Родитель.Родитель.Родитель = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		                     |			ТОГДА ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Родитель.Родитель.Родитель
		                     |		КОГДА ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Родитель.Родитель.Родитель = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		                     |			ТОГДА ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Родитель.Родитель
		                     |		КОГДА ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Родитель.Родитель = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		                     |			ТОГДА ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Родитель
		                     |		ИНАЧЕ ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Родитель
		                     |	КОНЕЦ КАК Родитель
		                     |ИЗ
		                     |	(ВЫБРАТЬ
		                     |		ТоварныйАссортиментТочекСрезПоследних.Номенклатура КАК Номенклатура
		                     |	ИЗ
		                     |		ВТ_Ассортимент КАК ТоварныйАссортиментТочекСрезПоследних
		                     |	
		                     |	ОБЪЕДИНИТЬ ВСЕ
		                     |	
		                     |	ВЫБРАТЬ
		                     |		ВТ_Движения.Номенклатура
		                     |	ИЗ
		                     |		ВТ_Движения КАК ВТ_Движения) КАК ТоварныйАссортиментТочекСрезПоследних
		                     |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		                     |				&ДатаПрайса,
		                     |				ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦен.ОсновнойТипЦенПродаж)
		                     |					И ТорговаяТочка = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК ЦеныНоменклатурыСрезПоследних
		                     |		ПО ТоварныйАссортиментТочекСрезПоследних.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
		                     |ГДЕ
		                     |	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) <> 0
		                     |
		                     |УПОРЯДОЧИТЬ ПО
		                     |	Родитель,
		                     |	ТоварныйАссортиментТочекСрезПоследних.Номенклатура.Наименование";
							 
		ВыборкаТовары = ЗапросТовары.Выполнить().Выбрать();
		Пока ВыборкаТовары.СледующийПоЗначениюПоля("Родитель") Цикл
			ТекСтрока  = 1;
			Если ТабДокумент.ВысотаТаблицы > 0 Тогда
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			Область = Макет.ПолучитьОбласть("Шапка");
			Область.Параметры.Дата = ТекущаяДата();
			Область.Параметры.ИмяТТ = ВыборкаТТ.Ссылка;
			ТабДокумент.Вывести(Область);
			
			Область = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабДокумент.Вывести(Область);
			Область = Макет.ПолучитьОбласть("Группа");
			Область.Параметры.Группа = ВыборкаТовары.Родитель;
			ТабДокумент.Вывести(Область);
			Пока ВыборкаТовары.СледующийПоЗначениюПоля("Номенклатура") Цикл
				Область = Макет.ПолучитьОбласть("Строка");
				Область.Параметры.НомерПП = ТекСтрока;
				Область.Параметры.Наименование = ВыборкаТовары.Номенклатура;
				Область.Параметры.Вес = Формат(ВыборкаТовары.Вес, "ЧДЦ=3; ЧГ=0");
				Область.Параметры.Упаковка = НРег(ВыборкаТовары.ЕдИзм);
				Область.Параметры.Цена = ВыборкаТовары.Цена;
				ТабДокумент.Вывести(Область);
				ТекСтрока = ТекСтрока + 1;
			КонецЦикла;
		КонецЦикла;	
		
		МассивТабДок.Добавить(Новый Структура("ТТ, ТабДокумент", ВыборкаТТ.Ссылка, ТабДокумент));
	КонецЦикла;
	
	Возврат МассивТабДок;
	
КонецФункции

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Объект.ТорговыеТочки.Количество() = 0 Тогда
		Предупреждение("Формировать прайсы по всем точкам можно только в файлы");
		Возврат;
	КонецЕсли;	
	
	ТабДокументы = СформироватьПрайсыДляТочек();
	Для Каждого ПрайсЭлемент Из ТабДокументы Цикл
		ПрайсЭлемент.ТабДокумент.ОтображатьЗаголовки = Ложь;
		ПрайсЭлемент.ТабДокумент.ОтображатьСетку = Ложь;
		ПрайсЭлемент.ТабДокумент.Показать(ПрайсЭлемент.ТТ);
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьВФайлы(Команда)
	
	ДиалогВыб = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Если ДиалогВыб.Выбрать() Тогда
		ТабДокументы = СформироватьПрайсыДляТочек();
		Для Каждого ПрайсЭлемент Из ТабДокументы Цикл
			ИмяТТДляФайла = СтрЗаменить(ПрайсЭлемент.ТТ, "\", "");
			ИмяТТДляФайла = СтрЗаменить(ИмяТТДляФайла, "/", "");
			ПрайсЭлемент.ТабДокумент.Записать(ДиалогВыб.Каталог + "\" + ИмяТТДляФайла + ".pdf", ТипФайлаТабличногоДокумента.PDF);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры
