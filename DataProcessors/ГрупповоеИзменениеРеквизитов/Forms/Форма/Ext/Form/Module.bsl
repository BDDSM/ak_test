
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрокаРеквизитов = Параметры.СписокРеквизитов;
	Имена = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СтрокаРеквизитов, ",");
	
	ВыбранныеОбъектыВКонтексте.ЗагрузитьЗначения(Параметры.МассивОбъектов);
	
	ТипОбъекта			= Параметры.ТипОбъекта;
	МетаданныеОбъекта 	= Метаданные.НайтиПоТипу(ТипОбъекта);
	ИмяОбъекта 			= МетаданныеОбъекта.Имя;
	пРеквизитыОбъекта 	= МетаданныеОбъекта.Реквизиты;
	
	РеквизитыОбъекта.Очистить();
	
	Для каждого ИмяРеквизита Из Имена Цикл
		Рекв = пРеквизитыОбъекта.Найти(ИмяРеквизита);
		Если Рекв <> Неопределено Тогда
			
			ТипРеквизита = Рекв.Тип;
			
			ОписаниеРеквизита = РеквизитыОбъекта.Добавить();
			ОписаниеРеквизита.Имя = ИмяРеквизита;
			ОписаниеРеквизита.Представление = Рекв.Синоним;
			ОписаниеРеквизита.ДопустимыеТипы = ТипРеквизита;
			
		Иначе
			Попытка
				
				Рекв = МетаданныеОбъекта.СтандартныеРеквизиты[ИмяРеквизита];
				ТипРеквизита = Рекв.Тип;
				
				ОписаниеРеквизита = РеквизитыОбъекта.Добавить();
				ОписаниеРеквизита.Имя = ИмяРеквизита;
				ОписаниеРеквизита.Представление = ИмяРеквизита;
				ОписаниеРеквизита.ДопустимыеТипы = ТипРеквизита;
				
			Исключение
				Сообщить("Не найден реквизит: " + ИмяОбъекта + "." + ИмяРеквизита);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПередНачаломИзменения(Элемент, Отказ)
	
	УстановитьОграниченияВыбираемыхТиповИПараметрыВыбораЗначения(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОграниченияВыбираемыхТиповИПараметрыВыбораЗначения(ТабличноеПоле)
	
	Если ТабличноеПоле.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолеВвода = ТабличноеПоле.ПодчиненныеЭлементы[ТабличноеПоле.Имя + "Значение"];
	ПолеВвода.ОграничениеТипа = ТабличноеПоле.ТекущиеДанные.ДопустимыеТипы;
	
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	РеквизитыКИзменению = РеквизитыОбъекта.НайтиСтроки(Новый Структура("Изменять", Истина));
	Если РеквизитыКИзменению.Количество() = 0 Тогда
		Предупреждение("Выберите реквизиты для редактирования.");
		Возврат;
	КонецЕсли; 
	
	Для Каждого Стр Из ВыбранныеОбъектыВКонтексте Цикл
		
		УспешноеИзменение = ИзменитьОбъектНаСервере(Стр.Значение);
		Если Не УспешноеИзменение Тогда
			Сообщить("Не удалось записать: " + Строка(Стр.Значение));
		КонецЕсли; 
		
	КонецЦикла;
	
	ОповеститьОбИзменении(ТипОбъекта);
	
КонецПроцедуры

&НаСервере
Функция ИзменитьОбъектНаСервере(СсылкаНаОбъект)
	
	РедактируемыйОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	
	МассивРедактируемыхРеквизитов = РеквизитыОбъекта.НайтиСтроки(Новый Структура("Изменять", Истина));
	
	Для Каждого СтрМассива Из МассивРедактируемыхРеквизитов Цикл
		РедактируемыйОбъект[СтрМассива.Имя] = СтрМассива.Значение;
	КонецЦикла;
	
	Попытка 
		РедактируемыйОбъект.Записать();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции








