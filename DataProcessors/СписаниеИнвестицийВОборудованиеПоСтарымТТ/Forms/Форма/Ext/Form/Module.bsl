
Процедура СоздатьОперациюБухСервер()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала"		, Объект.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания"	, КонецДня(Объект.ДатаОкончания));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТЧНоменклатура.Ссылка.СкладПолучатель КАК Справочник.СтруктурныеЕдиницы) КАК ТТ,
	|	ТЧНоменклатура.ОсновноеСредство
	|ПОМЕСТИТЬ ВТПеремещения
	|ИЗ
	|	Документ.ПеремещениеОС.Номенклатура КАК ТЧНоменклатура
	|ГДЕ
	|	ТЧНоменклатура.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ТЧНоменклатура.Ссылка.НеФормироватьПроводкиПоБалансовымСчетам
	|	И ТЧНоменклатура.Ссылка.СчетПолучатель = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.Инвестиции)
	|	И ВЫРАЗИТЬ(ТЧНоменклатура.Ссылка.СкладПолучатель КАК Справочник.СтруктурныеЕдиницы).ДатаОткрытия < &ДатаНачала
	|	И ТЧНоменклатура.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПеремещения.ТТ КАК ТТ,
	|	ВЫБОР
	|		КОГДА ТЧНоменклатура.Ссылка.Контрагент ССЫЛКА Справочник.ФизическиеЛица
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСотрудник,
	|	ТЧНоменклатура.Ссылка.Контрагент КАК Контрагент,
	|	ТЧНоменклатура.Ссылка.ЦФОСотрудника КАК ЦФОСотрудника,
	|	ТЧНоменклатура.ОсновноеСредство,
	|	ТЧНоменклатура.ОсновноеСредство.Наименование КАК НаименованиеОС,
	|	ТЧНоменклатура.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомерОС,
	|	ТЧНоменклатура.Сумма
	|ИЗ
	|	Документ.ПоступлениеОС.Номенклатура КАК ТЧНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПеремещения КАК ВТПеремещения
	|		ПО (ВТПеремещения.ОсновноеСредство = ТЧНоменклатура.ОсновноеСредство)
	|ГДЕ
	|	ТЧНоменклатура.Ссылка.Проведен
	|	И ТЧНоменклатура.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПеремещения";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	
	ДокументОперации = Документы.Операция.СоздатьДокумент();
	ДокументОперации.Дата 			= КонецДня(Объект.ДатаОкончания);
	ДокументОперации.Организация 	= Объект.Организация;
	
	ДокументОперации.Комментарий 	= "Распределение новго оборудования по старым ТТ";
	ДокументОперации.Содержание 	= "Распределение новго оборудования по старым ТТ";
	
	
	Счет443 = ПланыСчетов.Финансовый.ЗатратыТочекДляРаспределения;
	Счет01	= ПланыСчетов.Финансовый.Инвестиции;
	мСуммаДокумента = 0;
	
	ДвиженияФинансовый = ДокументОперации.Движения.Финансовый;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НовоеДвижение = ДвиженияФинансовый.Добавить();
		НовоеДвижение.Период 		= ДокументОперации.Дата;
		НовоеДвижение.Организация 	= Объект.Организация;
		
		НовоеДвижение.СчетДт = Счет443;
		
		НовоеДвижение.СубконтоДт.ТорговыеТочки 			= Выборка.ТТ;
		НовоеДвижение.СубконтоДт.СтатьиДоходовРасходов 	= ЭтаФорма.СтатьяДР;
		
		ТекЦФО = ?(Выборка.ЭтоСотрудник, Выборка.ЦФОСотрудника,	ОбщиеПроцедуры.ПолучитьЦФОПоСтруктурнойЕдинице(Выборка.ТТ, ДокументОперации.Дата));
		Если НЕ ЗначениеЗаполнено(ТекЦФО) Тогда
            Сообщить("Не удалось определить ЦФО для торговой точки " + Выборка.ТТ);
		КонецЕсли;				
						
		НовоеДвижение.СубконтоДт.ЦФО 			= ТекЦФО;
		
		НовоеДвижение.СчетКт = Счет01;
		НовоеДвижение.СубконтоКт.ТорговыеТочки 	= Выборка.ТТ;
		
		НовоеДвижение.Сумма = Выборка.Сумма;
		
		НовоеДвижение.Содержание = СокрЛП(Выборка.НаименованиеОС) + " " + СокрЛП(Выборка.ИнвентарныйНомерОС);
		
		мСуммаДокумента = мСуммаДокумента + Выборка.Сумма;
		
	КонецЦикла;
	
	ДокументОперации.СуммаОперации = мСуммаДокумента;
	
	Попытка
		ДокументОперации.Записать();
		Сообщить("Документ " + ДокументОперации + " записан.");
		
		ЭтаФорма.ОперацияФин = ДокументОперации.Ссылка;
		
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры


&НаКлиенте
Процедура Сформировать(Команда)
	
	СоздатьОперациюБухСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.СтатьяДР = Справочники.СтатьиДоходовРасходов.НайтиПоКоду("39114");
	
КонецПроцедуры
