
#Область ПрограммныйИнтерфейс

//+++АК LATV 2018.10.30 ИП-00020276
Процедура СодатьДокументыПоРаспределениюНаСервере(СтруктураНастроек, АдресРезультатаВоВременномХранилище) Экспорт

	УИДРаспределения = СтруктураНастроек.УИДРаспределения;
	Если ЗначениеЗаполнено(УИДРаспределения) Тогда
		РегистрыСведений.СостоянияРаспределенийТоваров.ИзменитьСостояние(УИДРаспределения, ПредопределенноеЗначение("Перечисление.СостоянияРаспределенияТоваров.ЗагрузкаРасходников"));
	КонецЕсли;
	
	КоличествоДокументов = СоздатьДокументыПоРаспределению(СтруктураНастроек, Ложь);
	
	Если ЗначениеЗаполнено(УИДРаспределения) Тогда
		РегистрыСведений.СостоянияРаспределенийТоваров.ИзменитьСостояние(УИДРаспределения, ПредопределенноеЗначение("Перечисление.СостоянияРаспределенияТоваров.РасходникиЗагружены"));
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(КоличествоДокументов, АдресРезультатаВоВременномХранилище);

КонецПРоцедуры

//+++АК LATV 2018.10.30 ИП-00020276
Процедура СодатьДокументыПоРаспределениюНаСервереПоЗонам(СтруктураНастроек, АдресРезультатаВоВременномХранилище) Экспорт

	КоличествоДокументов = СоздатьДокументыПоРаспределению(СтруктураНастроек, Истина);
	ПоместитьВоВременноеХранилище(КоличествоДокументов, АдресРезультатаВоВременномХранилище);

КонецПроцедуры

//+++АК LATV 2018.10.17 ИП-00020001
Функция МагазиныКПересчетуНазначены(Дата) Экспорт

	Результат = Ложь;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьМагазин
		|ИЗ
		|	РегистрСведений.МагазиныКПересчетуНаДату КАК МагазиныКПересчетуНаДату
		|ГДЕ
		|	МагазиныКПересчетуНаДату.Дата = &Дата
		|	И НЕ МагазиныКПересчетуНаДату.ДобавленСкладом");
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.10.15 ИП-00020001
// Процедура выбирает случайным образом магазины и назначает их к пересчету на дату распределения.
// Далее эти данные используются при записи расходников
//
Процедура НазначитьМагазиныКПересчетуДатуРаспределения(СтруктураНастроек)

	// Очистка устаревших записей
	ДатаРаспределения = СтруктураНастроек.ДатаРаспределения;
	
	Набор = РегистрыСведений.МагазиныКПересчетуНаДату.СоздатьНаборЗаписей();
	Набор.Отбор.Дата.Установить(ДатаРаспределения - 7*86400);
	Набор.Записать(); //подчищаем чтобы регистр не забивался этой второстепенной инфой
	
	// Проверка повторного выполнения
	Если МагазиныКПересчетуНазначены(ДатаРаспределения) Тогда
		Возврат;
	КонецЕсли;
	
	//Настроить в алгоритме попадания в контрольную группу магазинов более сложный вероятностный алгоритм
	//а) 5% вероятность попадания, если магазин был в контрольной группе или обычной поставке с пересчётом вчера;
	//б) 10%, если магазин был - позавчера;
	//б) 15%, если - 3 дня назад;
	//в) 20%, если - 4 дня назад;
	//г) 25%, если - 5 дней назад;
	//д) 30%, если - 6 дней назад;
	//е) 35%, если - 7 дней назад;
	//ё) 40%, если - 8 дней назад;
	//ж) 45%, если - 9 дней назад;
	//з) 50%, если - 10 дней назад;
	//и) 55%, если - 11 дней назад;
	//й) 60%, если - 12 дней назад;
	//к) 65%, если - 13 дней назад;
	//л) 70%, если - 14 дней назад;
	//м) 75%, если - 15 дней назад;
	//н) 80%, если - 16 дней назад;
	//о) 85%, если - 17 дней назад;
	//п) 90%, если - 18 дней назад;
	//р) 95%, если - 19 дней назад;
	//с) 100%, если - 20 дней назад.
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДатаНач", ДатаРаспределения - 86400 * 20);
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаРаспределения));
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасходныйОрдерСклад.Получатель,
		|	МАКСИМУМ(РасходныйОрдерСклад.ДатаРаспределения) КАК ДатаРаспределения
		|ПОМЕСТИТЬ ВТ_Доки
		|ИЗ
		|	Документ.РасходныйОрдерСклад КАК РасходныйОрдерСклад
		|ГДЕ
		|	РасходныйОрдерСклад.Дата МЕЖДУ &ДатаНач И &ДатаКон
		|	И РасходныйОрдерСклад.Проведен
		|	И РасходныйОрдерСклад.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСкладскойУчет.ОтгрузкаВТорговуюТочку)
		|	И РасходныйОрдерСклад.ПриемкаВМагазинеБезПересчета В (2, -2, 0)
		|	И (РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		|			ИЛИ РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		|	И (ВЫРАЗИТЬ(РасходныйОрдерСклад.Получатель КАК Справочник.СтруктурныеЕдиницы)) <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|	И ВЫРАЗИТЬ(РасходныйОрдерСклад.Получатель КАК Справочник.СтруктурныеЕдиницы).ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)
		|	И ВЫРАЗИТЬ(РасходныйОрдерСклад.Получатель КАК Справочник.СтруктурныеЕдиницы).ПриемкаТовараБезПересчета
		|	И (ВЫРАЗИТЬ(РасходныйОрдерСклад.Получатель КАК Справочник.СтруктурныеЕдиницы).ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ВЫРАЗИТЬ(РасходныйОрдерСклад.Получатель КАК Справочник.СтруктурныеЕдиницы).ДатаЗакрытия > &ДатаКон)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходныйОрдерСклад.Получатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтруктурныеЕдиницы.Ссылка,
		|	ЕСТЬNULL(ВТ_Доки.ДатаРаспределения, &ДатаНач) КАК ДатаПоследнегоПересчета
		|ИЗ
		|	ВТ_Доки КАК ВТ_Доки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		|		ПО ВТ_Доки.Получатель = СтруктурныеЕдиницы.Ссылка
		|ГДЕ
		|	СтруктурныеЕдиницы.ТипРозничнойТочки = ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин)
		|	И (СтруктурныеЕдиницы.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СтруктурныеЕдиницы.ДатаЗакрытия > &ДатаКон)
		|	И СтруктурныеЕдиницы.ПриемкаТовараБезПересчета";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ГСЧ = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СлучЧисло = ГСЧ.СлучайноеЧисло(1, 100);
		ПроцентПопадания = Цел((ДатаРаспределения - Выборка.ДатаПоследнегоПересчета) / 86400) * 5;
		Если ПроцентПопадания >= СлучЧисло Тогда
			ЗаписьРегистра = РегистрыСведений.МагазиныКПересчетуНаДату.СоздатьМенеджерЗаписи();
			ЗаписьРегистра.Дата		= ДатаРаспределения;
			ЗаписьРегистра.Магазин	= Выборка.Ссылка;
			ЗаписьРегистра.Записать();
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

//+++АК LATV 2018.10.30 ИП-00020276
Функция СоздатьДокументыПоРаспределению(СтруктураНастроек, ПоЗонам = Ложь)

	УстановитьПривилегированныйРежим(Истина);
	
	Склад = СтруктураНастроек.Склад;
	ДанныеСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "НеОчищатьЗаданияНаРазборкуПриНовомРаспределении");
	
	Если ДанныеСклада.НеОчищатьЗаданияНаРазборкуПриНовомРаспределении
	 Или Не ПоЗонам Тогда
		НазначитьМагазиныКПересчетуДатуРаспределения(СтруктураНастроек);
	КонецЕсли;
	
	Если ДанныеСклада.НеОчищатьЗаданияНаРазборкуПриНовомРаспределении Тогда
		КоличествоДокументов = СодатьДокументыПоРаспределениюНаСервере_ЗаданияНаРазборку(СтруктураНастроек);
	ИначеЕсли Не ПоЗонам Тогда
		КоличествоДокументов = СодатьДокументыПоРаспределениюНаСервере_РО(СтруктураНастроек);
	Иначе
		КоличествоДокументов = СодатьДокументыПоРаспределениюНаСервереПоЗонам_РО(СтруктураНастроек);
	КонецЕсли;
	
	Возврат КоличествоДокументов;

КонецФункции

#Область ГруппыУРЗ

//+++АК LATV 2018.10.30 ИП-00020276
Функция СодатьДокументыПоРаспределениюНаСервере_ЗаданияНаРазборку(СтруктураНастроек)

	ДатаРаспределения			= СтруктураНастроек.ДатаРаспределения;
	СтруктурноеПодразделение	= СтруктураНастроек.СтруктурноеПодразделение;
	Склад						= СтруктураНастроек.Склад;
	Организация					= СтруктураНастроек.Организация;
	НомерРаспределения			= СтруктураНастроек.НомерРаспределения;
	НомераРаспределений			= Новый Соответствие;
	
	ДлительныеОперации.СообщитьПрогресс(0,"Блокировка документов");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаРаспределения"		, ДатаРаспределения);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("Склад"					, Склад);
	Запрос.УстановитьПараметр("Организация"				, Организация);
	
	Запрос.Текст = ТекстЗапросаПолучитьЗаданияНаРазборкуДляПерезаписи();
	тзЗаданияСкладыНоменклатура = Запрос.Выполнить().Выгрузить();
	тзЗадания = тзЗаданияСкладыНоменклатура.Скопировать(,"Ссылка");
	
	ДлительныеОперации.СообщитьПрогресс(0, "Получение данных распределения");
	
	Запрос.Текст = ТекстЗапросаПолучитьСкладыТТ();
	
	Результаты = Запрос.ВыполнитьПакет();
	ТабТТ 		= Результаты[1].Выгрузить();
	ТабСклады 	= Результаты[2].Выгрузить();
	
	ТаблицаДанных = ЗаполнитьПоРаспределениюНаСервереГруппыУРЗ(СтруктураНастроек, ТабТТ, ТабСклады);
	ТаблицаДанных.Колонки.Добавить("Организация",			Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ДатаПроизводства",		Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ВВедомостиНаРазборку",	Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("ЗаданиеНаРазборку",		Новый ОписаниеТипов("ДокументСсылка.ЗаданиеНаРазборку"));
	
	//сразу откинем уже загруженное и напечатаное
	//ДлительныеОперации.СообщитьПрогресс(0, "Очистка документов от предыдущих загрузок");
	
	х100	= тзЗадания.Количество();
	х		= 0;
	
	//почистим доки нераспечатанные, у которых количество больше 0
	//Для Каждого ДокТЗ Из тзЗадания Цикл
	//	х = х+1;
	//	ДокОбъект = ДокТЗ.Ссылка.ПолучитьОбъект();
	//	КолвоСтрок = ДокОбъект.Товары.Количество();
	//	Для н = 1 По КолвоСтрок Цикл
	//		ТекСтрока = ДокОбъект.Товары[КолвоСтрок - н];
	//		Если ТекСтрока.Количество > 0 Тогда
	//			ДокОбъект.Товары.Удалить(КолвоСтрок - н);
	//		КонецЕсли;
	//	КонецЦикла;
	//	Если ДокОбъект.Модифицированность() Тогда 
	//		ДокОбъект.ОбменДанными.Загрузка = Истина;
	//		ДокОбъект.Проведен = Истина;
	//		Попытка
	//			ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	//		Исключение
	//			СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,ДокОбъект.Ссылка,-1,ОписаниеОшибки());
	//		КонецПопытки;
	//		ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Очистка документов от предыдущих загрузок");
	//	КонецЕсли;
	//КонецЦикла;
	
	ДлительныеОперации.СообщитьПрогресс(0, "Распределение по датам производства");
	
	//распределим здесь также по датам производства остатки
	Запрос.Текст = ПолучитьТекстЗапросаОстаткиТоваровНаСкладе();
	ТабОстаткиНоменклатуры = Запрос.Выполнить().Выгрузить();
	ТабОстаткиНоменклатуры.Индексы.Добавить("Склад, Номенклатура, Характеристика");
	
	ТабСортировкиСтрокОстатков = Новый ТаблицаЗначений();
	ТабСортировкиСтрокОстатков.Колонки.Добавить("СтрокаОстатка");
	ТабСортировкиСтрокОстатков.Колонки.Добавить("ДатаПроизводства");
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		СтрокаТаб = ТаблицаДанных[КолвоСтрок - н];
				
		СтрокиЗадания = тзЗаданияСкладыНоменклатура.НайтиСтроки(Новый Структура("Склад, Номенклатура, Характеристика", СтрокаТаб.Склад, СтрокаТаб.Номенклатура, СтрокаТаб.Характеристика));
		Если СтрокиЗадания.Количество() > 0 Тогда
			СтрокаТаб.ЗаданиеНаРазборку = СтрокиЗадания[0].Ссылка;
		КонецЕсли;
		
		СтрокиОстатков = ТабОстаткиНоменклатуры.НайтиСтроки(Новый Структура("Склад, Номенклатура, Характеристика", СтрокаТаб.Склад, СтрокаТаб.Номенклатура, СтрокаТаб.Характеристика));
		ТабСортировкиСтрокОстатков.Очистить();
		
		//предварительная сортировка, слетает потому после первого цикла не понятно почему
		Для Каждого СтрокаОстаток Из СтрокиОстатков Цикл
			СтрокаДоб = ТабСортировкиСтрокОстатков.Добавить();
			СтрокаДоб.СтрокаОстатка = СтрокаОстаток;
			СтрокаДоб.ДатаПроизводства = СтрокаОстаток.ДатаПроизводства;
		КонецЦикла;
		ТабСортировкиСтрокОстатков.Сортировать("ДатаПроизводства");
		КолвоКРаспределению = СтрокаТаб.Количество;
		
		СтрокаТаб.Количество = 0;
		
		Для Каждого СтрокаОстаток Из ТабСортировкиСтрокОстатков Цикл
			Если СтрокаОстаток.СтрокаОстатка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			Если КолвоКРаспределению <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			МинКолво = Мин(КолвоКРаспределению, СтрокаОстаток.СтрокаОстатка.Количество);
			СтрокаОстаток.СтрокаОстатка.Количество = СтрокаОстаток.СтрокаОстатка.Количество - МинКолво;
			КолвоКРаспределению = КолвоКРаспределению - МинКолво;
			СтрокаДоб = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДоб, СтрокаТаб);
			СтрокаДоб.Количество = МинКолво;
			СтрокаДоб.ДатаПроизводства = СтрокаОстаток.СтрокаОстатка.ДатаПроизводства;
		КонецЦикла;	
	КонецЦикла;
	
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		Если ТаблицаДанных[КолвоСтрок - н].Количество <= 0 Тогда
			ТаблицаДанных.Удалить(КолвоСтрок - н);
		КонецЕсли;	
	КонецЦикла;
	ДлительныеОперации.СообщитьПрогресс(100, "Распределение по датам производства");
	
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.СвойстваОбъектов.КоличествоВУпаковке);
	Запрос.УстановитьПараметр("Таб", ТаблицаДанных);
	Запрос.УстановитьПараметр("Дата1", НачалоДня(ДатаРаспределения));
	Запрос.УстановитьПараметр("Дата2", КонецДня(ДатаРаспределения));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таб.Организация,
		|	Таб.Склад,
		|	Таб.ТорговаяТочка,
		|	Таб.Номенклатура,
		|	Таб.Характеристика,
		|	Таб.ДатаПроизводства,
		|	Таб.НомерРаспределения,
		|	Таб.Количество,
		|	Таб.ЗаданиеНаРазборку
		|ПОМЕСТИТЬ ВТ_ТаблицаДанных
		|ИЗ
		|	&Таб КАК Таб
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЗ_Запрос.Организация,
		|	ВЗ_Запрос.Склад КАК Склад,
		|	СкладыСпр.Владелец КАК СкладВладелец,
		|	ВЗ_Запрос.ТорговаяТочка,
		|	ВЗ_Запрос.ЗаданиеНаРазборку,
		|	ВЗ_Запрос.Номенклатура КАК Номенклатура,
		|	НоменклатураСпр.ЕдиницаХраненияОстатков КАК ЕдИзм,
		|	ВЗ_Запрос.Характеристика КАК Характеристика,
		|	ВЗ_Запрос.ДатаПроизводства,
		|	ВЗ_Запрос.НомерРаспределения,
		|	ВЗ_Запрос.Количество,
		|	ВЫБОР
		|		КОГДА НЕ КоличествоВКоробкеСрезПоследних.Характеристика ЕСТЬ NULL
		|			ТОГДА ВЗ_Запрос.Количество / ВЫБОР
		|					КОГДА КоличествоВКоробкеСрезПоследних.Количество = 0
		|						ТОГДА 1
		|					ИНАЧЕ КоличествоВКоробкеСрезПоследних.Количество
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, 0) = 0
		|					ТОГДА 0
		|				ИНАЧЕ ВЗ_Запрос.Количество / (ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Значение КАК ЧИСЛО(15, 3)))
		|			КОНЕЦ
		|	КОНЕЦ КАК КоличествоКоробок,
		|	ЕСТЬNULL(КоличествоВКоробкеСрезПоследних.Количество, 0) КАК КоличествоВКоробке
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ТаблицаДанных.Организация КАК Организация,
		|		ВТ_ТаблицаДанных.Склад КАК Склад,
		|		ВТ_ТаблицаДанных.ТорговаяТочка КАК ТорговаяТочка,
		|		ВТ_ТаблицаДанных.ЗаданиеНаРазборку КАК ЗаданиеНаРазборку,
		|		ВТ_ТаблицаДанных.Номенклатура КАК Номенклатура,
		|		ВТ_ТаблицаДанных.Характеристика КАК Характеристика,
		|		ВТ_ТаблицаДанных.ДатаПроизводства КАК ДатаПроизводства,
		|		МАКСИМУМ(ВТ_ТаблицаДанных.НомерРаспределения) КАК НомерРаспределения,
		|		СУММА(ВТ_ТаблицаДанных.Количество) КАК Количество
		|	ИЗ
		|		ВТ_ТаблицаДанных КАК ВТ_ТаблицаДанных
		|	ГДЕ
		|		ВТ_ТаблицаДанных.Количество > 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТ_ТаблицаДанных.Организация,
		|		ВТ_ТаблицаДанных.Склад,
		|		ВТ_ТаблицаДанных.ТорговаяТочка,
		|		ВТ_ТаблицаДанных.ЗаданиеНаРазборку,
		|		ВТ_ТаблицаДанных.Номенклатура,
		|		ВТ_ТаблицаДанных.Характеристика,
		|		ВТ_ТаблицаДанных.ДатаПроизводства) КАК ВЗ_Запрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладыСпр
		|		ПО ВЗ_Запрос.Склад = СкладыСпр.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
		|		ПО ВЗ_Запрос.Номенклатура = НоменклатураСпр.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоличествоВКоробке.СрезПоследних(, ) КАК КоличествоВКоробкеСрезПоследних
		|		ПО ВЗ_Запрос.Номенклатура = КоличествоВКоробкеСрезПоследних.Номенклатура
		|			И ВЗ_Запрос.Характеристика = КоличествоВКоробкеСрезПоследних.Характеристика
		|			И (СкладыСпр.Владелец = КоличествоВКоробкеСрезПоследних.СтруктурнаяЕдиница)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|		ПО (ЗначенияСвойствОбъектов.Объект = ВЗ_Запрос.Характеристика)
		|			И (ЗначенияСвойствОбъектов.Свойство = &Свойство)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НЕ КоличествоВКоробкеСрезПоследних.Характеристика ЕСТЬ NULL
		|				ТОГДА ВЗ_Запрос.Количество / ВЫБОР
		|						КОГДА КоличествоВКоробкеСрезПоследних.Количество = 0
		|							ТОГДА 1
		|						ИНАЧЕ КоличествоВКоробкеСрезПоследних.Количество
		|					КОНЕЦ
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, 0) = 0
		|						ТОГДА 0
		|					ИНАЧЕ ВЗ_Запрос.Количество / (ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Значение КАК ЧИСЛО(15, 3)))
		|				КОНЕЦ
		|		КОНЕЦ > 0.3
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВЗ_Запрос.Номенклатура,
		|	ВЗ_Запрос.Характеристика,
		|	ВЗ_Запрос.Склад,
		|	ВЗ_Запрос.ЗаданиеНаРазборку
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ТаблицаДанных";
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	х100 = Выборка.Количество();
	х=0;
	
	ДлительныеОперации.СообщитьПрогресс(0, "Создание документов");
	
	КоличествоДокументов = 0;
	Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("Характеристика") Цикл
			Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл
				Пока Выборка.СледующийПоЗначениюПоля("ЗаданиеНаРазборку") Цикл
					Если НЕ ЗначениеЗаполнено(Выборка.ЗаданиеНаРазборку) Тогда
						ЗаданиеНаРазборкуОбъект = Документы.ЗаданиеНаРазборку.СоздатьДокумент();
						ЗаданиеНаРазборкуОбъект.Дата			= ДатаРаспределения;
						ЗаданиеНаРазборкуОбъект.УстановитьНовыйНомер();
						ЗаданиеНаРазборкуОбъект.Автор 			= ПараметрыСеанса.ТекущийПользователь;
						ЗаданиеНаРазборкуОбъект.Склад 			= Выборка.Склад;
						ЗаданиеНаРазборкуОбъект.Номенклатура	= Выборка.Номенклатура;
						ЗаданиеНаРазборкуОбъект.Характеристика  = Выборка.Характеристика;
						ЗаданиеНаРазборкуОбъект.Напечатан		= Ложь;
						ЗаданиеНаРазборкуОбъект.Комментарий		= "#Загрузить распределение";
					Иначе
						ЗаданиеНаРазборкуОбъект = Выборка.ЗаданиеНаРазборку.ПолучитьОбъект();
					КонецЕсли;
					Пока Выборка.Следующий() Цикл
						х = х+1;
						КоличествоКЗагрузке = Выборка.Количество;
						Если КоличествоКЗагрузке <= 0.001 Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокиТовар = ЗаданиеНаРазборкуОбъект.Товары.НайтиСтроки(Новый Структура("СтруктурнаяЕдиница, ДатаПроизводства", Выборка.ТорговаяТочка, Выборка.ДатаПроизводства));
						Если СтрокиТовар.Количество() > 0 Тогда
							СтрокаТовар = СтрокиТовар[0];
						Иначе
							СтрокаТовар = ЗаданиеНаРазборкуОбъект.Товары.Добавить();
						КонецЕсли;	
						СтрокаТовар.СтруктурнаяЕдиница 	= Выборка.ТорговаяТочка;
						СтрокаТовар.ДатаПроизводства 	= Выборка.ДатаПроизводства;
						СтрокаТовар.ДатаПроизводстваПред 	= Выборка.ДатаПроизводства;
						
						СтрокаТовар.НомерРаспределения	= Выборка.НомерРаспределения;
						СтрокаТовар.КоличествоУРЗ		=  КоличествоКЗагрузке+СтрокаТовар.Количество;
						СтрокаТовар.Количество	 		=  КоличествоКЗагрузке+СтрокаТовар.Количество;
						СтрокаТовар.КоличествоКоробок	 		= Окр(Выборка.КоличествоКоробок,1)+СтрокаТовар.КоличествоКоробок;
						
						НомераРаспределений.Вставить(Выборка.НомерРаспределения);
						
						ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Создание документов");
					КонецЦикла;
					
					Если НЕ ЗаданиеНаРазборкуОбъект.ЭтоНовый()
						ИЛИ (ЗаданиеНаРазборкуОбъект.Товары.Количество() > 0 И ЗаданиеНаРазборкуОбъект.Модифицированность()) Тогда
						тз = ЗаданиеНаРазборкуОбъект.Товары.Выгрузить();
						тз.Колонки.Добавить("НомерТочки");
						Для Каждого СтрТЗ из тз Цикл
							СтрТЗ.НомерТочки = СтрТЗ.СтруктурнаяЕдиница.НомерТочки;
						КонецЦикла;
						тз.Сортировать("НомерТочки");
						ЗаданиеНаРазборкуОбъект.Товары.Загрузить(тз);
						
						ЗаданиеНаРазборкуОбъект.ОбменДанными.Загрузка = Истина;
						ЗаданиеНаРазборкуОбъект.Проведен = Истина;
						Попытка
							ЗаданиеНаРазборкуОбъект.Записать(РежимЗаписиДокумента.Запись);
						Исключение
							СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,ЗаданиеНаРазборкуОбъект.Ссылка,-1,ОписаниеОшибки());
						КонецПопытки;
						
						КоличествоДокументов = КоличествоДокументов+1;
					КонецЕсли;
				КонецЦикла; //док
			КонецЦикла;	//склад
		КонецЦикла;//хар-ка
	КонецЦикла;//номенклатура
	
	УстановитьСостояниеПоНомерамРаспределений(НомераРаспределений, Перечисления.СостоянияРаспределенияТоваров.РасходникиЗагружены);
	ПеренестиВАрхивОстаткиРаспределенныеПоТТSQL(НомераРаспределений);
	
	СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,Неопределено,КоличествоДокументов,"Ок");
	Возврат КоличествоДокументов;

КонецФункции

//+++АК LATV 2018.10.30 ИП-00020276
Функция СодатьДокументыПоРаспределениюНаСервере_РО(СтруктураНастроек)

	Склад						= СтруктураНастроек.Склад;
	ДатаРаспределения			= СтруктураНастроек.ДатаРаспределения;
	Организация					= СтруктураНастроек.Организация;
	СтруктурноеПодразделение	= СтруктураНастроек.СтруктурноеПодразделение;
	НомераРаспределений			= Новый Соответствие;
	
	ДлительныеОперации.СообщитьПрогресс(0, "Получение данных распределения");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаРаспределения"		, ДатаРаспределения);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("Склад"					, Склад);
	Запрос.УстановитьПараметр("Организация"				, Организация);
	
	Запрос.Текст = ТекстЗапросаПолучитьСкладыТТ();
	
	Результаты = Запрос.ВыполнитьПакет();
	ТабТТ 		= Результаты[1].Выгрузить();
	ТабСклады 	= Результаты[2].Выгрузить();
	
	ТаблицаДанных = ЗаполнитьПоРаспределениюНаСервереГруппыУРЗ(СтруктураНастроек, ТабТТ, ТабСклады);
	ТаблицаДанных.Колонки.Добавить("Организация",			Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ДатаПроизводства",		Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ВВедомостиНаРазборку",	Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Маршрут",				Новый ОписаниеТипов("СправочникСсылка.Маршруты"));
	ТаблицаДанных.Колонки.Добавить("Автомобиль",			Новый ОписаниеТипов("СправочникСсылка.Автомобили"));
	ТаблицаДанных.Колонки.Добавить("РасходныйОрдер",		Новый ОписаниеТипов("ДокументСсылка.РасходныйОрдерСклад"));
	ТаблицаДанных.Колонки.Добавить("МаршрутныйЛист",		Новый ОписаниеТипов("ДокументСсылка.МаршрутныйЛист"));
	
	ДлительныеОперации.СообщитьПрогресс(0, "Чтение документов от предыдущих загрузок");
	
	//почистим перед загрузкой документы расходных ордеров от предыдущей загрузки и также маршрутные листы
	//+++АК KIRN 2018.04.10 ИП-00018330 
	#Область Запрос
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасходныйОрдерСклад.Ссылка,
		|	РасходныйОрдерСклад.Склад КАК Склад,
		|	РасходныйОрдерСклад.Склад.НеОчищатьЗаданияНаРазборкуПриНовомРаспределении КАК НеОчищатьЗаданияНаРазборкуПриНовомРаспределении,
		|	РасходныйОрдерСклад.Получатель КАК ТорговаяТочка,
		|	РасходныйОрдерСклад.Организация
		|ИЗ
		|	Документ.РасходныйОрдерСклад.Товары КАК РасходныйОрдерСкладТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерСклад КАК РасходныйОрдерСклад
		|		ПО РасходныйОрдерСкладТовары.Ссылка = РасходныйОрдерСклад.Ссылка
		|ГДЕ
		|	РасходныйОрдерСклад.ПометкаУдаления = ЛОЖЬ
		|	И РасходныйОрдерСкладТовары.КоличествоУРЗ > 0
		|	И НАЧАЛОПЕРИОДА(РасходныйОрдерСклад.ДатаРаспределения, ДЕНЬ) = &ДатаРаспределения
		|	И РасходныйОрдерСклад.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.НеОбработан)
		|	И РасходныйОрдерСклад.Склад.Владелец = &СтруктурноеПодразделение
		|	И РасходныйОрдерСклад.Организация = &Организация
		|	И (РасходныйОрдерСклад.Склад = &Склад
		|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|	И (РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		|			ИЛИ РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокМаршЛист.Ссылка КАК Ссылка,
		|	ДокМаршЛист.Маршрут,
		|	ДокМаршЛист.СтруктурноеПодразделение,
		|	МаршрутныйЛистРасходныеОрдера.Документ КАК Расходник,
		|	ВЫБОР
		|		КОГДА МаршрутныйЛистРасходныеОрдера.Документ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВСборке
		|ИЗ
		|	Документ.МаршрутныйЛист КАК ДокМаршЛист
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛист.РасходныеОрдера КАК МаршрутныйЛистРасходныеОрдера
		|		ПО ДокМаршЛист.Ссылка = МаршрутныйЛистРасходныеОрдера.Ссылка
		|ГДЕ
		|	ДокМаршЛист.ПометкаУдаления = ЛОЖЬ
		|	И ДокМаршЛист.Отгружено = ЛОЖЬ
		|	И НАЧАЛОПЕРИОДА(ДокМаршЛист.Дата, ДЕНЬ) = &ДатаРаспределения
		|	И ДокМаршЛист.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И ДокМаршЛист.Организация = &Организация
		|	И (МаршрутныйЛистРасходныеОрдера.Документ.Склад = &Склад
		|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|	И (ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		|			ИЛИ ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
	//+++shae 2018.12.06 закомментирован без номера		
		//|ВЫБРАТЬ
		//|	РасходныйОрдерСклад.Организация,
		//|	РасходныйОрдерСклад.Склад,
		//|	РасходныйОрдерСклад.Получатель КАК ТорговаяТочка,
		//|	РасходныйОрдерСкладТовары.Номенклатура,
		//|	РасходныйОрдерСкладТовары.Характеристика,
		//|	СУММА(РасходныйОрдерСкладТовары.КоличествоУРЗ) КАК КоличествоУРЗ
		//|ИЗ
		//|	Документ.РасходныйОрдерСклад.Товары КАК РасходныйОрдерСкладТовары
		//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерСклад КАК РасходныйОрдерСклад
		//|		ПО РасходныйОрдерСкладТовары.Ссылка = РасходныйОрдерСклад.Ссылка
		//|ГДЕ
		//|	РасходныйОрдерСклад.Проведен = ИСТИНА
		//|	И РасходныйОрдерСкладТовары.КоличествоУРЗ > 0
		//|	И НАЧАЛОПЕРИОДА(РасходныйОрдерСклад.ДатаРаспределения, ДЕНЬ) = &ДатаРаспределения
		//|	И РасходныйОрдерСклад.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
		//|	И (РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		//|			ИЛИ РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		//|
		//|СГРУППИРОВАТЬ ПО
		//|	РасходныйОрдерСклад.Организация,
		//|	РасходныйОрдерСклад.Склад,
		//|	РасходныйОрдерСклад.Получатель,
		//|	РасходныйОрдерСкладТовары.Номенклатура,
		//|	РасходныйОрдерСкладТовары.Характеристика
		//|;
		//|
		//|////////////////////////////////////////////////////////////////////////////////
	//+++shae 2018.12.06 закомментирован без номера		
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Склад.Организация КАК Организация,
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
		|	ТоварыНаСкладахОстатки.ДатаПроизводства КАК ДатаПроизводства,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			,
		|			Склад.Владелец = &СтруктурноеПодразделение
		|				И Склад.Организация = &Организация
		|				И ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)) КАК ТоварыНаСкладахОстатки
		|ГДЕ
		|	ТоварыНаСкладахОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склад,
		|	Номенклатура,
		|	Характеристика,
		|	ДатаПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокМаршЛист.Ссылка КАК Ссылка,
		|	ДокМаршЛист.Маршрут,
		|	ДокМаршЛист.Организация КАК Организация,
		|	ДокМаршЛист.СтруктурноеПодразделение,
		|	МаршрутныйЛистРасходныеОрдера.Документ КАК Расходник,
		|	ВЫБОР
		|		КОГДА МаршрутныйЛистРасходныеОрдера.Документ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВСборке
		|ИЗ
		|	Документ.МаршрутныйЛист КАК ДокМаршЛист
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛист.РасходныеОрдера КАК МаршрутныйЛистРасходныеОрдера
		|		ПО ДокМаршЛист.Ссылка = МаршрутныйЛистРасходныеОрдера.Ссылка
		|ГДЕ
		|	НЕ ДокМаршЛист.ПометкаУдаления
		|	И НЕ ДокМаршЛист.Отгружено
		|	И НАЧАЛОПЕРИОДА(ДокМаршЛист.Дата, ДЕНЬ) = &ДатаРаспределения
		|	И ДокМаршЛист.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И ДокМаршЛист.Организация = &Организация
		|	И (ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		|			ИЛИ ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";	
			   
	#КонецОбласти
	
	/// СФОРМИРОВАТЬ ПОЗИЦИИ С ЗАДАНИЯМИ НА РАЗБОРКУ
	ТЗПозицииСЗаданиямиНаРазборку=Новый ТаблицаЗначений;
	ТЗПозицииСЗаданиямиНаРазборку.Колонки.Добавить("Номенклатура");
	ТЗПозицииСЗаданиямиНаРазборку.Колонки.Добавить("Характеристика");
	ТЗПозицииСЗаданиямиНаРазборку.Колонки.Добавить("ТТ");
	ТЗПозицииСЗаданиямиНаРазборку.Колонки.Добавить("Количество");
	ТЗПозицииСЗаданиямиНаРазборку.Индексы.Добавить("Номенклатура, Характеристика, ТТ");
	
	Результаты = Запрос.ВыполнитьПакет();
	ТабРасходникиВБазе = Результаты[0].Выгрузить();
	ТабМаршЛисты = Результаты[1].Выгрузить();
	
	ДлительныеОперации.СообщитьПрогресс(0, "Очистка документов от предыдущих загрузок");
	
	х100 = ТабРасходникиВБазе.Количество()+ТабМаршЛисты.Количество();
	//почистим доки нераспечатанные
	х = 0;

	Для Каждого СтрокаРасходник Из ТабРасходникиВБазе Цикл
		х = х+1;
		ДокОбъект = СтрокаРасходник.Ссылка.ПолучитьОбъект();
		КолвоСтрок = ДокОбъект.Товары.Количество();		
		Для н = 1 По КолвоСтрок Цикл
			ТекСтрока = ДокОбъект.Товары[КолвоСтрок - н];
			Если ТекСтрока.КоличествоУРЗ > 0 Тогда
				Если ЗначениеЗаполнено(ТекСтрока.ЗаданиеНаРазборку) И СтрокаРасходник.НеОчищатьЗаданияНаРазборкуПриНовомРаспределении Тогда
					НовСтр=ТЗПозицииСЗаданиямиНаРазборку.Добавить();
					НовСтр.Номенклатура		= ТекСтрока.Номенклатура;
					НовСтр.Характеристика	= ТекСтрока.Характеристика;
					НовСтр.ТТ				= ДокОбъект.Получатель;
					НовСтр.Количество		= ТекСтрока.КоличествоУРЗ;
				Иначе	
					ДокОбъект.Товары.Удалить(КолвоСтрок - н);
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;	
		Если ДокОбъект.Модифицированность() Тогда 
			ДокОбъект.ОбменДанными.Загрузка = Истина;
			ДокОбъект.Проведен = Истина;
			Попытка
				ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад, ДокОбъект.Ссылка, -1,ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
		ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Очистка документов от предыдущих загрузок");
	КонецЦикла;
	
	// Данный блок убрать после того как будет реализован учет позиций с заданиями на разборку на СКЛ
	#Область КорректировкаТаблицыДанных
	ТаблицаДанных.Индексы.Добавить("Номенклатура, Характеристика, ТорговаяТочка");
	Если ТЗПозицииСЗаданиямиНаРазборку.Количество() > 0 Тогда 
		ОткорректироватьТаблицуДанных(ТаблицаДанных, ТЗПозицииСЗаданиямиНаРазборку);
	КонецЕсли;
	#КонецОбласти
	///////////////////////////////////////////////////////////////////	
	
	Выборка = Результаты[1].Выбрать();
	//почистим маршрутные листы перед загрузкой
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		х = х+1;
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Пока Выборка.Следующий() Цикл
			Если Не ЗначениеЗаполнено(Выборка.Расходник)
				ИЛИ (Выборка.ВСборке) Тогда
				Продолжить;
			КонецЕсли;	
			ДокОбъект.РасходныеОрдера.Удалить(ДокОбъект.РасходныеОрдера.Найти(Выборка.Расходник, "Документ"));
		КонецЦикла;	
		Если ДокОбъект.Модифицированность() Тогда
			ДокОбъект.ОбменДанными.Загрузка = Истина;
			ДокОбъект.Проведен = Истина;
			Попытка
				ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,ДокОбъект.Ссылка, -1,ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;	
		ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Очистка документов от предыдущих загрузок");
	КонецЦикла;
	
	//+++shae 2018.12.06 закомментирован без номера
	////сразу откинем уже загруженное и собранное из обработки
	//ТабЗагруженное = Результаты[2].Выгрузить();
	//Для Каждого СтрокаЗагруженное Из ТабЗагруженное Цикл
	//	КолвоКВычетанию = СтрокаЗагруженное.КоличествоУРЗ;
	//	Если КолвоКВычетанию <= 0 Тогда
	//		Продолжить;
	//	КонецЕсли;	
	//	СтрокиДанных = ТаблицаДанных.НайтиСтроки(Новый Структура("ТорговаяТочка, Номенклатура, Характеристика"
	//										, СтрокаЗагруженное.ТорговаяТочка, СтрокаЗагруженное.Номенклатура, СтрокаЗагруженное.Характеристика));
	//										
	//	Для Каждого СтрокаДанные Из СтрокиДанных Цикл
	//		Если КолвоКВычетанию <= 0 Тогда
	//			Прервать;
	//		КонецЕсли;
	//		КолвоМин = Мин(КолвоКВычетанию, СтрокаДанные.Количество);
	//		СтрокаДанные.Количество = СтрокаДанные.Количество - КолвоМин;
	//		КолвоКВычетанию = КолвоКВычетанию - КолвоМин;
	//	КонецЦикла;										
	//КонецЦикла;	
	//---shae 2018.12.06 закомментирован без номера

	ДлительныеОперации.СообщитьПрогресс(0, "Распределение по датам производства");
	
	ТабОстаткиНоменклатуры = Результаты[2].Выгрузить();
	ТабОстаткиНоменклатуры.Индексы.Добавить("Склад, Номенклатура, Характеристика");
	
	ТабСформированныеМаршЛисты = СоответствиеДляСформированныхМаршрутов(Результаты[3]); // Получить из результата запроса соответствие маршрутных листов
	
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		Если ТаблицаДанных[КолвоСтрок - н].Количество <= 0 Тогда
			ТаблицаДанных.Удалить(КолвоСтрок - н);
		КонецЕсли;	
	КонецЦикла;	
	
	//теперь определим расходники и маршруты, куда должна попасть номенклатура
	Запрос.УстановитьПараметр("МассивНоменклатуры"		, ТаблицаДанных.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("ДатаСреза", ДатаРаспределения);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦФОСтруктурныхЕдиницСрезПоследних.СтруктурнаяЕдиница,
		|	ЦФОСтруктурныхЕдиницСрезПоследних.Организация
		|ПОМЕСТИТЬ втТТ
		|ИЗ
		|	РегистрСведений.ЦФОСтруктурныхЕдиниц.СрезПоследних КАК ЦФОСтруктурныхЕдиницСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МаршрутыТорговыеТочки.Ссылка КАК Маршрут,
		|	МаршрутыТорговыеТочки.СтруктурнаяЕдиница КАК ТорговаяТочка,
		|	ВЗ_НоменклатураМаршрутов.Номенклатура,
		|	ВодителиПоМаршрутуСрезПоследних.Автомобиль
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВЗ_Номенклатура.Номенклатура КАК Номенклатура,
		|		ВЗ_Склады.Маршрут КАК Маршрут
		|	ИЗ
		|		(ВЫБРАТЬ
		|			Маршруты.Ссылка КАК Маршрут,
		|			ЕСТЬNULL(МаршрутыСклады.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
		|			Маршруты.Организация КАК Организация
		|		ИЗ
		|			Справочник.Маршруты КАК Маршруты
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Маршруты.Склады КАК МаршрутыСклады
		|				ПО Маршруты.Ссылка = МаршрутыСклады.Ссылка
		|		ГДЕ
		|			Маршруты.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|			И Маршруты.Организация = &Организация
		|			И Маршруты.ПометкаУдаления = ЛОЖЬ) КАК ВЗ_Склады
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|				ДоступностьТоваровНаСкладах.Номенклатура КАК Номенклатура,
		|				ДоступностьТоваровНаСкладах.Склад КАК Склад
		|			ИЗ
		|				РегистрСведений.ДоступностьТоваровНаСкладах КАК ДоступностьТоваровНаСкладах
		|			ГДЕ
		|				ДоступностьТоваровНаСкладах.Номенклатура В(&МассивНоменклатуры)
		|				И ДоступностьТоваровНаСкладах.Склад.Организация = &Организация
		|				И ДоступностьТоваровНаСкладах.СтруктурнаяЕдиница = &СтруктурноеПодразделение) КАК ВЗ_Номенклатура
		|			ПО (ВЗ_Склады.Склад = ВЗ_Номенклатура.Склад
		|					ИЛИ ВЗ_Склады.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))) КАК ВЗ_НоменклатураМаршрутов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Маршруты.ТорговыеТочки КАК МаршрутыТорговыеТочки
		|		ПО ВЗ_НоменклатураМаршрутов.Маршрут = МаршрутыТорговыеТочки.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТТ КАК втТТ
		|		ПО (втТТ.СтруктурнаяЕдиница = МаршрутыТорговыеТочки.СтруктурнаяЕдиница)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВодителиПоМаршруту.СрезПоследних(&ДатаСреза, ) КАК ВодителиПоМаршрутуСрезПоследних
		|		ПО (МаршрутыТорговыеТочки.Ссылка = ВодителиПоМаршрутуСрезПоследних.Маршрут)
		|ГДЕ
		|	втТТ.Организация = &Организация";

	ТабНоменклатураМаршрутов = Запрос.Выполнить().Выгрузить();
	ТабНоменклатураМаршрутов.Индексы.Добавить("ТорговаяТочка, Номенклатура");
	
	Для Каждого СтрокаТаб Из ТаблицаДанных Цикл
		СтрокиНоменклатуры = ТабНоменклатураМаршрутов.НайтиСтроки(Новый Структура("ТорговаяТочка, Номенклатура", СтрокаТаб.ТорговаяТочка, СтрокаТаб.Номенклатура));
		Если СтрокиНоменклатуры.Количество() > 0 Тогда
			СтрокаТаб.Маршрут = СтрокиНоменклатуры[0].Маршрут;
			СтрокаТаб.Автомобиль = СтрокиНоменклатуры[0].Автомобиль;
		КонецЕсли;	
	КонецЦикла;
	
	//распределим здесь также по датам производства остатки
	ТабСортировкиСтрокОстатков = Новый ТаблицаЗначений();
	ТабСортировкиСтрокОстатков.Колонки.Добавить("СтрокаОстатка");
	ТабСортировкиСтрокОстатков.Колонки.Добавить("ДатаПроизводства");
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		СтрокаТаб = ТаблицаДанных[КолвоСтрок - н];
		СтрокаТаб.МаршрутныйЛист = ТабСформированныеМаршЛисты[СтрокаТаб.Маршрут];
				
		СтрокиРасхОрдер = ТабРасходникиВБазе.НайтиСтроки(Новый Структура("Склад, ТорговаяТочка", СтрокаТаб.Склад, СтрокаТаб.ТорговаяТочка));
		Если СтрокиРасхОрдер.Количество() > 0 Тогда
			СтрокаТаб.РасходныйОрдер = СтрокиРасхОрдер[0].Ссылка;
		КонецЕсли;
		
		СтрокиОстатков = ТабОстаткиНоменклатуры.НайтиСтроки(Новый Структура("Склад, Номенклатура, Характеристика", СтрокаТаб.Склад, СтрокаТаб.Номенклатура, СтрокаТаб.Характеристика));
		ТабСортировкиСтрокОстатков.Очистить();
		//предварительная сортировка, слетает потому после первого цикла не понятно почему
		Для Каждого СтрокаОстаток Из СтрокиОстатков Цикл
			СтрокаДоб = ТабСортировкиСтрокОстатков.Добавить();
			СтрокаДоб.СтрокаОстатка = СтрокаОстаток;
			СтрокаДоб.ДатаПроизводства = СтрокаОстаток.ДатаПроизводства;
		КонецЦикла;
		ТабСортировкиСтрокОстатков.Сортировать("ДатаПроизводства");
		КолвоКРаспределению = СтрокаТаб.Количество;
		Для Каждого СтрокаОстаток Из ТабСортировкиСтрокОстатков Цикл
			Если СтрокаОстаток.СтрокаОстатка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;	
			Если СтрокаТаб.Количество <= 0 Тогда
				Прервать;
			КонецЕсли;
			МинКолво = Мин(СтрокаТаб.Количество, СтрокаОстаток.СтрокаОстатка.Количество);
			СтрокаОстаток.СтрокаОстатка.Количество = СтрокаОстаток.СтрокаОстатка.Количество - МинКолво;
			СтрокаТаб.Количество = СтрокаТаб.Количество - МинКолво;
			СтрокаДоб = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДоб, СтрокаТаб);
			СтрокаДоб.Количество = МинКолво;
			СтрокаДоб.ДатаПроизводства = СтрокаОстаток.СтрокаОстатка.ДатаПроизводства;
		КонецЦикла;	
	КонецЦикла;
	
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		Если ТаблицаДанных[КолвоСтрок - н].Количество <= 0 Тогда
			ТаблицаДанных.Удалить(КолвоСтрок - н);
		КонецЕсли;	
	КонецЦикла;
	
	ДлительныеОперации.СообщитьПрогресс(100, "Распределение по датам производства");
	
	тзТарифы = РегистрыСведений.СтоимостьУслугПоДоставкеТовараНаТТ.ПолучитьТЗТарифы(ДатаРаспределения);
	СтруктураОтбора = Новый Структура();
	
	Запрос.УстановитьПараметр("Таб", ТаблицаДанных);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таб.Склад,
		|	Таб.ТорговаяТочка,
		|	Таб.Номенклатура,
		|	Таб.Характеристика,
		|	Таб.ДатаПроизводства,
		|	Таб.НомерРаспределения,
		|	Таб.Количество,
		|	Таб.Маршрут,
		|	Таб.Автомобиль,
		|	Таб.РасходныйОрдер,
		|	Таб.МаршрутныйЛист
		|ПОМЕСТИТЬ ВТ_ТаблицаДанных
		|ИЗ
		|	&Таб КАК Таб
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ВЗ_Запрос.Склад КАК Справочник.Склады).Организация КАК Организация,
		|	ВЗ_Запрос.Склад,
		|	ВЗ_Запрос.ТорговаяТочка,
		|	ВЗ_Запрос.Номенклатура,
		|	НоменклатураСпр.ЕдиницаХраненияОстатков КАК ЕдИзм,
		|	ВЗ_Запрос.Характеристика,
		|	ВЗ_Запрос.ДатаПроизводства,
		|	ВЗ_Запрос.НомерРаспределения,
		|	ВЗ_Запрос.Количество,
		|	ВЗ_Запрос.Маршрут,
		|	ВЗ_Запрос.Автомобиль,
		|	ВЗ_Запрос.РасходныйОрдер,
		|	ВЗ_Запрос.МаршрутныйЛист
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ТаблицаДанных.Склад КАК Склад,
		|		ВТ_ТаблицаДанных.ТорговаяТочка КАК ТорговаяТочка,
		|		ВТ_ТаблицаДанных.Номенклатура КАК Номенклатура,
		|		ВТ_ТаблицаДанных.Характеристика КАК Характеристика,
		|		ВТ_ТаблицаДанных.ДатаПроизводства КАК ДатаПроизводства,
		|		МАКСИМУМ(ВТ_ТаблицаДанных.НомерРаспределения) КАК НомерРаспределения,
		|		СУММА(ВТ_ТаблицаДанных.Количество) КАК Количество,
		|		ВТ_ТаблицаДанных.Маршрут КАК Маршрут,
		|		ВТ_ТаблицаДанных.Автомобиль КАК Автомобиль,
		|		ВТ_ТаблицаДанных.РасходныйОрдер КАК РасходныйОрдер,
		|		ВТ_ТаблицаДанных.МаршрутныйЛист КАК МаршрутныйЛист
		|	ИЗ
		|		ВТ_ТаблицаДанных КАК ВТ_ТаблицаДанных
		|	ГДЕ
		|		ВТ_ТаблицаДанных.Количество > 0
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТ_ТаблицаДанных.Склад,
		|		ВТ_ТаблицаДанных.ТорговаяТочка,
		|		ВТ_ТаблицаДанных.Номенклатура,
		|		ВТ_ТаблицаДанных.Характеристика,
		|		ВТ_ТаблицаДанных.ДатаПроизводства,
		|		ВТ_ТаблицаДанных.Маршрут,
		|		ВТ_ТаблицаДанных.Автомобиль,
		|		ВТ_ТаблицаДанных.РасходныйОрдер,
		|		ВТ_ТаблицаДанных.МаршрутныйЛист) КАК ВЗ_Запрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
		|		ПО ВЗ_Запрос.Номенклатура = НоменклатураСпр.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВЗ_Запрос.Маршрут,
		|	ВЗ_Запрос.МаршрутныйЛист,
		|	ВЗ_Запрос.ТорговаяТочка,
		|	ВЗ_Запрос.Склад,
		|	ВЗ_Запрос.РасходныйОрдер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ТаблицаДанных";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	х100 = Выборка.Количество();
	х=0;
	
	КоличествоДокументов = 0;
	
	ДлительныеОперации.СообщитьПрогресс(0, "Создание документов");
	
	ВидПеревозкиНаТТ = Справочники.АК_ВидыПеревозки.ДоставкаНаТТ;
	
	ОтгрузкаВТоргТочку = Перечисления.ВидыОперацийРасходСкладскойУчет.ОтгрузкаВТорговуюТочку;
	СтатусНеОбработан = Перечисления.СтатусыРасходныхОрдеров.НеОбработан;
	Пока Выборка.СледующийПоЗначениюПоля("Маршрут") Цикл
		
		Пока Выборка.СледующийПоЗначениюПоля("МаршрутныйЛист") Цикл
			Если НЕ ЗначениеЗаполнено(Выборка.МаршрутныйЛист) Тогда
				МаршЛистОбъект = СкладыСервер.ПолучитьРейсПоМаршруту(Выборка, тзТарифы, ДатаРаспределения);
			Иначе
				МаршЛистОбъект = Выборка.МаршрутныйЛист.ПолучитьОбъект();
			КонецЕсли;
			
			Пока Выборка.СледующийПоЗначениюПоля("ТорговаяТочка") Цикл
				Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл
					Пока Выборка.СледующийПоЗначениюПоля("РасходныйОрдер") Цикл
						Если НЕ ЗначениеЗаполнено(Выборка.РасходныйОрдер) Тогда
							РасхОрдерОбъект = Документы.РасходныйОрдерСклад.СоздатьДокумент();
							РасхОрдерОбъект.НеМенятьДатуПриПроведении = Истина;
							РасхОрдерОбъект.Дата			= ДатаРаспределения;
							РасхОрдерОбъект.УстановитьНовыйНомер();
							РасхОрдерОбъект.Автор 			= ПараметрыСеанса.ТекущийПользователь;
							РасхОрдерОбъект.ВидОперации		= ОтгрузкаВТоргТочку;
							РасхОрдерОбъект.Получатель 		= Выборка.ТорговаяТочка;
							РасхОрдерОбъект.Организация		= Организация;
							РасхОрдерОбъект.Склад 			= Выборка.Склад;
							РасхОрдерОбъект.АвтозагрузкаУРЗ = Истина;
							РасхОрдерОбъект.Статус 			= СтатусНеОбработан;
							РасхОрдерОбъект.ДатаРаспределения 	= ДатаРаспределения;
						Иначе
							РасхОрдерОбъект = Выборка.РасходныйОрдер.ПолучитьОбъект();
						КонецЕсли;
						Пока Выборка.Следующий() Цикл
							х = х+1;
							КоличествоКЗагрузке = Выборка.Количество;
							Если КоличествоКЗагрузке <= 0.001 Тогда
								Продолжить;
							КонецЕсли;
							МасСтр=ТЗПозицииСЗаданиямиНаРазборку.НайтиСтроки(
								Новый Структура("Номенклатура, Характеристика, ТТ",Выборка.Номенклатура, Выборка.Характеристика, Выборка.ТорговаяТочка));
							Если МасСтр.Количество() Тогда
								Продолжить;
							КонецЕсли; 
							
							СтрокиТовар = РасхОрдерОбъект.Товары.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, ДатаПроизводства", Выборка.Номенклатура, Выборка.Характеристика, Выборка.ДатаПроизводства));
							Если СтрокиТовар.Количество() > 0 Тогда
								СтрокаТовар = СтрокиТовар[0];
							Иначе
								СтрокаТовар = РасхОрдерОбъект.Товары.Добавить();
							КонецЕсли;	
							СтрокаТовар.Номенклатура 		= Выборка.Номенклатура;
							СтрокаТовар.Характеристика 		= Выборка.Характеристика;
							СтрокаТовар.ДатаПроизводства 	= Выборка.ДатаПроизводства;
							СтрокаТовар.НомерРаспределения	= Выборка.НомерРаспределения;
							СтрокаТовар.ЕдиницаИзмерения 	= Выборка.ЕдИзм;
							СтрокаТовар.КоличествоУРЗ 		= СтрокаТовар.КоличествоУРЗ + КоличествоКЗагрузке;
							СтрокаТовар.Количество 			= СтрокаТовар.Количество + КоличествоКЗагрузке;
							
							НомераРаспределений.Вставить(Выборка.НомерРаспределения);
							
							ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Создание документов");
						КонецЦикла;
						
						Если НЕ РасхОрдерОбъект.ЭтоНовый()
								ИЛИ (РасхОрдерОбъект.Товары.Количество() > 0 И РасхОрдерОбъект.Модифицированность()) Тогда
							РасхОрдерОбъект.Товары.Сортировать("Номенклатура");
							РасхОрдерОбъект.ОбменДанными.Загрузка = Истина;
							РасхОрдерОбъект.Проведен = Истина;
							Попытка
								РасхОрдерОбъект.Записать(РежимЗаписиДокумента.Запись);
								СтрокаРасхОрдер = МаршЛистОбъект.РасходныеОрдера.Найти(РасхОрдерОбъект.Ссылка, "Документ");
								Если СтрокаРасхОрдер = Неопределено Тогда
									СтрокаРасхОрдер = МаршЛистОбъект.РасходныеОрдера.Добавить();
									СтрокаРасхОрдер.Документ = РасхОрдерОбъект.Ссылка;
								КонецЕсли;
							Исключение
								СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,РасхОрдерОбъект.Ссылка,-1,ОписаниеОшибки());
							КонецПопытки;

							КоличествоДокументов = КоличествоДокументов+1;
							
						КонецЕсли;	
					КонецЦикла;	
				КонецЦикла;
			КонецЦикла;	
			Если НЕ МаршЛистОбъект.ЭтоНовый()
					ИЛИ (МаршЛистОбъект.РасходныеОрдера.Количество() > 0 И МаршЛистОбъект.Модифицированность()) Тогда
				МаршЛистОбъект.ОбменДанными.Загрузка = Истина;
				МаршЛистОбъект.Проведен = Истина;
				Попытка
					МаршЛистОбъект.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,МаршЛистОбъект.Ссылка,-1,ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	УстановитьСостояниеПоНомерамРаспределений(НомераРаспределений, Перечисления.СостоянияРаспределенияТоваров.РасходникиЗагружены);
	ПеренестиВАрхивОстаткиРаспределенныеПоТТSQL(НомераРаспределений);
	
	СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,Неопределено,КоличествоДокументов,"Ок");
	Возврат КоличествоДокументов;

КонецФункции

//+++АК LATV 2018.10.30 ИП-00020276
//заполнение из ОстаткиРаспределенныеПоТТ
Функция ЗаполнитьПоРаспределениюНаСервереГруппыУРЗ(СтруктураПараметров, ТабТТ, ТабСклады)

	ДатаРаспределения			= СтруктураПараметров.ДатаРаспределения;
	Организация					= СтруктураПараметров.Организация;
	СтруктурноеПодразделение	= СтруктураПараметров.СтруктурноеПодразделение;
	НомерРаспределения			= СтруктураПараметров.НомерРаспределения;
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Склад"			, Новый ОписаниеТипов("СправочникСсылка.Склады"));	
	ТаблицаДанных.Колонки.Добавить("ТорговаяТочка"	, Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы"));
	ТаблицаДанных.Колонки.Добавить("Номенклатура"	, Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаДанных.Колонки.Добавить("Характеристика"	, Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаДанных.Колонки.Добавить("Количество"			, Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("НомерРаспределения"	, Новый ОписаниеТипов("Число"));
	
	// Получили все остатки, распределенные по ТТ
	Запрос = Новый Запрос();
	Запрос.Текст = ПолучитьТекстЗапроса_ОстаткиРаспределенныеПоТТ();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаРаспределения", ДатаРаспределения);
	Запрос.УстановитьПараметр("НомерРаспределения", НомерРаспределения);
	Запрос.УстановитьПараметр("НеОтбиратьПоНомеру", НомерРаспределения=0);
	
	Результаты = Запрос.ВыполнитьПакет();
	//НомераРаспределений	= Результаты[4].Выгрузить().ВыгрузитьКолонку("НомерРаспределения"); //latv убрать
	ТабРаспределение	= Результаты[5].Выгрузить();
	
	ТабТТРаспр = ТабРаспределение.Скопировать();
	ТабТТРаспр.Свернуть("ТорговаяТочка");
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЗ_Запрос.Номенклатура,
		|	ВЗ_Запрос.СтруктурноеПодразделение,
		|	ВЗ_Запрос.ТорговаяТочка
		|ИЗ
		|	(ВЫБРАТЬ
		|		спрНоменклатура.Ссылка КАК Номенклатура,
		|		ПорядокОбеспеченияТорговыхТочекСрезПоследних.Расчетчик КАК СтруктурноеПодразделение,
		|		ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка КАК ТорговаяТочка
		|	ИЗ
		|		РегистрСведений.ПорядокОбеспеченияТорговыхТочек.СрезПоследних(
		|				КОНЕЦПЕРИОДА(&ДатаРаспределения, ДЕНЬ),
		|				ТорговаяТочка В (&МассивТТ)
		|					И ТорговаяТочка.id_TT <> 0) КАК ПорядокОбеспеченияТорговыхТочекСрезПоследних
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|			ПО ПорядокОбеспеченияТорговыхТочекСрезПоследних.ГруппаУРЗ = спрНоменклатура.ГруппаНоменклатурыУРЗ
		|	ГДЕ
		|		спрНоменклатура.ЭтоГруппа = ЛОЖЬ) КАК ВЗ_Запрос
		|ГДЕ
		|	ВЗ_Запрос.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	спрНоменклатура.Ссылка КАК Номенклатура,
		|	ДоступностьТоваровНаСкладах.Склад КАК Склад
		|ИЗ
		|	РегистрСведений.ДоступностьТоваровНаСкладах КАК ДоступностьТоваровНаСкладах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДоступностьТоваровНаСкладах.Номенклатура = спрНоменклатура.Ссылка
		|ГДЕ
		|	ДоступностьТоваровНаСкладах.Склад В(&Склады)
		|	И ДоступностьТоваровНаСкладах.Склад.Организация = &Организация
		|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	спрНоменклатура.Ссылка КАК Номенклатура,
		|	ДоступностьТоваровНаСкладах.Склад КАК Склад
		|ИЗ
		|	РегистрСведений.ДоступностьТоваровНаСкладах КАК ДоступностьТоваровНаСкладах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДоступностьТоваровНаСкладах.Номенклатура = спрНоменклатура.Ссылка
		|ГДЕ
		|	ДоступностьТоваровНаСкладах.Склад.Владелец = &СтруктурноеПодразделение
		|	И ДоступностьТоваровНаСкладах.Склад.Организация = &Организация
		|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МассивТТ", ТабТТРаспр.ВыгрузитьКолонку("ТорговаяТочка"));
	Запрос.УстановитьПараметр("Склады", ТабСклады.ВыгрузитьКолонку("Склад"));
	Запрос.УстановитьПараметр("Таб", ТабРаспределение);
	
	Результаты = Запрос.ВыполнитьПакет();
	НастройкиПорядкаОбеспечения = Результаты[0].Выгрузить();
	НастройкиПорядкаОбеспечения.Индексы.Добавить("ТорговаяТочка, Номенклатура");
	НастройкиДоступностиТоваров = Результаты[1].Выгрузить();
	НастройкиДоступностиТоваров.Индексы.Добавить("Номенклатура");
	НастройкиДоступностиТоваровДляСтруктПодразделения = Результаты[2].Выгрузить();
	НастройкиДоступностиТоваровДляСтруктПодразделения.Индексы.Добавить("Номенклатура");
	
	ОсновнойСклад = СтруктурноеПодразделение.СкладТорговогоЗала;
	
	МассивСкладовМагазинаСтороннейРозницы = Новый Массив;//shae 2018.10.11 ИП-00020073
	Для Каждого СтрокаРаспределение Из ТабРаспределение Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаРаспределение.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиПорядкаОбеспечения = НастройкиПорядкаОбеспечения.НайтиСтроки(Новый Структура("ТорговаяТочка, Номенклатура", СтрокаРаспределение.ТорговаяТочка, СтрокаРаспределение.Номенклатура));
		Если СтрокиПорядкаОбеспечения.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Склад = Неопределено;
		СтрокаСклад = НастройкиДоступностиТоваров.Найти(СтрокаРаспределение.Номенклатура, "Номенклатура");
		Если СтрокаСклад = Неопределено Тогда
			СтрокаОбщиеНастройки = НастройкиДоступностиТоваровДляСтруктПодразделения.Найти(СтрокаРаспределение.Номенклатура, "Номенклатура");
			Если СтрокаОбщиеНастройки = Неопределено Тогда
				Если ЗначениеЗаполнено(Склад)
					И Склад = ОсновнойСклад Тогда
					Склад = ОсновнойСклад;
				КонецЕсли;	
			КонецЕсли;
		Иначе
			Склад = СтрокаСклад.Склад;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(Склад) Тогда
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока = ТаблицаДанных.Добавить();
		НоваяСтрока.Склад = Склад;
		//+++shae 2018.10.10 ИП-00020073
		Если ЗначениеЗаполнено(СтрокаРаспределение.СкладМагазинаСтороннейРозницы) Тогда
			Если МассивСкладовМагазинаСтороннейРозницы.Найти(СтрокаРаспределение.СкладМагазинаСтороннейРозницы) = Неопределено Тогда 
				МассивСкладовМагазинаСтороннейРозницы.Добавить(СтрокаРаспределение.СкладМагазинаСтороннейРозницы);
			КонецЕсли;
			ТорговаяТочка 		  = СтрокаРаспределение.СкладМагазинаСтороннейРозницы;
		Иначе
			ТорговаяТочка 		  = СтрокаРаспределение.ТорговаяТочка;
		КонецЕсли;	
	 	 НоваяСтрока.ТорговаяТочка 		  = ТорговаяТочка;
		//---shae 2018.10.10 ИП-00020073 
		
		НоваяСтрока.Номенклатура 		= СтрокаРаспределение.Номенклатура;
		Если НЕ СтрокаРаспределение.НеВедетсяУчетПоХарактеристикам Тогда
			НоваяСтрока.Характеристика 		= СтрокаРаспределение.Характеристика;
		КонецЕсли;	
		НоваяСтрока.Количество	 		= СтрокаРаспределение.Количество;
		НоваяСтрока.НомерРаспределения	= СтрокаРаспределение.НомерРаспределения;
		
	КонецЦикла;
	
	//+++shae 2018.10.11 ИП-00020073 
	Если МассивСкладовМагазинаСтороннейРозницы.Количество()>0 Тогда 
		СвернутьТаблицуДанных(ТаблицаДанных);
		ОтредактироватьПотребностьСУчетомОстатковСклада(ТаблицаДанных,МассивСкладовМагазинаСтороннейРозницы, ДатаРаспределения, СтруктурноеПодразделение);
	КонецЕсли;
	//---shae 2018.10.11 ИП-00020073 
	
	Возврат ТаблицаДанных;

КонецФункции

Процедура ОткорректироватьТаблицуДанных(ТаблицаДанных, ТЗПозицииСЗаданиямиНаРазборку)
	НенайденныеСтроки = Новый Массив;
	Для Каждого СтрокаТЗ Из ТЗПозицииСЗаданиямиНаРазборку Цикл 
		НайденныеСтроки = ТаблицаДанных.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, ТорговаяТочка", СтрокаТЗ.Номенклатура, СтрокаТЗ.Характеристика, СтрокаТЗ.ТТ));
		Если НайденныеСтроки.Количество() > 0 Тогда 			
			НайденныеСтроки[0].ВВедомостиНаРазборку = Истина;
		Иначе
			НенайденныеСтроки.Добавить(СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЗ Из НенайденныеСтроки Цикл 
		НайденныеСтроки = ТаблицаДанных.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, ВВедомостиНаРазборку", СтрокаТЗ.Номенклатура, СтрокаТЗ.Характеристика, Ложь));
		
		КоличествоДляУменьшения = СтрокаТЗ.Количество;
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			УменьшитьНа				   = Мин(КоличествоДляУменьшения, НайденнаяСтрока.Количество);
			КоличествоДляУменьшения    = КоличествоДляУменьшения - УменьшитьНа;
			НайденнаяСтрока.Количество = НайденнаяСтрока.Количество - УменьшитьНа;
			Если КоличествоДляУменьшения = 0 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

// Заполнить из результата запроса соответствие полем ссылка
Функция СоответствиеДляСформированныхМаршрутов(РезультатЗапроса)
	Перем Соответствие;
	Соответствие = Новый Соответствие;
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл 
			Соответствие[Выборка.Маршрут] = Выборка.Ссылка;
		КонецЦикла;
	КонецЕсли;
	Возврат Соответствие;
КонецФункции

#КонецОбласти

#Область ПоЗонам

//+++АК LATV 2018.10.30 ИП-00020276
Функция СодатьДокументыПоРаспределениюНаСервереПоЗонам_РО(СтруктураНастроек)

	ДлительныеОперации.СообщитьПрогресс(0,"Блокировка документов");
	
	ДатаРаспределения			= СтруктураНастроек.ДатаРаспределения;
	СтруктурноеПодразделение	= СтруктураНастроек.СтруктурноеПодразделение;
	Склад						= СтруктураНастроек.Склад;
	Организация					= СтруктураНастроек.Организация;
	НомераРаспределений			= Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаРаспределения"		, ДатаРаспределения);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("Склад"					, Склад);
	Запрос.УстановитьПараметр("Организация"				, Организация);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦФОСтруктурныхЕдиницСрезПоследних.СтруктурнаяЕдиница,
		|	ЦФОСтруктурныхЕдиницСрезПоследних.Организация
		|ПОМЕСТИТЬ втТТ
		|ИЗ
		|	РегистрСведений.ЦФОСтруктурныхЕдиниц.СрезПоследних КАК ЦФОСтруктурныхЕдиницСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка КАК ТТ,
		|	втТТ.Организация
		|ИЗ
		|	РегистрСведений.ПорядокОбеспеченияТорговыхТочек.СрезПоследних(&ДатаРаспределения, Расчетчик = &СтруктурноеПодразделение) КАК ПорядокОбеспеченияТорговыхТочекСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ втТТ КАК втТТ
		|		ПО ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка = втТТ.СтруктурнаяЕдиница
		|ГДЕ
		|	втТТ.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка,
		|	втТТ.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Склады.Ссылка КАК Склад
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.Владелец = &СтруктурноеПодразделение
		|	И (Склады.Ссылка = &Склад
		|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|	И Склады.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоступностьТоваровНаСкладах.Номенклатура
		|ИЗ
		|	РегистрСведений.ДоступностьТоваровНаСкладах КАК ДоступностьТоваровНаСкладах
		|ГДЕ
		|	ДоступностьТоваровНаСкладах.СтруктурнаяЕдиница = &СтруктурноеПодразделение
		|	И (ДоступностьТоваровНаСкладах.Склад = &Склад
		|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|	И ДоступностьТоваровНаСкладах.Склад.Организация = &Организация";
	
	ДлительныеОперации.СообщитьПрогресс(0, "Получение данных распределения");
	
	Результаты = Запрос.ВыполнитьПакет();
	ТабТТ 		= Результаты[1].Выгрузить();
	ТабСклады 	= Результаты[2].Выгрузить();
	ТабНоменклатураРаспределения = Неопределено;
	Если ЗначениеЗаполнено(Склад) Тогда
		ТабНоменклатураРаспределения = Результаты[3].Выгрузить();
		ТабНоменклатураРаспределения.Индексы.Добавить("Номенклатура");
	КонецЕсли;	
	
	ТаблицаДанных = ЗаполнитьПоРаспределениюНаСервереПоЗонам(СтруктураНастроек, ТабТТ, ТабСклады);
	ТаблицаДанных.Колонки.Добавить("Организация",		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ДатаПроизводства",	Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("Маршрут",			Новый ОписаниеТипов("СправочникСсылка.Маршруты"));
	ТаблицаДанных.Колонки.Добавить("Автомобиль",		Новый ОписаниеТипов("СправочникСсылка.Автомобили"));
	ТаблицаДанных.Колонки.Добавить("РасходныйОрдер",	Новый ОписаниеТипов("ДокументСсылка.РасходныйОрдерСклад"));
	ТаблицаДанных.Колонки.Добавить("МаршрутныйЛист",	Новый ОписаниеТипов("ДокументСсылка.МаршрутныйЛист"));
	ТаблицаДанных.Колонки.Добавить("ИзначальныйСклад",	Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	Для Каждого СтрокаТаб Из ТаблицаДанных Цикл
		Если ЗначениеЗаполнено(СтрокаТаб.ЗонаОтгрузки) Тогда
			СтрокаТаб.ИзначальныйСклад = СтрокаТаб.Склад;
			СтрокаТаб.Склад = СтрокаТаб.ЗонаОтгрузки;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МаршрутыСклады.ЗонаОтгрузки КАК Склад
		|ПОМЕСТИТЬ ВТ_Склады
		|ИЗ
		|	Справочник.Маршруты.Склады КАК МаршрутыСклады
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Маршруты КАК СпрМаршруты
		|		ПО МаршрутыСклады.Ссылка = СпрМаршруты.Ссылка
		|ГДЕ
		|	СпрМаршруты.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И СпрМаршруты.Организация = &Организация
		|	И (МаршрутыСклады.Склад = &Склад
		|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Склады.Ссылка
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.Владелец = &СтруктурноеПодразделение
		|	И Склады.Организация = &Организация
		|	И (Склады.Ссылка = &Склад
		|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасходныйОрдерСклад.Ссылка,
		|	РасходныйОрдерСклад.Склад КАК Склад,
		|	РасходныйОрдерСклад.Склад.НеОчищатьЗаданияНаРазборкуПриНовомРаспределении КАК НеОчищатьЗаданияНаРазборкуПриНовомРаспределении,
		|	РасходныйОрдерСклад.Получатель КАК ТорговаяТочка,
		|	РасходныйОрдерСклад.СкладСборкиДляЗоны КАК СкладСборкиДляЗоны
		|ИЗ
		|	Документ.РасходныйОрдерСклад КАК РасходныйОрдерСклад
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерСклад.Товары КАК РасходныйОрдерСкладТовары
		|		ПО РасходныйОрдерСклад.Ссылка = РасходныйОрдерСкладТовары.Ссылка
		|ГДЕ
		|	РасходныйОрдерСклад.ПометкаУдаления = ЛОЖЬ
		|	И РасходныйОрдерСклад.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСкладскойУчет.ОтгрузкаВТорговуюТочку)
		|	И РасходныйОрдерСкладТовары.КоличествоУРЗ > 0
		|	И НАЧАЛОПЕРИОДА(РасходныйОрдерСклад.ДатаРаспределения, ДЕНЬ) = &ДатаРаспределения
		|	И РасходныйОрдерСклад.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
		|	И РасходныйОрдерСклад.Склад.Владелец = &СтруктурноеПодразделение
		|	И РасходныйОрдерСклад.Организация = &Организация
		|	И (РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		|			ИЛИ РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		|	И РасходныйОрдерСклад.Склад В
		|			(ВЫБРАТЬ
		|				Таб.Склад
		|			ИЗ
		|				ВТ_Склады КАК Таб)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокМаршЛист.Ссылка КАК Ссылка,
		|	ДокМаршЛист.Маршрут,
		|	ДокМаршЛист.Организация КАК Организация,
		|	ДокМаршЛист.СтруктурноеПодразделение,
		|	МаршрутныйЛистРасходныеОрдера.Документ КАК Расходник,
		|	ВЫБОР
		|		КОГДА МаршрутныйЛистРасходныеОрдера.Документ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВСборке
		|ИЗ
		|	Документ.МаршрутныйЛист КАК ДокМаршЛист
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛист.РасходныеОрдера КАК МаршрутныйЛистРасходныеОрдера
		|		ПО ДокМаршЛист.Ссылка = МаршрутныйЛистРасходныеОрдера.Ссылка
		|ГДЕ
		|	ДокМаршЛист.ПометкаУдаления = ЛОЖЬ
		|	И ДокМаршЛист.Организация = &Организация
		|	И ДокМаршЛист.Отгружено = ЛОЖЬ
		|	И НАЧАЛОПЕРИОДА(ДокМаршЛист.Дата, ДЕНЬ) = &ДатаРаспределения
		|	И ДокМаршЛист.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И МаршрутныйЛистРасходныеОрдера.Документ.Склад В
		|			(ВЫБРАТЬ
		|				Таб.Склад
		|			ИЗ
		|				ВТ_Склады КАК Таб)
		|	И (ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		|			ИЛИ ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
	//+++shae 2018.12.06 закомментирован без номера			
		//|ВЫБРАТЬ
		//|	РасходныйОрдерСкладТовары.Ссылка.Склад,
		//|	РасходныйОрдерСкладТовары.Ссылка.Получатель КАК ТорговаяТочка,
		//|	РасходныйОрдерСкладТовары.Номенклатура,
		//|	РасходныйОрдерСкладТовары.Характеристика,
		//|	СУММА(РасходныйОрдерСкладТовары.КоличествоУРЗ) КАК КоличествоУРЗ
		//|ИЗ
		//|	Документ.РасходныйОрдерСклад.Товары КАК РасходныйОрдерСкладТовары
		//|ГДЕ
		//|	РасходныйОрдерСкладТовары.Ссылка.Проведен = ИСТИНА
		//|	И РасходныйОрдерСкладТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСкладскойУчет.ОтгрузкаВТорговуюТочку)
		//|	И РасходныйОрдерСкладТовары.КоличествоУРЗ > 0
		//|	И НАЧАЛОПЕРИОДА(РасходныйОрдерСкладТовары.Ссылка.ДатаРаспределения, ДЕНЬ) = &ДатаРаспределения
		//|	И РасходныйОрдерСкладТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
		//|	И (РасходныйОрдерСкладТовары.Ссылка.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		//|			ИЛИ РасходныйОрдерСкладТовары.Ссылка.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		//|
		//|СГРУППИРОВАТЬ ПО
		//|	РасходныйОрдерСкладТовары.Ссылка.Склад,
		//|	РасходныйОрдерСкладТовары.Ссылка.Получатель,
		//|	РасходныйОрдерСкладТовары.Номенклатура,
		//|	РасходныйОрдерСкладТовары.Характеристика
		//|;
		//|
		//|////////////////////////////////////////////////////////////////////////////////
	//---shae 2018.12.06 закомментирован без номера			
		|ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
		|	ТоварыНаСкладахОстатки.ДатаПроизводства КАК ДатаПроизводства,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			,
		|			Склад.Владелец = &СтруктурноеПодразделение
		|				И Склад.Организация = &Организация
		|				И ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)) КАК ТоварыНаСкладахОстатки
		|ГДЕ
		|	ТоварыНаСкладахОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склад,
		|	Номенклатура,
		|	Характеристика,
		|	ДатаПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокМаршЛист.Ссылка КАК Ссылка,
		|	ДокМаршЛист.Маршрут,
		|	ДокМаршЛист.СтруктурноеПодразделение,
		|	ДокМаршЛист.Организация КАК Организация,
		|	МаршрутныйЛистРасходныеОрдера.Документ КАК Расходник,
		|	ВЫБОР
		|		КОГДА МаршрутныйЛистРасходныеОрдера.Документ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВСборке
		|ИЗ
		|	Документ.МаршрутныйЛист КАК ДокМаршЛист
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛист.РасходныеОрдера КАК МаршрутныйЛистРасходныеОрдера
		|		ПО ДокМаршЛист.Ссылка = МаршрутныйЛистРасходныеОрдера.Ссылка
		|ГДЕ
		|	ДокМаршЛист.ПометкаУдаления = ЛОЖЬ
		|	И ДокМаршЛист.Отгружено = ЛОЖЬ
		|	И НАЧАЛОПЕРИОДА(ДокМаршЛист.Дата, ДЕНЬ) = &ДатаРаспределения
		|	И ДокМаршЛист.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И ДокМаршЛист.Организация = &Организация
		|	И (ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
		|			ИЛИ ДокМаршЛист.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
	
	ТЗПозицииСЗаданиямиНаРазборку=Новый ТаблицаЗначений;
	ТЗПозицииСЗаданиямиНаРазборку.Колонки.Добавить("Номенклатура");
	ТЗПозицииСЗаданиямиНаРазборку.Колонки.Добавить("ТТ");
	ТЗПозицииСЗаданиямиНаРазборку.Индексы.Добавить("Номенклатура,ТТ");
	
	Результаты = Запрос.ВыполнитьПакет();
	ТабРасходникиВБазе = Результаты[1].Выгрузить();
	ТабМаршЛисты = Результаты[2].Выгрузить();
	
	ДлительныеОперации.СообщитьПрогресс(0, "Чтение документов от предыдущих загрузок");
	х100 = ТабРасходникиВБазе.Количество()+ТабМаршЛисты.Количество();
	//почистим доки нераспечатанные
	х = 0;

	Для Каждого СтрокаРасходник Из ТабРасходникиВБазе Цикл
		х = х+1;
		ДокОбъект = СтрокаРасходник.Ссылка.ПолучитьОбъект();
		КолвоСтрок = ДокОбъект.Товары.Количество();
		Для н = 1 По КолвоСтрок Цикл
			Если ДокОбъект.Товары[КолвоСтрок - н].КоличествоУРЗ > 0 Тогда
				Если ЗначениеЗаполнено(ДокОбъект.Товары[КолвоСтрок - н].ЗаданиеНаРазборку) И СтрокаРасходник.НеОчищатьЗаданияНаРазборкуПриНовомРаспределении Тогда
					НовСтр=ТЗПозицииСЗаданиямиНаРазборку.Добавить();
					НовСтр.Номенклатура=ДокОбъект.Товары[КолвоСтрок - н].Номенклатура;
					НовСтр.ТТ=ДокОбъект.Получатель;
				Иначе
					ДокОбъект.Товары.Удалить(КолвоСтрок - н);
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;	
		Попытка
			ДокОбъект.Записать(?(ДокОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		Исключение
			СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,ДокОбъект.Ссылка,-1,ОписаниеОшибки());
		КонецПопытки;
		ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Очистка документов от предыдущих загрузок");
	КонецЦикла;
	
	Выборка = Результаты[2].Выбрать();
	//почистим маршрутные листы перед загрузкой
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		х = х+1;
		ДокОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Пока Выборка.Следующий() Цикл
			Если Не ЗначениеЗаполнено(Выборка.Расходник)
				ИЛИ (Выборка.ВСборке) Тогда
				Продолжить;
			КонецЕсли;	
			ДокОбъект.РасходныеОрдера.Удалить(ДокОбъект.РасходныеОрдера.Найти(Выборка.Расходник, "Документ"));
		КонецЦикла;	
		Если ДокОбъект.Модифицированность() Тогда
			Попытка
				ДокОбъект.Записать(?(ДокОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
			Исключение
				СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,ДокОбъект.Ссылка,-1,ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;	
		ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Очистка документов от предыдущих загрузок");
	КонецЦикла;	
	
	ДлительныеОперации.СообщитьПрогресс(0, "Распределение по датам производства");
	
	//+++shae 2018.10.11 закомментирован без номера	
	////сразу откинем уже загруженное и собранное из обработки
	//ТабЗагруженное = Результаты[3].Выгрузить();
	//Для Каждого СтрокаЗагруженное Из ТабЗагруженное Цикл
	//	КолвоКВычетанию = СтрокаЗагруженное.КоличествоУРЗ;
	//	Если КолвоКВычетанию <= 0 Тогда
	//		Продолжить;
	//	КонецЕсли;
	//	СтрокиДанных = ТаблицаДанных.НайтиСтроки(Новый Структура("ТорговаяТочка, Номенклатура, Характеристика"
	//										, СтрокаЗагруженное.ТорговаяТочка, СтрокаЗагруженное.Номенклатура, СтрокаЗагруженное.Характеристика));
	//										
	//	Для Каждого СтрокаДанные Из СтрокиДанных Цикл
	//		Если КолвоКВычетанию <= 0 Тогда
	//			Прервать;
	//		КонецЕсли;
	//		КолвоМин = Мин(КолвоКВычетанию, СтрокаДанные.Количество);
	//		СтрокаДанные.Количество = СтрокаДанные.Количество - КолвоМин;
	//		КолвоКВычетанию = КолвоКВычетанию - КолвоМин;
	//	КонецЦикла;										
	//КонецЦикла;
	//---shae 2018.10.11 закомментирован без номера
	
	ТабОстаткиНоменклатуры = Результаты[3].Выгрузить();
	ТабОстаткиНоменклатуры.Индексы.Добавить("Склад, Номенклатура, Характеристика");
	
	ТабСформированныеМаршЛисты = Результаты[4].Выгрузить();
	
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		Если ТаблицаДанных[КолвоСтрок - н].Количество <= 0 Тогда
			ТаблицаДанных.Удалить(КолвоСтрок - н);
		КонецЕсли;
	КонецЦикла;
	
	//теперь определим расходники и маршруты куда должна попасть номенклатура
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СпрМаршруты.Организация КАК Организация,
		|	МаршрутыСклады.Склад,
		|	МаршрутыТорговыеТочки.СтруктурнаяЕдиница КАК ТорговаяТочка,
		|	МаршрутыСклады.Ссылка КАК Маршрут
		|ИЗ
		|	Справочник.Маршруты КАК СпрМаршруты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Маршруты.ТорговыеТочки КАК МаршрутыТорговыеТочки
		|		ПО СпрМаршруты.Ссылка = МаршрутыТорговыеТочки.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Маршруты.Склады КАК МаршрутыСклады
		|		ПО СпрМаршруты.Ссылка = МаршрутыСклады.Ссылка
		|ГДЕ
		|	СпрМаршруты.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И СпрМаршруты.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СпрМаршруты.Организация,
		|	МаршрутыСклады.ЗонаОтгрузки,
		|	МаршрутыТорговыеТочки.СтруктурнаяЕдиница,
		|	МаршрутыТорговыеТочки.Ссылка
		|ИЗ
		|	Справочник.Маршруты КАК СпрМаршруты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Маршруты.ТорговыеТочки КАК МаршрутыТорговыеТочки
		|		ПО СпрМаршруты.Ссылка = МаршрутыТорговыеТочки.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Маршруты.Склады КАК МаршрутыСклады
		|		ПО СпрМаршруты.Ссылка = МаршрутыСклады.Ссылка
		|ГДЕ
		|	СпрМаршруты.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|	И СпрМаршруты.Организация = &Организация
		|	И МаршрутыСклады.ЗонаОтгрузки <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	
	ТабМаршруты = Запрос.Выполнить().Выгрузить();
	ТабМаршруты.Индексы.Добавить("Склад, ТорговаяТочка");
	
	Для Каждого СтрокаТаб Из ТаблицаДанных Цикл
		СтрокиНоменклатуры = ТабМаршруты.НайтиСтроки(Новый Структура("Склад, ТорговаяТочка", СтрокаТаб.Склад, СтрокаТаб.ТорговаяТочка));
		Если СтрокиНоменклатуры.Количество() > 0 Тогда
			СтрокаТаб.Маршрут = СтрокиНоменклатуры[0].Маршрут;
		КонецЕсли;
	КонецЦикла;
	
	//распределим здесь также по датам производства остатки
	ТабСортировкиСтрокОстатков = Новый ТаблицаЗначений();
	ТабСортировкиСтрокОстатков.Колонки.Добавить("СтрокаОстатка");
	ТабСортировкиСтрокОстатков.Колонки.Добавить("ДатаПроизводства");
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		СтрокаТаб = ТаблицаДанных[КолвоСтрок - н];
		СтрокаМаршЛист = ТабСформированныеМаршЛисты.Найти(СтрокаТаб.Маршрут, "Маршрут");
		Если СтрокаМаршЛист <> Неопределено Тогда
			СтрокаТаб.МаршрутныйЛист = СтрокаМаршЛист.Ссылка;
		КонецЕсли;
		
		СтрокиРасхОрдер = ТабРасходникиВБазе.НайтиСтроки(Новый Структура("Склад, ТорговаяТочка, СкладСборкиДляЗоны", СтрокаТаб.Склад, СтрокаТаб.ТорговаяТочка, СтрокаТаб.ИзначальныйСклад));
		Если СтрокиРасхОрдер.Количество() > 0 Тогда
			СтрокаТаб.РасходныйОрдер = СтрокиРасхОрдер[0].Ссылка;
		КонецЕсли;
		
		СтрокиОстатков = ТабОстаткиНоменклатуры.НайтиСтроки(Новый Структура("Склад, Номенклатура, Характеристика", СтрокаТаб.Склад, СтрокаТаб.Номенклатура, СтрокаТаб.Характеристика));
		ТабСортировкиСтрокОстатков.Очистить();
		//предварительная сортировка, слетает потому после первого цикла не понятно почему
		Для Каждого СтрокаОстаток Из СтрокиОстатков Цикл
			СтрокаДоб = ТабСортировкиСтрокОстатков.Добавить();
			СтрокаДоб.СтрокаОстатка = СтрокаОстаток;
			СтрокаДоб.ДатаПроизводства = СтрокаОстаток.ДатаПроизводства;
		КонецЦикла;
		ТабСортировкиСтрокОстатков.Сортировать("ДатаПроизводства");
		КолвоКРаспределению = СтрокаТаб.Количество;
		Для Каждого СтрокаОстаток Из ТабСортировкиСтрокОстатков Цикл
			Если СтрокаОстаток.СтрокаОстатка.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;	
			Если СтрокаТаб.Количество <= 0 Тогда
				Прервать;
			КонецЕсли;
			МинКолво = Мин(СтрокаТаб.Количество, СтрокаОстаток.СтрокаОстатка.Количество);
			СтрокаОстаток.СтрокаОстатка.Количество = СтрокаОстаток.СтрокаОстатка.Количество - МинКолво;
			СтрокаТаб.Количество = СтрокаТаб.Количество - МинКолво;
			СтрокаДоб = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДоб, СтрокаТаб);
			СтрокаДоб.Количество = МинКолво;
			СтрокаДоб.ДатаПроизводства = СтрокаОстаток.СтрокаОстатка.ДатаПроизводства;
		КонецЦикла;	
	КонецЦикла;
	
	КолвоСтрок = ТаблицаДанных.Количество();
	Для н = 1 По КолвоСтрок Цикл
		Если ТаблицаДанных[КолвоСтрок - н].Количество <= 0 Тогда
			ТаблицаДанных.Удалить(КолвоСтрок - н);
		КонецЕсли;	
	КонецЦикла;
	
	ДлительныеОперации.СообщитьПрогресс(100, "Распределение по датам производства");
	
	тзТарифы = РегистрыСведений.СтоимостьУслугПоДоставкеТовараНаТТ.ПолучитьТЗТарифы(ДатаРаспределения);
	СтруктураОтбора = Новый Структура();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таб.Организация,
		|	Таб.Склад,
		|	Таб.ИзначальныйСклад,
		|	Таб.ТорговаяТочка,
		|	Таб.Номенклатура,
		|	Таб.Характеристика,
		|	Таб.ДатаПроизводства,
		|	Таб.НомерРаспределения,
		|	Таб.Количество,
		|	Таб.Автомобиль,
		|	Таб.Маршрут,
		|	Таб.РасходныйОрдер,
		|	Таб.МаршрутныйЛист
		|ПОМЕСТИТЬ ВТ_ТаблицаДанных
		|ИЗ
		|	&Таб КАК Таб
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ВЗ_Запрос.Склад КАК Справочник.Склады).Организация КАК Организация,
		|	ВЗ_Запрос.Склад,
		|	ВЗ_Запрос.ИзначальныйСклад,
		|	ВЗ_Запрос.ТорговаяТочка,
		|	ВЗ_Запрос.Номенклатура,
		|	ВЗ_Запрос.Характеристика,
		|	ВЗ_Запрос.ДатаПроизводства,
		|	ВЗ_Запрос.НомерРаспределения,
		|	ВЗ_Запрос.Количество,
		|	ВЗ_Запрос.Маршрут,
		|	ВЗ_Запрос.Автомобиль,
		|	ВЗ_Запрос.РасходныйОрдер,
		|	ВЗ_Запрос.МаршрутныйЛист
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ТаблицаДанных.Склад КАК Склад,
		|		ВТ_ТаблицаДанных.ИзначальныйСклад КАК ИзначальныйСклад,
		|		ВТ_ТаблицаДанных.ТорговаяТочка КАК ТорговаяТочка,
		|		ВТ_ТаблицаДанных.Номенклатура КАК Номенклатура,
		|		ВТ_ТаблицаДанных.Характеристика КАК Характеристика,
		|		ВТ_ТаблицаДанных.ДатаПроизводства КАК ДатаПроизводства,
		|		МАКСИМУМ(ВТ_ТаблицаДанных.НомерРаспределения) КАК НомерРаспределения,
		|		СУММА(ВТ_ТаблицаДанных.Количество) КАК Количество,
		|		ВТ_ТаблицаДанных.Маршрут КАК Маршрут,
		|		ВТ_ТаблицаДанных.Автомобиль КАК Автомобиль,
		|		ВТ_ТаблицаДанных.РасходныйОрдер КАК РасходныйОрдер,
		|		ВТ_ТаблицаДанных.МаршрутныйЛист КАК МаршрутныйЛист
		|	ИЗ
		|		ВТ_ТаблицаДанных КАК ВТ_ТаблицаДанных
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТ_ТаблицаДанных.Склад,
		|		ВТ_ТаблицаДанных.ИзначальныйСклад,
		|		ВТ_ТаблицаДанных.ТорговаяТочка,
		|		ВТ_ТаблицаДанных.Номенклатура,
		|		ВТ_ТаблицаДанных.Характеристика,
		|		ВТ_ТаблицаДанных.ДатаПроизводства,
		|		ВТ_ТаблицаДанных.Маршрут,
		|		ВТ_ТаблицаДанных.Автомобиль,
		|		ВТ_ТаблицаДанных.РасходныйОрдер,
		|		ВТ_ТаблицаДанных.МаршрутныйЛист) КАК ВЗ_Запрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВЗ_Запрос.Маршрут,
		|	ВЗ_Запрос.МаршрутныйЛист,
		|	ВЗ_Запрос.ТорговаяТочка,
		|	ВЗ_Запрос.Склад,
		|	ВЗ_Запрос.ИзначальныйСклад,
		|	ВЗ_Запрос.РасходныйОрдер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ТаблицаДанных";
	
	Запрос.УстановитьПараметр("Таб", ТаблицаДанных);
	Выборка = Запрос.Выполнить().Выбрать();
	
	х100 = Выборка.Количество();
	х=0;
	
	ДлительныеОперации.СообщитьПрогресс(0, "Создание документов");
	
	ВидПеревозкиНаТТ = Справочники.АК_ВидыПеревозки.ДоставкаНаТТ;
	
	КоличествоДокументов = 0;
	Пока Выборка.СледующийПоЗначениюПоля("Маршрут") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("МаршрутныйЛист") Цикл
			Если НЕ ЗначениеЗаполнено(Выборка.МаршрутныйЛист) Тогда
				МаршЛистОбъект = СкладыСервер.ПолучитьРейсПоМаршруту(Выборка, тзТарифы, ДатаРаспределения);
			Иначе
				МаршЛистОбъект = Выборка.МаршрутныйЛист.ПолучитьОбъект();
			КонецЕсли;
			Пока Выборка.СледующийПоЗначениюПоля("ТорговаяТочка") Цикл
				Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл
					Пока Выборка.СледующийПоЗначениюПоля("ИзначальныйСклад") Цикл
						Пока Выборка.СледующийПоЗначениюПоля("РасходныйОрдер") Цикл
							Если НЕ ЗначениеЗаполнено(Выборка.РасходныйОрдер) Тогда
								РасхОрдерОбъект = Документы.РасходныйОрдерСклад.СоздатьДокумент();
								РасхОрдерОбъект.НеМенятьДатуПриПроведении = Истина;
								РасхОрдерОбъект.Дата			= ДатаРаспределения;
								РасхОрдерОбъект.УстановитьНовыйНомер();
								РасхОрдерОбъект.Автор 			= ПараметрыСеанса.ТекущийПользователь;
								РасхОрдерОбъект.ВидОперации		= Перечисления.ВидыОперацийРасходСкладскойУчет.ОтгрузкаВТорговуюТочку;
								РасхОрдерОбъект.Получатель 		= Выборка.ТорговаяТочка;
								РасхОрдерОбъект.Склад 			= Выборка.Склад;
								РасхОрдерОбъект.Организация     = Организация;
								
								РасхОрдерОбъект.СкладСборкиДляЗоны = Выборка.ИзначальныйСклад;
								РасхОрдерОбъект.АвтозагрузкаУРЗ = Истина;
								РасхОрдерОбъект.Статус 			= Перечисления.СтатусыРасходныхОрдеров.НеОбработан;
								РасхОрдерОбъект.ДатаРаспределения 			= ДатаРаспределения;
								Если Выборка.Склад <> Выборка.ИзначальныйСклад Тогда
									РасхОрдерОбъект.Комментарий = "По складу: " + Выборка.ИзначальныйСклад;
								КонецЕсли;	
							Иначе
								РасхОрдерОбъект = Выборка.РасходныйОрдер.ПолучитьОбъект();
							КонецЕсли;
							Пока Выборка.Следующий() Цикл
								х = х+1;
								КоличествоКЗагрузке = Выборка.Количество;
								Если КоличествоКЗагрузке <= 0.01 Тогда
									Продолжить;
								КонецЕсли;
								МасСтр=ТЗПозицииСЗаданиямиНаРазборку.НайтиСтроки(
								Новый Структура("Номенклатура,ТТ",Выборка.Номенклатура,Выборка.ТорговаяТочка));
								Если МасСтр.Количество() Тогда
									Продолжить;
								КонецЕсли; 
								СтрокиТовар = РасхОрдерОбъект.Товары.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, ДатаПроизводства", Выборка.Номенклатура, Выборка.Характеристика, Выборка.ДатаПроизводства));
								Если СтрокиТовар.Количество() > 0 Тогда
									СтрокаТовар = СтрокиТовар[0];
								Иначе
									СтрокаТовар = РасхОрдерОбъект.Товары.Добавить();
								КонецЕсли;	
								СтрокаТовар.Номенклатура 		= Выборка.Номенклатура;
								СтрокаТовар.Характеристика 		= Выборка.Характеристика;
								СтрокаТовар.ДатаПроизводства 	= Выборка.ДатаПроизводства;
								СтрокаТовар.НомерРаспределения	= Выборка.НомерРаспределения;
								СтрокаТовар.ЕдиницаИзмерения 	= Выборка.Номенклатура.ЕдиницаХраненияОстатков;
								СтрокаТовар.КоличествоУРЗ 		= СтрокаТовар.КоличествоУРЗ + КоличествоКЗагрузке;
								СтрокаТовар.Количество 			= СтрокаТовар.Количество + КоличествоКЗагрузке;
								
								НомераРаспределений.Вставить(Выборка.НомерРаспределения);
								
								ДлительныеОперации.СообщитьПрогресс(Окр(х*100/х100,0), "Создание документов");
							КонецЦикла;
							
							Если НЕ РасхОрдерОбъект.ЭтоНовый()
									ИЛИ РасхОрдерОбъект.Товары.Количество() > 0 Тогда
								РасхОрдерОбъект.Товары.Сортировать("Номенклатура");
								РасхОрдерОбъект.ОбменДанными.Загрузка = Истина;
								РасхОрдерОбъект.Проведен = Истина;
								Попытка
									РасхОрдерОбъект.Записать(РежимЗаписиДокумента.Запись);
									
									СтрокаРасхОрдер = МаршЛистОбъект.РасходныеОрдера.Найти(РасхОрдерОбъект.Ссылка, "Документ");
									Если СтрокаРасхОрдер = Неопределено Тогда
										СтрокаРасхОрдер = МаршЛистОбъект.РасходныеОрдера.Добавить();
										СтрокаРасхОрдер.Документ = РасхОрдерОбъект.Ссылка;
									КонецЕсли;
									
								Исключение
									СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,РасхОрдерОбъект.Ссылка,-1,ОписаниеОшибки());
								КонецПопытки;
								КоличествоДокументов = КоличествоДокументов+1;
							КонецЕсли;	
						КонецЦикла;
					КонецЦикла;	
				КонецЦикла;
			КонецЦикла;	
			Если НЕ МаршЛистОбъект.ЭтоНовый()
					ИЛИ МаршЛистОбъект.РасходныеОрдера.Количество() > 0 Тогда
				МаршЛистОбъект.ОбменДанными.Загрузка = Истина;
				МаршЛистОбъект.Проведен = Истина;
				Попытка
					МаршЛистОбъект.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,МаршЛистОбъект.Ссылка,-1,ОписаниеОшибки());
				КонецПопытки;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	УстановитьСостояниеПоНомерамРаспределений(НомераРаспределений, Перечисления.СостоянияРаспределенияТоваров.РасходникиЗагружены);
	ПеренестиВАрхивОстаткиРаспределенныеПоТТSQL(НомераРаспределений);
	
	СкладыСервер.ЗаписатьЛогЗагрузкиДокументовПоРаспределению(Склад,Неопределено, КоличествоДокументов,"Ок");
	Возврат КоличествоДокументов;

КонецФункции

//+++АК LATV 2018.10.31 ИП-00020276
Функция ЗаполнитьПоРаспределениюНаСервереПоЗонам(СтруктураПараметров, ТабТТ, ТабСклады)

	ДатаРаспределения			= СтруктураПараметров.ДатаРаспределения;
	Организация					= СтруктураПараметров.Организация;
	СтруктурноеПодразделение	= СтруктураПараметров.СтруктурноеПодразделение;
	НомерРаспределения			= СтруктураПараметров.НомерРаспределения;
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Склад",					Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаДанных.Колонки.Добавить("ЗонаОтгрузки",			Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаДанных.Колонки.Добавить("ТорговаяТочка",			Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы"));
	ТаблицаДанных.Колонки.Добавить("Номенклатура",			Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаДанных.Колонки.Добавить("ЕдиницаИзмерения",		Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	ТаблицаДанных.Колонки.Добавить("Характеристика",		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаДанных.Колонки.Добавить("Количество",			Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("НомерРаспределения",	Новый ОписаниеТипов("Число"));
	
	Запрос = Новый Запрос();
	Запрос.Текст = ПолучитьТекстЗапроса_ОстаткиРаспределенныеПоТТ();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаРаспределения", ДатаРаспределения);
	Запрос.УстановитьПараметр("НомерРаспределения", НомерРаспределения);
	Запрос.УстановитьПараметр("НеОтбиратьПоНомеру", НомерРаспределения=0);
	
	Результаты = Запрос.ВыполнитьПакет();
	//НомераРаспределений	= Результаты[4].Выгрузить().ВыгрузитьКолонку("НомерРаспределения"); //latv удалить
	ТабРаспределение	= Результаты[5].Выгрузить();
	
	ТабТТРаспр = ТабРаспределение.Скопировать();
	ТабТТРаспр.Свернуть("ТорговаяТочка");
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЗ_Запрос.Номенклатура,
		|	ВЗ_Запрос.СтруктурноеПодразделение,
		|	ВЗ_Запрос.ТорговаяТочка
		|ИЗ
		|	(ВЫБРАТЬ
		|		спрНоменклатура.Ссылка КАК Номенклатура,
		|		ПорядокОбеспеченияТорговыхТочекСрезПоследних.Расчетчик КАК СтруктурноеПодразделение,
		|		ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка КАК ТорговаяТочка
		|	ИЗ
		|		РегистрСведений.ПорядокОбеспеченияТорговыхТочек.СрезПоследних(
		|				КОНЕЦПЕРИОДА(&ДатаРаспределения, ДЕНЬ),
		|				ТорговаяТочка В (&МассивТТ)
		|					И ТорговаяТочка.id_TT <> 0) КАК ПорядокОбеспеченияТорговыхТочекСрезПоследних
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|			ПО ПорядокОбеспеченияТорговыхТочекСрезПоследних.ГруппаУРЗ = спрНоменклатура.ГруппаНоменклатурыУРЗ
		|	ГДЕ
		|		спрНоменклатура.ЭтоГруппа = ЛОЖЬ) КАК ВЗ_Запрос
		|ГДЕ
		|	ВЗ_Запрос.СтруктурноеПодразделение = &СтруктурноеПодразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	спрНоменклатура.Ссылка КАК Номенклатура,
		|	ДоступностьТоваровНаСкладах.Склад КАК Склад
		|ИЗ
		|	РегистрСведений.ДоступностьТоваровНаСкладах КАК ДоступностьТоваровНаСкладах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДоступностьТоваровНаСкладах.Номенклатура = спрНоменклатура.Ссылка
		|ГДЕ
		|	ДоступностьТоваровНаСкладах.Склад В(&Склады)
		|	И ДоступностьТоваровНаСкладах.Склад.Организация = &Организация
		|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	спрНоменклатура.Ссылка КАК Номенклатура,
		|	ДоступностьТоваровНаСкладах.Склад КАК Склад
		|ИЗ
		|	РегистрСведений.ДоступностьТоваровНаСкладах КАК ДоступностьТоваровНаСкладах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДоступностьТоваровНаСкладах.Номенклатура = спрНоменклатура.Ссылка
		|ГДЕ
		|	ДоступностьТоваровНаСкладах.Склад.Владелец = &СтруктурноеПодразделение
		|	И ДоступностьТоваровНаСкладах.Склад.Организация = &Организация
		|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаршрутыСклады.Склад,
		|	МаршрутыСклады.Склад.НеСоздаватьПеремещениеВЗонуОтгрузки,
		|	МаршрутыСклады.ЗонаОтгрузки КАК ОтгружаетсяИзЗоны,
		|	МаршрутыТорговыеТочки.СтруктурнаяЕдиница КАК ТорговаяТочка
		|ИЗ
		|	Справочник.Маршруты.ТорговыеТочки КАК МаршрутыТорговыеТочки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Маршруты.Склады КАК МаршрутыСклады
		|		ПО МаршрутыТорговыеТочки.Ссылка = МаршрутыСклады.Ссылка
		|ГДЕ
		|	МаршрутыСклады.Ссылка.СтруктурноеПодразделение = &СтруктурноеПодразделение";
	
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("Организация", Организация);

	Запрос.УстановитьПараметр("МассивТТ", ТабТТРаспр.ВыгрузитьКолонку("ТорговаяТочка"));
	Запрос.УстановитьПараметр("Склады", ТабСклады.ВыгрузитьКолонку("Склад"));
	Запрос.УстановитьПараметр("Таб", ТабРаспределение);
	
	Результаты = Запрос.ВыполнитьПакет();
	НастройкиПорядкаОбеспечения = Результаты[0].Выгрузить();
	НастройкиПорядкаОбеспечения.Индексы.Добавить("ТорговаяТочка, Номенклатура");
	НастройкиДоступностиТоваров = Результаты[1].Выгрузить();
	НастройкиДоступностиТоваров.Индексы.Добавить("Номенклатура");
	НастройкиДоступностиТоваровДляСтруктПодразделения = Результаты[2].Выгрузить();
	НастройкиДоступностиТоваровДляСтруктПодразделения.Индексы.Добавить("Номенклатура");
	ТабНастройкиЗонПоМаршрутам = Результаты[3].Выгрузить();
	ТабНастройкиЗонПоМаршрутам.Индексы.Добавить("Склад, ТорговаяТочка");
	
	ТабНоменклатураИСкладыРаспределение = НастройкиДоступностиТоваров.Скопировать();
	
	ОсновнойСклад = СтруктурноеПодразделение.СкладТорговогоЗала;
	МассивСкладовМагазинаСтороннейРозницы = Новый Массив;//shae 2018.10.11 ИП-00020073
	Для Каждого СтрокаРаспределение Из ТабРаспределение Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаРаспределение.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиПорядкаОбеспечения = НастройкиПорядкаОбеспечения.НайтиСтроки(Новый Структура("ТорговаяТочка, Номенклатура", СтрокаРаспределение.ТорговаяТочка, СтрокаРаспределение.Номенклатура));
		Если СтрокиПорядкаОбеспечения.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Склад = Неопределено;
		СтрокаСклад = НастройкиДоступностиТоваров.Найти(СтрокаРаспределение.Номенклатура, "Номенклатура");
		Если СтрокаСклад = Неопределено Тогда
			СтрокаОбщиеНастройки = НастройкиДоступностиТоваровДляСтруктПодразделения.Найти(СтрокаРаспределение.Номенклатура, "Номенклатура");
			Если СтрокаОбщиеНастройки = Неопределено Тогда
				Если ЗначениеЗаполнено(Склад)
					И Склад = ОсновнойСклад Тогда
					Склад = ОсновнойСклад;
				КонецЕсли;	
			КонецЕсли;
		Иначе
			Склад = СтрокаСклад.Склад;
		КонецЕсли;	
		
		Если НЕ ЗначениеЗаполнено(Склад) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаДанных.Добавить();
		НоваяСтрока.Склад = Склад;
		
		//+++shae 2018.10.10 ИП-00020073
		Если ЗначениеЗаполнено(СтрокаРаспределение.СкладМагазинаСтороннейРозницы) Тогда 
			Если МассивСкладовМагазинаСтороннейРозницы.Найти(СтрокаРаспределение.СкладМагазинаСтороннейРозницы) = Неопределено Тогда 
				МассивСкладовМагазинаСтороннейРозницы.Добавить(СтрокаРаспределение.СкладМагазинаСтороннейРозницы);
			КонецЕсли;
			ТорговаяТочка 		  = СтрокаРаспределение.СкладМагазинаСтороннейРозницы;
		Иначе
			ТорговаяТочка 		  = СтрокаРаспределение.ТорговаяТочка;
		КонецЕсли;		
		СтрокиНастройкаЗоны = ТабНастройкиЗонПоМаршрутам.НайтиСтроки(Новый Структура("Склад, ТорговаяТочка", Склад, ТорговаяТочка));
		//---shae 2018.10.10 ИП-00020073 	
		Если СтрокиНастройкаЗоны.Количество() > 0
			И ЗначениеЗаполнено(СтрокиНастройкаЗоны[0].ОтгружаетсяИзЗоны)
			И НЕ СтрокиНастройкаЗоны[0].СкладНеСоздаватьПеремещениеВЗонуОтгрузки Тогда
			НоваяСтрока.ЗонаОтгрузки = СтрокиНастройкаЗоны[0].ОтгружаетсяИзЗоны;
		КонецЕсли;
		
		НоваяСтрока.ТорговаяТочка 			= ТорговаяТочка;
		НоваяСтрока.Номенклатура 		= СтрокаРаспределение.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения 		= СтрокаРаспределение.Номенклатура.ЕдиницаХраненияОстатков;
		Если НЕ СтрокаРаспределение.НеВедетсяУчетПоХарактеристикам Тогда
			НоваяСтрока.Характеристика 		= СтрокаРаспределение.Характеристика;
		КонецЕсли;
		НоваяСтрока.Количество	 		= СтрокаРаспределение.Количество;
		НоваяСтрока.НомерРаспределения	= СтрокаРаспределение.НомерРаспределения;
		
	КонецЦикла;
	
	//+++shae 2018.10.11 ИП-00020073 
	Если МассивСкладовМагазинаСтороннейРозницы.Количество()>0 Тогда 
		СвернутьТаблицуДанных(ТаблицаДанных);
		ОтредактироватьПотребностьСУчетомОстатковСклада(ТаблицаДанных,МассивСкладовМагазинаСтороннейРозницы, ДатаРаспределения, СтруктурноеПодразделение);
	КонецЕсли;
	//---shae 2018.10.11 ИП-00020073 
	
	Возврат ТаблицаДанных;

КонецФункции

#КонецОбласти

#Область ПереносВАрхив

//+++АК LATV 2018.10.30 ИП-00020294
Процедура ПеренестиВАрхивОстаткиРаспределенныеПоТТSQL(НомераРаспределений)

	ADOСоединение = Новый COMОбъект("ADODB.Connection");
	ADOСоединение.ConnectionTimeOut	= 0;
	ADOСоединение.CommandTimeOut	= 0;
	ADOСоединение.ConnectionString	= ОбменСAccess.ПолучитьСтрокуСоединения("M2");
	ADOСоединение.Open();
	
	Для Каждого НомерРаспределения Из НомераРаспределений Цикл
		
		ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"exec M2.dbo.clear_inforg4020_1c %1", Формат(НомерРаспределения.Ключ, "ЧГ=0"));
		
		Попытка
			ADOСоединение.Execute(ТекстЗапроса);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось очистить данные по распределению %1 по причине: %2'"), НомерРаспределения.Ключ, ОписаниеОшибки());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	ADOСоединение.Close();
	
	// После реструктуризации можно включить этот код
	//Для Каждого НомерРаспределения Из НомераРаспределений Цикл
	//	ВнешниеИсточникиДанных.m2.clear_inforg4020_1c(НомерРаспределения);
	//КонецЦикла;

КонецПроцедуры

#КонецОбласти

//+++shae 2018.10.11 ИП-00020073
Процедура СвернутьТаблицуДанных(ТаблицаДанных)
	
	Если ТаблицаДанных.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстПоля = "ВЫБРАТЬ";
	ТекстГруппировки = "";
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ";
	Для Каждого Колонка из ТаблицаДанных.Колонки Цикл 
		Если Колонка.Имя = "НомерРаспределения" Или Колонка.Имя = "Количество" Тогда 
			Продолжить;	
		КонецЕсли;	
		ТекстПоле = "
					 |Табл." + Колонка.Имя + ",";	
		Запрос.Текст 		= Запрос.Текст + ТекстПоле;
		ТекстПоле = СтрЗаменить(ТекстПоле, "Табл.", "ВТ_ТаблицаДанных.");
		ТекстПоля 			= ТекстПоля + ТекстПоле;
		ТекстГруппировки 	= ТекстГруппировки + ТекстПоле;	
	КонецЦикла;	
	Запрос.Текст =  Запрос.Текст + "
					|Табл.НомерРаспределения, Табл.Количество 
					| 					
					|ПОМЕСТИТЬ ВТ_ТаблицаДанных
					|ИЗ
					|	&Табл КАК Табл
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|" + ТекстПоля + "
					|МАКСИМУМ(ВТ_ТаблицаДанных.НомерРаспределения) КАК НомерРаспределения, СУММА(ВТ_ТаблицаДанных.Количество) КАК Количество
					| 
					|ИЗ
					|	ВТ_ТаблицаДанных КАК ВТ_ТаблицаДанных
					|
					|СГРУППИРОВАТЬ ПО
					|" + Лев(ТекстГруппировки, СтрДлина(ТекстГруппировки)-1);	
	Запрос.УстановитьПараметр("Табл", ТаблицаДанных);	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры	

//+++shae 2018.10.21 ИП-00020073 
Процедура ОтредактироватьПотребностьСУчетомОстатковСклада(ТаблицаДанных, Склады, ДатаРаспределения, СтруктурноеПодразделение)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб.ТорговаяТочка,
	|	Таб.Номенклатура,
	|	Таб.Характеристика,
	|	Таб.Количество
	|ПОМЕСТИТЬ ВТ_ТаблицаДанных
	|ИЗ
	|	&Табл КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДанных.ТорговаяТочка,
	|	ВТ_ТаблицаДанных.Номенклатура,
	|	ВТ_ТаблицаДанных.Характеристика,
	|	ВТ_ТаблицаДанных.Количество
	|ПОМЕСТИТЬ ВТ_ТаблицаДанныхСклад
	|ИЗ
	|	ВТ_ТаблицаДанных КАК ВТ_ТаблицаДанных
	|ГДЕ
	|	ВТ_ТаблицаДанных.ТорговаяТочка В(&ТорговаяТочка)
	|	И ВТ_ТаблицаДанных.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДанныхСклад.ТорговаяТочка,
	|	ВТ_ТаблицаДанныхСклад.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаДанныхСклад.Характеристика КАК Характеристика,
	|	ВТ_ТаблицаДанныхСклад.Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХарактеристикиСпр.БратьКоличествоВКоробкеПоСкладуДляРаспределения, ЛОЖЬ)
	|			ТОГДА ЕСТЬNULL(КоличествоВКоробкеСрезПоследних.Количество, 0)
	|		ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, 0) КАК ЧИСЛО(15, 3))
	|	КОНЕЦ КАК КоличествоВКоробке,
	|	НоменклатураСпр.БезУпаковки = ЛОЖЬ
	|		И НоменклатураСпр.РаспределяетсяНеЦелымиКоробками = ЛОЖЬ КАК РаспределяетьКоробками,
	|	НоменклатураСпр.id_tov
	|ИЗ
	|	ВТ_ТаблицаДанныхСклад КАК ВТ_ТаблицаДанныхСклад
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ПО (ЗначенияСвойствОбъектов.Объект = ВТ_ТаблицаДанныхСклад.Характеристика)
	|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.КоличествоВУпаковке))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоличествоВКоробке.СрезПоследних(, СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК КоличествоВКоробкеСрезПоследних
	|		ПО ВТ_ТаблицаДанныхСклад.Номенклатура = КоличествоВКоробкеСрезПоследних.Номенклатура
	|			И ВТ_ТаблицаДанныхСклад.Характеристика = КоличествоВКоробкеСрезПоследних.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО ВТ_ТаблицаДанныхСклад.Номенклатура = НоменклатураСпр.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиСпр
	|		ПО ВТ_ТаблицаДанныхСклад.Характеристика = ХарактеристикиСпр.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаДанных";
	
	Запрос.УстановитьПараметр("Табл", 					ТаблицаДанных);
	Запрос.УстановитьПараметр("ТорговаяТочка",  		Склады);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница",  	СтруктурноеПодразделение);
	ТаблицаДанныхСклады = Запрос.Выполнить().Выгрузить();
	ТаблицаДанныхСклады.Индексы.Добавить("ТорговаяТочка, id_tov");

	ADOСоединение = ВнешниеДанные.ПолучитьADOСоединение();
	
	Для Каждого Склад из Склады Цикл 
		
		//остатки на складе магазине сторонней розницы
		ТекстЗапросаSQL = "Select id_tov, КонОст
		|FROM  SMS_REPL.dbo.List_Ucheta (" + ВнешниеДанные.ФорматПоля(Склад.НомерТочки) + "," + ВнешниеДанные.ФорматПоля(ДатаРаспределения) + "," + ВнешниеДанные.ФорматПоля(ДатаРаспределения) + ")
		|WHERE КонОст > 0";	
		
		rs = ADOСоединение.Execute(ТекстЗапросаSQL);
		Попытка
			rs.MoveFirst();
			Пока НЕ rs.Eof() Цикл
				id_tov 			= rs.Fields("id_tov").Value;
				КонОст 			= rs.Fields("КонОст").Value;
				СтрокиТаблицыДанныхСклады = ТаблицаДанныхСклады.НайтиСтроки(Новый Структура("ТорговаяТочка, id_tov",Склад, id_tov));	
				Для Каждого Стр Из СтрокиТаблицыДанныхСклады Цикл 
					Если КонОст > 0 Тогда
						КоличествоДо 	= Стр.Количество;
						КоличествоЕсть 	= Мин(КонОст, Стр.Количество); 
						Стр.Количество 	= Стр.Количество - КоличествоЕсть;  //уменьшение на количество, что есть на складе магазине сторонней розницы
						Если Стр.РаспределяетьКоробками 
							И Стр.Количество > 0
							И Стр.КоличествоВКоробке > 0 Тогда     //измененное количество должно быть = целым коробкам
							КоличествоКоробок 		= Стр.Количество/Стр.КоличествоВКоробке;
							ЦелКоличествоКоробок 	= Цел(КоличествоКоробок);
							Если ЦелКоличествоКоробок < КоличествоКоробок Тогда 
								Стр.Количество = (ЦелКоличествоКоробок+1)*Стр.КоличествоВКоробке;
							КонецЕсли;			
						КонецЕсли;
						КоличествоБеремСоСклада = КоличествоДо - Стр.Количество;
						Если КоличествоБеремСоСклада > 0 Тогда 
							СтрокиТаблицыДанных = ТаблицаДанных.НайтиСтроки(Новый Структура("ТорговаяТочка, Номенклатура, Характеристика, Количество", Склад, Стр.Номенклатура, Стр.Характеристика, КоличествоДо));
							Если СтрокиТаблицыДанных.Количество() > 0 Тогда
								КонОст = КонОст - КоличествоБеремСоСклада;
								СтрокиТаблицыДанных[0].Количество = Стр.Количество;
							КонецЕсли;		
						КонецЕсли;	
					Иначе 
						Прервать;
					КонецЕсли;
				КонецЦикла;	
				
				rs.MoveNext();
			КонецЦикла;
		Исключение
		КонецПопытки;		
	КонецЦикла;	
	
	ADOСоединение.Close();
	
КонецПроцедуры	

//+++АК SHEP 2018.10.31 ИП-00020026.01
Процедура УстановитьСостояниеПоНомерамРаспределений(НомераРаспределений, НовоеСостояние)

	Перем МассивНомеровРаспределений;
	
	МассивНомеровРаспределений = Новый Массив;
	Для Каждого НомерРаспределения Из НомераРаспределений Цикл
		МассивНомеровРаспределений.Добавить(Формат(НомерРаспределения.Ключ, "ЧГ="));
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РаспределенияТоваров.УИД,
		|	РаспределенияТоваров.Номер
		|ИЗ
		|	РегистрСведений.РаспределенияТоваров КАК РаспределенияТоваров
		|ГДЕ
		|	РаспределенияТоваров.Номер В (&МассивНомеровРаспределений)");
	Запрос.УстановитьПараметр("МассивНомеровРаспределений", МассивНомеровРаспределений);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда Возврат; КонецЕсли;
	
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		РегистрыСведений.СостоянияРаспределенийТоваров.ИзменитьСостояние(ВыборкаЗапроса.УИД, НовоеСостояние);
	КонецЦикла;

КонецПроцедуры

#Область ТекстЗапроса

//+++АК KIRN 2018.05.24 ИП-00018743
Функция ТекстЗапросаПолучитьЗаданияНаРазборкуДляПерезаписи()
	Возврат "
	|////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданиеНаРазборку.Ссылка,
	|	ЗаданиеНаРазборку.Склад КАК Склад,
	|	ЗаданиеНаРазборку.Номенклатура КАК Номенклатура,
	|	ЗаданиеНаРазборку.Характеристика КАК Характеристика
	//|	ЗаданиеНаРазборкуТовары.СтруктурнаяЕдиница КАК ТорговаяТочка
	|ИЗ
	|	Документ.ЗаданиеНаРазборку.Товары КАК ЗаданиеНаРазборкуТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаРазборку КАК ЗаданиеНаРазборку
	|		ПО ЗаданиеНаРазборкуТовары.Ссылка = ЗаданиеНаРазборку.Ссылка
	|ГДЕ
	|	ЗаданиеНаРазборку.ПометкаУдаления = ЛОЖЬ
	|	И ЗаданиеНаРазборкуТовары.РасходныйОрдер = ЗНАЧЕНИЕ(Документ.РасходныйОрдерСклад.ПустаяСсылка)
	|	И НАЧАЛОПЕРИОДА(ЗаданиеНаРазборку.Дата, ДЕНЬ) = &ДатаРаспределения
	|	И ЗаданиеНаРазборку.Напечатан = ЛОЖЬ
	|	И ЗаданиеНаРазборку.Склад.Владелец = &СтруктурноеПодразделение
	|	И (ЗаданиеНаРазборку.Склад = &Склад
	|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|
	|";
КонецФункции

//+++АК KIRN 2018.05.24 ИП-00018743
Функция ТекстЗапросаПолучитьСкладыТТ();
	Возврат "
	|////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦФОСтруктурныхЕдиницСрезПоследних.СтруктурнаяЕдиница,
	|	ЦФОСтруктурныхЕдиницСрезПоследних.Организация
	|ПОМЕСТИТЬ втТТ
	|ИЗ
	|	РегистрСведений.ЦФОСтруктурныхЕдиниц.СрезПоследних КАК ЦФОСтруктурныхЕдиницСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка КАК ТТ,
	|	втТТ.Организация
	|ИЗ
	|	РегистрСведений.ПорядокОбеспеченияТорговыхТочек.СрезПоследних(&ДатаРаспределения, Расчетчик = &СтруктурноеПодразделение) КАК ПорядокОбеспеченияТорговыхТочекСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТТ КАК втТТ
	|		ПО ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка = втТТ.СтруктурнаяЕдиница
	|ГДЕ
	|	втТТ.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПорядокОбеспеченияТорговыхТочекСрезПоследних.ТорговаяТочка,
	|	втТТ.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Владелец = &СтруктурноеПодразделение
	|	И (Склады.Ссылка = &Склад
	|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И Склады.Организация = &Организация";	
КонецФункции

//+++АК KIRN 2018.05.24 ИП-00018743
Функция ПолучитьТекстЗапросаЗаданияНаРазборкуНеДляЗаписи()
	Возврат "ВЫБРАТЬ
	        |	ЗаданиеНаРазборку.Склад,
	        |	ЗаданиеНаРазборкуТовары.СтруктурнаяЕдиница КАК ТорговаяТочка,
	        |	ЗаданиеНаРазборку.Номенклатура,
	        |	ЗаданиеНаРазборку.Характеристика,
	        |	СУММА(ЗаданиеНаРазборкуТовары.КоличествоУРЗ) КАК КоличествоУРЗ
	        |ИЗ
	        |	Документ.ЗаданиеНаРазборку.Товары КАК ЗаданиеНаРазборкуТовары
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаРазборку КАК ЗаданиеНаРазборку
	        |		ПО ЗаданиеНаРазборкуТовары.Ссылка = ЗаданиеНаРазборку.Ссылка
	        |ГДЕ
	        |	ЗаданиеНаРазборку.Проведен = ИСТИНА
	        |	И ЗаданиеНаРазборкуТовары.Количество > 0
	        |	И НАЧАЛОПЕРИОДА(ЗаданиеНаРазборку.Дата, ДЕНЬ) = &ДатаРаспределения
	        |	И ЗаданиеНаРазборку.Напечатан = ИСТИНА
			//|	И ЗаданиеНаРазборкуТовары.РасходныйОрдер <> ЗНАЧЕНИЕ(Документ.РасходныйОрдерСклад.ПустаяСсылка)
			//+++АК KIRN 2018.08.28 
			|	И (ЗаданиеНаРазборку.Склад = &Склад
			|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
			//---АК KIRN 
			|
	        |СГРУППИРОВАТЬ ПО
	        |	ЗаданиеНаРазборку.Склад,
	        |	ЗаданиеНаРазборкуТовары.СтруктурнаяЕдиница,
	        |	ЗаданиеНаРазборку.Номенклатура,
	        |	ЗаданиеНаРазборку.Характеристика
			|;	";
КонецФункции

//+++АК KIRN 2018.05.24 ИП-00018743
Функция ПолучитьТекстЗапросаОстаткиТоваровНаСкладе()
	Возврат  "
	|////////////////////////////////////////////////////////	
	|	ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Склад.Организация КАК Организация,
	|	ТоварыНаСкладахОстатки.Склад КАК Склад,
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ТоварыНаСкладахОстатки.ДатаПроизводства КАК ДатаПроизводства,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			Склад.Владелец = &СтруктурноеПодразделение
	|				И Склад.Организация = &Организация
	|				И ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)) КАК ТоварыНаСкладахОстатки
	|ГДЕ
	|	ТоварыНаСкладахОстатки.КоличествоОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Склад,
	|	Номенклатура,
	|	Характеристика,
	|	ДатаПроизводства
	|;
	|";	
	
КонецФункции

//+++АК KIRN 2018.05.29 ИП-00018743
Функция ТекстЗапросаПолучитьРасхОрдераНеДляЗаписи()
	Возврат "
	|//0//////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходныйОрдерСклад.Организация,
	|	РасходныйОрдерСклад.Склад,
	|	РасходныйОрдерСклад.Получатель КАК ТорговаяТочка,
	|	РасходныйОрдерСкладТовары.Номенклатура,
	|	РасходныйОрдерСкладТовары.Характеристика,
	|	СУММА(РасходныйОрдерСкладТовары.КоличествоУРЗ) КАК КоличествоУРЗ
	|ИЗ
	|	Документ.РасходныйОрдерСклад.Товары КАК РасходныйОрдерСкладТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерСклад КАК РасходныйОрдерСклад
	|		ПО РасходныйОрдерСкладТовары.Ссылка = РасходныйОрдерСклад.Ссылка
	|ГДЕ
	|	РасходныйОрдерСклад.Проведен = ИСТИНА
	|	И РасходныйОрдерСкладТовары.КоличествоУРЗ > 0
	|	И НАЧАЛОПЕРИОДА(РасходныйОрдерСклад.ДатаРаспределения, ДЕНЬ) = &ДатаРаспределения
	//+++АК KIRN 2018.08.28  
	|	И (РасходныйОрдерСклад.Склад = &Склад
	|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	//---АК KIRN 
	|	И РасходныйОрдерСклад.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ВСборке)
	|	И	(
	|        		РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ОсновнаяПоставка)
	|			ИЛИ	РасходныйОрдерСклад.ПричинаПеревозки = ЗНАЧЕНИЕ(Перечисление.ПричиныПеревозки.ПустаяСсылка)
	|		)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходныйОрдерСклад.Организация,
	|	РасходныйОрдерСклад.Склад,
	|	РасходныйОрдерСклад.Получатель,
	|	РасходныйОрдерСкладТовары.Номенклатура,
	|	РасходныйОрдерСкладТовары.Характеристика
	|;
	|";
КонецФункции

//+++АК LATV 2018.10.31 ИП-00020276
Функция ПолучитьТекстЗапроса_ОстаткиРаспределенныеПоТТ()

	Возврат
		"ВЫБРАТЬ
		|	ЦФОСтруктурныхЕдиницСрезПоследних.СтруктурнаяЕдиница,
		|	ЦФОСтруктурныхЕдиницСрезПоследних.Организация
		|ПОМЕСТИТЬ втТТ
		|ИЗ
		|	РегистрСведений.ЦФОСтруктурныхЕдиниц.СрезПоследних КАК ЦФОСтруктурныхЕдиницСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиРаспределенныеПоТТ.Номенклатура КАК Номенклатура,
		|	ОстаткиРаспределенныеПоТТ.Характеристика КАК Характеристика,
		|	ОстаткиРаспределенныеПоТТ.ТорговаяТочка КАК ТорговаяТочка,
		|	ОстаткиРаспределенныеПоТТ.Количество КАК Количество,
		|	ОстаткиРаспределенныеПоТТ.НомерРаспределения КАК НомерРаспределения
		|ПОМЕСТИТЬ вт_ОстаткиРаспр
		|ИЗ
		|	РегистрСведений.ОстаткиРаспределенныеПоТТ КАК ОстаткиРаспределенныеПоТТ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТТ КАК втТТ
		|		ПО ОстаткиРаспределенныеПоТТ.ТорговаяТочка = втТТ.СтруктурнаяЕдиница
		|ГДЕ
		|	ОстаткиРаспределенныеПоТТ.Дата = &ДатаРаспределения
		|	И (ОстаткиРаспределенныеПоТТ.НомерРаспределения = &НомерРаспределения
		|			ИЛИ &НеОтбиратьПоНомеру)
		|	И втТТ.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиРаспределенныеПоТТ.Номенклатура КАК Номенклатура,
		|	ОстаткиРаспределенныеПоТТ.Характеристика КАК Характеристика,
		|	ОстаткиРаспределенныеПоТТ.ТорговаяТочка КАК ТорговаяТочка
		|ПОМЕСТИТЬ вт_ОстаткиРаспр0
		|ИЗ
		|	вт_ОстаткиРаспр КАК ОстаткиРаспределенныеПоТТ
		|ГДЕ
		|	ОстаткиРаспределенныеПоТТ.Номенклатура <> ОстаткиРаспределенныеПоТТ.Характеристика.Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЗ_Запрос.Номенклатура,
		|	ВЗ_Запрос.ХарактеристикаНеПравильная,
		|	ВЗ_Запрос.ТорговаяТочка,
		|	МАКСИМУМ(ЗначенияСвойствОбъектов.Объект) КАК ХарактеристикаПравильная
		|ПОМЕСТИТЬ ВТ_Коллизии
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОстаткиРаспределенныеПоТТ.Номенклатура КАК Номенклатура,
		|		ОстаткиРаспределенныеПоТТ.Характеристика КАК ХарактеристикаНеПравильная,
		|		ОстаткиРаспределенныеПоТТ.ТорговаяТочка КАК ТорговаяТочка,
		|		ЗначенияСвойствОбъектов.Значение КАК Поставщик
		|	ИЗ
		|		вт_ОстаткиРаспр0 КАК ОстаткиРаспределенныеПоТТ
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|			ПО ОстаткиРаспределенныеПоТТ.Характеристика = ЗначенияСвойствОбъектов.Объект
		|				И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))) КАК ВЗ_Запрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|		ПО (ВЗ_Запрос.Номенклатура = ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Объект КАК Справочник.ХарактеристикиНоменклатуры).Владелец)
		|			И ВЗ_Запрос.Поставщик = ЗначенияСвойствОбъектов.Значение
		|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.Производитель))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЗ_Запрос.Номенклатура,
		|	ВЗ_Запрос.ХарактеристикаНеПравильная,
		|	ВЗ_Запрос.ТорговаяТочка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	вт_ОстаткиРаспр.НомерРаспределения
		|ИЗ
		|	вт_ОстаткиРаспр КАК вт_ОстаткиРаспр
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиРаспределенныеПоТТ.Номенклатура,
		|	ВЫБОР
		|		КОГДА НЕ ВТ_Коллизии.ХарактеристикаПравильная ЕСТЬ NULL
		|			ТОГДА ВТ_Коллизии.ХарактеристикаПравильная
		|		ИНАЧЕ ОстаткиРаспределенныеПоТТ.Характеристика
		|	КОНЕЦ КАК Характеристика,
		|	ОстаткиРаспределенныеПоТТ.ТорговаяТочка,
		//+++shae 2018.10.10 ИП-00020073
		|	ОстаткиРаспределенныеПоТТ.Номенклатура.НеВедетсяУчетПоХарактеристикам КАК НеВедетсяУчетПоХарактеристикам,
		|	ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, Значение(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК СкладМагазинаСтороннейРозницы,	 	
		//---shae 2018.10.10 ИП-00020073 
		|	СУММА(ОстаткиРаспределенныеПоТТ.Количество) КАК Количество,
		|	МАКСИМУМ(ОстаткиРаспределенныеПоТТ.НомерРаспределения) КАК НомерРаспределения
		|ИЗ
		|	вт_ОстаткиРаспр КАК ОстаткиРаспределенныеПоТТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Коллизии КАК ВТ_Коллизии
		|		ПО ОстаткиРаспределенныеПоТТ.Номенклатура = ВТ_Коллизии.Номенклатура
		|			И ОстаткиРаспределенныеПоТТ.Характеристика = ВТ_Коллизии.ХарактеристикаНеПравильная
		|			И ОстаткиРаспределенныеПоТТ.ТорговаяТочка = ВТ_Коллизии.ТорговаяТочка
		//+++shae 2018.10.10 ИП-00020073
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|		ПО ОстаткиРаспределенныеПоТТ.ТорговаяТочка = ЗначенияСвойствОбъектов.Объект
		|			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.СкладМагазинаСтороннейРозницы))	 	
		//---shae 2018.10.10 ИП-00020073 	
		|ГДЕ
		|	ОстаткиРаспределенныеПоТТ.Количество <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиРаспределенныеПоТТ.Номенклатура,
		|	ОстаткиРаспределенныеПоТТ.ТорговаяТочка,
		//+++shae 2018.10.10 ИП-00020073
		|	ОстаткиРаспределенныеПоТТ.Номенклатура.НеВедетсяУчетПоХарактеристикам,	
		|	ЕСТЬNULL(ЗначенияСвойствОбъектов.Значение, Значение(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)),	 	
		//---shae 2018.10.10 ИП-00020073 
		|	ВЫБОР
		|		КОГДА НЕ ВТ_Коллизии.ХарактеристикаПравильная ЕСТЬ NULL
		|			ТОГДА ВТ_Коллизии.ХарактеристикаПравильная
		|		ИНАЧЕ ОстаткиРаспределенныеПоТТ.Характеристика
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_Коллизии";
	
КонецФункции

#КонецОбласти 

#КонецОбласти
