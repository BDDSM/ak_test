
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Организация") Тогда
		Объект.Организация = Параметры.Организация;
	Иначе
		Объект.Организация = Справочники.Организации.НайтиПоРеквизиту("ИНН", "7734675810");
	КонецЕсли;
	
	Если Параметры.Свойство("ДатаРаспределения") Тогда
		Объект.ДатаРаспределения = Параметры.ДатаРаспределения;
	КонецЕсли;
	Если Параметры.Свойство("РасходныйОрдер") Тогда
		Объект.РасходныйОрдер = Параметры.РасходныйОрдер;
	КонецЕсли;
	
	//+++АК KIRN 2018.09.25  
	Если Параметры.Свойство("Номер") Тогда
		Объект.НомерРаспределения = Параметры.Номер;
	КонецЕсли;
	
	//+++АК SHEP 2018.07.25 ИП-00019254: через структуру, т.к. таких параметров может не быть
	СтруктураПараметры = Новый Структура("СтруктурноеПодразделение,Склад");
	ЗаполнитьЗначенияСвойств(СтруктураПараметры, Параметры);
	ЗаполнитьЗначенияСвойств(Объект, СтруктураПараметры);
	
	СтруктураПараметры = Новый Структура("УИДРаспределения");
	ЗаполнитьЗначенияСвойств(СтруктураПараметры, Параметры);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураПараметры);
	//---АК SHEP 2018.07.25
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурноеПодразделение) Тогда
		Объект.СтруктурноеПодразделение = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "ОсновноеСтруктурноеПодразделение");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Склад)
		И ЗначениеЗаполнено(Объект.СтруктурноеПодразделение) Тогда
		Объект.Склад = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(ПараметрыСеанса.ТекущийПользователь, "ОсновнойСклад");
	КонецЕсли;
	
	Элементы.ГруппаБезМаршрута.Видимость = Ложь;
	
	ОперацияАпдекс = APDEX_ОценкаПроизводительностиСерверВызовСервера.ПолучитьОперацию("Загрузка распределения склада");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаРаспределения) Тогда
		Объект.ДатаРаспределения = ТекущаяДата();
	КонецЕсли;	
	
КонецПроцедуры

//+++АК KIRN 2018.04.09 ИП-00018330 
&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Справочник.Склады.Форма.ФормаВыбораУправляемая" Тогда
		Объект.Склад = ВыбранноеЗначение;
	КонецЕСлИ;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Декорация2Нажатие(Элемент)
	
	ОткрытьФорму("Справочник.Маршруты.ФормаСписка");
	
КонецПроцедуры

//+++АК KIRN 2018.04.09 ИП-00018330 
&НаКлиенте
Процедура СкладНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = ПолучитьФорму("Справочник.Склады.Форма.ФормаВыбораУправляемая",Новый Структура("Организация",Объект.Организация),ЭтаФорма);
	ФормаВыбора.Открыть();	
КонецПроцедуры

//+++АК SHEP 2018.10.30 ИП-00020026.01
&НаКлиенте
Процедура НомерРаспределенияПриИзменении(Элемент)
	УИДРаспределения = ?(ЗначениеЗаполнено(Объект.НомерРаспределения), УИДПоНомеруРаспределения(Объект.НомерРаспределения), "");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура СоздатьДокументыПоРаспределениюГруппыУРЗ(Команда)
	
	APDEX_ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(ОперацияАпдекс);
	Если НЕ ЭтаФорма.ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
		
	ТаблицаБезМаршрута.Очистить();
	Элементы.ГруппаБезМаршрута.Видимость = Ложь;
	
	//+++АК KIRN 2018.08.20  ИП-00018130
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения	= Истина;
	ПараметрыОжидания.ТекстСообщения				= "Загрузка распределения по складу "+Объект.Склад;
	ПараметрыОжидания.ОповещениеПользователя.Показать	= Истина;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение	= "Завершена загрузка распределения по складу "+Объект.Склад;
	
	ДлительнаяОперация = СоздатьДокументыПоРаспределениюСервер();
	Описание = Новый ОписаниеОповещения("ЗагрузкаДокументовРаспределенияЗавершение",ЭтаФорма);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,Описание, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыПоЗонамОтгрузки(Команда)
	
	APDEX_ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(ОперацияАпдекс);
	Если НЕ ЭтаФорма.ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
		
	ТаблицаБезМаршрута.Очистить();
	Элементы.ГруппаБезМаршрута.Видимость = Ложь;

	
	//+++АК KIRN 2018.08.20  ИП-00018130
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения	= Истина;
	ПараметрыОжидания.ТекстСообщения				= "Загрузка распределения по зонам отгрузки склада "+Объект.Склад;
	ПараметрыОжидания.ОповещениеПользователя.Показать	= Истина;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение	= "Завершена загрузка распределения по зонам отгрузки склада "+Объект.Склад;
	
	
	ДлительнаяОперация = СоздатьДокументыПоРаспределениюСерверПоЗонам();
	Описание = Новый ОписаниеОповещения("ЗагрузкаДокументовРаспределенияЗавершение",ЭтаФорма);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,Описание, ПараметрыОжидания);

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК KIRN 2018.08.16  
&НаСервере
Функция СоздатьДокументыПоРаспределениюСервер()

	ИмяМетода = "Обработки.ЗагрузитьРаспределение1.СодатьДокументыПоРаспределениюНаСервере";
	НаименованиеЗадания = "Загрузка распределения по складу "+Объект.Склад;
	
	ПараметрыЗадания = Новый Структура("НомерРаспределения, УИДРаспределения, Организация, СтруктурноеПодразделение, Склад, ДатаРаспределения",объект.НомерРаспределения, УИДРаспределения, Объект.Организация,Объект.СтруктурноеПодразделение,Объект.Склад,  Объект.ДатаРаспределения);
	
	// Запуск фонового задания
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыЗадания, ПараметрыВыполнения);

КонецФункции

//+++АК KIRN 2018.08.16  
&НаСервере
Функция СоздатьДокументыПоРаспределениюСерверПоЗонам()

	ИмяМетода = "Обработки.ЗагрузитьРаспределение1.СодатьДокументыПоРаспределениюНаСервереПоЗонам";
	НаименованиеЗадания = "Загрузка распределения по зонам отгрузки склада "+Объект.Склад;
	
	ПараметрыЗадания = Новый Структура("НомерРаспределения, УИДРаспределения, Организация, СтруктурноеПодразделение, Склад, ДатаРаспределения",объект.НомерРаспределения, УИДРаспределения, Объект.Организация,Объект.СтруктурноеПодразделение,Объект.Склад,  Объект.ДатаРаспределения);
	
	// Запуск фонового задания
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыЗадания, ПараметрыВыполнения);

КонецФункции

//+++АК LATV 2018.12.06 ИП-00020651
&НаКлиенте
Процедура ЗагрузкаДокументовРаспределенияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если ТаблицаБезМаршрута.Количество() > 0 Тогда
		ТаблицаБезМаршрута.Сортировать("ТорговаяТочка, Номенклатура");
		Элементы.ГруппаБезМаршрута.Видимость = Истина;
	КонецЕсли;	
	
	Оповестить("ЗаписанМаршрутныйЛист");	
	
	APDEX_ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(ОперацияАпдекс, "Стр. подр " + Объект.СтруктурноеПодразделение + " склад " + Объект.Склад);
	
	// Обработка результата
	Если Результат = Неопределено Тогда // Задание отменено
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Завершено %1. Статус задания: %2.'"), ТекущаяДата(), "Отменено");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Завершено %1. Статус задания: %2.'"), ТекущаяДата(), Результат.Статус);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		Если Результат.Сообщения <> Неопределено Тогда
			Для Каждого Сообщение Из Результат.Сообщения Цикл
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ВсегоДокументов			= РезультатВыполнения.Обработано;
		Документ_ОшибкиЗагрузки	= РезультатВыполнения.Документ_ОшибкиЗагрузки;
		
		Если Документ_ОшибкиЗагрузки <> Неопределено Тогда
			Документ_ОшибкиЗагрузки.ОтображатьСетку		= Ложь;
			Документ_ОшибкиЗагрузки.ОтображатьЗаголовки	= Ложь;
			
			Документ_ОшибкиЗагрузки.Показать(НСтр("ru = 'Результат загрузки распределения'"));
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Завершено %1. Статус задания: %2. Результат: %3'"), ТекущаяДата(), Результат.Статус, ВсегоДокументов);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;

КонецПРоцедуры

//+++АК SHEP 2018.10.30 ИП-00020026.01
&НаСервереБезКонтекста
Функция УИДПоНомеруРаспределения(НомерРаспределения)
	Возврат РегистрыСведений.РаспределенияТоваров.УИДПоНомеруРаспределения(НомерРаспределения);
КонецФункции

#КонецОбласти
