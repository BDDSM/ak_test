
Функция ПолучитьТабДокЦенников(МассивТоваров) Экспорт
	
	ДатаПечати = ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК НоменклатураНаименование,
	               |	ЦеныНоменклатурыСрезПоследних.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаНаименование,
	               |	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	               |ИЗ
	               |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	               |			&РабочаяДата,
	               |			Номенклатура В (&МассивТоваров)
	               |				И ТорговаяТочка = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	               |				И ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦен.ОсновнойТипЦенПродаж)) КАК ЦеныНоменклатурыСрезПоследних";
				   
	Запрос.УстановитьПараметр("РабочаяДата", ДатаПечати);
	Запрос.УстановитьПараметр("МассивТоваров", МассивТоваров);	
	
	ТзТовары = Запрос.Выполнить().Выгрузить();
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = Обработки.ПечатьЦенниковМагазин.ПолучитьМакет("Ценник");
	ОбластьЦенника                  = Макет.ПолучитьОбласть("Строка|Столбец");
	
	ТекСтолбец = 0;
	ТекСтрока  = 0;

	Для Каждого СтрокаТаблицы Из ТзТовары Цикл
		ЗаполнитьЗначенияСвойств(ОбластьЦенника.Параметры, СтрокаТаблицы);
		ОбластьЦенника.Параметры.ДатаПечати                      = ДатаПечати;
		
		Если ТекСтолбец = 0 Тогда
			ТабДокумент.Вывести(ОбластьЦенника);
		Иначе
			ТабДокумент.Присоединить(ОбластьЦенника);
		КонецЕсли;

		ТекСтолбец = ТекСтолбец + 1;

		Если ТекСтолбец = 3 Тогда
			ТекСтрока  = ТекСтрока + 1;
			ТекСтолбец = 0;
		КонецЕсли;

		Если ТекСтрока = 7 Тогда
			ТекСтрока = 0;
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;		
	КонецЦикла;

	ТабДокумент.ТолькоПросмотр = Истина;
	
	ТабДокумент.ОтображатьСетку = Ложь;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ТолькоПросмотр = Истина;
	//ТабДокумент.Показать("Ценники");

	Возврат ТабДокумент; 
	
КонецФункции

Функция ПолучитьТабДокПрайс(МассивМеста) Экспорт
	
	ДатаПечати = ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Планограмма", ПараметрыСеанса.ТорговаяТочкаПоАйпи.Планограмма);
	Запрос.УстановитьПараметр("ТорговаяТочка", ПараметрыСеанса.ТорговаяТочкаПоАйпи);
	Запрос.УстановитьПараметр("РабочаяДата", ДатаПечати);
	Запрос.УстановитьПараметр("УпаковкаДляЦенника", ПланыВидовХарактеристик.СвойстваОбъектов.НайтиПоНаименованию("УпаковкаДляЦенника"));
	Запрос.УстановитьПараметр("МестаВыкладки", МассивМеста);
	Запрос.УстановитьПараметр("ЕстьОтборПоМестам", МассивМеста.Количество() > 0);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК НоменклатураНаименование,
	               |	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	               |ПОМЕСТИТЬ ВТ_Данные
	               |ИЗ
	               |	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	               |			&РабочаяДата,
	               |			ТорговаяТочка = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	               |				И ТипЦен = ЗНАЧЕНИЕ(Справочник.ТипыЦен.ОсновнойТипЦенПродаж)) КАК ЦеныНоменклатурыСрезПоследних
	               |ГДЕ
	               |	ЦеныНоменклатурыСрезПоследних.Номенклатура.Выведена = ЛОЖЬ
	               |	И ЦеныНоменклатурыСрезПоследних.Номенклатура В
	               |			(ВЫБРАТЬ
	               |				ТоварныйАссортиментТочекСрезПоследних.Номенклатура
	               |			ИЗ
	               |				РегистрСведений.ТоварныйАссортиментТочек.СрезПоследних(&РабочаяДата, ТорговаяТочка = &ТорговаяТочка
	               |					ИЛИ &ТорговаяТочка = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК ТоварныйАссортиментТочекСрезПоследних
	               |			ГДЕ
	               |				ТоварныйАссортиментТочекСрезПоследних.Выведена = ЛОЖЬ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВыкладкаПланограммы.Номенклатура,
	               |	ВыкладкаПланограммы.МестоВыкладки,
	               |	ВТ_Данные.Цена,
	               |	ВыкладкаПланограммы.Номенклатура.Наименование
	               |ИЗ
	               |	РегистрСведений.ВыкладкаПланограммы КАК ВыкладкаПланограммы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Данные КАК ВТ_Данные
	               |		ПО ВыкладкаПланограммы.Номенклатура = ВТ_Данные.НоменклатураНаименование
	               |ГДЕ
	               |	(ВыкладкаПланограммы.Планограмма = &Планограмма
	               |			ИЛИ &Планограмма = ЗНАЧЕНИЕ(Справочник.Планограммы.ПустаяСсылка))
	               |	И (ВыкладкаПланограммы.МестоВыкладки В ИЕРАРХИИ(&МестаВыкладки)
	               |			ИЛИ &ЕстьОтборПоМестам = ЛОЖЬ)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВыкладкаПланограммы.МестоВыкладки.Наименование,
	               |	ВыкладкаПланограммы.Номенклатура.Наименование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХарактеристикиНоменклатуры.Ссылка,
	               |	ЗначенияСвойствОбъектов.Значение КАК Страна,
	               |	ЗначенияСвойствОбъектовЦенник.Значение КАК Упаковка,
	               |	ХарактеристикиНоменклатуры.Владелец
	               |ИЗ
	               |	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	               |		ПО ХарактеристикиНоменклатуры.Ссылка = ЗначенияСвойствОбъектов.Объект
	               |			И (ЗначенияСвойствОбъектов.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СвойстваОбъектов.СтранаПроисхождения))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектовЦенник
	               |		ПО ХарактеристикиНоменклатуры.Ссылка = ЗначенияСвойствОбъектовЦенник.Объект
	               |			И (ЗначенияСвойствОбъектовЦенник.Свойство = &УпаковкаДляЦенника)
	               |ГДЕ
	               |	ХарактеристикиНоменклатуры.Владелец В
	               |			(ВЫБРАТЬ
	               |				ВыкладкаПланограммы.Номенклатура
	               |			ИЗ
	               |				РегистрСведений.ВыкладкаПланограммы КАК ВыкладкаПланограммы ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Данные КАК ВТ_Данные
	               |					ПО
	               |						ВыкладкаПланограммы.Номенклатура = ВТ_Данные.НоменклатураНаименование
	               |			ГДЕ
	               |				(ВыкладкаПланограммы.Планограмма = &Планограмма
	               |					ИЛИ &Планограмма = ЗНАЧЕНИЕ(Справочник.Планограммы.ПустаяСсылка))
	               |				И (ВыкладкаПланограммы.МестоВыкладки В ИЕРАРХИИ (&МестаВыкладки)
	               |					ИЛИ &ЕстьОтборПоМестам = ЛОЖЬ))
	               |	И ХарактеристикиНоменклатуры.Неактивная = ЛОЖЬ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ВТ_Данные";
				   
	
				   
	Результаты = Запрос.ВыполнитьПакет();
	Выборка = Результаты[1].Выбрать();
	ТабКеш = Результаты[2].Выгрузить();
	
	СоотвествиеХарактеристик = ВнешниеДанные.ПолучитьХарактеристикуДляМассиваНоменклатуры(ТабКеш.ВыгрузитьКолонку("Владелец"));
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = Обработки.ПечатьЦенниковМагазин.ПолучитьМакет("Прайс");
	
	Пока Выборка.СледующийПоЗначениюПоля("МестоВыкладки") Цикл
		ТекСтрока  = 1;
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Дата = ДатаПечати;
		ТабДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("Группа");
		Область.Параметры.Группа = Выборка.МестоВыкладки;
		ТабДокумент.Вывести(Область);
		Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл
			ХаркаПоТовару = СоотвествиеХарактеристик.Получить(Выборка.Номенклатура);
			СтрокаКешХарка = ТабКеш.Найти(ХаркаПоТовару, "Ссылка");
			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.НомерПП = ТекСтрока;
			Область.Параметры.Наименование = Выборка.НоменклатураНаименование;
			Если СтрокаКешХарка <> Неопределено Тогда
				Область.Параметры.Страна = СтрокаКешХарка.Страна;
				Область.Параметры.Упаковка = СтрокаКешХарка.Упаковка;
			КонецЕсли;	
			Область.Параметры.Цена = Выборка.Цена;
			ТабДокумент.Вывести(Область);
			ТекСтрока = ТекСтрока + 1;
		КонецЦикла;	
	КонецЦикла;

	ТабДокумент.Вывести(Макет.ПолучитьОбласть("Надпись"));
	ТабДокумент.ТолькоПросмотр = Истина;
	//ТабДокумент.ПовторятьПриПечатиСтроки = ТабДокумент.Область(7,,8,);
	ТабДокумент.ОтображатьСетку = Ложь;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ТолькоПросмотр = Истина;
	//ТабДокумент.Показать("Ценники");

	Возврат ТабДокумент;
	
КонецФункции	
