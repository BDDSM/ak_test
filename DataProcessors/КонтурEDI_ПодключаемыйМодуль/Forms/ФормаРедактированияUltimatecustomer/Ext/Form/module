
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если Не НовоеРаспределениеВерное() Тогда
		Предупреждение("Распределение не верно!");
		Возврат;
	Иначе
		Товары.Очистить();	
		Для Каждого ГруппСтрока Из ДеревоТоваров.Строки Цикл
			Для Каждого ДочерняяСтрока Из ГруппСтрока.Строки Цикл
				НоваяСтрокаТоваров=Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТоваров,ДочерняяСтрока);
				
				НоваяСтрокаТоваров.СуммаСНДС	= НоваяСтрокаТоваров.Количество*НоваяСтрокаТоваров.ЦенаСНДС;
				НоваяСтрокаТоваров.СуммаБезНДС	= НоваяСтрокаТоваров.Количество*НоваяСтрокаТоваров.ЦенаБезНДС;
			КонецЦикла;
		КонецЦикла;
		ЭтаФорма.Закрыть(Товары);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	//Собрать из Товары Дерево товаров
	Товары=Сообщение.Товары;
	ДеревоТоваров = Новый ДеревоЗначений;
	
	Для Каждого Колонка ИЗ Товары.Колонки Цикл
    	ДеревоТоваров.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
    ДеревоТоваров.Колонки.Добавить("КоличествоОригинальное");

	ТаблицаНоменклатурыСвернутая=Товары.Скопировать();
	ТаблицаНоменклатурыСвернутая.Свернуть("Номенклатура,GTIN,КодТовараПоставщика,КодТовараПокупателя,ЦенаСНДС,ЦенаБезНДС","Количество,КоличествоЗаказанное");
	
	Для Каждого ГруппСтрока Из ТаблицаНоменклатурыСвернутая Цикл
		НоваяСтрока=ДеревоТоваров.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ГруппСтрока);
		НоваяСтрока.КоличествоОригинальное=НоваяСтрока.Количество;
		ОтборСтрок=Новый Структура("Номенклатура,GTIN,КодТовараПоставщика,КодТовараПокупателя",ГруппСтрока.Номенклатура,ГруппСтрока.GTIN,ГруппСтрока.КодТовараПоставщика,ГруппСтрока.КодТовараПокупателя);
		ДочерниеСтроки=Товары.НайтиСтроки(ОтборСтрок);
		Для Каждого ДочерняяСтрокаТоваров Из ДочерниеСтроки Цикл
			НоваяДочерняяСтрока=НоваяСтрока.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяДочерняяСтрока,ДочерняяСтрокаТоваров);
			НоваяДочерняяСтрока.КоличествоОригинальное=НоваяДочерняяСтрока.Количество;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДеревоТоваровПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Цвет = Новый Цвет(215,255,215);
	
	ЦветЦенаБезНДС 	= Новый Цвет(255,255,255);
	ЦветЦенаСНДС 	= Новый Цвет(255,255,255);
	ЦветСуммаБезНДС = Новый Цвет(255,255,255);
	ЦветСуммаСНДС 	= Новый Цвет(255,255,255);
	
	Если НЕ ДанныеСтроки.КоличествоЗаказанное = ДанныеСтроки.Количество Тогда
		
		Если ДанныеСтроки.Количество = 0 Тогда
			Цвет = WebЦвета.СветлоРозовый;
		Иначе
			Цвет = WebЦвета.СветлоЗолотистый;
		КонецЕсли;
		
	КонецЕсли;
	
	
	Если Не ЗначениеЗаполнено(ДанныеСтроки.Родитель) Тогда
	  ОформлениеСтроки.Ячейки.Количество.Шрифт 				= Новый Шрифт(,,Истина);
	  ОформлениеСтроки.Ячейки.КоличествоЗаказанное.Шрифт 	= Новый Шрифт(,,Истина);
	  ОформлениеСтроки.Ячейки.Номенклатура.Шрифт 			= Новый Шрифт(,,Истина);
	КонецЕсли;
	
 	ОформлениеСтроки.Ячейки.Количество.ЦветФона = Цвет;
	Разница=ДанныеСтроки.Количество-ДанныеСтроки.КоличествоОригинальное;
	Если Разница <>0 Тогда  
   	 ОформлениеСтроки.Ячейки.Количество.Текст = Формат(ДанныеСтроки.Количество,"ЧДЦ=2")+" ("+?(Разница>0,"+","")+Формат(Разница,"ЧДЦ=2")+")";
	КонецЕсли;
КонецПроцедуры

Процедура ДеревоТоваровКоличествоПриИзменении(Элемент)
	ПерерассчитатьИтоговыеПоказателиСтрокиНовоеРаспределение();	
КонецПроцедуры

Процедура ДеревоТоваровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель)
	Отказ=Истина;
КонецПроцедуры

Процедура ДеревоТоваровПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

Процедура ДеревоТоваровПередНачаломИзменения(Элемент, Отказ)
	//групповые строки интерактивно не редактируем
	Если Элемент.ТекущаяСтрока.Родитель=Неопределено Тогда Отказ=Истина; КонецЕсли;
КонецПроцедуры

Процедура ПерерассчитатьИтоговыеПоказателиСтрокиНовоеРаспределение()
	Для Каждого ГруппСтрока Из ДеревоТоваров.Строки Цикл
		ГруппСтрока.Количество=0;
		Для Каждого ДочерняяСтрокаТоваров Из ГруппСтрока.Строки Цикл
			 ГруппСтрока.Количество=ГруппСтрока.Количество+ДочерняяСтрокаТоваров.Количество;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Функция НовоеРаспределениеВерное()
	Для Каждого ГруппСтрока Из ДеревоТоваров.Строки Цикл
		Если ГруппСтрока.Количество<>ГруппСтрока.КоличествоОригинальное Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции
