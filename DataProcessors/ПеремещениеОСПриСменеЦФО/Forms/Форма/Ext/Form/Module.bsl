
Процедура СформироватьПеремещениеСервер()
	
	ЗапросПоОстаткамОС = Новый Запрос;
	ЗапросПоОстаткамОС.Текст =
	"ВЫБРАТЬ
	|	ОстаткиФин.Счет,
	|	ОстаткиФин.Субконто1 КАК ЦФО,
	|	ОстаткиФин.Субконто2 КАК ТорговаяТочка,
	|	ОстаткиФин.Субконто3 КАК ОсновноеСредство,
	|	ОстаткиФин.Организация,
	|	ОстаткиФин.Валюта,
	|	ОстаткиФин.СтруктурнаяЕдиница,
	|	ОстаткиФин.КоличествоОстаток КАК Количество,
	|	ОстаткиФин.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Финансовый.Остатки(&ДатаОстатков, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Финансовый.ОсновныеСредства)), , Субконто2 = &ТорговаяТочка) КАК ОстаткиФин";
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала"		, Объект.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания"	, КонецДня(Объект.ДатаОкончания));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛистУчета.ТорговаяТочка,
	|	ЛистУчета.ЦФО,
	|	МИНИМУМ(ЛистУчета.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТДаты
	|ИЗ
	|	Документ.ЛистУчета КАК ЛистУчета
	|ГДЕ
	|	ЛистУчета.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ЛистУчета.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ЛистУчета.ТорговаяТочка,
	|	ЛистУчета.ЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДаты.ТорговаяТочка КАК ТорговаяТочка,
	|	МИНИМУМ(ВТДаты.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТДатыТТ
	|ИЗ
	|	ВТДаты КАК ВТДаты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДаты.ТорговаяТочка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТорговаяТочка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДаты.ТорговаяТочка КАК ТорговаяТочка,
	|	ВТДаты.ЦФО,
	|	ВТДаты.Дата КАК Дата
	|ИЗ
	|	ВТДаты КАК ВТДаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыТТ КАК ВТДатыТТ
	|		ПО (ВТДатыТТ.ТорговаяТочка = ВТДаты.ТорговаяТочка)
	|ГДЕ
	|	НЕ ВТДатыТТ.Дата = ВТДаты.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТДаты.ТорговаяТочка.id_TT
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДатыТТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДаты";
	
	Если ЭтаФорма.ДокументыПеремещения.Количество() > 0 Тогда
		ЭтаФорма.ДокументыПеремещения.Очистить();
	КонецЕсли;
	
	мДокументыПеремещения = Документы.ПеремещениеОС;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекТорговаяТочка = Выборка.ТорговаяТочка;
		
		Сообщить("***---***");
		Сообщить("Обрабатывается " + СокрЛП(ТекТорговаяТочка.Наименование));
		Сообщить(Выборка.ЦФО);
		
		ДокументПеремещения = мДокументыПеремещения.СоздатьДокумент();
		
		ТЧНоменклатура = ДокументПеремещения.Номенклатура;
		
		ЗапросПоОстаткамОС.УстановитьПараметр("ДатаОстатков"	, КонецДня(Выборка.Дата) + 1);
		ЗапросПоОстаткамОС.УстановитьПараметр("ТорговаяТочка"	, ТекТорговаяТочка);
		
		ВыборкаПоОС = ЗапросПоОстаткамОС.Выполнить().Выбрать();
		Пока ВыборкаПоОС.Следующий() Цикл
			
			Если ВыборкаПоОС.ЦФО = Выборка.ЦФО Тогда
				Продолжить;
			КонецЕсли;
			
			ДокументПеремещения.ЦФООтправитель = ВыборкаПоОС.ЦФО;
			
			НоваяСтрока = ТЧНоменклатура.Добавить();
			НоваяСтрока.ОсновноеСредство 		= ВыборкаПоОС.ОсновноеСредство;
			НоваяСтрока.СчетУчетаЗабалансовый 	= ВыборкаПоОС.Счет;
			НоваяСтрока.Сумма					= ВыборкаПоОС.Сумма;
			
		КонецЦикла;
		
		Если ТЧНоменклатура.Количество() = 0 Тогда
			ДокументПеремещения = Неопределено;
			Продолжить;
		КонецЕсли;
		
		ДокументПеремещения.Дата 				= КонецДня(Выборка.Дата);
		ДокументПеремещения.СкладОтправитель 	= ТекТорговаяТочка;
		ДокументПеремещения.СкладПолучатель 	= ТекТорговаяТочка;
		ДокументПеремещения.ЦФОПолучатель 		= Выборка.ЦФО;
		
		ДокументПеремещения.СуммаДокумента		= ТЧНоменклатура.Итог("Сумма");
		
		ДокументПеремещения.НеФормироватьПроводкиПоБалансовымСчетам = Истина;
		//ДокументПеремещения.Комментарий								= "Смена розничной группы [создан автоматически]";
		
		Попытка
			ДокументПеремещения.Записать(РежимЗаписиДокумента.Проведение);
			Сообщить("Создан и проведен документ " + ДокументПеремещения);
			
			НоваяСтрока = ЭтаФорма.ДокументыПеремещения.Добавить();
			НоваяСтрока.ДокументПеремещения = ДокументПеремещения.Ссылка;
			
		Исключение
			Сообщить("Документ не проведен!" + ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПеремещение(Команда)
	
	СформироватьПеремещениеСервер();
	
КонецПроцедуры
