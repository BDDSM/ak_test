
//+++АК SHEP 2018.10.25 ИП-00019584 +справочник, +форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НоменклатураСплитТестирования.Отбор, "ПометкаУдаления", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСплитТестированияПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.НоменклатураСплитТестирования.ТекущиеДанные;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(АссортиментСплитТестирования.Отбор, "НоменклатураСплитТестирования",
		?(ТекущиеДанные = Неопределено, ПредопределенноеЗначение("Справочник.НоменклатураСплитТестирования.ПустаяСсылка"), ТекущиеДанные.Ссылка));
	
	//нужно найти последнюю дату включения и установить ПериодС равный ей
	//АссортиментСплитТестирования.Параметры.УстановитьЗначениеПараметра("НоменклатураСплитТестирования",
	//	?(ТекущиеДанные = Неопределено, ПредопределенноеЗначение("Справочник.НоменклатураСплитТестирования.ПустаяСсылка"), ТекущиеДанные.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьНоменклатуруСплитТестирования(Команда)
	ВключитьОтключитьНоменклатуруСплитТестирования(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ВключитьНоменклатуруСплитТестирования(Команда)
	ВключитьОтключитьНоменклатуруСплитТестирования(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВключитьОтключитьНоменклатуруСплитТестирования(Отключить)
	
	ТекущиеДанные = Элементы.НоменклатураСплитТестирования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат; КонецЕсли;
	
	НоменклатураСплитТестированияСсылка = ТекущиеДанные.Ссылка;
	ДопПараметры = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НоменклатураСплитТестированияСсылка);
	ДопПараметры.Добавить(Отключить);
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВключитьОтключитьНоменклатуруСплитТестированияПриЗавершении", ЭтаФорма, ДопПараметры);
	ПоказатьВопрос(ОповещениеЗавершения, "" + ?(Отключить, "Отключить", "Включить") + " номенклатуру сплит-тестирования " + НоменклатураСплитТестированияСсылка + "?",
		РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьОтключитьНоменклатуруСплитТестированияПриЗавершении(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда Возврат; КонецЕсли;
	
	ВключитьОтключитьНоменклатуруСплитТестированияНаСервере(Параметры[0], Параметры[1]);
	
КонецПроцедуры

&НаСервере
Процедура ВключитьОтключитьНоменклатуруСплитТестированияНаСервере(НоменклатураСплитТестированияСсылка, Отключена)
	
	СпрНоменклатураСплитТестированияОбъект = НоменклатураСплитТестированияСсылка.ПолучитьОбъект();
	СпрНоменклатураСплитТестированияОбъект.Отключена = Отключена;
	СпрНоменклатураСплитТестированияОбъект.Записать();
	
	Элементы.НоменклатураСплитТестирования.Обновить();
	Элементы.АссортиментСплитТестирования.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьИзАссортиментаВсехМагазинов(Команда)
	
	ТекущиеДанные = Элементы.НоменклатураСплитТестирования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат; КонецЕсли;
	
	ОписаниеЗавершения = Новый ОписаниеОповещения("УбратьИзАссортиментаВсехМагазиновЗавершение", ЭтаФорма, Новый Структура("Ссылка", ТекущиеДанные.Ссылка));
	ПоказатьВопрос(ОписаниеЗавершения, "Убрать номенклатуру сплит-тестирования из всех торговых точек?", РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьИзАссортиментаВсехМагазиновЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда Возврат; КонецЕсли;
	
	УбратьИзАссортиментаВсехМагазиновНаСервере(ДополнительныеПараметры.Ссылка);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УбратьИзАссортиментаВсехМагазиновНаСервере(НоменклатураСплитТестированияСсылка)
	
	НоменклатураСплитТестированияДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураСплитТестированияСсылка, "Номенклатура,ХарактеристикаНоменклатуры");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТоварныйАссортиментТочекСрезПоследних.ТорговаяТочка
		|ИЗ
		|	РегистрСведений.ТоварныйАссортиментТочек.СрезПоследних(&ТекДата, Номенклатура = &Номенклатура) КАК ТоварныйАссортиментТочекСрезПоследних
		|ГДЕ
		|	НЕ ТоварныйАссортиментТочекСрезПоследних.Выведена
		|	И ТоварныйАссортиментТочекСрезПоследних.Характеристика = &ХарактеристикаНоменклатуры");
	Запрос.УстановитьПараметр("ТекДата", КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("Номенклатура", НоменклатураСплитТестированияДанные.Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", НоменклатураСплитТестированияДанные.ХарактеристикаНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда Возврат; КонецЕсли;
	
	ДатаЗаписиРС = НачалоДня(ТекущаяДата());
	МенеджерЗаписиТАТ = РегистрыСведений.ТоварныйАссортиментТочек.СоздатьМенеджерЗаписи();
	МенеджерЗаписиАСТ = РегистрыСведений.АссортиментСплитТестирования.СоздатьМенеджерЗаписи();
	
	СтруктураОтбор = Новый Структура("Период,Номенклатура,Характеристика,НоменклатураСплитТестирования",
		ДатаЗаписиРС, НоменклатураСплитТестированияДанные.Номенклатура, НоменклатураСплитТестированияДанные.ХарактеристикаНоменклатуры, НоменклатураСплитТестированияСсылка);
	
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		ДобавленаТТ = -1;
		ТекТорговаяТочка = ВыборкаЗапроса.ТорговаяТочка;
		СтруктураОтбор.Вставить("ТорговаяТочка", ТекТорговаяТочка);
		
		// ТоварныйАссортиментТочек
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиТАТ, СтруктураОтбор);
		МенеджерЗаписиТАТ.Прочитать();
		
		Если НЕ МенеджерЗаписиТАТ.Выбран() Тогда
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиТАТ, СтруктураОтбор);
		КонецЕсли;
		
		МенеджерЗаписиТАТ.Выведена = Истина;
		МенеджерЗаписиТАТ.Комментарий = "Сплит-тестирование товаров: выводим из ассортимента";
		МенеджерЗаписиТАТ.Записать();
		
		// АссортиментСплитТестирования
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиАСТ, СтруктураОтбор);
		МенеджерЗаписиАСТ.Прочитать();
		
		Если НЕ МенеджерЗаписиАСТ.Выбран() Тогда
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиАСТ, СтруктураОтбор);
		КонецЕсли;
		
		МенеджерЗаписиАСТ.Добавлен = ДобавленаТТ;
		МенеджерЗаписиАСТ.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьМассивДоступныхТТДляДобавления(НоменклатураСплитТестированияСсылка)
	
	МассивТТ = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НоменклатураСплитТестированияДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураСплитТестированияСсылка, "Номенклатура,ХарактеристикаНоменклатуры");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СтруктурныеЕдиницы.Ссылка,
		|	СтруктурныеЕдиницы.НомерТочки КАК НомерТочки
		|ПОМЕСТИТЬ СтруктурныеЕдиницы
		|ИЗ
		|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		|ГДЕ
		|	СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.Розница)
		|	И СтруктурныеЕдиницы.ТипРозничнойТочки В (ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Магазин), ЗНАЧЕНИЕ(Перечисление.ТипыРозничныхТочек.Избенка))
		|	И СтруктурныеЕдиницы.Активное
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварныйАссортиментТочекСрезПоследних.ТорговаяТочка,
		|	ТоварныйАссортиментТочекСрезПоследних.Характеристика,
		|	ТоварныйАссортиментТочекСрезПоследних.Выведена
		|ПОМЕСТИТЬ ТоварныйАссортиментТочекСрезПоследних
		|ИЗ
		|	РегистрСведений.ТоварныйАссортиментТочек.СрезПоследних(&ТекДата, Номенклатура = &Номенклатура) КАК ТоварныйАссортиментТочекСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтруктурныеЕдиницы.Ссылка,
		|	СтруктурныеЕдиницы.НомерТочки КАК НомерТочки
		|ИЗ
		|	СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварныйАссортиментТочекСрезПоследних КАК ТоварныйАссортиментТочекСрезПоследних
		|		ПО СтруктурныеЕдиницы.Ссылка = ТоварныйАссортиментТочекСрезПоследних.ТорговаяТочка
		|ГДЕ
		|	ЕСТЬNULL(ТоварныйАссортиментТочекСрезПоследних.Характеристика, &ХарактеристикаНоменклатуры) = &ХарактеристикаНоменклатуры
		|	И ЕСТЬNULL(ТоварныйАссортиментТочекСрезПоследних.Выведена, ИСТИНА) = ИСТИНА
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерТочки");
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	Запрос.УстановитьПараметр("Номенклатура", НоменклатураСплитТестированияДанные.Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", НоменклатураСплитТестированияДанные.ХарактеристикаНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		МассивТТ = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат МассивТТ;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьВАссортиментМагазинов(Команда)
	
	ТекущиеДанные = Элементы.НоменклатураСплитТестирования.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат; КонецЕсли;
	
	НоменклатураСплитТестированияСсылка = ТекущиеДанные.Ссылка;
	ОписаниеЗавершения = Новый ОписаниеОповещения("ДобавитьВАссортиментМагазиновЗавершение", ЭтаФорма, Новый Структура("Ссылка", НоменклатураСплитТестированияСсылка));
	СписокМагазинов = Новый СписокЗначений;
	СписокМагазинов.ЗагрузитьЗначения(ЗаполнитьМассивДоступныхТТДляДобавления(НоменклатураСплитТестированияСсылка));
	//СписокМагазинов.ПоказатьВыборЭлемента(ОписаниеЗавершения, "Выберите ТТ для добавления");
	СписокМагазинов.ПоказатьОтметкуЭлементов(ОписаниеЗавершения, "Выберите ТТ для добавления");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВАссортиментМагазиновЗавершение(СписокЗн, ДополнительныеПараметры) Экспорт
	
	Если СписокЗн = Неопределено Тогда Возврат; КонецЕсли;
	
	МассивТТ = Новый Массив;
	Для Каждого ЭлементСписка Из СписокЗн Цикл
		Если ЭлементСписка.Пометка Тогда
			МассивТТ.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивТТ.Количество() > 0 Тогда
		ДобавитьВАссортиментМагазиновНаСервере(ДополнительныеПараметры.Ссылка, МассивТТ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьВАссортиментМагазиновНаСервере(НоменклатураСплитТестированияСсылка, МассивТТ)
	
	ДатаЗаписиРС = НачалоДня(ТекущаяДата());
	МенеджерЗаписиТАТ = РегистрыСведений.ТоварныйАссортиментТочек.СоздатьМенеджерЗаписи();
	МенеджерЗаписиАСТ = РегистрыСведений.АссортиментСплитТестирования.СоздатьМенеджерЗаписи();
	
	НоменклатураСплитТестированияДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураСплитТестированияСсылка, "Номенклатура,ХарактеристикаНоменклатуры");
	
	СтруктураОтбор = Новый Структура("Период,Номенклатура,Характеристика,НоменклатураСплитТестирования",
		ДатаЗаписиРС, НоменклатураСплитТестированияДанные.Номенклатура, НоменклатураСплитТестированияДанные.ХарактеристикаНоменклатуры, НоменклатураСплитТестированияСсылка);
	
	Для Каждого ТекТорговаяТочка Из МассивТТ Цикл
		
		ДобавленаТТ = 1;
		СтруктураОтбор.Вставить("ТорговаяТочка", ТекТорговаяТочка);
		
		// ТоварныйАссортиментТочек
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиТАТ, СтруктураОтбор);
		МенеджерЗаписиТАТ.Прочитать();
		
		Если НЕ МенеджерЗаписиТАТ.Выбран() Тогда
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиТАТ, СтруктураОтбор);
		КонецЕсли;
		
		МенеджерЗаписиТАТ.Выведена = Ложь;
		МенеджерЗаписиТАТ.Комментарий = "Сплит-тестирование товаров: добавили в ассортимент";
		МенеджерЗаписиТАТ.Записать();
		
		// АссортиментСплитТестирования
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиАСТ, СтруктураОтбор);
		МенеджерЗаписиАСТ.Прочитать();
		
		Если НЕ МенеджерЗаписиАСТ.Выбран() Тогда
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиАСТ, СтруктураОтбор);
		КонецЕсли;
		
		МенеджерЗаписиАСТ.Добавлен = ДобавленаТТ;
		МенеджерЗаписиАСТ.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
