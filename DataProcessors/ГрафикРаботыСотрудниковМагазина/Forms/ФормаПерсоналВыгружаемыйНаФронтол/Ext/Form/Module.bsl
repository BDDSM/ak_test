
//+++АК MIND 2017.11.01 форма, где магазин может принудительно настроить выгрузку отдельных личностей, и посомтреть кого выгрузит по табелю
// и выполнить выгузку на фронтол по своему желанию

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ТекущаяДата() - 3*86400));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ТекущаяДата() + 3*86400));
	Запрос.УстановитьПараметр("Магазин", ПараметрыСеанса.ТорговаяТочкаПоАйпи);
	Запрос.Текст =
				"ВЫБРАТЬ
				|	ПерсоналККМ.Ссылка КАК Сотрудник
				|ИЗ
				|	Справочник.ПерсоналККМ КАК ПерсоналККМ
				|ГДЕ
				|	ПерсоналККМ.ПометкаУдаления = ЛОЖЬ
				|	И ПерсоналККМ.Код В
				|			(ВЫБРАТЬ
				|				ТабельРаботыПродавцов.Сотрудник.Код
				|			ИЗ
				|				РегистрСведений.ТабельРаботыПродавцов КАК ТабельРаботыПродавцов
				|			ГДЕ
				|				ТабельРаботыПродавцов.Период МЕЖДУ &ДатаНач И &ДатаКон
				|				И ТабельРаботыПродавцов.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ТабельРаботыПродавцов.Сотрудник.Код
				|			ИЗ
				|				РегистрСведений.ТабельРаботыВнештатныхСотрудников КАК ТабельРаботыПродавцов
				|			ГДЕ
				|				ТабельРаботыПродавцов.Период МЕЖДУ &ДатаНач И &ДатаКон
				|				И ТабельРаботыПродавцов.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ТабельРаботыПродавцов.Сотрудник.Код
				|			ИЗ
				|				РегистрСведений.ТабельРаботыГрузчиков КАК ТабельРаботыПродавцов
				|			ГДЕ
				|				ТабельРаботыПродавцов.Период МЕЖДУ &ДатаНач И &ДатаКон
				|				И ТабельРаботыПродавцов.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ТабельРаботыПродавцов.Сотрудник.Код
				|			ИЗ
				|				РегистрСведений.ТабельРаботыКассиров КАК ТабельРаботыПродавцов
				|			ГДЕ
				|				ТабельРаботыПродавцов.Период МЕЖДУ &ДатаНач И &ДатаКон
				|				И ТабельРаботыПродавцов.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ТабельРаботыПродавцов.Сотрудник.Код
				|			ИЗ
				|				РегистрСведений.ТабельРаботыПромоутеров КАК ТабельРаботыПродавцов
				|			ГДЕ
				|				ТабельРаботыПродавцов.Период МЕЖДУ &ДатаНач И &ДатаКон
				|				И ТабельРаботыПродавцов.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ТабельРаботыУборщиц.Сотрудник.Код
				|			ИЗ
				|				РегистрСведений.ТабельРаботыУборщиц КАК ТабельРаботыУборщиц
				|			ГДЕ
				|				ТабельРаботыУборщиц.Период МЕЖДУ &ДатаНач И &ДатаКон
				|				И ТабельРаботыУборщиц.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ДокТаб.Продавец.Код
				|			ИЗ
				|				Документ.ЛистУчета.Продавцы КАК ДокТаб
				|			ГДЕ
				|				ДокТаб.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
				|				И ДокТаб.Ссылка.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ДокТаб.Промоутер.Код
				|			ИЗ
				|				Документ.ЛистУчета.Промоутеры КАК ДокТаб
				|			ГДЕ
				|				ДокТаб.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
				|				И ДокТаб.Ссылка.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ДокТаб.Кассир.Код
				|			ИЗ
				|				Документ.ЛистУчета.Кассиры КАК ДокТаб
				|			ГДЕ
				|				ДокТаб.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
				|				И ДокТаб.Ссылка.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ДокТаб.ФизЛицо.Код
				|			ИЗ
				|				Документ.ЛистУчета.Грузчики КАК ДокТаб
				|			ГДЕ
				|				ДокТаб.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
				|				И ДокТаб.Ссылка.ТорговаяТочка = &Магазин
				|		
				|			ОБЪЕДИНИТЬ ВСЕ
				|		
				|			ВЫБРАТЬ
				|				ДокТаб.ФизЛицо.Код
				|			ИЗ
				|				Документ.ЛистУчета.Уборщицы КАК ДокТаб
				|			ГДЕ
				|				ДокТаб.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
				|				И ДокТаб.Ссылка.ТорговаяТочка = &Магазин)";
	
	ПоТабелямРаботы.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ПринудительноВыгружать.Параметры.УстановитьЗначениеПараметра("Магазин", ПараметрыСеанса.ТорговаяТочкаПоАйпи);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РабочиеМеста.Ссылка КАК РабочееМесто,
	               |	ИСТИНА КАК Выгружать
	               |ИЗ
	               |	Справочник.РабочиеМеста КАК РабочиеМеста
	               |ГДЕ
	               |	РабочиеМеста.СтруктурнаяЕдиница = &Магазин";
	
	РабочиеМестаДляВыгрузки.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДанныеНаКассыНаСервере()
	
	ТабДанные = РабочиеМестаДляВыгрузки.Выгрузить();
	ТабДанные.Колонки.Добавить("СПользователями");
	ТабДанные.ЗаполнитьЗначения(Истина, "СПользователями");

	Обработки.ВыгрузкаДанныхВоФронтол.ВыгрузитьДанныеВоФронтол(ТабДанные, ПараметрыСеанса.ТорговаяТочкаПоАйпи);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеНаКассы(Команда)
	ВыгрузитьДанныеНаКассыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВвестиШКНаСервере(ШК)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПерсоналККМ.Ссылка
	               |ИЗ
	               |	Справочник.ПерсоналККМ КАК ПерсоналККМ
	               |ГДЕ
	               |	ПерсоналККМ.Пароль = &Пароль";
				   
	Запрос.УстановитьПараметр("Пароль", СокрЛП(ШК));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Запись = РегистрыСведений.ПерсоналПринудительноВыгружатьНаФронтол.СоздатьМенеджерЗаписи();
		Запись.Магазин = ПараметрыСеанса.ТорговаяТочкаПоАйпи;
		Запись.ПерсоналККМ = Выборка.Ссылка;
		Запись.ДатаДобавления = ТекущаяДата();
		Запись.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиШК(Команда)
	
	ШК = "";
	Если ВвестиСтроку(ШК, "Введите ШК") Тогда
		ВвестиШКНаСервере(ШК);
		Элементы.ПринудительноВыгружать.Обновить();
	КонецЕсли;
	
КонецПроцедуры
