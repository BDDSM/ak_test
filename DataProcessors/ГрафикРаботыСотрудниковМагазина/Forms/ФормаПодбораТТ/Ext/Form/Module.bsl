
&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивТТ = Новый Массив;
	
	Для Каждого Строка Из СписокТорговыхТочек Цикл
		Если Строка.Пометка Тогда
			МассивТТ.Добавить(Строка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивТТ.Количество()>0 Тогда
		Закрыть(МассивТТ);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Адрес = Параметры.Адрес;
	ГоловноеПодразделение = Параметры.ГоловноеПодразделение;
	ОбновитьСписокТорговыхТочек();	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокТорговыхТочек(Отбор = Истина)
	
	МассивТочек = ПолучитьИзВременногоХранилища(Адрес);
	Для Каждого Строка Из СписокТорговыхТочек Цикл
		Если Строка.Пометка Тогда
			МассивТочек.Добавить(Строка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СписокТорговыхТочек.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивТочек", МассивТочек);
	
	Если Отбор Тогда
		ТекстОтбор = " И СтруктурныеЕдиницы.ГоловноеПодразделение = &ГоловноеПодразделение";
		Запрос.УстановитьПараметр("ГоловноеПодразделение", ГоловноеПодразделение);
	Иначе
		ТекстОтбор = "";
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтруктурныеЕдиницы.Ссылка КАК Значение,
	|	ВЫБОР
	|		КОГДА СтруктурныеЕдиницы.Ссылка В (&МассивТочек)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
	|ГДЕ
	|	НЕ СтруктурныеЕдиницы.ПометкаУдаления" + ТекстОтбор + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтруктурныеЕдиницы.Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий()Цикл
		
		НовСтр = СписокТорговыхТочек.Добавить(Выборка.Значение,, Выборка.Пометка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОтбор(Команда)
	
	Элементы.ФормаОтключитьОтбор.Доступность = Ложь;
	ОбновитьСписокТорговыхТочек(Ложь);	
	
КонецПроцедуры
