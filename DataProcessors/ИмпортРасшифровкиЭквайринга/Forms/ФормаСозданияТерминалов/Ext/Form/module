
#Область ОбработчикиСобытийФормы

//+++АК LATV 2018.07.13 ИП-00018778
Процедура ПриОткрытии()
	ЗаполнитьТерминалы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

//+++АК LATV 2018.07.13 ИП-00018778
Процедура КоманднаяПанель2Обновить(Кнопка)
	ЗаполнитьТерминалы();
КонецПроцедуры

//+++АК LATV 2018.07.13 ИП-00018778
Процедура КоманднаяПанель2СоздатьТерминалы(Кнопка)
	СоздатьТерминалы();
КонецПроцедуры

//+++АК LATV 2018.07.13 ИП-00018778
Процедура КоманднаяПанель2УстановитьФлажки(Кнопка)

	МассивСтрок = Терминалы.НайтиСтроки(Новый Структура("Выбрать", Ложь));
	Для Каждого ТекСтрока Из МассивСтрок Цикл
		ТекСтрока.Выбрать = Истина;
	КонецЦикла;

КонецПроцедуры

//+++АК LATV 2018.07.13 ИП-00018778
Процедура КоманднаяПанель2СнятьФлажки(Кнопка)

	МассивСтрок = Терминалы.НайтиСтроки(Новый Структура("Выбрать", Истина));
	Для Каждого ТекСтрока Из МассивСтрок Цикл
		ТекСтрока.Выбрать = Ложь;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.07.13 ИП-00018778
Процедура ЗаполнитьТерминалы()

	ВремТЗ = Терминалы.Скопировать(, "НомерТТ");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДанныеТерминалов.НомерТТ КАК НомерТТ,
		|	ДанныеТерминалов.НомерТТ + ""[^0-9]%"" КАК НомерТТДляПоиска
		|ПОМЕСТИТЬ втДанныеТерминалов
		|ИЗ
		|	&ДанныеТерминалов КАК ДанныеТерминалов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерТТДляПоиска
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДанныеТерминалов.НомерТТ,
		|	СтруктурныеЕдиницы.Ссылка КАК СтруктурнаяЕдиница
		|ИЗ
		|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеТерминалов КАК втДанныеТерминалов
		|		ПО (СтруктурныеЕдиницы.Наименование ПОДОБНО втДанныеТерминалов.НомерТТДляПоиска)
		|			И (НЕ СтруктурныеЕдиницы.ПометкаУдаления)");
	
	Запрос.УстановитьПараметр("ДанныеТерминалов", ВремТЗ);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Отбор = Новый Структура("НомерТТ", Выборка.НомерТТ);
		НайденныеСтроки = Терминалы.НайтиСтроки(Отбор);
		Для Каждого ТекСтрока Из НайденныеСтроки Цикл
			ТекСтрока.ТорговаяТочка = Выборка.СтруктурнаяЕдиница;
			ТекСтрока.Выбрать = Истина;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

//+++АК LATV 2018.07.13 ИП-00018778
Процедура СоздатьТерминалы()

	Отбор = Новый Структура("Выбрать, Терминал", Истина, Справочники.Терминалы.ПустаяСсылка());
	ВыбранныеСтроки = Терминалы.НайтиСтроки(Отбор);
	Для Каждого ТекСтрока Из ВыбранныеСтроки Цикл
		
		Если ТекСтрока.ТорговаяТочка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		// Создание терминала
		Терминал = Справочники.Терминалы.СоздатьЭлемент();
		Терминал.Владелец		= ТекСтрока.ТорговаяТочка;
		Терминал.Код			= ТекСтрока.ИД_Терминала;
		Терминал.Наименование	= ТекСтрока.ИД_Терминала;
		Терминал.ИД				= ТекСтрока.ИД_Терминала;
		
		Терминал.Мерчант	= ТекСтрока.Мерчант;
		Терминал.ID_SQL		= ТекСтрока.ID_SQL;
		
		Попытка
			Терминал.Записать();
			ТекСтрока.Терминал = Терминал.Ссылка;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

