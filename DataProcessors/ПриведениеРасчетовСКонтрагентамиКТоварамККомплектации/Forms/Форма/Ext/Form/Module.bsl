
&НаСервере
Процедура ПривестиРасчетыНаСервере()
	
	Если ЗначениеЗаполнено(Корректировка) Тогда
		Док=Корректировка.ПолучитьОбъект();
		Док.Движения.РасчетыСКонтрагентами.Очистить();
		Док.Движения.ТМЦКПоступлению.Очистить();
		док.ТаблицаРегистровНакопления.Очистить();
		Док.Движения.РасчетыПоСделкамСПоставщиками.Очистить();
		Док.Записать();
	Иначе	
		Док = Документы.КорректировкаЗаписейРегистровНакопления.СоздатьДокумент();
	КонецЕсли;
	Док.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Док.Дата = НаДату;
	НС = Док.ТаблицаРегистровНакопления.Добавить();
	НС.Имя = "РасчетыСКонтрагентами";
	НС.Представление = "Расчеты с контрагентами";
		
	НС = Док.ТаблицаРегистровНакопления.Добавить();
	НС.Имя ="ТМЦКПоступлению";
	НС.Представление = "ТМЦ к поступлению по сделкам";
	
	НС = Док.ТаблицаРегистровНакопления.Добавить();
	НС.Имя ="РасчетыПоСделкамСПоставщиками";
	НС.Представление = "Расчеты по сделкам";
	
	СчетРасчетов = ПланыСчетов.Финансовый.ПрочаяЗадолженность;
	Вкусвилл  = Справочники.Организации.НайтиПоКоду("000000006");
	
	ТекстЗапроса="ВЫБРАТЬ
	             |	РасчетыСКонтрагентамиОстатки.Контрагент,
	             |	РасчетыСКонтрагентамиОстатки.Сделка КАК Сделка,
	             |	РасчетыСКонтрагентамиОстатки.СуммаОстаток КАК Сумма,
	             |	РасчетыСКонтрагентамиОстатки.АвансПоСделкеОстаток КАК АвансПоСделке,
	             |	РасчетыСКонтрагентамиОстатки.СчетУчета,
	             |	РасчетыСКонтрагентамиОстатки.Организация
	             |ИЗ
	             |	РегистрНакопления.РасчетыСКонтрагентами.Остатки(
	             |			&НаДату,
	             |			СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.ПрочаяЗадолженность)
	             |				И Контрагент = &Контрагент) КАК РасчетыСКонтрагентамиОстатки
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	РасчетыСКонтрагентамиОстатки.Сделка.Дата
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Сделка.Контрагент КАК Контрагент,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Сделка,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Номенклатура,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.КоличествоОстаток КАК ОстатокПоТоварамКПоступлению,
	             |	СделкаСПоставщикомГрафикОплат.Цена,
	             |	СУММА(СделкаСПоставщикомЭтапыСделки.ПроцентОплаты) КАК ПроцентАванса,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.КоличествоОстаток * СделкаСПоставщикомГрафикОплат.Цена * СделкаСПоставщикомЭтапыСделки.ПроцентОплаты / 100 КАК СуммаАванса,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.УИН_Этапа
	             |ИЗ
	             |	РегистрНакопления.ТоварыККомплектацииСделокСПоставщиками.Остатки(&НаДату, Сделка.Контрагент = &Контрагент) КАК ТоварыККомплектацииСделокСПоставщикамиОстатки
	             |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СделкаСПоставщиком.ГрафикОплат КАК СделкаСПоставщикомГрафикОплат
	             |			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СделкаСПоставщиком.ЭтапыСделки КАК СделкаСПоставщикомЭтапыСделки
	             |			ПО СделкаСПоставщикомГрафикОплат.Ссылка = СделкаСПоставщикомЭтапыСделки.Ссылка
	             |				И (СделкаСПоставщикомЭтапыСделки.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплаты.ВУказаннуюДату))
	             |				И (СделкаСПоставщикомЭтапыСделки.КоличествоДней = 0)
	             |		ПО ТоварыККомплектацииСделокСПоставщикамиОстатки.Сделка = СделкаСПоставщикомГрафикОплат.Ссылка
	             |			И ТоварыККомплектацииСделокСПоставщикамиОстатки.Номенклатура = СделкаСПоставщикомГрафикОплат.Номенклатура
	             |			И ТоварыККомплектацииСделокСПоставщикамиОстатки.УИН_Этапа = СделкаСПоставщикомГрафикОплат.УИН_Строки
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Сделка.Контрагент,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Сделка,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Номенклатура,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.КоличествоОстаток,
	             |	СделкаСПоставщикомГрафикОплат.Цена,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.КоличествоОстаток * СделкаСПоставщикомГрафикОплат.Цена * СделкаСПоставщикомЭтапыСделки.ПроцентОплаты / 100,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.УИН_Этапа
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТМЦКПоступлениюОстатки.Предпоступление,
	             |	ТМЦКПоступлениюОстатки.Сделка,
	             |	ТМЦКПоступлениюОстатки.Номенклатура,
	             |	ТМЦКПоступлениюОстатки.КоличествоОстаток КАК Количество
	             |ИЗ
	             |	РегистрНакопления.ТМЦКПоступлению.Остатки(
	             |			&НаДату,
	             |			Предпоступление.Контрагент = &Контрагент
	             |				ИЛИ Сделка.Контрагент = &Контрагент) КАК ТМЦКПоступлениюОстатки
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Сделка,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.УИН_Этапа,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Комплектация,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.УИН_СтрокиКомплектации,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.ДатаПлатежа,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.СуммаОстаток КАК Сумма
	             |ИЗ
	             |	РегистрНакопления.РасчетыПоСделкамСПоставщиками.Остатки(
	             |			&НаДату,
	             |			Сделка.Контрагент = &Контрагент
	             |				И (Комплектация <> НЕОПРЕДЕЛЕНО
	             |					ИЛИ ДатаПлатежа <> ДАТАВРЕМЯ(1, 1, 1))) КАК РасчетыПоСделкамСПоставщикамиОстатки";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Контрагент",Контрагент);
	Запрос.УстановитьПараметр("НаДату",НаДату);
	Результат = Запрос.ВыполнитьПакет();
	
	ТЗОстатковПоРасчетам = Результат[0].Выгрузить();
	ТЗОстатковПоАвансам = Результат[1].Выгрузить();
	
	ТЗАвансовПоСделкам = ТЗОстатковПоАвансам.Скопировать();
	ТЗАвансовПоСделкам.Свернуть("Сделка","СуммаАванса");
	
	ДолгПередПоставщиком = ТЗОстатковПоРасчетам.Итог("Сумма")-ТЗАвансовПоСделкам.Итог("СуммаАванса");
	
	Выборка = Результат[2].Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НД = Док.Движения.ТМЦКПоступлению.ДобавитьРасход();
		НД.Период = Док.Дата;
		ЗаполнитьЗначенияСвойств(НД,Выборка);
	
	КонецЦикла; 
	
	Выборка = Результат[3].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НД = Док.Движения.РасчетыПоСделкамСПоставщиками.ДобавитьРасход();
		НД.Период = Док.Дата;
		ЗаполнитьЗначенияСвойств(НД,Выборка);
	
	КонецЦикла; 
	//СуммаКРаспределению = 0;
	//Если ТЗОстатковПоРасчетам.Количество()>0 Тогда
	//	Если Не ЗначениеЗаполнено(ТЗОстатковПоРасчетам[0].Сделка) Тогда
	//		СуммаКРаспределению = ТЗОстатковПоРасчетам[0].Сумма;
	//	КонецЕсли;	
	//КонецЕсли;	
	
	//Если СуммаКРаспределению <> 0 Тогда
		СуммаСписанного = 0;
		 Для каждого Стр Из ТЗОстатковПоРасчетам Цикл  // списываем всё что есть по сделкам
		 
			 Если ТипЗнч(Стр.Сделка) = Тип("ДокументСсылка.СделкаСПоставщиком") ИЛИ НЕ ЗначениеЗаполнено(Стр.Сделка)Тогда
				 НД = Док.Движения.РасчетыСКонтрагентами.ДобавитьРасход();
				 НД.Период = Док.Дата;
				 ЗаполнитьЗначенияСвойств(НД,Стр);
				 СуммаСписанного = СуммаСписанного + НД.Сумма;
			 КонецЕсли;	 
		 
		 КонецЦикла; 
		 СуммаОприходованного = 0;
		 Для каждого Стр Из ТЗАвансовПоСделкам Цикл // Приходуем долг по авансам
		 
		 	НД = Док.Движения.РасчетыСКонтрагентами.ДобавитьПриход();
			НД.Период = Док.Дата;
			НД.Контрагент = Контрагент;
			НД.СчетУчета = СчетРасчетов;
			НД.Организация = Вкусвилл;
			НД.АвансПоСделке = Стр.СуммаАванса;
			НД.Сумма = Стр.СуммаАванса;
			НД.Сделка = Стр.Сделка;
		 	СуммаОприходованного = СуммаОприходованного + НД.Сумма;
		КонецЦикла; 
		
		ОсталосьОприходовать = СуммаСписанного - СуммаОприходованного;
		
		Для каждого Стр Из ТЗОстатковПоРасчетам Цикл  // закрываем заявки на остаток
			Если ОсталосьОприходовать >= 0 Тогда
				Прервать;
			КонецЕсли;	
			Если Стр.Сумма <= 0 Тогда
				Продолжить;
			КонецЕсли;	
			 Если ТипЗнч(Стр.Сделка) = Тип("ДокументСсылка.ЗаявкаНаУслугиМатериалы")Тогда
				 НД = Док.Движения.РасчетыСКонтрагентами.ДобавитьПриход();
				 НД.Период = Док.Дата;
				 ЗаполнитьЗначенияСвойств(НД,Стр);
				 НД.Сумма = -Мин(НД.Сумма,-ОсталосьОприходовать);
				 ОсталосьОприходовать = ОсталосьОприходовать - НД.Сумма;
			 КонецЕсли;	 
		 
		 КонецЦикла; 
		 
		 Если ОсталосьОприходовать <> 0 Тогда
			НД = Док.Движения.РасчетыСКонтрагентами.ДобавитьПриход();
			НД.Период = Док.Дата;
			НД.Контрагент = Контрагент;
			НД.СчетУчета = СчетРасчетов;
			НД.Организация = Вкусвилл;
			НД.АвансПоСделке = 0;
			НД.Сумма = ОсталосьОприходовать;
			НД.Сделка = Неопределено;
		 КонецЕсли;	 
	//КонецЕсли;	
	
	
	
	//ТЗ = Док.Движения.РасчетыСКонтрагентами.Выгрузить();
	//Если ТЗ.Итог("Сумма")<>0 Тогда
	//	Сообщить("Документ заполнен неверно, он не должен менять остаток расчетов с контрагентами");
	//КонецЕсли;	
	
	Док.Комментарий = "Приведение к товарам к комплектации расчетов контрагента "+Контрагент;
	Док.Записать();
	Корректировка = Док.Ссылка;
	//Предупреждение("Обработка завершена!");
КонецПроцедуры

&НаКлиенте
Процедура ПривестиРасчеты(Команда)
	ПривестиРасчетыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбнулитьОстаткиТоваровККомплектацииНаСервере()
	ТекстЗапроса="ВЫБРАТЬ
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Сделка,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.Номенклатура,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.УИН_Этапа,
	             |	ТоварыККомплектацииСделокСПоставщикамиОстатки.КоличествоОстаток КАК Количество
	             |ИЗ
	             |	РегистрНакопления.ТоварыККомплектацииСделокСПоставщиками.Остатки(&НаДату, Сделка.Контрагент = &Контрагент) КАК ТоварыККомплектацииСделокСПоставщикамиОстатки";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату",НаДату);
	Запрос.УстановитьПараметр("Контрагент",Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ЗначениеЗаполнено(Корректировка) Тогда
		Док=Корректировка.ПолучитьОбъект();
		док.ТаблицаРегистровНакопления.Очистить();
		Док.Движения.ТоварыККомплектацииСделокСПоставщиками.Очистить();
		Док.Записать();
	Иначе	
		Док = Документы.КорректировкаЗаписейРегистровНакопления.СоздатьДокумент();
	КонецЕсли;
	Док.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	Док.Дата = НаДату;
	НС = Док.ТаблицаРегистровНакопления.Добавить();
	НС.Имя = "ТоварыККомплектацииСделокСПоставщиками";
	НС.Представление = "Товары к комплектации сделок с поставщиками";
			
	Пока Выборка.Следующий() Цикл
	    Если Выборка.Количество > 0 Тогда
			НД = Док.Движения.ТоварыККомплектацииСделокСПоставщиками.ДобавитьРасход();
			НД.Количество = Выборка.Количество;
		Иначе
			НД = Док.Движения.ТоварыККомплектацииСделокСПоставщиками.ДобавитьПриход();
			НД.Количество = -Выборка.Количество;
		КонецЕсли;
		НД.Период = Док.Дата;
		ЗаполнитьЗначенияСвойств(НД,Выборка,,"Количество");
	
	КонецЦикла; 

	Док.Комментарий = "Списание остатков по товарам к комплектации контрагента "+Контрагент;
	Док.Записать();
	Корректировка = Док.Ссылка;

КонецПроцедуры

&НаКлиенте
Процедура ОбнулитьОстаткиТоваровККомплектации(Команда)
	ОбнулитьОстаткиТоваровККомплектацииНаСервере();
КонецПроцедуры
