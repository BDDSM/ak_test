
Перем ТЗФайлыФорума;


//////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ФУНКЦИИ И ПРОЦЕДУРЫ

// Возращает элемент справочника "Форум", выбранный в левой части формы
//
Функция ПолучитьТекущуюТему()

	Если ЭлементыФормы.Панель2.ТекущаяСтраница = ЭлементыФормы.Панель2.Страницы.Документы Тогда
		Если ЭлементыФормы.ТабличноеПолеДокументы.ТекущаяСтрока <> Неопределено Тогда
			Возврат ЭлементыФормы.ТабличноеПолеДокументы.ТекущаяСтрока.Ссылка;
		КонецЕсли;	
	ИначеЕсли ЭлементыФормы.Панель2.ТекущаяСтраница = ЭлементыФормы.Панель2.Страницы.Форум Тогда
		Если ЭлементыФормы.ТабличноеПолеФорум.ТекущаяСтрока <> Неопределено Тогда
			Возврат ЭлементыФормы.ТабличноеПолеФорум.ТекущаяСтрока.Ссылка;
		КонецЕсли;	
	ИначеЕсли ЭлементыФормы.Панель2.ТекущаяСтраница = ЭлементыФормы.Панель2.Страницы.Справочники Тогда
		Если ЭлементыФормы.ТабличноеПолеСправочники.ТекущаяСтрока <> Неопределено Тогда
			Возврат ЭлементыФормы.ТабличноеПолеСправочники.ТекущаяСтрока.Ссылка;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Неопределено;

КонецФункции

// Для интеграции с не типовой надо переопределить пользователя из текущей конфигурации. 
//Т.е. нужно изменить ПараметрыСеанса.ТекущийПользователь на используемое выражение вашей конфигурации
//Можно ничего не менять тогда пользователь будет получаться функцией ИмяПользователя(), 
//нежелательно изменять имена пользователей конфигурации при последнем варианте.
Функция ПолучитьТекущегоПользователя() Экспорт 
	
	Попытка //типовая 
		
		Если НЕ ЗначениеЗаполнено(ПараметрыСеанса.ТекущийПользователь) Тогда
			ВызватьИсключение "Пользователь не авторизован в программе (нет в справочнике пользователи)";
		КонецЕсли;	
		
		Пользователь = Справочники.ФорумПользователи.НайтиПоРеквизиту("Пользователь", ПараметрыСеанса.ТекущийПользователь);
		Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
			ПользовательОб = Справочники.ФорумПользователи.СоздатьЭлемент();
			ПользовательОб.Код 					= ПараметрыСеанса.ТекущийПользователь.Код;
			ПользовательОб.Наименование 		= ПараметрыСеанса.ТекущийПользователь.Наименование;
			ПользовательОб.Пользователь 		= ПараметрыСеанса.ТекущийПользователь;
			ПользовательОб.Аватар 				= БиблиотекаКартинок.NoAvatar;
			ПользовательОб.КоличествоСообщений 	= 0;
			ПользовательОб.Записать();
			Пользователь = ПользовательОб.Ссылка;
		КонецЕсли;
		
	Исключение //не типовая ипользуем функцию ИмяПользователя()
		
		Пользователь = Справочники.ФорумПользователи.НайтиПоНаименованию(ИмяПользователя(), Истина);
		Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
			ПользовательОб = Справочники.ФорумПользователи.СоздатьЭлемент();
			ПользовательОб.Код 			= ИмяПользователя();
			ПользовательОб.Наименование = ИмяПользователя();
			//ПользовательОб.Пользователь = ПараметрыСеанса.ТекущийПользователь;
			ПользовательОб.Аватар 		= БиблиотекаКартинок.NoAvatar;
			ПользовательОб.КоличествоСообщений = 0;
			ПользовательОб.Записать();
			Пользователь = ПользовательОб.Ссылка;
		КонецЕсли;
		
	КонецПопытки;
	
	Возврат Пользователь;
	
КонецФункции

// Для интеграции с нетиповой надо переопределить, что является признаком полных прав, для типовой это Роль "ПолныеПрава"
Функция Админ() Экспорт
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ЭтаФорма.Пользователь);
	
	Если ПользовательИБ <> Неопределено Тогда
		Попытка
			Админ = ПользовательИБ.Роли.Содержит(Метаданные.Роли["ПолныеПрава"])
					ИЛИ ПользовательИБ.Роли.Содержит(Метаданные.Роли["АК_ФорумАдминистратор"]);
		Исключение
			Админ = Ложь;
		КонецПопытки;	
	Иначе
		Админ = Ложь;
	КонецЕсли;
	
	Возврат Админ;
	
КонецФункции

Процедура ОбновитьСписокТем(Кнопка) Экспорт 
	
	Если ЭтаФорма.Тема = Неопределено Тогда
		ЭлементыФормы.ПолеHTMLДокумента1.Перейти("about:blank");
		Возврат;
	ИначеЕсли ЭтаФорма.Тема.Пустая() Тогда
		ЭлементыФормы.ПолеHTMLДокумента1.Перейти("about:blank");
		Возврат;
	КонецЕсли;	
	
	//
	ОбработкаФорум.ОбновитьСписокТемРаздела(ЭтаФорма.Тема, ЭлементыФормы);
	
КонецПроцедуры

Процедура ОткрытьФормуНовойТемыОбъекта(ОбъектСсылка, МетаданныеОбъекта) Экспорт 
	
	ЭлементыФормы.Панель2.ТекущаяСтраница = ЭлементыФормы.Панель2.Страницы.Документы;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ОбъектСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Форум.Ссылка
	|ИЗ
	|	Справочник.Форум КАК Форум
	|ГДЕ
	|	Форум.СсылкаНаОбъект = &Ссылка";
	ТЗ = Запрос.Выполнить().Выгрузить();
	Если ТЗ.Количество() Тогда
		Если ЗначениеЗаполнено(ТЗ[0].Ссылка) Тогда
			Тема = ТЗ[0].Ссылка;
			Обновить1(0);
			Активизировать();
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	Форма = Справочники.Форум.ПолучитьФормуНовогоЭлемента("ФормаЭлемента", ЭтаФорма, "ФормаНовойТемы");
	Форма.Пользователь = Пользователь;
	Форма.ИспользоватьФормуОбъектаКонфигурации = Истина;
	Форма.ЭтотОбъект.СсылкаНаОбъект = ОбъектСсылка;
	Форма.ЭтотОбъект.Наименование 	= "Обсуждение: " + ОбъектСсылка;
	Форма.ЭтотОбъект.ВидОбъекта 	= МетаданныеОбъекта.Синоним;

	Если НЕ Форма.Открыта() Тогда
		Форма.Открыть();
	КонецЕсли;
	
	Форма.Активизировать();
	
КонецПроцедуры


//////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ПолучитьТекущегоПользователя();	
	ИмяПользователя = Пользователь.Наименование;
	Аватар = Пользователь.Аватар.Получить();
	ЭлементыФормы.ПолеКартинки1.Картинка = ?(Аватар = Неопределено, БиблиотекаКартинок.NoAvatar, Аватар);
	
	//Отбор1 = СписокФорумовДокументы.Отбор;
	//Отбор1.Ссылка.Использование = Истина;
	//Отбор1.Ссылка.Значение  	= Справочники.Форум.Документы;
	//Отбор1.Ссылка.ВидСравнения  = ВидСравнения.ВИерархии;
	//
	//Отбор = СписокФорумов.Отбор;
	//Отбор.Ссылка.Использование 	= Истина;
	//Отбор.Ссылка.Значение  		= Справочники.Форум.Форум;
	//Отбор.Ссылка.ВидСравнения  	= ВидСравнения.ВИерархии;
	//
	//Отбор = СписокФорумовСправочники.Отбор;
	//Отбор.Ссылка.Использование 	= Истина;
	//Отбор.Ссылка.Значение  		= Справочники.Форум.Справочники;
	//Отбор.Ссылка.ВидСравнения  	= ВидСравнения.ВИерархии;
	
	Если НЕ Админ() Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанель1.Кнопки.Настройки);
	КонецЕсли;	
	
	ЭтаФорма.ФлажокПоискПоТемам = Истина;
	
КонецПроцедуры


//////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КоманднаяПанель1Назад(Кнопка)
	
	ЭлементыФормы.ПолеHTMLДокумента1.Документ.parentWindow.history.back();
	ОбработкаФорум.ИзменитьТекущуюТему(ЭтаФорма);
	
КонецПроцедуры

Процедура КоманднаяПанель1Вперед(Кнопка)
	
	ЭлементыФормы.ПолеHTMLДокумента1.Документ.parentWindow.history.forward();
	ОбработкаФорум.ИзменитьТекущуюТему(ЭтаФорма);
	
КонецПроцедуры

Процедура Обновить1(Кнопка) Экспорт
	
	Если ЭтаФорма.Тема.Пустая() Тогда
		Возврат;
	КонецЕсли;	
	Если ЭтаФорма.Тема.ЭтоГруппа Тогда
		ОбновитьСписокТем(0);
		Возврат;
	КонецЕсли;	
	
	//
	ОбработкаФорум.ВывестиТему(ЭтаФорма.Тема, ЭлементыФормы);
	
КонецПроцедуры

Процедура КоманднаяПанель1Темы(Кнопка)
	
	Если ЭлементыФормы.КоманднаяПанель1.Кнопки.Темы.Пометка = Истина Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.Темы.Пометка = Ложь;
		
		ЭлементыФормы.Панель1.УстановитьПривязку(ГраницаЭлементаУправления.Право);
		ЭлементыФормы.Разделитель1.УстановитьПривязку(ГраницаЭлементаУправления.Лево, ЭлементыФормы.Панель1, ГраницаЭлементаУправления.Право);
		ЭлементыФормы.Панель1.Свертка 		= РежимСверткиЭлементаУправления.Лево;
		ЭлементыФормы.Разделитель1.Свертка 	= РежимСверткиЭлементаУправления.Лево;
	Иначе
		ЭлементыФормы.КоманднаяПанель1.Кнопки.Темы.Пометка = Истина;
		ЭлементыФормы.Панель1.Свертка  		= РежимСверткиЭлементаУправления.Нет;
		ЭлементыФормы.Разделитель1.Свертка 	= РежимСверткиЭлементаУправления.Нет;
		ЭлементыФормы.Разделитель1.УстановитьПривязку(ГраницаЭлементаУправления.Лево);
		ЭлементыФормы.Разделитель1.УстановитьПривязку(ГраницаЭлементаУправления.Право	, ЭлементыФормы.ПолеHTMLДокумента1	, ГраницаЭлементаУправления.Лево);
		ЭлементыФормы.Панель1.УстановитьПривязку(ГраницаЭлементаУправления.Право		, ЭлементыФормы.Разделитель1		, ГраницаЭлементаУправления.Лево);
		ЭлементыФормы.Разделитель1.Ширина 	= 7;
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель1Настройки(Кнопка)
	
	ФормаН = Обработки.Форум.ПолучитьФорму("Настройки",, "Настройки");
	Если НЕ ФормаН.Открыта() Тогда
		ФормаН.Открыть();
	КонецЕсли;
	
	ФормаН.Активизировать();
	
КонецПроцедуры

Процедура КоманднаяПанель1Уведомления(Кнопка)
	
	ФормаУв = ОбработкаФорум.ПолучитьФорму("Уведомления",, "Уведомления");
	Если НЕ ФормаУв.Открыта() Тогда
		ФормаУв.Открыть();
	КонецЕсли;
	
	ОтборУв = ФормаУв.ЗадачаСписок.Отбор.Пользователь;
	ОтборУв.Использование 	= Истина;
	ОтборУв.ВидСравнения 	= ВидСравнения.Равно;
	ОтборУв.Значение 		= Пользователь;
	ФормаУв.Активизировать();
		
КонецПроцедуры

Процедура КоманднаяПанель1Подписка(Кнопка)
	
	ФормаП = РегистрыСведений.ФорумПодписка.ПолучитьФормуСписка(,, "Подписка");
	Если Не ФормаП.Открыта() Тогда
		ФормаП.Открыть();
	КонецЕсли;	
	ФормаП.Активизировать();
	
КонецПроцедуры

Процедура КоманднаяПанель1НоваяТема(Кнопка)
	
	ТекСтраница = ЭлементыФормы.Панель2.ТекущаяСтраница;
	Если ТекСтраница = ЭлементыФормы.Панель2.Страницы.Форум Тогда
		ТекТабПоле = ЭлементыФормы.ТабличноеПолеФорум;
	ИначеЕсли ТекСтраница = ЭлементыФормы.Панель2.Страницы.Документы Тогда
		ТекТабПоле = ЭлементыФормы.ТабличноеПолеДокументы;
	ИначеЕсли ТекСтраница = ЭлементыФормы.Панель2.Страницы.Справочники Тогда
		ТекТабПоле = ЭлементыФормы.ТабличноеПолеСправочники;
	КонецЕсли;	
	Если ТекТабПоле.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Если НЕ ТекТабПоле.ТекущаяСтрока.Ссылка.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	//
	Форма = Справочники.Форум.ПолучитьФормуНовогоЭлемента("ФормаЭлемента", ЭтаФорма, "ФормаНовойТемы");
	Форма.Пользователь 			= Пользователь;
	Форма.ЭтотОбъект.Родитель 	= ТекТабПоле.ТекущиеДанные.Ссылка;
	Если НЕ Форма.Открыта() Тогда
		Форма.Открыть();
	КонецЕсли;
	Форма.Активизировать();
	
КонецПроцедуры

Процедура КоманднаяПанель1Ответ(Кнопка)
	
	Если ЭтаФорма.Тема.ЭтоГруппа
			ИЛИ ЭлементыФормы.ТабличноеПолеФорум.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.Тема.Закрыта
			И НЕ Админ() Тогда
		Сообщить("Выбранная тема закрыта!!!", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	//
	ФормаОтвета = Обработки.Форум.ПолучитьФорму("Ответ", ЭтаФорма, "Ответ");
	ФормаОтвета.Тема 			= ЭтаФорма.Тема;
	ФормаОтвета.Пользователь 	= ЭтаФорма.Пользователь;
	ФормаОтвета.Заголовок 		= "Ответ в тему: " + ЭтаФорма.Тема.Наименование;
	Если НЕ ФормаОтвета.Открыта() Тогда
		ФормаОтвета.Открыть();
	КонецЕсли;
	
	ФормаОтвета.Активизировать();
	
КонецПроцедуры


Процедура ПрофильНажатие(Элемент)
	
	ФормаПользователя = Пользователь.ПолучитьФорму("ФормаЭлемента", ЭтаФорма, "ПользовательФорума");
	Если НЕ ФормаПользователя.Открыта() Тогда
		ФормаПользователя.Открыть();
	КонецЕсли;	
	
	ФормаПользователя.Активизировать();
	
КонецПроцедуры


Процедура Панель2ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Тема = ПолучитьТекущуюТему();
	
	ОбновитьСписокТем(0);
	
КонецПроцедуры


Процедура ТабличноеПолеФорумВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЭтаФорма.Тема = ПолучитьТекущуюТему();
	ОбновитьСписокТем(0);
	
КонецПроцедуры

Процедура ТабличноеПолеФорумПриАктивизацииСтроки(Элемент)
	
	//ЭтаФорма.Тема = ПолучитьТекущуюТему();
	//
	ТабличноеПолеФорумВыбор(0, 0, 0, 0);
	
КонецПроцедуры


Процедура ПолеHTMLДокумента1ДокументСформирован(Элемент)
	
	Если НомерПоста Тогда
		ЭлементыФормы.ПолеHTMLДокумента1.Перейти(ЭлементыФормы.ПолеHTMLДокумента1.Документ.URLUnencoded + "#P" + НомерПоста);
		НомерПоста = 0;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолеHTMLДокументаOnClick(Элемент, pEvtObj)
	
	htmlElement = pEvtObj.srcElement;
	
	Если htmlElement = Неопределено
			ИЛИ htmlElement.id = "" Тогда
		Возврат;
	КонецЕсли;
	
	Найти = Найти(htmlElement.id, "PhorumUser_");
	Если Найти Тогда
		pEvtObj.returnValue = Ложь;
		НаименованиеПользователя = Прав(htmlElement.id, СтрДлина(htmlElement.id) - 11);
		ПользовательС = Справочники.ФорумПользователи.НайтиПоНаименованию(НаименованиеПользователя, Истина);
		Если ЗначениеЗаполнено(ПользовательС) Тогда
			ФормаПользователя = ПользовательС.ПолучитьФорму("ФормаЭлемента", ЭтаФорма, "ПользовательФорума");
			Если НЕ ФормаПользователя.Открыта() Тогда
				ФормаПользователя.Открыть();
			КонецЕсли;
			ФормаПользователя.Активизировать();
		КонецЕсли;	
		Возврат;
	КонецЕсли;
	
	Найти = Найти(htmlElement.id, "TemaObj_");
	Если Найти Тогда
		pEvtObj.returnValue = Ложь;
		КодТемы = Прав(htmlElement.id, СтрДлина(htmlElement.id) - 8);
		ТемаН = Справочники.Форум.НайтиПоКоду(КодТемы);
		ОткрытьЗначение(ТемаН.СсылкаНаОбъект);
		Возврат;
	КонецЕсли;
	
	Найти = Найти(htmlElement.id, "PhorumFile_");
	Если Найти Тогда
		pEvtObj.returnValue = Ложь;
		Параметры = Прав(htmlElement.id, СтрДлина(htmlElement.id) - 11);
		Параметры = СтрЗаменить(Параметры, "_", Символы.ПС);
		НомерПоста 	= СтрПолучитьСтроку(Параметры, 1);
		ИмяФайла 	= СтрПолучитьСтроку(Параметры, 2);
		
		МассФайлов = ТЗФайлыФорума.НайтиСтроки(Новый Структура("Номер, ИмяФайла", "" + НомерПоста, ИмяФайла));
		Если МассФайлов.Количество() Тогда
			
			ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
			ДиалогОткрытияФайла.ПолноеИмяФайла		= МассФайлов[0].ИмяФайла;
			ДиалогОткрытияФайла.Фильтр				= "Любые файлы(*.*)|*.*";
			ДиалогОткрытияФайла.МножественныйВыбор	= Ложь;
			ДиалогОткрытияФайла.Заголовок			= "Выберите файл";
			Если ДиалогОткрытияФайла.Выбрать() Тогда
				МасФайлов = ДиалогОткрытияФайла.ВыбранныеФайлы;
				Для каждого ТекФайл Из МасФайлов цикл
					ПутьКФайлу = ТекФайл;
				конецЦикла;
			КонецЕсли;
			Если СокрЛП(ПутьКФайлу) = "" тогда 
				//Сообщить("Не выбран файл !",СтатусСообщения.Важное);
				Возврат;
			КонецЕсли;
			Файл = МассФайлов[0].Файл.Получить();
			Файл.Записать(ПутьКФайлу);
			
			ЗаписьФайл = РегистрыСведений.ФорумФайлы.СоздатьМенеджерЗаписи();
			ЗаписьФайл.Объект 		= ЭтаФорма.Тема;
			ЗаписьФайл.ЭтоКартинка 	= МассФайлов[0].ЭтоКартинка;
			ЗаписьФайл.Номер 		= МассФайлов[0].Номер;
			ЗаписьФайл.ИмяФайла 	= МассФайлов[0].ИмяФайла;
			ЗаписьФайл.Прочитать();
			Если ЗаписьФайл.Выбран() Тогда
				ЗаписьФайл.КоличествоСкачиваний = ЗаписьФайл.КоличествоСкачиваний + 1;
				ЗаписьФайл.Записать();
			КонецЕсли;	
		КонецЕсли;	
		
		Возврат;
	КонецЕсли;
	
	Пока htmlElement <> Неопределено
			И ВРег(htmlElement.tagName) <> "A" Цикл
		htmlElement = htmlElement.parentElement;
	КонецЦикла;

	Если htmlElement = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Найти = Найти(htmlElement.id, "PhorumUser_");
	Если Найти Тогда
		pEvtObj.returnValue = Ложь;
		НаименованиеПользователя = Прав(htmlElement.id, СтрДлина(htmlElement.id) - 11);
		ПользовательС = Справочники.ФорумПользователи.НайтиПоНаименованию(НаименованиеПользователя, Истина);
		Если ЗначениеЗаполнено(ПользовательС) Тогда
			ФормаПользователя = ПользовательС.ПолучитьФорму("ФормаЭлемента", ЭтаФорма, "ПользовательФорума");
			Если НЕ ФормаПользователя.Открыта() Тогда
				ФормаПользователя.Открыть();
			КонецЕсли;
			ФормаПользователя.Активизировать();
		КонецЕсли;	
		Возврат;
	КонецЕсли;
	
	Найти = Найти(htmlElement.id, "threadlastpost_");
	Если Найти Тогда
		pEvtObj.returnValue = Ложь;
		КодТемы = Прав(htmlElement.id, СтрДлина(htmlElement.id) - 15);
		ЭтаФорма.Тема 		= Справочники.Форум.НайтиПоКоду(КодТемы);
		ЭтаФорма.НомерПоста = ЭтаФорма.Тема.КоличествоПостов + 1;
		Обновить1(0);
		Возврат;
	КонецЕсли;
	
	Найти = Найти(htmlElement.id, "thread_");
	Если Найти Тогда
		pEvtObj.returnValue = Ложь;
		КодТемы = Прав(htmlElement.id, СтрДлина(htmlElement.id) - 7);
		ЭтаФорма.Тема = Справочники.Форум.НайтиПоКоду(КодТемы);
		Обновить1(0);
		Возврат;
	КонецЕсли;
	
	Найти = Найти(htmlElement.href, "#P");
	Если НЕ Найти Тогда
		pEvtObj.returnValue = Ложь;
		ЗапуститьПриложение(htmlElement.href);
	КонецЕсли;
	
КонецПроцедуры


Процедура ФлажокПоискПоТемамПриИзменении(Элемент)
	
	ЭтаФорма.ФлажокПоискПоПостам = НЕ ЭтаФорма.ФлажокПоискПоТемам;
	
КонецПроцедуры

Процедура ФлажокПоискПоПостамПриИзменении(Элемент)
	
	ЭтаФорма.ФлажокПоискПоТемам = НЕ ЭтаФорма.ФлажокПоискПоПостам;
	
КонецПроцедуры

Процедура КнопкаНайтиНажатие(Элемент)
	
	//ТекТема = ПолучитьТекущуюТему();
	//Если ТекТема = Неопределено Тогда
	//	Возврат;
	//КонецЕсли;

	HTMLПрилеплена =
	"	<span style=""float:right"">
	|<img class=""inlineimg"" src=""%КаталогФорума%files/sticky00.gif"" alt=""Важная тема"" /> 
	|</span>
	|<strong style=""font-size:0.9em"">Важно:</strong>";

	HTMLРазделитель = "<tr><td class=""thead"" colspan=""7"">&nbsp;</td></tr>";
	
	
	Если ЭтаФорма.ФлажокПоискПоТемам Тогда
		//
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекстПоиска", "%" + СокрЛП(ЭтаФорма.СтрокаПоиска) + "%");
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СпрФорум.Код,
		|	СпрФорум.Наименование,
		|	СпрФорум.АвторПоследнегоПоста.Наименование КАК АвторПоследнегоПоста,
		|	СпрФорум.Автор.Наименование КАК Автор,
		|	СпрФорум.ДатаПоследнегоПоста КАК ДатаПоследнегоПоста,
		|	СпрФорум.Закрыта,
		|	СпрФорум.КоличествоПостов,
		|	СпрФорум.КоличествоПросмотров,
		|	СпрФорум.Описание,
		|	СпрФорум.ПоследнийПост,
		|	СпрФорум.КартинкаТемы,
		|	ЕСТЬNULL(СпрФорум.Прикреплена, ЛОЖЬ) КАК Прикреплена
		|ИЗ
		|	Справочник.Форум КАК СпрФорум
		|ГДЕ
		|	СпрФорум.Наименование ПОДОБНО &ТекстПоиска
		|	И НЕ СпрФорум.ЭтоГруппа
		|	И НЕ СпрФорум.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Прикреплена УБЫВ,
		|	ДатаПоследнегоПоста УБЫВ";
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Сообщить("Не найдены темы форума!");
			Возврат;
		КонецЕсли;
		
		МакетФорума 		= Обработки.Форум.ПолучитьМакет("ШаблонСписокТем");
		ШаблонФорума 		= МакетФорума.ПолучитьТекст();
		
		МакетТемыФорума 	= Обработки.Форум.ПолучитьМакет("ШаблонТема");
		ШаблонТемыФорума 	= МакетТемыФорума.ПолучитьТекст();

		ХТМЛТем = "";

		ПервыйПроход 		= Истина;
		ВыводитьРазделитель = Ложь;

		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл

			Если ПервыйПроход
					И Выборка.Прикреплена Тогда
				ВыводитьРазделитель = Истина;
			КонецЕсли;
			Если ПервыйПроход Тогда
				ПервыйПроход = Ложь;
			КонецЕсли;
			Если ВыводитьРазделитель
					И НЕ Выборка.Прикреплена Тогда
				ХТМЛТем = ХТМЛТем + HTMLРазделитель;
				ВыводитьРазделитель = Ложь;
			КонецЕсли;

			Если Выборка.Прикреплена Тогда
				ХТМЛТема = СтрЗаменить(ШаблонТемыФорума, "%Прилеплена%", HTMLПрилеплена);
			Иначе
				ХТМЛТема = СтрЗаменить(ШаблонТемыФорума, "%Прилеплена%", "");
			КонецЕсли;
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%КодТемы%"					, Выборка.Код);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%ТекстТемы%"					, Выборка.Описание);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%ДатаПоследнегоПоста%"		, Выборка.ДатаПоследнегоПоста);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%ЗаголовокТемы%"				, Выборка.Наименование);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%КартинкаТемы%"				, Выборка.КартинкаТемы);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%АвторПоста%"					, Выборка.Автор);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%АвторПоследнегоПоста%"		, Выборка.АвторПоследнегоПоста);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%Ответов%"					, Выборка.КоличествоПостов);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%Просмотров%"					, Выборка.КоличествоПросмотров);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%КодПользователя%"			, Выборка.Автор);
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%КодПоследнегоПользователя%"	, Выборка.АвторПоследнегоПоста);
			Если Выборка.Закрыта Тогда
				ХТМЛТема = СтрЗаменить(ХТМЛТема, "thread_h.gif"				, "thread_l.gif");
			КонецЕсли;
			ХТМЛТема = СтрЗаменить(ХТМЛТема, "%КаталогФорума%"				, КаталогВременныхФайлов());
			ХТМЛТем = ХТМЛТем + ХТМЛТема;

		КонецЦикла;
		ХТМЛ = СтрЗаменить(ШаблонФорума	, "%КаталогФорума%"	, КаталогВременныхФайлов());
		ХТМЛ = СтрЗаменить(ХТМЛ			, "%Темы%"			, ХТМЛТем);
		
		ХТМЛФайл = Новый ЗаписьТекста(КаталогВременныхФайлов() + "files\tempphorum" + Выборка.Код + ".html");
		ХТМЛФайл.Записать(ХТМЛ);
		ХТМЛФайл.Закрыть();

		ЭлементыФормы.ПолеHTMLДокумента1.Перейти(КаталогВременныхФайлов() + "files\tempphorum" + Выборка.Код + ".html");
		
	//	
	ИначеЕсли ЭтаФорма.ФлажокПоискПоПостам Тогда
		
		МакетФорума 		= Обработки.Форум.ПолучитьМакет("Шаблон");
		МакетПостаФорума 	= Обработки.Форум.ПолучитьМакет("ШаблонПоста");
		ШаблонФорума 		= МакетФорума.ПолучитьТекст();
		ШаблонПостаФорума 	= МакетПостаФорума.ПолучитьТекст();

		МассивПодключений = ПолучитьСоединенияИнформационнойБазы();
		СтрокаПользователи = "";
		Для каждого СтрМасива Из МассивПодключений Цикл
			Если СтрМасива.ИмяПриложения = "1CV8" Тогда
				СтрокаПользователи = СтрокаПользователи + "," + СтрМасива.Пользователь.Имя;
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
		Запрос.УстановитьПараметр("ТекстПоиска", "%" + СокрЛП(ЭтаФорма.СтрокаПоиска) + "%");
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ФорумСрезПоследних.Период КАК Период,
		|	ФорумСрезПоследних.Автор КАК Автор,
		|	ФорумСрезПоследних.Тема КАК Тема,
		|	ФорумСрезПоследних.Пост КАК Пост,
		|	ФорумСрезПоследних.Номер
		|ПОМЕСТИТЬ ВТПосты
		|ИЗ
		|	РегистрСведений.Форум.СрезПоследних(&ТекущаяДата, ) КАК ФорумСрезПоследних
		|ГДЕ
		|	(ФорумСрезПоследних.Тема.Описание ПОДОБНО &ТекстПоиска
		|			ИЛИ ФорумСрезПоследних.Пост ПОДОБНО &ТекстПоиска)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПосты.Период КАК Период,
		|	ВТПосты.Автор КАК Автор,
		|	ВТПосты.Тема КАК Тема,
		|	ВЫБОР
		|		КОГДА ВТПосты.Тема.Описание ПОДОБНО &ТекстПоиска
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТемаСодержит,
		|	ВТПосты.Пост КАК Пост,
		|	ВЫБОР
		|		КОГДА ВТПосты.Пост ПОДОБНО &ТекстПоиска
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПостСодержит,
		|	ВТПосты.Номер
		|ИЗ
		|	ВТПосты КАК ВТПосты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СпрФорум.ДатаСоздания,
		|	СпрФорум.Автор,
		|	СпрФорум.Ссылка,
		|	ИСТИНА,
		|	"""",
		|	ЛОЖЬ,
		|	0
		|ИЗ
		|	Справочник.Форум КАК СпрФорум
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПосты КАК ВТПосты
		|		ПО (ВТПосты.Тема = СпрФорум.Ссылка)
		|ГДЕ
		|	ВТПосты.Тема ЕСТЬ NULL 
		|	И СпрФорум.Описание ПОДОБНО &ТекстПоиска
		|   И НЕ СпрФорум.ПометкаУдаления
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФорумФайлы.Объект,
		|	ФорумФайлы.Номер,
		|	ФорумФайлы.ЭтоКартинка,
		|	ФорумФайлы.ИмяФайла,
		|	ФорумФайлы.ПолноеИмяФайла,
		|	ФорумФайлы.Файл,
		|	ФорумФайлы.КоличествоСкачиваний,
		|	ФорумФайлы.Размер
		|ИЗ
		|	РегистрСведений.ФорумФайлы КАК ФорумФайлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			ВТПосты.Тема КАК Тема
		|		ИЗ
		|			ВТПосты КАК ВТПосты) КАК ВТПосты
		|		ПО ФорумФайлы.Объект = ВТПосты.Тема";
		
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		Резу = РезультатыЗапроса[1];
		Если Резу.Пустой() Тогда
			Сообщить("Не найдены посты!");
			Возврат;
		КонецЕсли;
		
		ТаблицаФайлов = РезультатыЗапроса[2].Выгрузить();
		ТаблицаФайлов.Индексы.Добавить("Объект");
		ТаблицаФайлов.Индексы.Добавить("Номер");
		
		ХТМЛПостов = "";
		НомерПоста1 = 0;
		ПользовательАдмин = ОбработкаФорум.ТекущийПользовательАдмин();
		ХТМЛЗвездочка = "<IMG alt=* src=""files/staradmin.gif"" border=0>";
		
		ТекТема = "";
		
		//
		Выборка = Резу.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ТекТема  = Выборка.Тема;
			НомерПоста1 = НомерПоста1 + 1;
			
			Если Выборка.ТемаСодержит Тогда
				
				ТекАвтор 		= ТекТема.Автор;
				Попытка
					ПользовательНайден = Найти(СтрокаПользователи, ТекАвтор.Наименование);
				Исключение
					ПользовательНайден = 0;
				КонецПопытки;
				СтатусОнлайн 	= ?(ПользовательНайден, "useron", "useroff");
				ТекПост 		= Выборка.Тема.Описание;
				Тема1 			= Выборка.Тема.Наименование;
				
			ИначеЕсли Выборка.ПостСодержит Тогда
				
				ТекАвтор 		= Выборка.Автор;
				Попытка
					ПользовательНайден = Найти(СтрокаПользователи, ТекАвтор.Наименование);
				Исключение
					ПользовательНайден = 0;
				КонецПопытки;
				СтатусОнлайн 	= ?(ПользовательНайден, "useron", "useroff");
				ТекПост 		= Выборка.Пост;
				Тема1 			= "RE:" + Выборка.Тема.Наименование;
				
			КонецЕсли;
			
			
			МассивФайлов = ТаблицаФайлов.НайтиСтроки(Новый Структура("Объект, Номер", ТекТема, Выборка.Номер));    	// файлы к постам-ответам на тему
			Если МассивФайлов.Количество() = 0 Тогда
				МассивФайлов = ТаблицаФайлов.НайтиСтроки(Новый Структура("Объект, Номер", ТекТема, 0)); 			// файлы в карточке темы
			КонецЕсли;
			Для каждого ФайлПоста Из МассивФайлов Цикл
				Если ФайлПоста.ЭтоКартинка Тогда
					ИмяЗамены = "#IMG_" + ФайлПоста.ИмяФайла + "#";
					Нашли = Найти(ТекПост, ИмяЗамены);
					Если Нашли Тогда

						ФайлКартинки = Новый Файл(КаталогВременныхФайлов() + "\files\" + ФайлПоста.ПолноеИмяФайла);
						Если Не ФайлКартинки.Существует() Тогда
							Попытка
								ФайлПоста.Файл.Получить().Записать(КаталогВременныхФайлов() + "\files\" + ФайлПоста.ПолноеИмяФайла);
							Исключение

							КонецПопытки;
						КонецЕсли;

						ТекПост = СтрЗаменить(ТекПост, ИмяЗамены, "<img src=""files/" + ФайлПоста.ПолноеИмяФайла + """  border=""0"" />");
					КонецЕсли;
				КонецЕсли;
				ИмяЗамены = "#FILE_" + ФайлПоста.ИмяФайла + "#";
				Нашли = Найти(ТекПост, ИмяЗамены);
				Если Нашли Тогда

					ТекПост = СтрЗаменить(ТекПост, ИмяЗамены, "<strong style=""font-size:0.9em"">Файл: </strong><a href=""file"" id=""PhorumFile_" + Выборка.Номер + "_" + 
							ФайлПоста.ИмяФайла + """>" + ФайлПоста.ИмяФайла + "</a>" + "   Размер: " + ФайлПоста.Размер + " b. " + "   Количество скачиваний: " + 
							ФайлПоста.КоличествоСкачиваний);
				КонецЕсли;
			КонецЦикла;

			ХТМЛПост = СтрЗаменить(ШаблонПостаФорума, "%Пост%", ТекПост);
			
			Если ПользовательАдмин Тогда
				КоличествоЗвезд = 7;
			Иначе
				Если ТекАвтор.КоличествоСообщений / 100 > 1 Тогда
					КоличествоЗвезд = 1;
				ИначеЕсли ТекАвтор.КоличествоСообщений / 100 > 2 Тогда
					КоличествоЗвезд = 2;
				ИначеЕсли ТекАвтор.КоличествоСообщений / 100 > 3 Тогда
					КоличествоЗвезд = 3;
				ИначеЕсли ТекАвтор.КоличествоСообщений / 100 > 4 Тогда
					КоличествоЗвезд = 4;
				Иначе
					КоличествоЗвезд = 5;
				КонецЕсли;
			КонецЕсли;
			ХТМЛЗвезд = "";
			Для з = 1 По КоличествоЗвезд Цикл
				ХТМЛЗвезд = ХТМЛЗвезд + ХТМЛЗвездочка;
			КонецЦикла;
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%РейтингСообщений%", ХТМЛЗвезд);

			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%НомерОтветаФорум%"			, НомерПоста1);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%ДатаОтветаФорум%"			, Формат(Выборка.Период, "ДЛФ=DDT"));
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%КоличествоСообщенийФорум%"	, ТекАвтор.КоличествоСообщений);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%ЮзерОнлайнФорум%"			, ?(ПользовательНайден, "Онлайн", "Офлайн"));
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%ЮзерОнлайКартинкаФорум%"		, СтатусОнлайн);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%Автор%"						, ТекАвтор);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%Подразделение%"				, ТекАвтор.Подразделение);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%Должность%"					, ТекАвтор.Должность);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%Тема%"						, Тема1);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%Подпись%"					, ТекАвтор.Подпись);
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%КодПользователя%"			, ТекАвтор.Наименование);
			Если СокрЛП(ТекАвтор.ИмяФайлаАватара) = "" Тогда
				ХТМЛПост = СтрЗаменить(ХТМЛПост, "%ИмяФайлаКартинки%"		, "defavatar.jpeg");
			Иначе
				ХТМЛПост = СтрЗаменить(ХТМЛПост, "%ИмяФайлаКартинки%"		, ТекАвтор.ИмяФайлаАватара);
			КонецЕсли;
			ХТМЛПост = СтрЗаменить(ХТМЛПост, "%Фон%"						, ?(Цел(НомерПоста1 / 2) = НомерПоста1 / 2, "2", ""));

			ХТМЛПостов = ХТМЛПостов + ХТМЛПост;
			
		КонецЦикла;
		
		ХТМЛ = СтрЗаменить(ШаблонФорума	, "%КаталогФорума%"	, КаталогВременныхФайлов());
		ХТМЛ = СтрЗаменить(ХТМЛ			, "%Посты%"			, ХТМЛПостов);

		Если ЗначениеЗаполнено(Тема.СсылкаНаОбъект) Тогда
			ХТМЛ = СтрЗаменить(ХТМЛ, "%ИДОбъкта%", "TemaObj_" + ТекТема.Код);
		Иначе
			ХТМЛ = СтрЗаменить(ХТМЛ, "%ИДОбъкта%", "");
		КонецЕсли;

		ХТМЛ = СтрЗаменить(ХТМЛ, "%ИмяФайла%", КаталогВременныхФайлов() + "files\temptopik" + ТекТема.Код + ".html");
		ХТМЛФайл = Новый ЗаписьТекста(КаталогВременныхФайлов() + "files\temptopik" + ТекТема.Код + ".html");
		ХТМЛФайл.Записать(ХТМЛ);
		ХТМЛФайл.Закрыть();


		ЭлементыФормы.ПолеHTMLДокумента1.Перейти(КаталогВременныхФайлов() + "files\temptopik" + ТекТема.Код + ".html");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаОтменитьПоискНажатие(Элемент)
	
	ТабличноеПолеФорумВыбор(0, 0, 0, 0);
	
КонецПроцедуры
