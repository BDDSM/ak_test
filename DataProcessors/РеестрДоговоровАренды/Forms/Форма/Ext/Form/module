

Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	НастройкаПериода.УстановитьПериод(НачалоПериода, ?(КонецПериода='0001-01-01', КонецПериода, КонецДня(КонецПериода)));
	Если НастройкаПериода.Редактировать() Тогда
		НачалоПериода = НастройкаПериода.ПолучитьДатуНачала();
		КонецПериода = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры

Процедура Заполнить(Кнопка)
	Если Не ЗначениеЗаполнено(НачалоПериода) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала периода");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(КонецПериода) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата конца периода");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДата()));
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СтатусыДоговоровАрендыСрезПоследних.Договор.Организация КАК Организация,
	               |	СтатусыДоговоровАрендыСрезПоследних.Договор.Владелец КАК Контрагент,
	               |	СтатусыДоговоровАрендыСрезПоследних.Договор КАК ДоговорКонтрагента,
	               |	СтатусыДоговоровАрендыСрезПоследних.Договор.ОбъектАренды.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	               |	СтатусыДоговоровАрендыСрезПоследних.ДатаНачалаАренды КАК ДатаНачалаАренды,
	               |	СтатусыДоговоровАрендыСрезПоследних.ДатаОкончанияАренды КАК ДатаОкончанияАренды,
	               |	ВложенныйЗапрос.ТипАренднойПлаты КАК ТипАренднойПлаты,
	               |	ВложенныйЗапрос.ФормаОплаты КАК ФормаОплаты
	               |ПОМЕСТИТЬ ВТ_Договоры
	               |ИЗ
	               |	РегистрСведений.СтатусыДоговоровАренды.СрезПоследних(
	               |			&КонецПериода,
	               |			(Договор.Организация = &Организация
	               |				ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	               |				И (Договор.Владелец = &Контрагент
	               |					ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |				И (Договор.ОбъектАренды.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |					ИЛИ &СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))) КАК СтатусыДоговоровАрендыСрезПоследних,
	               |	(ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(перечисление.типыаренднойплаты.постояннаячасть) КАК ТипАренднойПлаты,
	               |		ЗНАЧЕНИЕ(перечисление.формыоплаты.безналичные) КАК ФормаОплаты
	               |	
	               |	ОБЪЕДИНИТЬ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(перечисление.типыаренднойплаты.постояннаячасть),
	               |		ЗНАЧЕНИЕ(перечисление.формыоплаты.наличные)
	               |	
	               |	ОБЪЕДИНИТЬ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(перечисление.типыаренднойплаты.переменнаячасть),
	               |		ЗНАЧЕНИЕ(перечисление.формыоплаты.безналичные)
	               |	
	               |	ОБЪЕДИНИТЬ
	               |	
	               |	ВЫБРАТЬ
	               |		ЗНАЧЕНИЕ(перечисление.типыаренднойплаты.переменнаячасть),
	               |		ЗНАЧЕНИЕ(перечисление.формыоплаты.наличные)) КАК ВложенныйЗапрос
	               |ГДЕ
	               |	СтатусыДоговоровАрендыСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	УслугиПоДоговорамАренды.Договор КАК ДоговорКонтрагента,
	               |	УслугиПоДоговорамАренды.ФормаОплаты,
	               |	УслугиПоДоговорамАренды.ТипАренднойПлаты
	               |ПОМЕСТИТЬ ВТ_Услуги
	               |ИЗ
	               |	ВТ_Договоры КАК ВТ_Договоры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УслугиПоДоговорамАренды.СрезПоследних(
	               |				&КонецПериода,
	               |				(Договор.Организация = &Организация
	               |					ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	               |					И (Договор.Владелец = &Контрагент
	               |						ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |					И (Договор.ОбъектАренды.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |						ИЛИ &СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))) КАК УслугиПоДоговорамАренды
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = УслугиПоДоговорамАренды.Договор
	               |			И ВТ_Договоры.ТипАренднойПлаты = УслугиПоДоговорамАренды.ТипАренднойПлаты
	               |			И ВТ_Договоры.ФормаОплаты = УслугиПоДоговорамАренды.ФормаОплаты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВложенныйЗапрос.ДоговорКонтрагента,
	               |	ВложенныйЗапрос.ФормаОплаты,
	               |	ЗНАЧЕНИЕ(Перечисление.ТипыАренднойПлаты.ПостояннаяЧасть) КАК ТипАренднойПлаты,
	               |	СУММА(ВложенныйЗапрос.СуммаЗалогаПлан) КАК СуммаЗалогаПлан,
	               |	СУММА(ВложенныйЗапрос.СуммаЗалогаПланВал) КАК СуммаЗалогаПланВал
	               |ПОМЕСТИТЬ ВТ_Депозиты
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ДепозитыПоДоговорамАренды.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |		ЗНАЧЕНИЕ(перечисление.формыоплаты.безналичные) КАК ФормаОплаты,
	               |		СУММА(ДепозитыПоДоговорамАренды.СуммаЗалогаБезналичные) КАК СуммаЗалогаПлан,
	               |		СУММА(ДепозитыПоДоговорамАренды.СуммаЗалогаБезналичныеВал) КАК СуммаЗалогаПланВал
	               |	ИЗ
	               |		ВТ_Договоры КАК ВТ_Договоры
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |				ДепозитыПоДоговорамАренды.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |				МАКСИМУМ(ДепозитыПоДоговорамАренды.Период) КАК Период,
	               |				ДепозитыПоДоговорамАренды.ФормаОплаты КАК ФормаОплаты
	               |			ИЗ
	               |				РегистрСведений.ДепозитыПоДоговорамАренды КАК ДепозитыПоДоговорамАренды
	               |			ГДЕ
	               |				ДепозитыПоДоговорамАренды.Период <= &КонецПериода
	               |				И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Организация = &Организация
	               |						ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	               |				И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Владелец = &Контрагент
	               |						ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |				И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.ОбъектАренды.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |						ИЛИ &СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				ДепозитыПоДоговорамАренды.ДоговорКонтрагента,
	               |				ДепозитыПоДоговорамАренды.ФормаОплаты) КАК ВложенныйЗапрос
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДепозитыПоДоговорамАренды КАК ДепозитыПоДоговорамАренды
	               |				ПО ВложенныйЗапрос.ДоговорКонтрагента = ДепозитыПоДоговорамАренды.ДоговорКонтрагента
	               |					И ВложенныйЗапрос.Период = ДепозитыПоДоговорамАренды.Период
	               |					И (ДепозитыПоДоговорамАренды.УчитыватьЗалог)
	               |					И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Организация = &Организация
	               |						ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	               |					И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Владелец = &Контрагент
	               |						ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |					И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.ОбъектАренды.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |						ИЛИ &СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))
	               |					И ВложенныйЗапрос.ФормаОплаты = ДепозитыПоДоговорамАренды.ФормаОплаты
	               |			ПО (ВТ_Договоры.ДоговорКонтрагента = ДепозитыПоДоговорамАренды.ДоговорКонтрагента)
	               |	ГДЕ
	               |		ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(перечисление.формыоплаты.безналичные)
	               |		И ВТ_Договоры.ТипАренднойПлаты = ЗНАЧЕНИЕ(Перечисление.ТипыАренднойПлаты.ПостояннаяЧасть)
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ДепозитыПоДоговорамАренды.ДоговорКонтрагента
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ДепозитыПоДоговорамАренды.ДоговорКонтрагента,
	               |		ЗНАЧЕНИЕ(перечисление.формыоплаты.наличные),
	               |		СУММА(ДепозитыПоДоговорамАренды.СуммаЗалогаНаличные),
	               |		СУММА(ДепозитыПоДоговорамАренды.СуммаЗалогаНаличныеВал)
	               |	ИЗ
	               |		ВТ_Договоры КАК ВТ_Договоры
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |				ДепозитыПоДоговорамАренды.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |				МАКСИМУМ(ДепозитыПоДоговорамАренды.Период) КАК Период,
	               |				ДепозитыПоДоговорамАренды.ФормаОплаты КАК ФормаОплаты
	               |			ИЗ
	               |				РегистрСведений.ДепозитыПоДоговорамАренды КАК ДепозитыПоДоговорамАренды
	               |			ГДЕ
	               |				ДепозитыПоДоговорамАренды.Период <= &КонецПериода
	               |				И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Организация = &Организация
	               |						ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	               |				И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Владелец = &Контрагент
	               |						ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |				И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.ОбъектАренды.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |						ИЛИ &СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				ДепозитыПоДоговорамАренды.ДоговорКонтрагента,
	               |				ДепозитыПоДоговорамАренды.ФормаОплаты) КАК ВложенныйЗапрос
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДепозитыПоДоговорамАренды КАК ДепозитыПоДоговорамАренды
	               |				ПО ВложенныйЗапрос.ДоговорКонтрагента = ДепозитыПоДоговорамАренды.ДоговорКонтрагента
	               |					И ВложенныйЗапрос.Период = ДепозитыПоДоговорамАренды.Период
	               |					И (ДепозитыПоДоговорамАренды.УчитыватьЗалог)
	               |					И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Организация = &Организация
	               |						ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	               |					И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.Владелец = &Контрагент
	               |						ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |					И (ДепозитыПоДоговорамАренды.ДоговорКонтрагента.ОбъектАренды.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |						ИЛИ &СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка))
	               |					И ВложенныйЗапрос.ФормаОплаты = ДепозитыПоДоговорамАренды.ФормаОплаты
	               |			ПО (ВТ_Договоры.ДоговорКонтрагента = ДепозитыПоДоговорамАренды.ДоговорКонтрагента)
	               |	ГДЕ
	               |		ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(перечисление.формыоплаты.наличные)
	               |		И ВТ_Договоры.ТипАренднойПлаты = ЗНАЧЕНИЕ(Перечисление.ТипыАренднойПлаты.ПостояннаяЧасть)
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ДепозитыПоДоговорамАренды.ДоговорКонтрагента) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.ДоговорКонтрагента,
	               |	ВложенныйЗапрос.ФормаОплаты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПланируемыеПлатежиПоДоговорамАренды.Договор КАК ДоговорКонтрагента,
	               |	ПланируемыеПлатежиПоДоговорамАренды.ФормаОплаты,
	               |	ПланируемыеПлатежиПоДоговорамАренды.ТипАренднойПлаты,
	               |	СУММА(ПланируемыеПлатежиПоДоговорамАренды.Сумма) КАК СуммаПлан,
	               |	СУММА(ПланируемыеПлатежиПоДоговорамАренды.СуммаВал) КАК СуммаПланВал
	               |ПОМЕСТИТЬ ВТ_ПланируемыеПлатежи
	               |ИЗ
	               |	ВТ_Договоры КАК ВТ_Договоры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПланируемыеПлатежиПоДоговорамАренды КАК ПланируемыеПлатежиПоДоговорамАренды
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |				МАКСИМУМ(ПланируемыеПлатежиПоДоговорамАренды.Период) КАК Период,
	               |				ПланируемыеПлатежиПоДоговорамАренды.Договор КАК Договор,
	               |				ПланируемыеПлатежиПоДоговорамАренды.ФормаОплаты КАК ФормаОплаты,
	               |				ПланируемыеПлатежиПоДоговорамАренды.ТипАренднойПлаты КАК ТипАренднойПлаты,
	               |				ПланируемыеПлатежиПоДоговорамАренды.Дата КАК Дата
	               |			ИЗ
	               |				РегистрСведений.ПланируемыеПлатежиПоДоговорамАренды КАК ПланируемыеПлатежиПоДоговорамАренды
	               |			ГДЕ
	               |				ПланируемыеПлатежиПоДоговорамАренды.Период <= &КонецПериода
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				ПланируемыеПлатежиПоДоговорамАренды.Договор,
	               |				ПланируемыеПлатежиПоДоговорамАренды.ФормаОплаты,
	               |				ПланируемыеПлатежиПоДоговорамАренды.ТипАренднойПлаты,
	               |				ПланируемыеПлатежиПоДоговорамАренды.Дата) КАК Периоды
	               |			ПО ПланируемыеПлатежиПоДоговорамАренды.Период = Периоды.Период
	               |				И ПланируемыеПлатежиПоДоговорамАренды.Договор = Периоды.Договор
	               |				И ПланируемыеПлатежиПоДоговорамАренды.ФормаОплаты = Периоды.ФормаОплаты
	               |				И ПланируемыеПлатежиПоДоговорамАренды.ТипАренднойПлаты = Периоды.ТипАренднойПлаты
	               |				И ПланируемыеПлатежиПоДоговорамАренды.Дата = Периоды.Дата
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = ПланируемыеПлатежиПоДоговорамАренды.Договор
	               |			И ВТ_Договоры.ФормаОплаты = ПланируемыеПлатежиПоДоговорамАренды.ФормаОплаты
	               |			И ВТ_Договоры.ТипАренднойПлаты = ПланируемыеПлатежиПоДоговорамАренды.ТипАренднойПлаты
	               |ГДЕ
	               |	ПланируемыеПлатежиПоДоговорамАренды.Период <= &КонецПериода
	               |	И ПланируемыеПлатежиПоДоговорамАренды.Дата >= &НачалоПериода
	               |	И ПланируемыеПлатежиПоДоговорамАренды.Дата <= &КонецПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПланируемыеПлатежиПоДоговорамАренды.Договор,
	               |	ПланируемыеПлатежиПоДоговорамАренды.ФормаОплаты,
	               |	ПланируемыеПлатежиПоДоговорамАренды.ТипАренднойПлаты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВзаиморасчетыПоДоговорамАренды.Договор КАК ДоговорКонтрагента,
	               |	ВзаиморасчетыПоДоговорамАренды.ФормаОплаты,
	               |	ВзаиморасчетыПоДоговорамАренды.ТипАренднойПлаты,
	               |	СУММА(ВзаиморасчетыПоДоговорамАренды.Сумма) КАК Оплачено
	               |ПОМЕСТИТЬ ВТ_Взаиморасчеты
	               |ИЗ
	               |	ВТ_Договоры КАК ВТ_Договоры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВзаиморасчетыПоДоговорамАренды КАК ВзаиморасчетыПоДоговорамАренды
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = ВзаиморасчетыПоДоговорамАренды.Договор
	               |			И ВТ_Договоры.ФормаОплаты = ВзаиморасчетыПоДоговорамАренды.ФормаОплаты
	               |			И ВТ_Договоры.ТипАренднойПлаты = ВзаиморасчетыПоДоговорамАренды.ТипАренднойПлаты
	               |ГДЕ
	               |	ВзаиморасчетыПоДоговорамАренды.Дата >= &НачалоПериода
	               |	И ВзаиморасчетыПоДоговорамАренды.Дата <= &КонецПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВзаиморасчетыПоДоговорамАренды.Договор,
	               |	ВзаиморасчетыПоДоговорамАренды.ФормаОплаты,
	               |	ВзаиморасчетыПоДоговорамАренды.ТипАренднойПлаты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	НачисленияПоДоговорамАренды.Договор КАК ДоговорКонтрагента,
	               |	НачисленияПоДоговорамАренды.ФормаОплаты,
	               |	НачисленияПоДоговорамАренды.ТипАренднойПлаты,
	               |	СУММА(НачисленияПоДоговорамАренды.Сумма) КАК Начислено
	               |ПОМЕСТИТЬ ВТ_Начисления
	               |ИЗ
	               |	ВТ_Договоры КАК ВТ_Договоры
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НачисленияПоДоговорамАренды КАК НачисленияПоДоговорамАренды
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |				МАКСИМУМ(НачисленияПоДоговорамАренды.Период) КАК Период,
	               |				НачисленияПоДоговорамАренды.Договор КАК Договор,
	               |				НачисленияПоДоговорамАренды.ФормаОплаты КАК ФормаОплаты,
	               |				НачисленияПоДоговорамАренды.ТипАренднойПлаты КАК ТипАренднойПлаты
	               |			ИЗ
	               |				РегистрСведений.НачисленияПоДоговорамАренды КАК НачисленияПоДоговорамАренды
	               |			ГДЕ
	               |				НЕ НачисленияПоДоговорамАренды.Депозит
	               |				И НачисленияПоДоговорамАренды.Период <= &КонецПериода
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				НачисленияПоДоговорамАренды.Договор,
	               |				НачисленияПоДоговорамАренды.ФормаОплаты,
	               |				НачисленияПоДоговорамАренды.ТипАренднойПлаты) КАК Периоды
	               |			ПО НачисленияПоДоговорамАренды.Период = Периоды.Период
	               |				И НачисленияПоДоговорамАренды.Договор = Периоды.Договор
	               |				И НачисленияПоДоговорамАренды.ФормаОплаты = Периоды.ФормаОплаты
	               |				И НачисленияПоДоговорамАренды.ТипАренднойПлаты = Периоды.ТипАренднойПлаты
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = НачисленияПоДоговорамАренды.Договор
	               |			И ВТ_Договоры.ФормаОплаты = НачисленияПоДоговорамАренды.ФормаОплаты
	               |			И ВТ_Договоры.ТипАренднойПлаты = НачисленияПоДоговорамАренды.ТипАренднойПлаты
	               |ГДЕ
	               |	НЕ НачисленияПоДоговорамАренды.Депозит
	               |	И НачисленияПоДоговорамАренды.Период <= &КонецПериода
	               |	И НачисленияПоДоговорамАренды.Дата >= &НачалоПериода
	               |	И НачисленияПоДоговорамАренды.Дата <= &КонецПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НачисленияПоДоговорамАренды.Договор,
	               |	НачисленияПоДоговорамАренды.ФормаОплаты,
	               |	НачисленияПоДоговорамАренды.ТипАренднойПлаты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Договоры.Организация КАК Организация,
	               |	ВТ_Договоры.Контрагент КАК Контрагент,
	               |	ВТ_Договоры.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |	ВТ_Договоры.ДоговорКонтрагента.СтавкаНДС КАК СтавкаНДС,
	               |	ВТ_Договоры.СтруктурнаяЕдиница,
	               |	ВТ_Договоры.ДатаНачалаАренды,
	               |	ВТ_Договоры.ДатаОкончанияАренды,
	               |	ВТ_Договоры.ТипАренднойПлаты КАК ТипАренднойПлаты,
	               |	СУММА(ВТ_Депозиты.СуммаЗалогаПлан) КАК СуммаЗалогаПлан,
	               |	СУММА(ВТ_Депозиты.СуммаЗалогаПланВал) КАК СуммаЗалогаПланВал,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |				ТОГДА ВТ_ПланируемыеПлатежи.СуммаПлан
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаПланБезналичные,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |				ТОГДА ВТ_ПланируемыеПлатежи.СуммаПланВал
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаПланБезналичныеВал,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |				ТОГДА ВТ_ПланируемыеПлатежи.СуммаПлан
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаПланНаличные,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |				ТОГДА ВТ_ПланируемыеПлатежи.СуммаПланВал
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаПланНаличныеВал,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |				ТОГДА ВТ_Взаиморасчеты.Оплачено
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ОплаченоБезналичные,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |				ТОГДА ВТ_Взаиморасчеты.Оплачено
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ОплаченоНаличные,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |				ТОГДА ВТ_Начисления.Начислено
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК НачисленоБезналичные,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |				ТОГДА ВТ_Начисления.Начислено
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК НачисленоНаличные,
	               |	РасходИзБанка.Ссылка КАК ДокументБезналичные,
	               |	РасходИзКассы.Ссылка КАК ДокументНаличные
	               |ИЗ
	               |	ВТ_Договоры КАК ВТ_Договоры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Депозиты КАК ВТ_Депозиты
	               |		ПО ВТ_Договоры.ФормаОплаты = ВТ_Депозиты.ФормаОплаты
	               |			И ВТ_Договоры.ДоговорКонтрагента = ВТ_Депозиты.ДоговорКонтрагента
	               |			И ВТ_Договоры.ТипАренднойПлаты = ВТ_Депозиты.ТипАренднойПлаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПланируемыеПлатежи КАК ВТ_ПланируемыеПлатежи
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = ВТ_ПланируемыеПлатежи.ДоговорКонтрагента
	               |			И ВТ_Договоры.ФормаОплаты = ВТ_ПланируемыеПлатежи.ФормаОплаты
	               |			И ВТ_Договоры.ТипАренднойПлаты = ВТ_ПланируемыеПлатежи.ТипАренднойПлаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Взаиморасчеты КАК ВТ_Взаиморасчеты
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = ВТ_Взаиморасчеты.ДоговорКонтрагента
	               |			И ВТ_Договоры.ФормаОплаты = ВТ_Взаиморасчеты.ФормаОплаты
	               |			И ВТ_Договоры.ТипАренднойПлаты = ВТ_Взаиморасчеты.ТипАренднойПлаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Начисления КАК ВТ_Начисления
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = ВТ_Начисления.ДоговорКонтрагента
	               |			И ВТ_Договоры.ФормаОплаты = ВТ_Начисления.ФормаОплаты
	               |			И ВТ_Договоры.ТипАренднойПлаты = ВТ_Начисления.ТипАренднойПлаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходИзБанка КАК РасходИзБанка
	               |		ПО ВТ_Договоры.Организация = РасходИзБанка.Организация
	               |			И ВТ_Договоры.Контрагент = РасходИзБанка.Контрагент
	               |			И ВТ_Договоры.ДоговорКонтрагента = РасходИзБанка.ДоговорКонтрагента
	               |			И (НАЧАЛОПЕРИОДА(РасходИзБанка.Дата, ДЕНЬ) = &ТекущаяДата)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходИзКассы КАК РасходИзКассы
	               |		ПО ВТ_Договоры.Организация = РасходИзКассы.Организация
	               |			И ВТ_Договоры.Контрагент = РасходИзКассы.Контрагент
	               |			И ВТ_Договоры.ДоговорКонтрагента = РасходИзКассы.ДоговорКонтрагента
	               |			И (НАЧАЛОПЕРИОДА(РасходИзКассы.Дата, ДЕНЬ) = &ТекущаяДата)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Услуги КАК ВТ_Услуги
	               |		ПО ВТ_Договоры.ДоговорКонтрагента = ВТ_Услуги.ДоговорКонтрагента
	               |			И ВТ_Договоры.ТипАренднойПлаты = ВТ_Услуги.ТипАренднойПлаты
	               |			И ВТ_Договоры.ФормаОплаты = ВТ_Услуги.ФормаОплаты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Договоры.ТипАренднойПлаты,
	               |	ВТ_Договоры.ДоговорКонтрагента,
	               |	ВТ_Договоры.Организация,
	               |	ВТ_Договоры.Контрагент,
	               |	ВТ_Договоры.СтруктурнаяЕдиница,
	               |	ВТ_Договоры.ДатаНачалаАренды,
	               |	ВТ_Договоры.ДатаОкончанияАренды,
	               |	РасходИзБанка.Ссылка,
	               |	РасходИзКассы.Ссылка,
	               |	ВТ_Договоры.ДоговорКонтрагента.СтавкаНДС
	               |
	               |ИМЕЮЩИЕ
	               |	(СУММА(ВТ_Депозиты.СуммаЗалогаПлан) <> 0
	               |		ИЛИ СУММА(ВТ_Депозиты.СуммаЗалогаПланВал) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |					ТОГДА ВТ_ПланируемыеПлатежи.СуммаПлан
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |					ТОГДА ВТ_ПланируемыеПлатежи.СуммаПланВал
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |					ТОГДА ВТ_ПланируемыеПлатежи.СуммаПлан
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |					ТОГДА ВТ_ПланируемыеПлатежи.СуммаПланВал
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |					ТОГДА ВТ_Взаиморасчеты.Оплачено
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |					ТОГДА ВТ_Взаиморасчеты.Оплачено
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичные)
	               |					ТОГДА ВТ_Начисления.Начислено
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0
	               |		ИЛИ СУММА(ВЫБОР
	               |				КОГДА ВТ_Договоры.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличные)
	               |					ТОГДА ВТ_Начисления.Начислено
	               |				ИНАЧЕ 0
	               |			КОНЕЦ) <> 0)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Организация,
	               |	Контрагент,
	               |	ДоговорКонтрагента,
	               |	ТипАренднойПлаты";
	Договоры.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

Процедура УсловияДоговора(Кнопка)
	ТекДанные = ЭлементыФормы.Договоры.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	пОтчет = Отчеты.УсловияДоговоровАренды.Создать();
	пФорма = пОтчет.ПолучитьФорму();
	пФорма.Открыть();
	
	НЭлемент = пОтчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДоговорКонтрагента");
	НЭлемент.ПравоеЗначение = ТекДанные.ДоговорКонтрагента;
	
	пОтчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Период", ТекущаяДата());
		
	пОтчет.СформироватьОтчет(пФорма.ЭлементыФормы.Результат);
КонецПроцедуры

Процедура ИсторияУсловийДоговора(Кнопка)
	ТекДанные = ЭлементыФормы.Договоры.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	пОтчет = Отчеты.ИсторияУсловийДоговора.Создать();
	пФорма = пОтчет.ПолучитьФорму();
	пФорма.Открыть();
	
	пОтчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Период", ТекущаяДата());
	пОтчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДоговорКонтрагента", ТекДанные.ДоговорКонтрагента);
	
	пОтчет.СформироватьОтчет(пФорма.ЭлементыФормы.Результат);
КонецПроцедуры

Процедура Реестр(Кнопка)
	ТабДокумент = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("Реестр");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.НачалоПериода = Формат(НачалоПериода, "ДФ=dd.MM.yyyy");
	ОбластьШапка.Параметры.КонецПериода = Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	ТабДокумент.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЗДоговоры", Договоры.Выгрузить());
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТЗДоговоры.Организация,
	               |	ТЗДоговоры.Контрагент,
	               |	ТЗДоговоры.ДоговорКонтрагента,
	               |	ТЗДоговоры.СтруктурнаяЕдиница,
	               |	ТЗДоговоры.Услуга,
	               |	ТЗДоговоры.СтавкаНДС,
	               |	ТЗДоговоры.ДатаНачалаАренды,
	               |	ТЗДоговоры.ДатаОкончанияАренды,
	               |	ТЗДоговоры.СпособНачисления,
	               |	ТЗДоговоры.СуммаЗалогаПлан,
	               |	ТЗДоговоры.СуммаЗалогаПланВал,
	               |	ТЗДоговоры.СуммаПланБезналичные,
	               |	ТЗДоговоры.СуммаПланБезналичныеВал,
	               |	ТЗДоговоры.СуммаПланНаличные,
	               |	ТЗДоговоры.СуммаПланНаличныеВал,
	               |	ТЗДоговоры.ОплаченоБезналичные,
	               |	ТЗДоговоры.ОплаченоНаличные,
	               |	ТЗДоговоры.НачисленоБезналичные,
	               |	ТЗДоговоры.НачисленоНаличные
	               |ПОМЕСТИТЬ ВТ_Договоры
	               |ИЗ
	               |	&ТЗДоговоры КАК ТЗДоговоры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВТ_Договоры.Организация КАК Организация,
	               |	ВТ_Договоры.Контрагент КАК Контрагент,
	               |	ВТ_Договоры.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |	ВТ_Договоры.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	               |	ВТ_Договоры.Услуга,
	               |	ВТ_Договоры.СтавкаНДС,
	               |	ВТ_Договоры.ДатаНачалаАренды,
	               |	ВТ_Договоры.ДатаОкончанияАренды,
	               |	ВТ_Договоры.СпособНачисления,
	               |	ВТ_Договоры.СуммаЗалогаПлан КАК СуммаЗалогаПлан,
	               |	ВТ_Договоры.СуммаЗалогаПланВал КАК СуммаЗалогаПланВал,
	               |	ВТ_Договоры.СуммаПланБезналичные КАК СуммаПланБезналичные,
	               |	ВТ_Договоры.СуммаПланБезналичныеВал КАК СуммаПланБезналичныеВал,
	               |	ВТ_Договоры.СуммаПланНаличные КАК СуммаПланНаличные,
	               |	ВТ_Договоры.СуммаПланНаличныеВал КАК СуммаПланНаличныеВал,
	               |	ВТ_Договоры.ОплаченоБезналичные КАК ОплаченоБезналичные,
	               |	ВТ_Договоры.ОплаченоНаличные КАК ОплаченоНаличные,
	               |	ВТ_Договоры.НачисленоБезналичные КАК НачисленоБезналичные,
	               |	ВТ_Договоры.НачисленоНаличные КАК НачисленоНаличные
	               |ИЗ
	               |	ВТ_Договоры КАК ВТ_Договоры
	               |ИТОГИ
	               |	СУММА(СуммаЗалогаПлан),
	               |	СУММА(СуммаЗалогаПланВал),
	               |	СУММА(СуммаПланБезналичные),
	               |	СУММА(СуммаПланБезналичныеВал),
	               |	СУММА(СуммаПланНаличные),
	               |	СУММА(СуммаПланНаличныеВал),
	               |	СУММА(ОплаченоБезналичные),
	               |	СУММА(ОплаченоНаличные),
	               |	СУММА(НачисленоБезналичные),
	               |	СУММА(НачисленоНаличные)
	               |ПО
	               |	ОБЩИЕ,
	               |	Организация,
	               |	Контрагент,
	               |	ДоговорКонтрагента,
	               |	СтруктурнаяЕдиница";
	ВыборкаОбщие = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщие.Следующий();
	ОбластьИтоги = Макет.ПолучитьОбласть("Итоги");
	ОбластьИтоги.Параметры.Заполнить(ВыборкаОбщие);
	
	ОбластьОрганизация = Макет.ПолучитьОбласть("Организация");
	ОбластьКонтрагент = Макет.ПолучитьОбласть("Контрагент");
	ОбластьДоговор = Макет.ПолучитьОбласть("Договор");
	ОбластьСтруктурнаяЕдиница = Макет.ПолучитьОбласть("СтруктурнаяЕдиница");
	ОбластьДетали = Макет.ПолучитьОбласть("Услуга");
	ВыборкаОрганизация = ВыборкаОбщие.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОрганизация.Следующий() Цикл
		ОбластьОрганизация.Параметры.Заполнить(ВыборкаОрганизация);
		ТабДокумент.Вывести(ОбластьОрганизация);
		
		ВыборкаКонтрагент = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагент.Следующий() Цикл
			ОбластьКонтрагент.Параметры.Заполнить(ВыборкаКонтрагент);
			ТабДокумент.Вывести(ОбластьКонтрагент);
			
			ВыборкаДоговор = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДоговор.Следующий() Цикл
				ОбластьДоговор.Параметры.Заполнить(ВыборкаДоговор);
				ТабДокумент.Вывести(ОбластьДоговор);
				
				ВыборкаСтруктурнаяЕдиница = ВыборкаДоговор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаСтруктурнаяЕдиница.Следующий() Цикл
					ОбластьСтруктурнаяЕдиница.Параметры.Заполнить(ВыборкаСтруктурнаяЕдиница);
					ТабДокумент.Вывести(ОбластьСтруктурнаяЕдиница);
					
					ВыборкаДетали = ВыборкаСтруктурнаяЕдиница.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаДетали.Следующий() Цикл
						ОбластьДетали.Параметры.Заполнить(ВыборкаДетали);
						ТабДокумент.Вывести(ОбластьДетали);
					КонецЦикла;					
				КонецЦикла;				
			КонецЦикла;			
		КонецЦикла;
	КонецЦикла;
	
	ТабДокумент.Вывести(ОбластьИтоги);
	ТабДокумент.Показать("Реестр договоров аренды");
КонецПроцедуры

Процедура СоздатьПлатежныеДокументы(Кнопка)
	Если Вопрос("Создать платежные документы?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Договоры Цикл
		Если Не Строка.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.СуммаПланБезналичные > 0 Тогда		
			Если Не ЗначениеЗаполнено(Строка.ДокументБезналичные) Тогда
				обДок = Документы.РасходИзБанка.СоздатьДокумент();
			Иначе
				обДок = Строка.ДокументБезналичные.ПолучитьОбъект();
			КонецЕсли;
			обДок.Дата = ТекущаяДата();
			обДок.Организация = Строка.Организация;
			обДок.СчетОрганизации = Строка.Организация.ОсновнойБанковскийСчет;
			обДок.ВалютаДокумента = Константы.ВалютаРегламентированногоУчета.Получить();
			обДок.ВидОперации = Перечисления.ВидыОперацийППИсходящее.ОплатаПоставщику;
			обДок.Контрагент = Строка.Контрагент;
			обДок.ДоговорКонтрагента = Строка.ДоговорКонтрагента;
			обДок.СчетКонтрагента = ?(ЗначениеЗаполнено(обДок.Контрагент.ОсновнойБанковскийСчет) И обДок.Контрагент.ОсновнойБанковскийСчет.Владелец = обДок.Контрагент, обДок.Контрагент.ОсновнойБанковскийСчет, Справочники.БанковскиеСчета.ПустаяСсылка());
			обДок.Ответственный = ПараметрыСеанса.ТекущийПользователь;
			обДок.СуммаДокумента = Строка.СуммаПланБезналичные;
			обДок.СчетБанк = ПланыСчетов.Хозрасчетный.РасчетныеСчета;
			обДок.УстановитьСчетаУчетаБУ();
			обДок.ЦФО = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ЦФО_18"); //Управление исполнительное
			обДок.ОчередностьПлатежа = 6;
			
			обДок.АвтоЗначенияРеквизитов = ФормированиеПечатныхФорм.СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(
				обДок.Организация, обДок.СчетОрганизации, обДок.Контрагент, обДок.СчетКонтрагента, обДок.ВидОперации, обДок.ПеречислениеВБюджет);
				
			Если НЕ обДок.Контрагент.СтавкаНДС.Пустая() Тогда
				обДок.СтавкаНДС = обДок.Контрагент.СтавкаНДС;
			КонецЕсли;
			Если НЕ обДок.Контрагент.СтатьяДвиженияДенежныхСредств.Пустая() Тогда
				обДок.СтатьяДвиженияДенежныхСредств = обДок.Контрагент.СтатьяДвиженияДенежныхСредств;
				//+++АК SUVV 2018.02.28 ИП-00017941
				//МассивСчетов = УправлениеДенежнымиСредствами.ПолучитьСчетРасчетовСКонтрагентом(обДок.СтатьяДвиженияДенежныхСредств);
				МассивСчетов = УправлениеДенежнымиСредствами.ПолучитьСчетРасчетовСКонтрагентом(обДок.Дата, обДок.СтатьяДвиженияДенежныхСредств);
				//---АК SUVV
				Если МассивСчетов.Количество() = 1 Тогда
					обДок.СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Финансовый.НайтиПоКоду(МассивСчетов[0]).Ссылка;
					
					Для Каждого СтрокаВидСубконто Из обДок.СчетУчетаРасчетовСКонтрагентом.ВидыСубконто Цикл
						Если СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.Организации Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.Организация;
						ИначеЕсли СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.СтатьиДвиженияДенежныхСредств Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.СтатьяДвиженияДенежныхСредств;
						ИначеЕсли СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.Контрагенты Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.Контрагент;
						ИначеЕсли СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.ТорговыеТочки Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.ДоговорКонтрагента.ОбъектАренды.СтруктурнаяЕдиница;
						КонецЕсли;
					КонецЦикла;				
				КонецЕсли;
			КонецЕсли;
			
			обДок.ПересчитатьСуммуНДСДокумент();
				
			обДок.ТекстПлательщика = обДок.АвтоЗначенияРеквизитов.ТекстПлательщика;
			обДок.ИННПлательщика = обДок.АвтоЗначенияРеквизитов.ИННПлательщика;
			обДок.КПППлательщика = обДок.АвтоЗначенияРеквизитов.КПППлательщика;
			
			обДок.ТекстПолучателя = обДок.АвтоЗначенияРеквизитов.ТекстПолучателя;
			обДок.ИННПолучателя   = обДок.АвтоЗначенияРеквизитов.ИННПолучателя;
			обДок.КПППолучателя   = обДок.АвтоЗначенияРеквизитов.КПППолучателя;
			
			СформироватьНазначениеПлатежа(обДок);
			
			обДок.УстановитьСчетаУчетаБУ();
			
			обДок.СтатьяДвиженияДенежныхСредствБУ=ОбщегоНазначенияСервер.ПолучитьСтатьюДДС_БУ(обДок.СтатьяДвиженияДенежныхСредств,обДок.ВидОперации);
			
			обДок.Записать(РежимЗаписиДокумента.Проведение);
			
			ОбщегоНазначения.СообщитьИнформациюПользователю("По строке № " + Строка.НомерСтроки + " " + ?(Не ЗначениеЗаполнено(Строка.ДокументБезналичные), "создан", "перезаполнен") + " документ " + обДок);
			
			Строка.ДокументБезналичные = обДок.Ссылка;
		КонецЕсли;
		
		Если Строка.СуммаПланНаличные > 0 Тогда		
			Если Не ЗначениеЗаполнено(Строка.ДокументНаличные) Тогда
				обДок = Документы.РасходИзКассы.СоздатьДокумент();
			Иначе
				обДок = Строка.ДокументНаличные.ПолучитьОбъект();
			КонецЕсли;
			обДок.Дата = ТекущаяДата();
			обДок.Организация = Строка.Организация;
			обДок.СчетОрганизации = Строка.Организация.ОсновнойБанковскийСчет;
			//обДок.ВалютаДокумента = Константы.ВалютаРегламентированногоУчета.Получить();
			обДок.ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПостояннойЧастиАрендыАрендодателю;
			обДок.Контрагент = Строка.Контрагент;
			обДок.ДоговорКонтрагента = Строка.ДоговорКонтрагента;
			обДок.Ответственный = ПараметрыСеанса.ТекущийПользователь;
			обДок.СуммаДокумента = Строка.СуммаПланНаличные;
			обДок.СчетКасса = ПланыСчетов.Хозрасчетный.КассаОрганизации;
			обДок.УстановитьСчетаУчетаБУ();
			обДок.ЦФО = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ЦФО_18"); //Управление исполнительное
			обДок.ПериодРегистрации = НачалоМесяца(НачалоПериода);
			обДок.СтруктурнаяЕдиница = Строка.ДоговорКонтрагента.ОбъектАренды.СтруктурнаяЕдиница;
							
			Если НЕ обДок.Контрагент.СтатьяДвиженияДенежныхСредств.Пустая() Тогда
				обДок.СтатьяДвиженияДенежныхСредств = обДок.Контрагент.СтатьяДвиженияДенежныхСредств;
				//+++АК SUVV 2018.02.28 ИП-00017941
				//МассивСчетов = УправлениеДенежнымиСредствами.ПолучитьСчетРасчетовСКонтрагентом(обДок.СтатьяДвиженияДенежныхСредств);
				МассивСчетов = УправлениеДенежнымиСредствами.ПолучитьСчетРасчетовСКонтрагентом(обДок.Дата, обДок.СтатьяДвиженияДенежныхСредств);
				//---АК SUVV
				Если МассивСчетов.Количество() = 1 Тогда
					обДок.СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Финансовый.НайтиПоКоду(МассивСчетов[0]).Ссылка;
					
					Для Каждого СтрокаВидСубконто Из обДок.СчетУчетаРасчетовСКонтрагентом.ВидыСубконто Цикл
						Если СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.Организации Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.Организация;
						ИначеЕсли СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.СтатьиДвиженияДенежныхСредств Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.СтатьяДвиженияДенежныхСредств;
						ИначеЕсли СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.Контрагенты Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.Контрагент;
						ИначеЕсли СтрокаВидСубконто.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.ТорговыеТочки Тогда
							обДок["СубконтоДт" + СтрокаВидСубконто.НомерСтроки] = обДок.ДоговорКонтрагента.ОбъектАренды.СтруктурнаяЕдиница;
						КонецЕсли;
					КонецЦикла;				                                                                      
				КонецЕсли;
			КонецЕсли;
			
			обДок.УстановитьСчетаУчетаБУ();
			
			обДок.СтатьяДвиженияДенежныхСредствБУ=ОбщегоНазначенияСервер.ПолучитьСтатьюДДС_БУ(обДок.СтатьяДвиженияДенежныхСредств,обДок.ВидОперации);
			
			обДок.Записать(РежимЗаписиДокумента.Проведение);
			
			ОбщегоНазначения.СообщитьИнформациюПользователю("По строке № " + Строка.НомерСтроки + " " + ?(Не ЗначениеЗаполнено(Строка.ДокументНаличные), "создан", "перезаполнен") + " документ " + обДок);
			
			Строка.ДокументНаличные = обДок.Ссылка;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СформироватьНазначениеПлатежа(обДок)

	АдресСуммы = Найти(обДок.НазначениеПлатежа, "Сумма");
	
	Если АдресСуммы > 0 Тогда
		ТекстНазначение = Лев(обДок.НазначениеПлатежа, АдресСуммы - 2);
	Иначе
		ТекстНазначение = обДок.НазначениеПлатежа;
	КонецЕсли;
	
	Если ТекстНазначение = "" Тогда
		ТекстНазначение = обДок.ДоговорКонтрагента.ОснованиеПлатежаПоУмолчанию;
		
		Если НЕ обДок.СчетКонтрагента.Пустая() Тогда
			ТекстНазначение = ТекстНазначение + ?(ТекстНазначение = "", "", Символы.ПС + Символы.ВК) + обДок.СчетКонтрагента.ТекстНазначения;
		КонецЕсли;
	КонецЕсли;
	//Если ТекстНазначение = "" Тогда
		//И РасшифровкаПлатежа.Количество() = 1
		//И ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
		
		//ТекстНазначение = ТекстНазначение
		//	+ ?(ПустаяСтрока(ТекстНазначение), "По ", " по ")
//			+ Строка(СтрокаПлатеж.ДоговорКонтрагента);
	//КонецЕсли;

	ТекстСумма = "Сумма " + Формат(обДок.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ=");
	
	ЕстьРасчетыСКонтрагентами = УправлениеДенежнымиСредствами.ЕстьРасчетыСКонтрагентами(обДок.ВидОперации);
	Если ЕстьРасчетыСКонтрагентами Тогда
		
		ТекстНДС = "";
	
		//ТабНДС=РасшифровкаПлатежа.Выгрузить();
		//ТабНДС.Свернуть("СтавкаНДС","СуммаНДС");
		
		//Для Каждого СтрокаНДС ИЗ ТабНДС Цикл
		//	ТекстНДС = ТекстНДС + ?(ПустаяСтрока(ТекстНДС), "", ", ");
			Если НЕ ЗначениеЗаполнено(обДок.СтавкаНДС) ИЛИ обДок.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
				ТекстНДС = ТекстНДС + "Без налога (НДС)";
			Иначе
				ТекстНДС = ТекстНДС + "НДС(" + обДок.СтавкаНДС + ") " + Формат(обДок.СуммаНДС, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ=");
			КонецЕсли;
		
		//КонецЦикла;
		
		Если обДок.СуммаНДС > 0 Тогда
			ТекстНДС = "В т.ч. " + ТекстНДС;
		КонецЕсли;
		
		ТекстСумма = ТекстСумма + Символы.ПС + ТекстНДС;
		
	КонецЕсли;
	
	обДок.НазначениеПлатежа = СокрЛП(ТекстНазначение) + Символы.ПС + ТекстСумма;

КонецПроцедуры // СформироватьНазначениеПлатежа()

Процедура ОтметитьВсе(Кнопка)
	Для Каждого Строка Из Договоры Цикл
		Строка.Использование = Истина;
	КонецЦикла;
КонецПроцедуры

Процедура СнятьВсе(Кнопка)
	Для Каждого Строка Из Договоры Цикл
		Строка.Использование = Ложь;
	КонецЦикла;
КонецПроцедуры
