
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	СостояниеОССрезПоследних.ОсновноеСредство.Номенклатура КАК Номенклатура
	                      |ПОМЕСТИТЬ ВТ_Номенклатура
	                      |ИЗ
	                      |	РегистрСведений.СостояниеОС.СрезПоследних(
	                      |			КОНЕЦПЕРИОДА(&НаДату, ДЕНЬ),
	                      |			ОсновноеСредство.Номенклатура.ЭтоХолодильник
	                      |				И ОсновноеСредство.Номенклатура.МодельХолодильника = ЗНАЧЕНИЕ(Справочник.МоделиХолодильников.ПустаяСсылка)) КАК СостояниеОССрезПоследних
	                      |ГДЕ
	                      |	НЕ СостояниеОССрезПоследних.Списано
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
	                      |	МоделиХолодильников.Ссылка КАК Модель
	                      |ИЗ
	                      |	ВТ_Номенклатура КАК ВТ_Номенклатура
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МоделиХолодильников КАК МоделиХолодильников
	                      |		ПО (ВТ_Номенклатура.Номенклатура.Наименование ПОДОБНО ""%"" + МоделиХолодильников.Наименование + ""%"")
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Номенклатура
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	
	Запрос.УстановитьПараметр("НаДату", Параметры.НаДату);
	
	ТаблицаНоменклатуры.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры


&НаСервере
Функция ЗаписатьНаСервере()
	Результат = Новый Соответствие;
	
	Для Каждого Строка Из ТаблицаНоменклатуры Цикл
		Если ЗначениеЗаполнено(Строка.Модель) Тогда
			НоменклатураОбъект = Строка.Номенклатура.ПолучитьОбъект();
			
			НоменклатураОбъект.МодельХолодильника = Строка.Модель;
			
			Попытка 
				НоменклатураОбъект.Записать();
			Исключение
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Не удалось записать номенклатуру <%1> по причине: %2", НоменклатураОбъект, ОписаниеОшибки());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
			КонецПопытки; 
		КонецЕсли; 
		
		Результат.Вставить(Строка.Номенклатура, Строка.Модель); 
	КонецЦикла;  
	
	Если Результат.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Возврат Результат;
КонецФункции


&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Закрыть(ЗаписатьНаСервере());
КонецПроцедуры

