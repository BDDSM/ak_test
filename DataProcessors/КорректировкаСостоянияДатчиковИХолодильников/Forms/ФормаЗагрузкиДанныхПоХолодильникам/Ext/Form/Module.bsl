
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Если Не ПустаяСтрока(ПутьКФайлу) Тогда
		ДиалогВыбора.ПолноеИмяФайла = ПутьКФайлу;
	КонецЕсли;
	
	ДиалогВыбора.Фильтр = "Книга Excel |*.xls;*.xlsx";	
	ДиалогВыбора.Заголовок = "Выберите файл";	
	
	ДиалогВыбора.Показать(Новый ОписаниеОповещения("ПутьКФайлуЗавершениеВыбора", ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуЗавершениеВыбора(ИмяФайла, ДополнительныеПараметры) Экспорт 
	Если ИмяФайла <> Неопределено И ИмяФайла.Количество() > 0 Тогда 
		ПутьКФайлу = ИмяФайла[0];
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере()
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Попытка 
    	ТабличныйДокумент.Прочитать(ПутьКФайлу, СпособЧтенияЗначенийТабличногоДокумента.Текст);
	Исключение	
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Не удалось прочитать файл по причине: %1", ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		
		Возврат;
	Конецпопытки;
	
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область(1, 1, ТабличныйДокумент.ВысотаТаблицы, 3));
	Построитель.Выполнить();
	
	ТаблицаФайла = Построитель.Результат.Выгрузить();
	
	ТаблицаФайла.Колонки[0].Имя = "Номенклатура"; 
	ТаблицаФайла.Колонки[1].Имя = "Модель";
	ТаблицаФайла.Колонки[2].Имя = "ТипХолодильника";
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТаблицаФайла.Номенклатура,
	                      |	ТаблицаФайла.Модель,
	                      |	ТаблицаФайла.ТипХолодильника
	                      |ПОМЕСТИТЬ ВТ_Данные
	                      |ИЗ
	                      |	&ТаблицаФайла КАК ТаблицаФайла
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВложенныйЗапрос.ТипХолодильника
	                      |ИЗ
	                      |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |		ВТ_Данные.ТипХолодильника КАК ТипХолодильника
	                      |	ИЗ
	                      |		ВТ_Данные КАК ВТ_Данные) КАК ВложенныйЗапрос
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторХолодильников КАК КлассификаторХолодильников
	                      |		ПО ВложенныйЗапрос.ТипХолодильника = КлассификаторХолодильников.Наименование
	                      |ГДЕ
	                      |	КлассификаторХолодильников.Ссылка ЕСТЬ NULL
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	МоделиХолодильников.Ссылка КАК Модель,
	                      |	КлассификаторХолодильников.Ссылка КАК Классификатор
	                      |ИЗ
	                      |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |		ВТ_Данные.Модель КАК Модель,
	                      |		ВТ_Данные.ТипХолодильника КАК ТипХолодильника
	                      |	ИЗ
	                      |		ВТ_Данные КАК ВТ_Данные) КАК ВложенныйЗапрос
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МоделиХолодильников КАК МоделиХолодильников
	                      |		ПО ВложенныйЗапрос.Модель = МоделиХолодильников.Наименование
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторХолодильников КАК КлассификаторХолодильников
	                      |		ПО ВложенныйЗапрос.ТипХолодильника = КлассификаторХолодильников.Наименование
	                      |ГДЕ
	                      |	МоделиХолодильников.Классификатор <> КлассификаторХолодильников.Ссылка
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СправочникНоменклатура.Ссылка КАК Номенклатура,
	                      |	МоделиХолодильников.Ссылка КАК Модель
	                      |ИЗ
	                      |	ВТ_Данные КАК ВТ_Данные
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	                      |		ПО ВТ_Данные.Номенклатура = СправочникНоменклатура.Наименование
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МоделиХолодильников КАК МоделиХолодильников
	                      |		ПО ВТ_Данные.Модель = МоделиХолодильников.Наименование
	                      |ГДЕ
	                      |	(СправочникНоменклатура.МодельХолодильника <> МоделиХолодильников.Ссылка
	                      |			ИЛИ НЕ СправочникНоменклатура.ЭтоХолодильник)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Данные.Номенклатура КАК Номенклатура
	                      |ИЗ
	                      |	ВТ_Данные КАК ВТ_Данные
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	                      |		ПО ВТ_Данные.Номенклатура = СправочникНоменклатура.Наименование
	                      |ГДЕ
	                      |	СправочникНоменклатура.Ссылка ЕСТЬ NULL
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Номенклатура
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Данные.Номенклатура,
	                      |	ВТ_Данные.Модель
	                      |ИЗ
	                      |	ВТ_Данные КАК ВТ_Данные
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	                      |		ПО ВТ_Данные.Номенклатура = СправочникНоменклатура.Наименование
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МоделиХолодильников КАК МоделиХолодильников
	                      |		ПО ВТ_Данные.Модель = МоделиХолодильников.Наименование
	                      |ГДЕ
	                      |	МоделиХолодильников.Ссылка ЕСТЬ NULL
	                      |	И ВТ_Данные.Модель <> """"
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ВТ_Данные.Номенклатура");
	
	Запрос.УстановитьПараметр("ТаблицаФайла", ТаблицаФайла);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	// Загрузка классификатора
	Выборка = РезультатыЗапроса[1].Выбрать();
	
	Сч = 0;
	Пока Выборка.Следующий() Цикл
		НовыйЭлемент = Справочники.КлассификаторХолодильников.СоздатьЭлемент();
		НовыйЭлемент.Наименование = Выборка.ТипХолодильника;
		
		Попытка 
			НовыйЭлемент.Записать();
			Сч = Сч + 1;
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Не удалось создать тип холодильника <%1> по причине: %2", НовыйЭлемент, ОписаниеОшибки());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		КонецПопытки; 
	КонецЦикла;   
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Загружено новых типов холодильников: %1", Сч);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
	
	// Привязка классификатора к моделям
	Выборка = РезультатыЗапроса[2].Выбрать();
	
	Сч = 0;
	Пока Выборка.Следующий() Цикл
		МодельОбъект = Выборка.Модель.ПолучитьОбъект();
		МодельОбъект.Классификатор = Выборка.Классификатор;
		
		Попытка 
			МодельОбъект.Записать();
			Сч = Сч + 1;
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Не удалось записать модель <%1> по причине: %2", МодельОбъект, ОписаниеОшибки());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		КонецПопытки; 
	КонецЦикла;   
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Изменен тип холодильника по классификатору для моделей: %1", Сч);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
	
	// Привязка моделей к номенклатуре
	Выборка = РезультатыЗапроса[3].Выбрать();
	
	Сч = 0;
	Пока Выборка.Следующий() Цикл
		НоменклатураОбъект = Выборка.Номенклатура.ПолучитьОбъект();
		НоменклатураОбъект.МодельХолодильника = Выборка.Модель;
		НоменклатураОбъект.ЭтоХолодильник = Истина;
		
		Попытка 
			НоменклатураОбъект.Записать();
			Сч = Сч + 1;
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Не удалось записать номенклатуру <%1> по причине: %2", НоменклатураОбъект, ОписаниеОшибки());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
		КонецПопытки; 
	КонецЦикла;   
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Изменена модель холодильника для номенклатуры: %1", Сч);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
	
	// Непривяазнные позиции
	Выборка = РезультатыЗапроса[4].Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		ТекстСообщения = "Для следующих позиций не найдено соответствие в справочнике ""Номенклатура"" ";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
	КонецЕсли; 
	
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Выборка.Номенклатура);			
	КонецЦикла;   
	
	Выборка = РезультатыЗапроса[5].Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		ТекстСообщения = "Для следующих позиций не найдено соответствие в справочнике ""Модели холодильников"" ";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);			
	КонецЕсли; 
	
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Выборка.Номенклатура);			
	КонецЦикла;   
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	ЗагрузитьНаСервере();
КонецПроцедуры
