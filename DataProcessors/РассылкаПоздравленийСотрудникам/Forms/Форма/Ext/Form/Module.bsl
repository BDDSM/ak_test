
#Область ОбработчикиСобытийФормы

//+++АК LATV 2018.08.09 ИП-00019120
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Объект.Открытки.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Файлы");
	ЗаполнитьОткрыткиНаСервере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

//+++АК LATV 2018.08.09 ИП-00019120
&НаКлиенте
Процедура ОтправитьПоздравления(Команда)

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Рассылка поздравлений сотрудникам'");
	ПараметрыОжидания.ОповещениеПользователя.Показать	= Истина;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение	= НСтр("ru = 'Рассылка поздравлений сотрудникам завершена'");
	
	ДлительнаяОперация = ОтправитьПоздравленияНаСервере();
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,, ПараметрыОжидания);

КонецПроцедуры

//+++АК LATV 2018.08.09 ИП-00019120
&НаКлиенте
Процедура ЗаполнитьОткрытки(Команда)

	Оповещение = Новый ОписаниеОповещения("ЗаполнитьОткрыткиЗавершение", ЭтаФорма);
	ОткрытьФорму("Обработка.РассылкаПоздравленийСотрудникам.Форма.СохранениеОткрыток",, ЭтаФорма,,,, Оповещение);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+++АК LATV 2018.08.09 ИП-00019120
&НаКлиенте
Процедура ЗаполнитьОткрыткиЗавершение(Результат, ДополнительныеПараметры) Экспорт

	ЗаполнитьОткрыткиНаСервере();

КонецПроцедуры

//+++АК LATV 2018.08.09 ИП-00019120
&НаСервере
Процедура ЗаполнитьОткрыткиНаСервере()

	Объект.Открытки.Очистить();
	
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с днем рождения", Истина), НСтр("ru = 'День рождения'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 1 годовщиной", Истина), НСтр("ru = '1 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 2 годовщиной", Истина), НСтр("ru = '2 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 3 годовщиной", Истина), НСтр("ru = '3 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 4 годовщиной", Истина), НСтр("ru = '4 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 5 годовщиной", Истина), НСтр("ru = '5 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 6 годовщиной", Истина), НСтр("ru = '6 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 7 годовщиной", Истина), НСтр("ru = '7 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 8 годовщиной", Истина), НСтр("ru = '8 годовщина'"));
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление с 9 годовщиной", Истина), НСтр("ru = '9 годовщина'"));
	//+++АК ILIK 2018.10.05 ИП-00019120.01
	Объект.Открытки.Добавить(Справочники.Файлы.НайтиПоНаименованию("Поздравление при приеме на работу" , Истина), НСтр("ru = 'Прием на работу'"));
	//---АК ILIK

КонецПроцедуры

//+++АК LATV 2018.08.09 ИП-00019120
&НаСервере
Функция ОтправитьПоздравленияНаСервере()

	// Выполняемый метод
	ПолноеИмяОбъекта = РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
	ИмяМетода = ПолноеИмяОбъекта + ".МодульОбъекта.ВыполнитьОбработкуВФоне";
	
	ПараметрыМетода = Новый Структура();
	ПараметрыМетода.Вставить("Открытки", Объект.Открытки);
	
	// Запуск фонового задания
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыМетода, ПараметрыВыполнения);

КонецФункции

#КонецОбласти
