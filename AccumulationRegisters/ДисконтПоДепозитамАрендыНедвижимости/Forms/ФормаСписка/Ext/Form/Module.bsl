
&НаКлиенте
Процедура ЗаписатьНачальныеОстатки(Команда)
	
	ДатаОстатков = ТекущаяДата();
	
	Если ВвестиДату(ДатаОстатков, "Введите дату ввода остатков", ЧастиДаты.Дата) Тогда
		ЗаписатьНачальныеОстаткиСервер(ДатаОстатков);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНачальныеОстаткиСервер(ДатаОстатков)
	
	ДокументКорректировка=Документы.КорректировкаЗаписейРегистровНакопления.СоздатьДокумент();
	ДокументКорректировка.Дата = ДатаОстатков - 1;
	ДокументКорректировка.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	ДокументКорректировка.Комментарий = "Ввод остатков дисконта по депозитам аренды недвижимости";	
	
	НС=ДокументКорректировка.ТаблицаРегистровНакопления.Добавить();
	НС.Имя="ДисконтПоДепозитамАрендыНедвижимости";
	НС.Представление="Дисконт по депозитам";
	
	//
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ДополнительноеСоглашениеСрезПоследних.Ссылка,
	                      |	ДополнительноеСоглашениеСрезПоследних.ДокументОснование,
	                      |	ДополнительноеСоглашениеСрезПоследних.ДатаОкончанияДоговора
	                      |ПОМЕСТИТЬ ВТ_ДополнительныеСоглашенияСрезПоследних
	                      |ИЗ
	                      |	Документ.ДополнительноеСоглашение КАК ДополнительноеСоглашениеСрезПоследних
	                      |ГДЕ
	                      |	ДополнительноеСоглашениеСрезПоследних.Ссылка В
	                      |			(ВЫБРАТЬ ПЕРВЫЕ 1
	                      |				ДополнительноеСоглашение.Ссылка КАК Ссылка
	                      |			ИЗ
	                      |				Документ.ДополнительноеСоглашение КАК ДополнительноеСоглашение
	                      |			ГДЕ
	                      |				ДополнительноеСоглашение.ДокументОснование = ДополнительноеСоглашениеСрезПоследних.ДокументОснование
	                      |				И ДополнительноеСоглашение.Дата < &Период
	                      |				И ДополнительноеСоглашение.Проведен
	                      |			УПОРЯДОЧИТЬ ПО
	                      |				ДополнительноеСоглашение.МоментВремени УБЫВ)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ЗаключениеДоговораАренды.Ссылка КАК Документ,
	                      |	ЗаключениеДоговораАренды.ОбъектАренды.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	                      |	ЗаключениеДоговораАренды.Контрагент КАК Контрагент,
	                      |	ЕСТЬNULL(ВТ_ДополнительныеСоглашенияСрезПоследних.ДатаОкончанияДоговора, ЗаключениеДоговораАренды.ДатаОкончанияДоговора) КАК ДатаОкончанияДоговора
	                      |ПОМЕСТИТЬ ВТ_ТорговыеТочкиПредварительно
	                      |ИЗ
	                      |	Документ.ЗаключениеДоговораАренды КАК ЗаключениеДоговораАренды
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасторжениеДоговораАренды КАК РасторжениеДоговораАренды
	                      |		ПО (РасторжениеДоговораАренды.ДокументОснование = ЗаключениеДоговораАренды.Ссылка)
	                      |			И (РасторжениеДоговораАренды.Дата < &Период)
	                      |			И (РасторжениеДоговораАренды.Проведен)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДополнительныеСоглашенияСрезПоследних КАК ВТ_ДополнительныеСоглашенияСрезПоследних
	                      |		ПО (ВТ_ДополнительныеСоглашенияСрезПоследних.ДокументОснование = ЗаключениеДоговораАренды.Ссылка)
	                      |ГДЕ
	                      |	ЗаключениеДоговораАренды.Проведен
	                      |	И РАЗНОСТЬДАТ(ВЫБОР
	                      |				КОГДА ЗаключениеДоговораАренды.ДатаЗаключенияДоговора = ДАТАВРЕМЯ(1, 1, 1)
	                      |					ТОГДА ЗаключениеДоговораАренды.ДатаАкта
	                      |				ИНАЧЕ ЗаключениеДоговораАренды.ДатаЗаключенияДоговора
	                      |			КОНЕЦ, ЕСТЬNULL(ВТ_ДополнительныеСоглашенияСрезПоследних.ДатаОкончанияДоговора, ЗаключениеДоговораАренды.ДатаОкончанияДоговора), ДЕНЬ) > 365
	                      |	И РасторжениеДоговораАренды.Ссылка ЕСТЬ NULL
	                      |	И ЗаключениеДоговораАренды.ДоговорКонтрагента В
	                      |			(ВЫБРАТЬ
	                      |				РегистрСведений.ДепозитыПоДоговорамАренды.ДоговорКонтрагента
	                      |			ИЗ
	                      |				РегистрСведений.ДепозитыПоДоговорамАренды)
	                      |	И ВЫБОР
	                      |			КОГДА ЗаключениеДоговораАренды.ДатаЗаключенияДоговора = ДАТАВРЕМЯ(1, 1, 1)
	                      |				ТОГДА ЗаключениеДоговораАренды.ДатаАкта
	                      |			ИНАЧЕ ЗаключениеДоговораАренды.ДатаЗаключенияДоговора
	                      |		КОНЕЦ < &Период
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_ТорговыеТочкиПредварительноСрезПоследних.Документ,
	                      |	ВТ_ТорговыеТочкиПредварительноСрезПоследних.СтруктурнаяЕдиница,
	                      |	ВТ_ТорговыеТочкиПредварительноСрезПоследних.Контрагент,
	                      |	ВТ_ТорговыеТочкиПредварительноСрезПоследних.ДатаОкончанияДоговора
	                      |ПОМЕСТИТЬ ВТ_ТорговыеТочки
	                      |ИЗ
	                      |	ВТ_ТорговыеТочкиПредварительно КАК ВТ_ТорговыеТочкиПредварительноСрезПоследних
	                      |ГДЕ
	                      |	ВТ_ТорговыеТочкиПредварительноСрезПоследних.Документ В
	                      |			(ВЫБРАТЬ ПЕРВЫЕ 1
	                      |				ВТ_ТорговыеТочкиПредварительно.Документ
	                      |			ИЗ
	                      |				ВТ_ТорговыеТочкиПредварительно КАК ВТ_ТорговыеТочкиПредварительно
	                      |			ГДЕ
	                      |				ВТ_ТорговыеТочкиПредварительно.СтруктурнаяЕдиница = ВТ_ТорговыеТочкиПредварительноСрезПоследних.СтруктурнаяЕдиница
	                      |				И ВТ_ТорговыеТочкиПредварительно.Контрагент = ВТ_ТорговыеТочкиПредварительноСрезПоследних.Контрагент
	                      |			УПОРЯДОЧИТЬ ПО
	                      |				ВТ_ТорговыеТочкиПредварительно.Документ.МоментВремени УБЫВ)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ФинансовыйОстатки.Субконто2 КАК Контрагент,
	                      |	ФинансовыйОстатки.Субконто3 КАК СтруктурнаяЕдиница,
	                      |	ВТ_ТорговыеТочки.Документ,
	                      |	ВТ_ТорговыеТочки.ДатаОкончанияДоговора,
	                      |	ФинансовыйОстатки.СуммаМСФООстатокКт КАК ОстатокДисконта,
	                      |	ФинансовыйОстатки.СуммаОстатокДт КАК ОстатокДепозита
	                      |ПОМЕСТИТЬ ВТ_ОстаткиПоСчету
	                      |ИЗ
	                      |	РегистрБухгалтерии.Финансовый.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Финансовый.ОбеспечительныйВзнос), , ) КАК ФинансовыйОстатки
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТорговыеТочки КАК ВТ_ТорговыеТочки
	                      |		ПО ФинансовыйОстатки.Субконто2 = ВТ_ТорговыеТочки.Контрагент
	                      |			И ФинансовыйОстатки.Субконто3 = ВТ_ТорговыеТочки.СтруктурнаяЕдиница
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_ОстаткиПоСчету.Контрагент,
	                      |	ВТ_ОстаткиПоСчету.СтруктурнаяЕдиница,
	                      |	ВТ_ОстаткиПоСчету.Документ,
	                      |	ВТ_ОстаткиПоСчету.ДатаОкончанияДоговора,
	                      |	ВЫБОР
	                      |		КОГДА ВТ_ОстаткиПоСчету.Документ.ДатаЗаключенияДоговора = ДАТАВРЕМЯ(1, 1, 1)
	                      |			ТОГДА ВТ_ОстаткиПоСчету.Документ.ДатаАкта
	                      |		ИНАЧЕ ВТ_ОстаткиПоСчету.Документ.ДатаЗаключенияДоговора
	                      |	КОНЕЦ КАК ДатаНачалаДоговора,
	                      |	ВТ_ОстаткиПоСчету.ОстатокДисконта,
	                      |	ВТ_ОстаткиПоСчету.ОстатокДепозита
	                      |ПОМЕСТИТЬ ВТ_Остатки
	                      |ИЗ
	                      |	ВТ_ОстаткиПоСчету КАК ВТ_ОстаткиПоСчету
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДисконтПоДепозитамАрендыНедвижимости.Остатки(&Период, ) КАК ДисконтПоДепозитамАрендыНедвижимостиОстатки
	                      |		ПО ВТ_ОстаткиПоСчету.Документ = ДисконтПоДепозитамАрендыНедвижимостиОстатки.Документ
	                      |ГДЕ
	                      |	ДисконтПоДепозитамАрендыНедвижимостиОстатки.Документ ЕСТЬ NULL
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Остатки.ДатаНачалаДоговора,
	                      |	МАКСИМУМ(СтавкиДисконтированияДепозитов.Период) КАК Период
	                      |ПОМЕСТИТЬ ВТ_ПериодыСтавок
	                      |ИЗ
	                      |	ВТ_Остатки КАК ВТ_Остатки
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиДисконтированияДепозитов КАК СтавкиДисконтированияДепозитов
	                      |		ПО (СтавкиДисконтированияДепозитов.ВидСтавки = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокДисконтированияДепозитов.НачальныеОстатки))
	                      |			И ВТ_Остатки.ДатаНачалаДоговора >= СтавкиДисконтированияДепозитов.Период
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_Остатки.ДатаНачалаДоговора
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДОБАВИТЬКДАТЕ(&Период, СЕКУНДА, -1) КАК Период,
	                      |	ВТ_Остатки.Документ.Организация КАК Организация,
	                      |	ВТ_Остатки.Контрагент,
	                      |	ВТ_Остатки.Документ.ДоговорКонтрагента КАК ДоговорКонтрагента,
	                      |	ВТ_Остатки.Документ,
	                      |	ВТ_Остатки.СтруктурнаяЕдиница,
	                      |	ВТ_Остатки.ДатаОкончанияДоговора КАК ДатаОкончанияДоговора,
	                      |	ВТ_Остатки.ОстатокДисконта КАК СуммаДисконта,
	                      |	ВТ_Остатки.ОстатокДепозита КАК СуммаДепозита,
	                      |	СтавкиДисконтированияДепозитов.ЗначениеСтавки КАК СтавкаДисконта
	                      |ИЗ
	                      |	ВТ_Остатки КАК ВТ_Остатки
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыСтавок КАК ВТ_ПериодыСтавок
	                      |		ПО ВТ_Остатки.ДатаНачалаДоговора = ВТ_ПериодыСтавок.ДатаНачалаДоговора
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиДисконтированияДепозитов КАК СтавкиДисконтированияДепозитов
	                      |		ПО (СтавкиДисконтированияДепозитов.ВидСтавки = ЗНАЧЕНИЕ(Перечисление.ВидыСтавокДисконтированияДепозитов.НачальныеОстатки))
	                      |			И (ВТ_ПериодыСтавок.Период = СтавкиДисконтированияДепозитов.Период)");                                                                  
	
	Запрос.УстановитьПараметр("Период", ДатаОстатков);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Документ = NULL Тогда
			Сообщить("ТТ: " + Выборка.СтруктурнаяЕдиница + ". Контрагент: " + Выборка.Контрагент + ". Не удалось найти документ заключения договора аренды!", СтатусСообщения.Важное);			
		ИначеЕсли Выборка.СтавкаДисконта = NULL Тогда
			Сообщить("ТТ: " + Выборка.СтруктурнаяЕдиница + ". Контрагент: " + Выборка.Контрагент + ". Не удалось найти ставку дисконта, действующую на момент заключения договора аренды!", СтатусСообщения.Важное);			
		Иначе
			Движение = ДокументКорректировка.Движения.ДисконтПоДепозитамАрендыНедвижимости.ДобавитьПриход();			
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЕсли; 
	КонецЦикла;  
	
	Если ДокументКорректировка.Движения.ДисконтПоДепозитамАрендыНедвижимости.Количество()>0 Тогда
		ДокументКорректировка.Записать();
		Сообщить("Записан документ " + ДокументКорректировка);
	КонецЕсли; 
	
КонецПроцедуры 