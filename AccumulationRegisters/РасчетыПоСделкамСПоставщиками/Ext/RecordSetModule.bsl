
Процедура ПриЗаписи(Отказ, Замещение)
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;	
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Не значениеЗаполнено(Запись.Комплектация) Тогда
			Запись.Комплектация = Неопределено;
		КонецЕсли;	
	КонецЦикла;	
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;	
	БезКонтроля = ОбщегоНазначенияПовтИсп.ПолучитьПравоПользователяУпр(ПланыВидовХарактеристик.ПраваПользователей.РазрешитьУвеличениеПереплатПоСделкам, Ложь);
		
	Регистратор = ЭтотОбъект.Отбор.Регистратор.Значение;
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.РасходИзБанка") И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Оплачено") Тогда
		Возврат;
	КонецЕсли;	
	ТЗ = ЭтотОбъект.Выгрузить();
	ТекстЗапроса="ВЫБРАТЬ
	             |	Тз.Сделка,
	             |	ВЫБОР
	             |		КОГДА Тз.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	             |			ТОГДА -Тз.Сумма
	             |		ИНАЧЕ Тз.Сумма
	             |	КОНЕЦ КАК СуммаОстаток,
	             |	Тз.УИН_Этапа,
	             |	Тз.Комплектация,
	             |	Тз.УИН_СтрокиКомплектации,
	             |	Тз.ДатаПлатежа
	             |ПОМЕСТИТЬ ВТ_НовыеДвижения
	             |ИЗ
	             |	&ТЗ КАК Тз
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ОбщиеДвижения.Сделка,
	             |	СУММА(ОбщиеДвижения.СуммаОстаток) КАК СуммаОстаток,
	             |	ОбщиеДвижения.УИН_Этапа,
	             |	ОбщиеДвижения.Комплектация,
	             |	ОбщиеДвижения.УИН_СтрокиКомплектации,
	             |	ОбщиеДвижения.ДатаПлатежа
	             |ПОМЕСТИТЬ ВТ_ДвиженияДельта
	             |ИЗ
	             |	(ВЫБРАТЬ
	             |		РасчетыПоСделкамСПоставщиками.Сделка КАК Сделка,
	             |		СУММА(ВЫБОР
	             |				КОГДА РасчетыПоСделкамСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	             |					ТОГДА -РасчетыПоСделкамСПоставщиками.Сумма
	             |				ИНАЧЕ РасчетыПоСделкамСПоставщиками.Сумма
	             |			КОНЕЦ) КАК СуммаОстаток,
	             |		РасчетыПоСделкамСПоставщиками.УИН_Этапа КАК УИН_Этапа,
	             |		РасчетыПоСделкамСПоставщиками.Комплектация КАК Комплектация,
	             |		РасчетыПоСделкамСПоставщиками.УИН_СтрокиКомплектации КАК УИН_СтрокиКомплектации,
	             |		РасчетыПоСделкамСПоставщиками.ДатаПлатежа КАК ДатаПлатежа
	             |	ИЗ
	             |		РегистрНакопления.РасчетыПоСделкамСПоставщиками КАК РасчетыПоСделкамСПоставщиками
	             |	ГДЕ
	             |		РасчетыПоСделкамСПоставщиками.Регистратор = &Регистратор
	             |	
	             |	СГРУППИРОВАТЬ ПО
	             |		РасчетыПоСделкамСПоставщиками.Сделка,
	             |		РасчетыПоСделкамСПоставщиками.УИН_Этапа,
	             |		РасчетыПоСделкамСПоставщиками.Комплектация,
	             |		РасчетыПоСделкамСПоставщиками.УИН_СтрокиКомплектации,
	             |		РасчетыПоСделкамСПоставщиками.ДатаПлатежа
	             |	
	             |	ОБЪЕДИНИТЬ ВСЕ
	             |	
	             |	ВЫБРАТЬ
	             |		ВТ_НовыеДвижения.Сделка,
	             |		ВТ_НовыеДвижения.СуммаОстаток,
	             |		ВТ_НовыеДвижения.УИН_Этапа,
	             |		ВТ_НовыеДвижения.Комплектация,
	             |		ВТ_НовыеДвижения.УИН_СтрокиКомплектации,
	             |		ВТ_НовыеДвижения.ДатаПлатежа
	             |	ИЗ
	             |		ВТ_НовыеДвижения КАК ВТ_НовыеДвижения) КАК ОбщиеДвижения
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ОбщиеДвижения.Сделка,
	             |	ОбщиеДвижения.УИН_Этапа,
	             |	ОбщиеДвижения.Комплектация,
	             |	ОбщиеДвижения.УИН_СтрокиКомплектации,
	             |	ОбщиеДвижения.ДатаПлатежа
	             |
	             |ИМЕЮЩИЕ
	             |	СУММА(ОбщиеДвижения.СуммаОстаток) < 0
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Сделка,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Сделка.Представление КАК СделкаПр,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.СуммаОстаток КАК СуммаОстаток,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.УИН_Этапа,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Комплектация,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.Комплектация.Представление КАК КомплектацияПр,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.УИН_СтрокиКомплектации,
	             |	РасчетыПоСделкамСПоставщикамиОстатки.ДатаПлатежа
	             |ИЗ
	             |	РегистрНакопления.РасчетыПоСделкамСПоставщиками.Остатки(
	             |			,
	             |			(Сделка, УИН_ЭТапа, Комплектация, УИН_СтрокиКомплектации, ДатаПлатежа) В
	             |				(ВЫБРАТЬ
	             |					ДвиженияДельта.Сделка,
	             |					ДвиженияДельта.УИН_Этапа,
	             |					ДвиженияДельта.Комплектация,
	             |					ДвиженияДельта.УИН_СтрокиКомплектации,
	             |					ДвиженияДельта.ДатаПлатежа
	             |				ИЗ
	             |					ВТ_ДвиженияДельта КАК ДвиженияДельта)) КАК РасчетыПоСделкамСПоставщикамиОстатки
	             |ГДЕ
	             |	РасчетыПоСделкамСПоставщикамиОстатки.СуммаОстаток < 0
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	СуммаОстаток
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	СделкаСПоставщикомГрафикОплат.Номенклатура,
	             |	СделкаСПоставщикомГрафикОплат.Номенклатура.Представление КАК НоменклатураПр,
	             |	СделкаСПоставщикомГрафикОплат.УИН_Строки,
	             |	СделкаСПоставщикомГрафикОплат.Ссылка КАК Сделка
	             |ИЗ
	             |	Документ.СделкаСПоставщиком.ГрафикОплат КАК СделкаСПоставщикомГрафикОплат
	             |ГДЕ
	             |	СделкаСПоставщикомГрафикОплат.Ссылка В
	             |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	             |				ДвиженияДельта.Сделка
	             |			ИЗ
	             |				ВТ_ДвиженияДельта КАК ДвиженияДельта)
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	СделкаСПоставщикомГрафикОплат.Номенклатура,
	             |	СделкаСПоставщикомГрафикОплат.УИН_Строки,
	             |	СделкаСПоставщикомГрафикОплат.Ссылка,
	             |	СделкаСПоставщикомГрафикОплат.Номенклатура.Представление";

				 
	
	Запрос=Новый Запрос(ТекстЗапроса);		
	
	Запрос.УстановитьПараметр("Регистратор",Регистратор);
	Запрос.УстановитьПараметр("ТЗ"		   ,ТЗ);
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[2].Выбрать();
	Если Результат[2].Пустой() Тогда 
		Возврат;
	Иначе
	
		Выборка = Результат[2].Выбрать();
		Расшифровки = Результат[3].Выгрузить();			
		
		Если НЕ БезКонтроля = Истина Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Проведение документа увеличивает суммы переплат в расчетах по сделкам, в записи отказано", Отказ);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Проведение документа увеличивает суммы переплат в расчетах по сделкам. Данные записаны");
		КонецЕсли;	
		Пока Выборка.Следующий() Цикл
			
		    СтрокиРасшифровки = Расшифровки.НайтиСтроки(Новый Структура("Сделка,УИН_Строки",Выборка.Сделка, Выборка.УИН_Этапа));
			Если СтрокиРасшифровки <> Неопределено Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Переплата в расчетах по сделкам по "
					+?(СтрокиРасшифровки.Количество()>0,""+СтрокиРасшифровки[0].НоменклатураПр+"/","")
					+Выборка.КомплектацияПр+"/"
					+Выборка.СделкаПр+"/дата платежа:"
					+выборка.ДатаПлатежа);
			КонецЕсли;
		
		КонецЦикла; 
		
	КонецЕсли;		
	
КонецПроцедуры
